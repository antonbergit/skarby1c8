#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Процедура ПриОпределенииНастроекОтчета(НастройкиОтчета, НастройкиВариантов) Экспорт
	
	НастройкиОтчета.ПоказыватьНастройкиДиаграммыНаФормеОтчета = Ложь;
	НастройкиВариантов["Основной"].Рекомендуемый = Истина;
	НастройкиВариантов["Основной"].Теги = НСтр("ru='Розница,Продажи';uk='Роздріб,Продажі'");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

// Процедура обработчик события "ПриКомпоновкеРезультата" объекта
//
Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ЭтотОбъект.НомерОтчета = ЭтотОбъект.НомерОтчета + 1;

	//Проверим параметры периода
	Если НЕ ДатаНачала = Дата(1,1,1)
		И НЕ ДатаОкончания = Дата(1,1,1)
		И ДатаНачала > ДатаОкончания Тогда
		
		Сообщение	 		= Новый СообщениеПользователю;
		Сообщение.Текст	 	= "Дата начала периода не должна превышать дату окончания";
		Сообщение.Сообщить();
		
		Возврат;
		
	КонецЕсли;
	Если СтруктурнаяЕдиница.РозничныйВидЦен.Пустая() Тогда
		Сообщение	 		= Новый СообщениеПользователю;
		Сообщение.Текст	 	= "У структурной единицы не указан розничный вид цен";
		Сообщение.Сообщить();
	КонецЕсли;
	
	ДокументРезультат.Вывести(СформироватьОтчетТоварныйОтчет(
		ДатаНачала,
		ДатаОкончания,
		СтруктурнаяЕдиница,
		Организация,
		НомерОтчета
	));
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Возвращает последний элемент структуры - группировку
Функция ПолучитьПоследнийЭлементСтруктуры(ЭлементСтруктурыНастроек, Строки = Истина)
	
	Если ТипЗнч(ЭлементСтруктурыНастроек) = Тип("КомпоновщикНастроекКомпоновкиДанных") тогда
		Настройки = ЭлементСтруктурыНастроек.Настройки;
	ИначеЕсли ТипЗнч(ЭлементСтруктурыНастроек) = Тип("НастройкиКомпоновкиДанных") тогда
		Настройки = ЭлементСтруктурыНастроек;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
	Структура = Настройки.Структура;
	Если Структура.Количество() = 0 Тогда
		Возврат Настройки;
	КонецЕсли;
	
	Если Строки Тогда
		ИмяСтруктурыТаблицы = "Строки";
		ИмяСтруктурыДиаграммы = "Серии";
	Иначе
		ИмяСтруктурыТаблицы = "Колонки";
		ИмяСтруктурыДиаграммы = "Точки";
	КонецЕсли;
	
	Пока Истина Цикл
		ЭлементСтруктуры = Структура[0];
		Если ТипЗнч(ЭлементСтруктуры) = Тип("ТаблицаКомпоновкиДанных") И ЭлементСтруктуры[ИмяСтруктурыТаблицы].Количество() > 0 Тогда
			Если ЭлементСтруктуры[ИмяСтруктурыТаблицы][0].Структура.Количество() = 0 Тогда
				Структура = ЭлементСтруктуры[ИмяСтруктурыТаблицы];
				Прервать;
			КонецЕсли;
			Структура = ЭлементСтруктуры[ИмяСтруктурыТаблицы][0].Структура;
		ИначеЕсли ТипЗнч(ЭлементСтруктуры) = Тип("ДиаграммаКомпоновкиДанных") И ЭлементСтруктуры[ИмяСтруктурыДиаграммы].Количество() > 0 Тогда
			Если ЭлементСтруктуры[ИмяСтруктурыДиаграммы][0].Структура.Количество() = 0 Тогда
				Структура = ЭлементСтруктуры[ИмяСтруктурыДиаграммы];
				Прервать;
			КонецЕсли;
			Структура = ЭлементСтруктуры[ИмяСтруктурыДиаграммы][0].Структура;
		ИначеЕсли ТипЗнч(ЭлементСтруктуры) = Тип("ГруппировкаКомпоновкиДанных")
			  ИЛИ ТипЗнч(ЭлементСтруктуры) = Тип("ГруппировкаТаблицыКомпоновкиДанных")
			  ИЛИ ТипЗнч(ЭлементСтруктуры) = Тип("ГруппировкаДиаграммыКомпоновкиДанных") Тогда
			Если ЭлементСтруктуры.Структура.Количество() = 0 Тогда
				Прервать;
			КонецЕсли;
			Структура = ЭлементСтруктуры.Структура;
		ИначеЕсли ТипЗнч(ЭлементСтруктуры) = Тип("ТаблицаКомпоновкиДанных") Тогда
			Возврат ЭлементСтруктуры[ИмяСтруктурыТаблицы];
		ИначеЕсли ТипЗнч(ЭлементСтруктуры) = Тип("ДиаграммаКомпоновкиДанных")	Тогда
			Возврат ЭлементСтруктуры[ИмяСтруктурыДиаграммы];
		Иначе
			Возврат ЭлементСтруктуры;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Структура[0];
	
КонецФункции

// Возвращает группировку - детальные записи компоновщика настроек
Функция ПолучитьЭлементСтруктурыДетальныеЗаписи(КомпоновщикНастроек)
	
	ПоследнийЭлементСтруктуры = ПолучитьПоследнийЭлементСтруктуры(КомпоновщикНастроек, Истина);
	Если ТипЗнч(ПоследнийЭлементСтруктуры) = Тип("ГруппировкаКомпоновкиДанных")
	 ИЛИ ТипЗнч(ПоследнийЭлементСтруктуры) = Тип("ГруппировкаТаблицыКомпоновкиДанных")
	 ИЛИ ТипЗнч(ПоследнийЭлементСтруктуры) = Тип("ГруппировкаДиаграммыКомпоновкиДанных") Тогда
		Если ПоследнийЭлементСтруктуры.ПоляГруппировки.Элементы.Количество() = 0 Тогда
			Возврат ПоследнийЭлементСтруктуры;
		КонецЕсли;
	КонецЕсли;
	
КонецФункции

// Добавляет группировку в компоновщик настроек в самый нижний уровень структуры, если поле не указано - детальные поля
Функция ДобавитьГруппировку(КомпоновщикНастроек, Знач Поле = Неопределено, Строки = Истина)
	
	ЭлементСтруктуры = ПолучитьПоследнийЭлементСтруктуры(КомпоновщикНастроек, Строки);
	Если ЭлементСтруктуры = Неопределено 
	 ИЛИ ПолучитьЭлементСтруктурыДетальныеЗаписи(КомпоновщикНастроек) <> Неопределено 
	   И Поле = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если ТипЗнч(Поле) = Тип("Строка") Тогда
		Поле = Новый ПолеКомпоновкиДанных(Поле);
	КонецЕсли;
	
	Если ТипЗнч(ЭлементСтруктуры) = Тип("ГруппировкаТаблицыКомпоновкиДанных") 
	 ИЛИ ТипЗнч(ЭлементСтруктуры) = Тип("ГруппировкаДиаграммыКомпоновкиДанных") Тогда
		НоваяГруппировка = ЭлементСтруктуры.Структура.Добавить();
	ИначеЕсли ТипЗнч(ЭлементСтруктуры) = Тип("КоллекцияЭлементовСтруктурыТаблицыКомпоновкиДанных")
			ИЛИ ТипЗнч(ЭлементСтруктуры) = Тип("КоллекцияЭлементовСтруктурыДиаграммыКомпоновкиДанных") Тогда
		НоваяГруппировка = ЭлементСтруктуры.Добавить();
	Иначе
		НоваяГруппировка = ЭлементСтруктуры.Структура.Добавить(Тип("ГруппировкаКомпоновкиДанных"));
	КонецЕсли;
	
	НоваяГруппировка.Выбор.Элементы.Добавить(Тип("АвтоВыбранноеПолеКомпоновкиДанных"));
	НоваяГруппировка.Порядок.Элементы.Добавить(Тип("АвтоЭлементПорядкаКомпоновкиДанных"));
	Если Поле <> Неопределено Тогда
		ПолеГруппировки = НоваяГруппировка.ПоляГруппировки.Элементы.Добавить(Тип("ПолеГруппировкиКомпоновкиДанных"));
		ПолеГруппировки.Поле = Поле;
	КонецЕсли;
	Возврат НоваяГруппировка;
	
КонецФункции

Функция УстановитьПараметр(Настройки, Параметр, Значение, Использование = истина)
	
	ЗначениеПараметра = Неопределено;
	ПолеПараметр = ?(ТипЗнч(Параметр) = Тип("Строка"), Новый ПараметрКомпоновкиДанных(Параметр), Параметр);
	
	Если ТипЗнч(Настройки) = Тип("НастройкиКомпоновкиДанных") тогда
		ЗначениеПараметра = Настройки.ПараметрыДанных.НайтиЗначениеПараметра(ПолеПараметр);
	ИначеЕсли ТипЗнч(Настройки) = Тип("ПользовательскиеНастройкиКомпоновкиДанных") тогда
		Для каждого ЭлементНастройки из Настройки.Элементы Цикл
			Если ТипЗнч(ЭлементНастройки) = Тип("ЗначениеПараметраНастроекКомпоновкиДанных") и ЭлементНастройки.Параметр = ПолеПараметр тогда
				ЗначениеПараметра = ЭлементНастройки;
				Прервать;
			КонецЕсли;
		КонецЦикла;
	ИначеЕсли ТипЗнч(Настройки) = Тип("КомпоновщикНастроекКомпоновкиДанных") тогда
		Для каждого ЭлементНастройки из Настройки.ПользовательскиеНастройки.Элементы Цикл
			Если ТипЗнч(ЭлементНастройки) = Тип("ЗначениеПараметраНастроекКомпоновкиДанных") и ЭлементНастройки.Параметр = ПолеПараметр тогда
				ЗначениеПараметра = ЭлементНастройки;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		Если ЗначениеПараметра = Неопределено тогда
			ЗначениеПараметра = Настройки.Настройки.ПараметрыДанных.НайтиЗначениеПараметра(ПолеПараметр);
		КонецЕсли;
	КонецЕсли;
	
	ЗначениеПараметра.Значение = Значение;
	ЗначениеПараметра.Использование = Использование;
	
	Возврат ЗначениеПараметра;
КонецФункции

// Устанавливает параметр вывода компоновщика настроек
Функция УстановитьПараметрВывода(КомпоновщикНастроекГруппировка, ИмяПараметра, Значение)
	
	Если ТипЗнч(КомпоновщикНастроекГруппировка) = Тип("КомпоновщикНастроекКомпоновкиДанных") Тогда
		ЗначениеПараметра = КомпоновщикНастроекГруппировка.Настройки.ПараметрыВывода.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных(ИмяПараметра));
	Иначе
		ЗначениеПараметра = КомпоновщикНастроекГруппировка.ПараметрыВывода.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных(ИмяПараметра));
	КонецЕсли;
	Если ЗначениеПараметра = Неопределено Тогда
		Возврат Неопределено;
	Иначе
		ЗначениеПараметра.Использование = Истина;
		ЗначениеПараметра.Значение = Значение;
		Возврат ЗначениеПараметра;
	КонецЕсли;
	
КонецФункции

// Добавляет отбор в набор отборов компоновщика или группы отборов
Функция ДобавитьОтбор(ЭлементСтруктуры, Знач Поле, Значение, ВидСравнения = Неопределено, Использование = Истина)
	
	Если ТипЗнч(Поле) = Тип("Строка") Тогда
		Поле = Новый ПолеКомпоновкиДанных(Поле);
	КонецЕсли;
	
	Если ТипЗнч(ЭлементСтруктуры) = Тип("КомпоновщикНастроекКомпоновкиДанных") Тогда
		Отбор = ЭлементСтруктуры.Настройки.Отбор;
	ИначеЕсли ТипЗнч(ЭлементСтруктуры) = Тип("НастройкиКомпоновкиДанных") Тогда
		Отбор = ЭлементСтруктуры.Отбор;
	Иначе
		Отбор = ЭлементСтруктуры;
	КонецЕсли;
	
	Если ВидСравнения = Неопределено Тогда
		ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	КонецЕсли;
	
	НовыйЭлемент = Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	НовыйЭлемент.Использование  = Использование;
	НовыйЭлемент.ЛевоеЗначение  = Поле;
	НовыйЭлемент.ВидСравнения   = ВидСравнения;
	НовыйЭлемент.ПравоеЗначение = Значение;
	Возврат НовыйЭлемент;
	
КонецФункции

// Стандартная для данной конфигурации функция форматирования сумм.
//
// Параметры: 
//  Сумма  - число, которое мы хотим форматировать, 
//  Валюта - ссылка на элемент справочника валют, если задан, то к в результирующую строку
//           будет добавлено представление валюты
//  ЧН     - строка, представляющая нулевое значение числа,
//  ЧРГ    - символ-разделитель групп целой части числа.
//
// Возвращаемое значение:
//  Отформатированная должным образом строковое представление суммы.
//
Функция ФорматСумм(Сумма, Валюта = Неопределено, ЧН = "", ЧРГ = "")

	ФорматнаяСтрока = "ЧЦ=15;ЧДЦ=2" +
					?(НЕ ЗначениеЗаполнено(ЧН), "", ";" + "ЧН=" + ЧН) +
					?(НЕ ЗначениеЗаполнено(ЧРГ),"", ";" + "ЧРГ=" + ЧРГ);
	РезультирующаяСтрока = СокрЛ(Формат(Сумма, ФорматнаяСтрока));
	
	Если ЗначениеЗаполнено(Валюта) Тогда
		РезультирующаяСтрока = РезультирующаяСтрока + " " + СокрП(Валюта);
	КонецЕсли;

	Возврат РезультирующаяСтрока;

КонецФункции // ФорматСумм()

// Функция выполняет формирование табличного документа отчета Товарный отчет.
//
// Параметры: 
//  Сумма  - число, которое мы хотим форматировать, 
//  Валюта - ссылка на элемент справочника валют, если задан, то к в результирующую строку
//           будет добавлено представление валюты
//  ЧН     - строка, представляющая нулевое значение числа,
//  ЧРГ    - символ-разделитель групп целой части числа.
//
// Возвращаемое значение:
//  Отформатированная должным образом строковое представление суммы.
//
Функция СформироватьОтчетТоварныйОтчет(ДатаНачала, ДатаОкончания, СтруктурнаяЕдиница, Организация, НомерОтчета)
	
	ТабДокумент = Новый ТабличныйДокумент;
	
	Макет = ПолучитьМакет("Макет");
	
	КодЯзыкаПечать = Локализация.ПолучитьЯзыкФормированияПечатныхФорм();	
	Макет.КодЯзыкаМакета = КодЯзыкаПечать;
	
	// Выведем заголовок.
	СведенияОПокупателе = УправлениеНебольшойФирмойСервер.СведенияОЮрФизЛице(Организация, ДатаОкончания, , , КодЯзыкаПечать);
	
	ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
	ОбластьМакета.Параметры.ОрганизацияПредставление = УправлениеНебольшойФирмойСервер.ОписаниеОрганизации(СведенияОПокупателе);
	ОбластьМакета.Параметры.СтруктурнаяЕдиницаПредставление = СтруктурнаяЕдиница.Наименование;
	ОбластьМакета.Параметры.ДатаСоставления = ТекущаяДата();
	ОбластьМакета.Параметры.ДатаНачала = ДатаНачала;
	ОбластьМакета.Параметры.ДатаКонца = ДатаОкончания;
	МОЛНаименование = СтруктурнаяЕдиница.МОЛ.Наименование;
	ОбластьМакета.Параметры.МОЛ = ?(ЗначениеЗаполнено(МОЛНаименование), МОЛНаименование, "");
	
	Запрос = Новый Запрос();
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	Сотрудники.Код КАК МОЛТабельныйНомер
	|ИЗ
	|	Справочник.Сотрудники КАК Сотрудники
	|ГДЕ
	|	Сотрудники.Физлицо = &Физлицо";
	Запрос.УстановитьПараметр("Физлицо", СтруктурнаяЕдиница.МОЛ);
	ВыборкаРезультатаЗапроса = Запрос.Выполнить().Выбрать();
	
	Если ВыборкаРезультатаЗапроса.Следующий() Тогда
		ОбластьМакета.Параметры.МОЛТабельныйНомер = ?(ЗначениеЗаполнено(ВыборкаРезультатаЗапроса.МОЛТабельныйНомер), ВыборкаРезультатаЗапроса.МОЛТабельныйНомер, "");
	КонецЕсли;
	
	ОбластьМакета.Параметры.Номер = НомерОтчета;
	
	ТабДокумент.Вывести(ОбластьМакета);
	
	ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
	ТабДокумент.Вывести(ОбластьМакета);
	
	// Повторение шапки
	ПовторятьПриПечатиСтроки = ТабДокумент.Область(1 + ОбластьМакета.ВысотаТаблицы, ,2 + ОбластьМакета.ВысотаТаблицы);
	
	СхемаКомпоновкиДанных = ПолучитьМакет("ОсновнаяСхемаКомпоновкиДанных");
	
	// Подготовка компоновщика макета компоновки данных.
	Компоновщик = Новый КомпоновщикНастроекКомпоновкиДанных;
	Компоновщик.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(СхемаКомпоновкиДанных));
	Компоновщик.ЗагрузитьНастройки(СхемаКомпоновкиДанных.НастройкиПоУмолчанию);
	
	// Выбранные поля.
	ОбязательныеПоля = Новый Массив;
	ОбязательныеПоля.Добавить("ПериодСекунда");
	ОбязательныеПоля.Добавить("Регистратор");
	ОбязательныеПоля.Добавить("СуммаНачальныйОстаток");
	ОбязательныеПоля.Добавить("СуммаПриход");
	ОбязательныеПоля.Добавить("СуммаРасход");
	ОбязательныеПоля.Добавить("СуммаКонечныйОстаток");
	Компоновщик.Настройки.Выбор.Элементы.Очистить();
	Для Каждого ОбязательноеПоле Из ОбязательныеПоля Цикл
		ПолеСКД = Обработки.ПечатьЭтикетокИЦенников.НайтиПолеСКДПоПолномуИмени(Компоновщик.Настройки.Выбор.ДоступныеПоляВыбора.Элементы, ОбязательноеПоле);
		Если ПолеСКД <> Неопределено Тогда
			ВыбранноеПоле = Компоновщик.Настройки.Выбор.Элементы.Добавить(Тип("ВыбранноеПолеКомпоновкиДанных"));
			ВыбранноеПоле.Поле = ПолеСКД.Поле;
		КонецЕсли;
	КонецЦикла;
	
	// Группировки.
	Компоновщик.Настройки.Структура.Очистить();
	ДобавитьГруппировку(Компоновщик, "ПериодСекунда");
	ДобавитьГруппировку(Компоновщик, "Регистратор");
	
	// ОтключениеИтогов.
	УстановитьПараметрВывода(Компоновщик,"ВертикальноеРасположениеОбщихИтогов", РасположениеИтоговКомпоновкиДанных.Нет);
	УстановитьПараметрВывода(Компоновщик,"ГоризонтальноеРасположениеОбщихИтогов", РасположениеИтоговКомпоновкиДанных.Нет);
	
	// Отборы.
	ДобавитьОтбор(Компоновщик, "СтруктурнаяЕдиница", СтруктурнаяЕдиница);
	
	// Установка параметра "Вид цен"
	ПараметрВидЦен = Новый ПараметрКомпоновкиДанных("ВидЦен");
	ЗначениеПараметраВидЦен = Компоновщик.Настройки.ПараметрыДанных.НайтиЗначениеПараметра(ПараметрВидЦен);
	Если ЗначениеПараметраВидЦен <> Неопределено Тогда
		ЗначениеПараметраВидЦен.Значение = СтруктурнаяЕдиница.РозничныйВидЦен;
		ЗначениеПараметраВидЦен.Использование = Истина;
	КонецЕсли;
	
	Период = Новый СтандартныйПериод;
	Период.ДатаНачала    = НачалоДня(ДатаНачала);
	Период.ДатаОкончания = КонецДня(ДатаОкончания);
	УстановитьПараметр(Компоновщик.Настройки, "ПериодОтчета", Период);
	
	
	// Компоновка макета компоновки данных.
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновкиДанных = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, Компоновщик.Настройки,,,Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"));
	
	// Построение таблицы значений.
	Процессор = Новый ПроцессорКомпоновкиДанных;
	Процессор.Инициализировать(МакетКомпоновкиДанных);
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений();
	ИсходныеДанные = Новый ТаблицаЗначений;
	ПроцессорВывода.УстановитьОбъект(ИсходныеДанные);
	ПроцессорВывода.Вывести(Процессор);
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	Таблица.ПериодСекунда КАК ПериодСекунда,
	|	ЕстьNULL(Таблица.Регистратор, НЕОПРЕДЕЛЕНО) КАК Регистратор,
	|	Таблица.СуммаНачальныйОстаток КАК НачОст,
	|	Таблица.СуммаПриход КАК Приход,
	|	Таблица.СуммаРасход КАК Расход,
	|	Таблица.СуммаКонечныйОстаток КАК КонОст
	|ПОМЕСТИТЬ ТаблицаИсходныеДанные
	|ИЗ
	|	&ИсходныеДанные КАК Таблица
	|ГДЕ НЕ ЕстьNULL(Таблица.Регистратор, НЕОПРЕДЕЛЕНО) = Неопределено
	|	И Таблица.ПериодСекунда >= &НачалоПериода
	|	И Таблица.ПериодСекунда <= &ОкончаниеПериода
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Таблица.ПериодСекунда КАК ПериодСекунда,
	|	Таблица.Регистратор КАК Регистратор,
	|	Таблица.Регистратор.Дата КАК РегистраторДата,
	|	Таблица.Регистратор.Номер КАК РегистраторНомер,
	|	Таблица.Регистратор.СуммаДокумента КАК РегистраторСумма,
	|	Таблица.НачОст КАК НачОст,
	|	Таблица.Приход КАК Приход,
	|	Таблица.Расход КАК Расход,
	|	Таблица.КонОст КАК КонОст
	|ПОМЕСТИТЬ ТаблицаДокументов
	|ИЗ
	|	ТаблицаИсходныеДанные КАК Таблица
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВЫБОР
	|		КОГДА ТаблицаДокументов.Регистратор ССЫЛКА Документ.ЧекККМ
	|				ИЛИ ТаблицаДокументов.Регистратор ССЫЛКА Документ.ЧекККМВозврат
	|			ТОГДА ТаблицаДокументов.Регистратор.КассоваяСмена
	|		ИНАЧЕ ТаблицаДокументов.Регистратор
	|	КОНЕЦ КАК Документ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокументов.Регистратор ССЫЛКА Документ.ЧекККМ
	|				ИЛИ ТаблицаДокументов.Регистратор ССЫЛКА Документ.ЧекККМВозврат
	|			ТОГДА ТаблицаДокументов.Регистратор.КассоваяСмена.Дата
	|		ИНАЧЕ 
	|			ВЫБОР 
	|				КОГДА ТаблицаДокументов.РегистраторДата <> ДАТАВРЕМЯ(1,1,1,0,0,0) 
	|					ТОГДА ТаблицаДокументов.РегистраторДата
	|				ИНАЧЕ ТаблицаДокументов.ПериодСекунда
	|			КОНЕЦ
	|	КОНЕЦ КАК ДатаДок,
	|	ВЫБОР
	|		КОГДА ТаблицаДокументов.Регистратор ССЫЛКА Документ.ЧекККМ
	|				ИЛИ ТаблицаДокументов.Регистратор ССЫЛКА Документ.ЧекККМВозврат
	|			ТОГДА ТаблицаДокументов.Регистратор.КассоваяСмена.Номер
	|		ИНАЧЕ ТаблицаДокументов.РегистраторНомер
	|	КОНЕЦ КАК НомерДок,
	|	ВЫБОР
	|		КОГДА ТаблицаДокументов.Регистратор ССЫЛКА Документ.ЧекККМ
	|				ИЛИ ТаблицаДокументов.Регистратор ССЫЛКА Документ.ЧекККМВозврат
	|			ТОГДА ТаблицаДокументов.Регистратор.КассоваяСмена.СуммаДокумента
	|		ИНАЧЕ ISNULL(ТаблицаДокументов.РегистраторСумма, 0)
	|	КОНЕЦ КАК РегистраторСумма,
	|	СУММА(ТаблицаДокументов.Приход) КАК Приход,
	|	СУММА(ТаблицаДокументов.Расход) КАК Расход
	|ИЗ
	|	ТаблицаДокументов КАК ТаблицаДокументов
	|
	|СГРУППИРОВАТЬ ПО
	|	ВЫБОР
	|		КОГДА ТаблицаДокументов.Регистратор ССЫЛКА Документ.ЧекККМ
	|				ИЛИ ТаблицаДокументов.Регистратор ССЫЛКА Документ.ЧекККМВозврат
	|			ТОГДА ТаблицаДокументов.Регистратор.КассоваяСмена
	|		ИНАЧЕ ТаблицаДокументов.Регистратор
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокументов.Регистратор ССЫЛКА Документ.ЧекККМ
	|				ИЛИ ТаблицаДокументов.Регистратор ССЫЛКА Документ.ЧекККМВозврат
	|			ТОГДА ТаблицаДокументов.Регистратор.КассоваяСмена.Дата
	|		ИНАЧЕ 
	|			ВЫБОР 
	|				КОГДА ТаблицаДокументов.РегистраторДата <> ДАТАВРЕМЯ(1,1,1,0,0,0) 
	|					ТОГДА ТаблицаДокументов.РегистраторДата
	|				ИНАЧЕ ТаблицаДокументов.ПериодСекунда
	|			КОНЕЦ
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокументов.Регистратор ССЫЛКА Документ.ЧекККМ
	|				ИЛИ ТаблицаДокументов.Регистратор ССЫЛКА Документ.ЧекККМВозврат
	|			ТОГДА ТаблицаДокументов.Регистратор.КассоваяСмена.Номер
	|		ИНАЧЕ ТаблицаДокументов.РегистраторНомер
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокументов.Регистратор ССЫЛКА Документ.ЧекККМ
	|				ИЛИ ТаблицаДокументов.Регистратор ССЫЛКА Документ.ЧекККМВозврат
	|			ТОГДА ТаблицаДокументов.Регистратор.КассоваяСмена.СуммаДокумента
	|		ИНАЧЕ ISNULL(ТаблицаДокументов.РегистраторСумма, 0)
	|	КОНЕЦ
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаДок,
	|	Документ
	|");
	
	Запрос.УстановитьПараметр("ИсходныеДанные", 	ИсходныеДанные);
	
	Запрос.УстановитьПараметр("НачалоПериода", 		НачалоДня(ДатаНачала));
	Запрос.УстановитьПараметр("ОкончаниеПериода",	КонецДня(ДатаОкончания));
	
	РезультатЗапроса = Запрос.Выполнить().Выгрузить();
	
	Если ИсходныеДанные.Количество() = 0 Тогда
		
		НачОст = 0;
		КонОст = 0;
		
	Иначе
		
		Если ИсходныеДанные[0].СуммаНачальныйОстаток = NULL Тогда
			НачОст = 0;
		Иначе
			
			Если ИсходныеДанные.Количество() > 1 Тогда
				
				Если ИсходныеДанные[0].Регистратор = Неопределено И ИсходныеДанные[1].Регистратор <> Неопределено Тогда
					НачОст = ИсходныеДанные[1].СуммаНачальныйОстаток;
				Иначе
					НачОст = ИсходныеДанные[0].СуммаНачальныйОстаток;
				КонецЕсли;
				
			Иначе
				НачОст = ИсходныеДанные[0].СуммаНачальныйОстаток;
			КонецЕсли;
			
		КонецЕсли;
		
		Если ИсходныеДанные[ИсходныеДанные.Количество()-1].СуммаКонечныйОстаток = NULL Тогда
			КонОст = 0;
		Иначе
			КонОст = ИсходныеДанные[ИсходныеДанные.Количество()-1].СуммаКонечныйОстаток;
		КонецЕсли;
		
	КонецЕсли;
	
	ОбластьМакета = Макет.ПолучитьОбласть("ОстатокНачала");
	ОбластьМакета.Параметры.ДатаНачала = НСтр("ru='Остаток на ';uk='Залишок на '", КодЯзыкаПечать) + Формат(ДатаНачала, "ДЛФ=Д");
	ОбластьМакета.Параметры.НачСтоимостьВсего = ФорматСумм(НачОст);
	ТабДокумент.Вывести(ОбластьМакета);
	
	ОбластьМакета = Макет.ПолучитьОбласть("Приход");
	ТабДокумент.Вывести(ОбластьМакета);
	
	ОбластьМакета = Макет.ПолучитьОбласть("Строка");
	Для Каждого СтрокаТЗ Из РезультатЗапроса Цикл
		
		Если СтрокаТЗ.Приход = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		ОбластьМакета.Параметры.Докум = СтрокаТЗ.Документ;
		ОбластьМакета.Параметры.ДатаДокумента = СтрокаТЗ.ДатаДок;
		ОбластьМакета.Параметры.НомерДокумента = Формат(СтрокаТЗ.НомерДок,"ЧЦ=11; ЧВН=; ЧГ=0");
		ОбластьМакета.Параметры.СуммаТовара = ФорматСумм(СтрокаТЗ.Приход);
		ОбластьМакета.Параметры.СуммаТары = ФорматСумм(0);
		Если ВыводитьСуммуДокументаИДельту Тогда
			ОбластьМакета.Параметры.РегистраторСумма = ФорматСумм(СтрокаТЗ.РегистраторСумма);
			ОбластьМакета.Параметры.ДельтаСуммы = ФорматСумм(СтрокаТЗ.Приход - СтрокаТЗ.РегистраторСумма);
		Иначе
			ОбластьМакета.Параметры.РегистраторСумма = "";
			ОбластьМакета.Параметры.ДельтаСуммы = "";
		КонецЕсли;
		Если ТипЗнч(СтрокаТЗ.Документ) = Тип("Строка") Тогда
			ОбластьМакета.Параметры.Расшифровка = Новый Структура("Документ, Дата, Сумма", СтрокаТЗ.Документ, СтрокаТЗ.ДатаДок, СтрокаТЗ.Приход);
		Иначе
			ОбластьМакета.Параметры.Расшифровка = СтрокаТЗ.Документ;
		КонецЕсли;
		ТабДокумент.Вывести(ОбластьМакета);
		
	КонецЦикла;
	
	Приход = РезультатЗапроса.Итог("Приход");
	
	ОбластьМакета = Макет.ПолучитьОбласть("ИтогоПриход");
	ОбластьМакета.Параметры.ПрихСтоимостьВсего = ФорматСумм(Приход);
	ТабДокумент.Вывести(ОбластьМакета);
	
	ОбластьМакета = Макет.ПолучитьОбласть("ВсегоПриход");
	ОбластьМакета.Параметры.ПриходСОстатком = ФорматСумм(Приход + НачОст);
	ТабДокумент.Вывести(ОбластьМакета);
	
	ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
	
	ОбластьМакета = Макет.ПолучитьОбласть("Расход");
	ТабДокумент.Вывести(ОбластьМакета);
	
	ОбластьМакета = Макет.ПолучитьОбласть("Строка");
	Для Каждого СтрокаТЗ Из РезультатЗапроса Цикл
		
		Если СтрокаТЗ.Расход = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		ОбластьМакета.Параметры.Докум = СтрокаТЗ.Документ;
		ОбластьМакета.Параметры.ДатаДокумента = СтрокаТЗ.ДатаДок;
		ОбластьМакета.Параметры.НомерДокумента = Формат(СтрокаТЗ.НомерДок,"ЧЦ=11; ЧВН=; ЧГ=0");
		ОбластьМакета.Параметры.СуммаТовара = ФорматСумм(СтрокаТЗ.Расход);
		ОбластьМакета.Параметры.СуммаТары = ФорматСумм(0);
		Если ТипЗнч(СтрокаТЗ.Документ) = Тип("Строка") Тогда
			ОбластьМакета.Параметры.Расшифровка = Новый Структура("Документ, Дата, Сумма", СтрокаТЗ.Документ, СтрокаТЗ.ДатаДок, СтрокаТЗ.Приход);
		Иначе
			ОбластьМакета.Параметры.Расшифровка = СтрокаТЗ.Документ;
		КонецЕсли;
		Если ВыводитьСуммуДокументаИДельту Тогда
			ОбластьМакета.Параметры.РегистраторСумма = ФорматСумм(СтрокаТЗ.РегистраторСумма);
			ОбластьМакета.Параметры.ДельтаСуммы = ФорматСумм(СтрокаТЗ.Расход - СтрокаТЗ.РегистраторСумма);
		Иначе
			ОбластьМакета.Параметры.РегистраторСумма = "";
			ОбластьМакета.Параметры.ДельтаСуммы = "";
		КонецЕсли;
		ТабДокумент.Вывести(ОбластьМакета);
		
	КонецЦикла;
	
	Расход = РезультатЗапроса.Итог("Расход");
	
	ОбластьМакета = Макет.ПолучитьОбласть("ИтогоРасход");
	ОбластьМакета.Параметры.РасхСтоимостьВсего = ФорматСумм(Расход);
	ТабДокумент.Вывести(ОбластьМакета);
	
	ОбластьМакета = Макет.ПолучитьОбласть("ОстатокКонец");
	ОбластьМакета.Параметры.ДатаКонца = НСтр("ru='Остаток на ';uk='Залишок на '", КодЯзыкаПечать) + Формат(ДатаОкончания, "ДЛФ=Д");
	ОбластьМакета.Параметры.КонСтоимостьВсего = ФорматСумм(КонОст);
	ТабДокумент.Вывести(ОбластьМакета);
	
	ОбластьМакета = Макет.ПолучитьОбласть("Подвал");
	//ОбластьМакета.Параметры.МОЛ = ?(ЗначениеЗаполнено(ВыбМОЛ), ВыбМОЛ, "");
	ТабДокумент.Вывести(ОбластьМакета);
	
	ТабДокумент.ПовторятьПриПечатиСтроки = ПовторятьПриПечатиСтроки;
	
	Возврат ТабДокумент;
	
КонецФункции // СформироватьОтчетТоварныйОтчет()

#КонецОбласти

ЭтоОтчетУНФ = Истина;

#КонецЕсли
