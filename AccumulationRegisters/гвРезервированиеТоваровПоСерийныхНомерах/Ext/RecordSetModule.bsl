#Область ОбработчикиСобытий

// Процедура - обработчик события ПередЗаписью набора записей.
//
Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка
		ИЛИ НЕ ДополнительныеСвойства.Свойство("ДляПроведения")
		ИЛИ НЕ ДополнительныеСвойства.ДляПроведения.Свойство("СтруктураВременныеТаблицы") Тогда
		Возврат;
	КонецЕсли;
	
	//СформироватьТаблицуИсходныхДвижений(Отказ, Замещение);
		
КонецПроцедуры // ПередЗаписью()

// Процедура - обработчик события ПриЗаписи набора записей.
//
Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка
		ИЛИ НЕ ДополнительныеСвойства.Свойство("ДляПроведения")
		ИЛИ НЕ ДополнительныеСвойства.ДляПроведения.Свойство("СтруктураВременныеТаблицы") Тогда
		Возврат;
	КонецЕсли;
	
	//СформироватьТаблицуИзмененийДвижений(Отказ, Замещение);
		
КонецПроцедуры // ПриЗаписи()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Процедура формирует таблицу исходных движений по регистру.
//
Процедура СформироватьТаблицуИсходныхДвижений(Отказ, Замещение)
	
	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	
	// Установка исключительной блокировки текущего набора записей регистратора.
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.гвРезервированиеТоваровПоСерийныхНомерах.НаборЗаписей");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Регистратор", Отбор.Регистратор.Значение);
	Блокировка.Заблокировать();
	
	Если НЕ СтруктураВременныеТаблицы.Свойство("ДвиженияРезервированиеСерийныхНомеровИзменение") ИЛИ
		СтруктураВременныеТаблицы.Свойство("ДвиженияРезервированиеСерийныхНомеровИзменение") И НЕ СтруктураВременныеТаблицы.ДвиженияРезервированиеСерийныхНомеровИзменение Тогда
		
		// Если временная таблица "ДвиженияРезервированиеСерийныхНомеровИзменение" не существует или не содержит записей
		// об изменении набора, значит набор записывается первый раз или для набора был выполнен контроль остатков.
		// Текущее состояние набора помещается во временную таблицу "ДвиженияРезервированиеСерийныхНомеровПередЗаписью",
		// чтобы при записи получить изменение нового набора относительно текущего.
		
		Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	РезервированиеТоваровПоСерийныхНомерах.НомерСтроки КАК НомерСтроки,
		|	РезервированиеТоваровПоСерийныхНомерах.Организация КАК Организация,
		|	РезервированиеТоваровПоСерийныхНомерах.ЗаказПокупателя КАК ЗаказПокупателя,
		|	РезервированиеТоваровПоСерийныхНомерах.Номенклатура КАК Номенклатура,
		|	РезервированиеТоваровПоСерийныхНомерах.Характеристика КАК Характеристика,
		|	РезервированиеТоваровПоСерийныхНомерах.Партия КАК Партия,
		|	РезервированиеТоваровПоСерийныхНомерах.СерийныйНомер КАК СерийныйНомер,
		|	ВЫБОР
		|		КОГДА РезервированиеТоваровПоСерийныхНомерах.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|			ТОГДА РезервированиеТоваровПоСерийныхНомерах.Количество
		|		ИНАЧЕ -РезервированиеТоваровПоСерийныхНомерах.Количество
		|	КОНЕЦ КАК КоличествоПередЗаписью
		|ПОМЕСТИТЬ ДвиженияРезервированиеСерийныхНомеровПередЗаписью
		|ИЗ
		|	РегистрНакопления.гвРезервированиеТоваровПоСерийныхНомерах КАК РезервированиеТоваровПоСерийныхНомерах
		|ГДЕ
		|	РезервированиеТоваровПоСерийныхНомерах.Регистратор = &Регистратор
		|	И &Замещение");
		
		Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
		Запрос.УстановитьПараметр("Замещение", Замещение);
		
		Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
		Запрос.Выполнить();
		
	Иначе
		
		// Если временная таблица "ДвиженияРезервированиеСерийныхНомеровИзменение" существует и содержит записи
		// об изменении набора, значит набор записывается не первый раз и для набора не был выполнен контроль остатков.
		// Текущее состояние набора и текущее состояние изменений помещаются во временную таблцу "ДвиженияРезервированиеСерийныхНомеровПередЗаписью",
		// чтобы при записи получить изменение нового набора относительно первоначального.
		
		Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ДвиженияРезервированиеСерийныхНомеровИзменение.НомерСтроки КАК НомерСтроки,
		|	ДвиженияРезервированиеСерийныхНомеровИзменение.Организация КАК Организация,
		|	ДвиженияРезервированиеСерийныхНомеровИзменение.ЗаказПокупателя КАК ЗаказПокупателя,
		|	ДвиженияРезервированиеСерийныхНомеровИзменение.Номенклатура КАК Номенклатура,
		|	ДвиженияРезервированиеСерийныхНомеровИзменение.Характеристика КАК Характеристика,
		|	ДвиженияРезервированиеСерийныхНомеровИзменение.Партия КАК Партия,
		|	ДвиженияРезервированиеСерийныхНомеровИзменение.СерийныйНомер КАК СерийныйНомер,
		|	ДвиженияРезервированиеСерийныхНомеровИзменение.КоличествоПередЗаписью КАК КоличествоПередЗаписью
		|ПОМЕСТИТЬ ДвиженияРезервированиеСерийныхНомеровПередЗаписью
		|ИЗ
		|	ДвиженияРезервированиеСерийныхНомеровИзменение КАК ДвиженияРезервированиеСерийныхНомеровИзменение
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	РезервированиеТоваровПоСерийныхНомерах.НомерСтроки,
		|	РезервированиеТоваровПоСерийныхНомерах.Организация,
		|	РезервированиеТоваровПоСерийныхНомерах.ЗаказПокупателя,
		|	РезервированиеТоваровПоСерийныхНомерах.Номенклатура,
		|	РезервированиеТоваровПоСерийныхНомерах.Характеристика,
		|	РезервированиеТоваровПоСерийныхНомерах.Партия,
		|	РезервированиеТоваровПоСерийныхНомерах.СерийныйНомер,
		|	ВЫБОР
		|		КОГДА РезервированиеТоваровПоСерийныхНомерах.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|			ТОГДА РезервированиеТоваровПоСерийныхНомерах.Количество
		|		ИНАЧЕ -РезервированиеТоваровПоСерийныхНомерах.Количество
		|	КОНЕЦ
		|ИЗ
		|	РегистрНакопления.гвРезервированиеТоваровПоСерийныхНомерах КАК РезервированиеТоваровПоСерийныхНомерах
		|ГДЕ
		|	РезервированиеТоваровПоСерийныхНомерах.Регистратор = &Регистратор
		|	И &Замещение");
		
		Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
		Запрос.УстановитьПараметр("Замещение", Замещение);
		
		Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
		Запрос.Выполнить();
		
	КонецЕсли;
	
	// Временная таблица "ДвиженияРезервированиеСерийныхНомеровИзменение" уничтожается
	// Удаляется информация о ее существовании.
	
	Если СтруктураВременныеТаблицы.Свойство("ДвиженияРезервированиеСерийныхНомеровИзменение") Тогда
		
		Запрос = Новый Запрос("УНИЧТОЖИТЬ ДвиженияРезервированиеСерийныхНомеровИзменение");
		Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
		Запрос.Выполнить();
		СтруктураВременныеТаблицы.Удалить("ДвиженияРезервированиеСерийныхНомеровИзменение");
		
	КонецЕсли;
	
КонецПроцедуры // СформироватьТаблицуИсходныхДвижений()

// Процедура формирует таблицу изменений движений по регистру.
//
Процедура СформироватьТаблицуИзмененийДвижений(Отказ, Замещение)
	
	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	
	// Рассчитывается изменение нового набора относительно текущего с учетом накопленных изменений
	// и помещается во временную таблицу "ДвиженияРезервированиеСерийныхНомеровИзменение".
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	МИНИМУМ(ДвиженияРезервированиеСерийныхНомеровИзменение.НомерСтроки) КАК НомерСтроки,
	|	ДвиженияРезервированиеСерийныхНомеровИзменение.Организация КАК Организация,
	|	ДвиженияРезервированиеСерийныхНомеровИзменение.ЗаказПокупателя КАК ЗаказПокупателя,
	|	ДвиженияРезервированиеСерийныхНомеровИзменение.Номенклатура КАК Номенклатура,
	|	ДвиженияРезервированиеСерийныхНомеровИзменение.Характеристика КАК Характеристика,
	|	ДвиженияРезервированиеСерийныхНомеровИзменение.Партия КАК Партия,
	|	ДвиженияРезервированиеСерийныхНомеровИзменение.СерийныйНомер КАК СерийныйНомер,
	|	СУММА(ДвиженияРезервированиеСерийныхНомеровИзменение.КоличествоПередЗаписью) КАК КоличествоПередЗаписью,
	|	СУММА(ДвиженияРезервированиеСерийныхНомеровИзменение.КоличествоИзменение) КАК КоличествоИзменение,
	|	СУММА(ДвиженияРезервированиеСерийныхНомеровИзменение.КоличествоПриЗаписи) КАК КоличествоПриЗаписи
	|ПОМЕСТИТЬ ДвиженияРезервированиеСерийныхНомеровИзменение
	|ИЗ
	|	(ВЫБРАТЬ
	|		ДвиженияРезервированиеСерийныхНомеровПередЗаписью.НомерСтроки КАК НомерСтроки,
	|		ДвиженияРезервированиеСерийныхНомеровПередЗаписью.Организация КАК Организация,
	|		ДвиженияРезервированиеСерийныхНомеровПередЗаписью.ЗаказПокупателя КАК ЗаказПокупателя,
	|		ДвиженияРезервированиеСерийныхНомеровПередЗаписью.Номенклатура КАК Номенклатура,
	|		ДвиженияРезервированиеСерийныхНомеровПередЗаписью.Характеристика КАК Характеристика,
	|		ДвиженияРезервированиеСерийныхНомеровПередЗаписью.Партия КАК Партия,
	|		ДвиженияРезервированиеСерийныхНомеровПередЗаписью.СерийныйНомер КАК СерийныйНомер,
	|		ДвиженияРезервированиеСерийныхНомеровПередЗаписью.КоличествоПередЗаписью КАК КоличествоПередЗаписью,
	|		ДвиженияРезервированиеСерийныхНомеровПередЗаписью.КоличествоПередЗаписью КАК КоличествоИзменение,
	|		0 КАК КоличествоПриЗаписи
	|	ИЗ
	|		ДвиженияРезервированиеСерийныхНомеровПередЗаписью КАК ДвиженияРезервированиеСерийныхНомеровПередЗаписью
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ДвиженияРезервированиеСерийныхНомеровПриЗаписи.НомерСтроки,
	|		ДвиженияРезервированиеСерийныхНомеровПриЗаписи.Организация,
	|		ДвиженияРезервированиеСерийныхНомеровПриЗаписи.ЗаказПокупателя,
	|		ДвиженияРезервированиеСерийныхНомеровПриЗаписи.Номенклатура,
	|		ДвиженияРезервированиеСерийныхНомеровПриЗаписи.Характеристика,
	|		ДвиженияРезервированиеСерийныхНомеровПриЗаписи.Партия КАК Партия,
	|		ДвиженияРезервированиеСерийныхНомеровПриЗаписи.СерийныйНомер КАК СерийныйНомер,
	|		0,
	|		ВЫБОР
	|			КОГДА ДвиженияРезервированиеСерийныхНомеровПриЗаписи.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -ДвиженияРезервированиеСерийныхНомеровПриЗаписи.Количество
	|			ИНАЧЕ ДвиженияРезервированиеСерийныхНомеровПриЗаписи.Количество
	|		КОНЕЦ,
	|		ДвиженияРезервированиеСерийныхНомеровПриЗаписи.Количество
	|	ИЗ
	|		РегистрНакопления.гвРезервированиеТоваровПоСерийныхНомерах КАК ДвиженияРезервированиеСерийныхНомеровПриЗаписи
	|	ГДЕ
	|		ДвиженияРезервированиеСерийныхНомеровПриЗаписи.Регистратор = &Регистратор) КАК ДвиженияРезервированиеСерийныхНомеровИзменение
	|
	|СГРУППИРОВАТЬ ПО
	|	ДвиженияРезервированиеСерийныхНомеровИзменение.Организация,
	|	ДвиженияРезервированиеСерийныхНомеровИзменение.ЗаказПокупателя,
	|	ДвиженияРезервированиеСерийныхНомеровИзменение.Номенклатура,
	|	ДвиженияРезервированиеСерийныхНомеровИзменение.Характеристика,
	|	ДвиженияРезервированиеСерийныхНомеровИзменение.Партия,
	|	ДвиженияРезервированиеСерийныхНомеровИзменение.СерийныйНомер
	|
	|ИМЕЮЩИЕ
	|	СУММА(ДвиженияРезервированиеСерийныхНомеровИзменение.КоличествоИзменение) <> 0");
	
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаИзРезультатаЗапроса = РезультатЗапроса.Выбрать();
	ВыборкаИзРезультатаЗапроса.Следующий();
	
	// Новые изменения были помещены во временную таблицу "ДвиженияРезервированиеСерийныхНомеровИзменение".
	// Добавляется информация о ее существовании и наличии в ней записей об изменении.
	СтруктураВременныеТаблицы.Вставить("ДвиженияРезервированиеСерийныхНомеровИзменение", ВыборкаИзРезультатаЗапроса.Количество > 0);
	
	// Временная таблица "ДвиженияРезервированиеСерийныхНомеровПередЗаписью" уничтожается
	Запрос = Новый Запрос("УНИЧТОЖИТЬ ДвиженияРезервированиеСерийныхНомеровПередЗаписью");
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Выполнить();
	
КонецПроцедуры // СформироватьТаблицуИзмененийДвижений()

#КонецОбласти
