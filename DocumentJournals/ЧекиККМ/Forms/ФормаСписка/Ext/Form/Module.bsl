///////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ПОЛЕЙ ФОРМЫ

// Процедура устанавливает отбор динамических списков формы.
//
&НаСервере
Процедура УстановитьОтборДинамическихСписков()
	
	УправлениеНебольшойФирмойКлиентСервер.УстановитьЭлементОтбораСписка(ЧекиККМ, "КассаККМ", КассаККМ, ЗначениеЗаполнено(КассаККМ), ВидСравненияКомпоновкиДанных.Равно);
	УправлениеНебольшойФирмойКлиентСервер.УстановитьЭлементОтбораСписка(ЧекиККМ, "КассоваяСмена", ТекущаяКассоваяСмена, ТолькоТекущаяСмена, ВидСравненияКомпоновкиДанных.Равно);
	
КонецПроцедуры // УстановитьОтборДинамическихСписков()

// Процедура - обработчик события "ПриИзменении" поля "КассаККМ".
//
&НаСервере
Процедура КассаККМОтборПриИзмененииНаСервере()
	
	ОбновитьСостояниеКассовойСменыНаСервере(КассаККМ);
	УстановитьОтборДинамическихСписков();
	Элементы.ВнесениеДенег.Видимость = НЕ КассаККМ.ИспользоватьБезПодключенияОборудования;
	Элементы.ВыемкаДенег.Видимость = НЕ КассаККМ.ИспользоватьБезПодключенияОборудования;
	Элементы.ПробитьНулевойЧек.Видимость = НЕ КассаККМ.ИспользоватьБезПодключенияОборудования;
	
КонецПроцедуры // КассаОтборПриИзмененииНаСервере()

// Процедура - обработчик события "ПриИзменении" поля "КассаККМ" на сервере.
//
&НаКлиенте
Процедура КассаККМОтборПриИзменении(Элемент)
	
	КассаККМОтборПриИзмененииНаСервере();
	
КонецПроцедуры // КассаОтборПриИзменении()

// Функция выполняет открытие кассовой смены на сервере.
//
&НаСервереБезКонтекста
Функция ОткрытьКассовуюСменуНаСервере(КассаККМ, ОписаниеОшибки = "")
	
	Возврат Документы.ОтчетОРозничныхПродажах.ОткрытьКассовуюСмену(КассаККМ, ОписаниеОшибки);
	
КонецФункции // ОткрытьКассовуюСменуНаСервере()

// Процедура выполняет закрытие кассовой смены на сервере.
//
&НаСервере
Функция ЗакрытьКассовуюСменуНаСервере(КассаККМ, ОписаниеОшибки = "")
	
	Возврат Документы.ОтчетОРозничныхПродажах.ЗакрытьКассовуюСменуВыполнитьАрхивацию(КассаККМ, ОписаниеОшибки);
	
КонецФункции // ЗакрытьКассовуюСменуНаСервере()

// Процедуру необходимо вызывать с клиента при открытии кассовой смены
&НаСервере
Процедура ОбновитьСостояниеКассовойСменыИУстановитьОтборДинамическихСписков(КассаККМ)
	
	ОбновитьСостояниеКассовойСменыНаСервере(КассаККМ);
	
	УстановитьОтборДинамическихСписков();
	
КонецПроцедуры // ОбновитьСостояниеКассовойСменыНаСервере()

// Процедура - обработчик команды "ОткрытьКассовуюСмену".
//
&НаКлиенте
Процедура ОткрытьКассовуюСмену(Команда)
	
	ОчиститьСообщения();
	
	Если НЕ ЗначениеЗаполнено(КассаККМ) Тогда
		ТекстОшибки = НСтр("ru='Выберите кассу ККМ!';uk='Виберіть касу ККМ!'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, ,"КассаККМ");
		Возврат;
	КонецЕсли;
	
	ОписаниеОшибки = "";
	
	ПараметрыКассыККМ = УправлениеНебольшойФирмойПовтИсп.ПолучитьПараметрыКассыККМ(КассаККМ);
	ИдентификаторУстройства = ПараметрыКассыККМ.ИдентификаторУстройства;
	ИспользоватьБезПодключенияОборудования = ПараметрыКассыККМ.ИспользоватьБезПодключенияОборудования;
	
	Если ИспользоватьПодключаемоеОборудование И НЕ ИспользоватьБезПодключенияОборудования И ЗначениеЗаполнено(ИдентификаторУстройства) Тогда
		ЭтаФорма.Доступность = Ложь;
		Оповещение = Новый ОписаниеОповещения("ОткрытьКассовуюСменуЗавершение", ЭтотОбъект);
		МенеджерОборудованияКлиент.НачатьОткрытиеСменыНаФискальномУстройстве(Оповещение, УникальныйИдентификатор, ИдентификаторУстройства, Ложь);
	Иначе
		Результат = ОткрытьКассовуюСменуНаСервере(КассаККМ, ОписаниеОшибки);
		Если НЕ Результат Тогда
			ТекстСообщения = НСтр("ru='При открытии смены произошла ошибка."
"Смена не открыта."
"Дополнительное описание:"
"%ДополнительноеОписание%';uk='При відкритті зміни відбулася помилка."
"Зміна не відкрита."
"Додатковий опис:"
"%ДополнительноеОписание%'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ДополнительноеОписание%", ОписаниеОшибки);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		КонецЕсли;
		ОбновитьСостояниеКассовойСменыИУстановитьОтборДинамическихСписков(КассаККМ);
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьКассовуюСменуЗавершение(РезультатВыполнения, Параметры) Экспорт
	
	ЭтаФорма.Доступность = Истина;
	
	Если РезультатВыполнения.Результат Тогда 
		ОписаниеОшибки = "";
		Результат = ОткрытьКассовуюСменуНаСервере(КассаККМ, ОписаниеОшибки);
		Если НЕ Результат Тогда
			ТекстСообщения = НСтр("ru='При открытии смены произошла ошибка. Смена не открыта."
"Дополнительное описание: %ДополнительноеОписание%';uk='При відкритті зміни відбулася помилка. Зміна не відкрита."
"Додатковий опис: %ДополнительноеОписание%'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ДополнительноеОписание%", ОписаниеОшибки);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		КонецЕсли;
		ОбновитьСостояниеКассовойСменыИУстановитьОтборДинамическихСписков(КассаККМ);
	Иначе
		ТекстСообщения = НСтр("ru='При открытии смены произошла ошибка."
"Смена не открыта на фискальном регистраторе."
"%ДополнительноеОписание%';uk='При відкритті зміни відбулася помилка."
"Зміна не відкрита на фіскальному реєстраторові."
"%ДополнительноеОписание%'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ДополнительноеОписание%", РезультатВыполнения.ОписаниеОшибки);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
	
КонецПроцедуры

// Функция проверяет наличие пробитых кассовых чеков за смену.
//
&НаСервере
Функция ЕстьПробитыеЧекиЗаСмену(КассаККМ)
	
	СтруктураСостояниеКассовойСмены = Документы.ОтчетОРозничныхПродажах.ПолучитьСостояниеКассовойСмены(КассаККМ);
	
	Если СтруктураСостояниеКассовойСмены.СтатусКассовойСмены <> Перечисления.СтатусыКассовойСмены.Открыта Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЧекККМЗапасы.Ссылка КАК КоличествоЧеков
	|ИЗ
	|	(ВЫБРАТЬ
	|		ЧекККМЗапасы.Ссылка КАК Ссылка
	|	ИЗ
	|		Документ.ЧекККМ.Запасы КАК ЧекККМЗапасы
	|	ГДЕ
	|		ЧекККМЗапасы.Ссылка.КассоваяСмена = &КассоваяСмена
	|		И ЧекККМЗапасы.Ссылка.Проведен
	|		И ЧекККМЗапасы.Ссылка.НомерЧекаККМ > 0
	|		И (НЕ ЧекККМЗапасы.Ссылка.Архивный)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ЧекККМЗапасы.Ссылка
	|	ИЗ
	|		Документ.ЧекККМВозврат.Запасы КАК ЧекККМЗапасы
	|	ГДЕ
	|		ЧекККМЗапасы.Ссылка.КассоваяСмена = &КассоваяСмена
	|		И ЧекККМЗапасы.Ссылка.Проведен
	|		И ЧекККМЗапасы.Ссылка.НомерЧекаККМ > 0
	|		И (НЕ ЧекККМЗапасы.Ссылка.Архивный)) КАК ЧекККМЗапасы";
	
	Запрос.УстановитьПараметр("КассоваяСмена", СтруктураСостояниеКассовойСмены.КассоваяСмена);
	
	Результат = Запрос.Выполнить();
	
	Возврат НЕ Результат.Пустой();
	
КонецФункции // ЕстьПробитыеЧекиЗаСмену()


// Процедура - обработчик команды "ЗакрытьКассовуюСмену".
//
&НаКлиенте
Процедура ЗакрытьКассовуюСмену(Команда)
	
	ОчиститьСообщения();
	
	Если НЕ ЗначениеЗаполнено(КассаККМ) Тогда
		ТекстОшибки = НСтр("ru='Выберите кассу ККМ!';uk='Виберіть касу ККМ!'");
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			ТекстОшибки,
			,
			"КассаККМ"
		);
		
		Возврат;
	КонецЕсли;
	
	ПараметрыКассыККМ = УправлениеНебольшойФирмойПовтИсп.ПолучитьПараметрыКассыККМ(КассаККМ);
	ИдентификаторУстройства = ПараметрыКассыККМ.ИдентификаторУстройства;
	ИспользоватьБезПодключенияОборудования = ПараметрыКассыККМ.ИспользоватьБезПодключенияОборудования;
	
	Если ЕстьПробитыеЧекиЗаСмену(КассаККМ) Тогда
		Если ИспользоватьБезПодключенияОборудования Тогда
			СформироватьОтчетыОРозничныхПродажах(ИспользоватьБезПодключенияОборудования);
		ИначеЕсли ИспользоватьПодключаемоеОборудование И ЗначениеЗаполнено(ИдентификаторУстройства) Тогда
			ЭтаФорма.Доступность = Ложь; // Блокируем интерфейс пользователя.
			ОтчетСГашением = Истина; // Тип операции (Истина - Отчет с гашением, Ложь - Отчет без гашения)
			ОтображатьСообщения = Ложь; // Не отображать сообщения об ошибке. Сообщения на экран выводиться в процедуре обработки оповещения.
			ОповещениеПриЗавершении = Новый ОписаниеОповещения("ПечатьФискальногоОтчетаЗавершение", ЭтотОбъект);
			МенеджерОборудованияКлиент.НачатьПечатьФискальногоОтчета(ОповещениеПриЗавершении, УникальныйИдентификатор, ИдентификаторУстройства, ОтчетСГашением, ОтображатьСообщения);
		КонецЕсли;
	Иначе
		СформироватьОтчетыОРозничныхПродажах(ИспользоватьБезПодключенияОборудования);
	КонецЕсли;
	
КонецПроцедуры

// Процедура - обработчик команды "ЗакрытьКассовуюСмену".
//
&НаКлиенте
Процедура СформироватьОтчетыОРозничныхПродажах(ИспользоватьБезПодключенияОборудования) Экспорт
	
	ОписаниеОшибки = "";
	
	МассивДокументов = ЗакрытьКассовуюСменуНаСервере(КассаККМ, ОписаниеОшибки);
	
	Если ЗначениеЗаполнено(ОписаниеОшибки)
	   И ИспользоватьБезПодключенияОборудования Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ОписаниеОшибки);
	ИначеЕсли ЗначениеЗаполнено(ОписаниеОшибки)
		 И НЕ ИспользоватьБезПодключенияОборудования Тогда
		ТекстСообщения = НСтр("ru='Смена закрыта на оборудовании, но при формировании отчета о розничных продажах возникли ошибки."
"Дополнительное описание:"
"%ДополнительноеОписание%';uk='Зміна закрита на обладнанні, але при формуванні звіту про роздрібні продажі виникли помилки."
"Додатковий опис:"
"%ДополнительноеОписание%'"
		);
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ДополнительноеОписание%", ОписаниеОшибки);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
	
	// Все результирующие документы выводим пользователю.
	Для каждого Документ Из МассивДокументов Цикл
		ОткрытьФорму("Документ.ОтчетОРозничныхПродажах.ФормаОбъекта", Новый Структура("Ключ", Документ));
	КонецЦикла;
	
	ОбновитьСостояниеКассовойСменыИУстановитьОтборДинамическихСписков(КассаККМ);
	Оповестить("ОбновитьФормыПослеСнятияZОтчета");
	
КонецПроцедуры

// Процедура - обработчик команды "ЗакрытьКассовуюСмену".
//
&НаКлиенте
Процедура ПечатьФискальногоОтчетаЗавершение(РезультатВыполнения, Параметры) Экспорт
	
	ЭтаФорма.Доступность = Истина; // Разблокировка интерфейса пользователя.
	
	Если РезультатВыполнения.Результат Тогда
		СформироватьОтчетыОРозничныхПродажах(Ложь);
	Иначе
		ТекстСообщения = НСтр("ru='При закрытии смены на фискальном регистраторе произошла ошибка."
"""%ОписаниеОшибки%"""
"Отчет на фискальном регистраторе не сформирован.';uk='При закритті зміни на фіскальному реєстраторові відбулася помилка."
"""% ОписаниеОшибки%"""
"Звіт на фіскальному реєстраторові не сформований.'"
		);
		ТекстСообщения = СтрЗаменить(
			ТекстСообщения,
			"%ОписаниеОшибки%",
			РезультатВыполнения.ОписаниеОшибки
		);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		ОбновитьСостояниеКассовойСменыИУстановитьОтборДинамическихСписков(КассаККМ);
		Оповестить("ОбновитьФормыПослеСнятияZОтчета");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПробитьНулевойЧек(Команда)
	
	ОчиститьСообщения();
	
	Если НЕ ЗначениеЗаполнено(КассаККМ) Тогда
		Возврат;
	КонецЕсли;
	
	Результат = Ложь;
	
	Если МенеджерОборудованияКлиент.ОбновитьРабочееМестоКлиента() Тогда
		
		// Подключение устройства
		ПараметрыКассыККМ = УправлениеНебольшойФирмойПовтИсп.ПолучитьПараметрыКассыККМ(КассаККМ);
		ИдентификаторУстройства                = ПараметрыКассыККМ.ИдентификаторУстройства;
		ИспользоватьБезПодключенияОборудования = ПараметрыКассыККМ.ИспользоватьБезПодключенияОборудования;
		
		Если ИдентификаторУстройства <> Неопределено ИЛИ ИспользоватьБезПодключенияОборудования Тогда
			
			ОписаниеОшибки = "";
			
			Если Не ИспользоватьБезПодключенияОборудования Тогда
				
				Результат = МенеджерОборудованияКлиент.ПодключитьОборудованиеПоИдентификатору(УникальныйИдентификатор,
				                                                                              ИдентификаторУстройства,
				                                                                              ОписаниеОшибки);
				
			КонецЕсли;
			
			Если Результат ИЛИ ИспользоватьБезПодключенияОборудования Тогда
				
				Если Не ИспользоватьБезПодключенияОборудования Тогда
					
					ВходныеПараметры  = Неопределено;
					ВыходныеПараметры = Неопределено;
					
					//Напечатать нулевой чек на фискальном регистраторе
					Результат = МенеджерОборудованияКлиент.ВыполнитьКоманду(ИдентификаторУстройства,
					                                                        "PrintNullReceipt",
					                                                        ВходныеПараметры,
					                                                        ВыходныеПараметры);
					
				КонецЕсли;
				
				Если Результат ИЛИ ИспользоватьБезПодключенияОборудования Тогда
					
				Иначе
					ТекстСообщения = НСтр("ru='При печати чека произошла ошибка."
"Чек не напечатан на фискальном регистраторе."
"Дополнительное описание:"
"%ДополнительноеОписание%';uk='При друку чека відбулася помилка."
"Чек не надрукований на фіскальному реєстраторові."
"Додатковий опис:"
"%ДополнительноеОписание%'");
					ТекстСообщения = СтрЗаменить(ТекстСообщения,
					                             "%ДополнительноеОписание%",
					                             ВыходныеПараметры[1]);
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
				КонецЕсли;
				
				Если Не ИспользоватьБезПодключенияОборудования Тогда
					
					МенеджерОборудованияКлиент.ОтключитьОборудованиеПоИдентификатору(УникальныйИдентификатор,
					                                                                 ИдентификаторУстройства);
					
				КонецЕсли;
				
			Иначе
				
				ТекстСообщения = НСтр("ru='При подключении устройства произошла ошибка."
"Чек не напечатан на фискальном регистраторе."
"Дополнительное описание:"
"%ДополнительноеОписание%';uk='При підключенні обладнання відбулася помилка."
"Чек не надрукований на фіскальному реєстраторові."
"Додатковий опис:"
"%ДополнительноеОписание%'");
				ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ДополнительноеОписание%", ОписаниеОшибки);
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
				
			КонецЕсли;
			
		КонецЕсли;
		
	Иначе
		
		ТекстСообщения = НСтр("ru='Предварительно необходимо выбрать рабочее место внешнего оборудования текущего сеанса.';uk='Попередньо необхідно вибрати робоче місце зовнішнього обладнання поточного сеансу.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		
	КонецЕсли;
	
КонецПроцедуры

// Процедура - обработчик команды "ВнесениеДенег".
//
&НаКлиенте
Процедура ВнесениеДенег(Команда)
	
	Если МенеджерОборудованияКлиент.ОбновитьРабочееМестоКлиента() Тогда
		
		ВносимаяСумма = 0;
		
		ЗаголовокОкна = НСтр("ru='Сумма внесения, %Валюта%';uk='Сума внесення, %Валюта%'");
		ЗаголовокОкна = СтрЗаменить(
			ЗаголовокОкна,
			"%Валюта%",
			СтруктураСостояниеКассовойСмены.ВалютаДокументаПредставление
		);
		
		ПоказатьВводЧисла(Новый ОписаниеОповещения("ВнесениеДенегЗавершение", ЭтотОбъект, Новый Структура("ВносимаяСумма", ВносимаяСумма)), ВносимаяСумма, ЗаголовокОкна, 15, 2);
		
	Иначе
		
		ТекстСообщения = НСтр("ru='Предварительно необходимо выбрать рабочее место внешнего оборудования текущего сеанса.';uk='Попередньо необхідно вибрати робоче місце зовнішнього обладнання поточного сеансу.'"
		);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		
	КонецЕсли;
	
КонецПроцедуры

// Процедура - обработчик команды "ВнесениеДенег".
//
&НаКлиенте
Процедура ВнесениеДенегЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	ВносимаяСумма = ?(Результат = Неопределено, ДополнительныеПараметры.ВносимаяСумма, Результат);
	
	Если (Результат <> Неопределено) Тогда
		ПараметрыКассыККМ = УправлениеНебольшойФирмойПовтИсп.ПолучитьПараметрыКассыККМ(КассаККМ);
		ИдентификаторУстройства = ПараметрыКассыККМ.ИдентификаторУстройства;
		ИспользоватьБезПодключенияОборудования = ПараметрыКассыККМ.ИспользоватьБезПодключенияОборудования;
		
		ЭтаФорма.Доступность = Ложь; // Блокируем интерфейс пользователя.
		ТипИнкассации = 1;   // Тип операции (0 - Выемка / 1 - Внесение)
		ОтображатьСообщения = Ложь; // Не отображать сообщения об ошибке. Сообщения на экран выводиться в процедуре обработки оповещения.
		ОповещениеПриЗавершении = Новый ОписаниеОповещения("ИнкассацияНаФискальномУстройствеЗавершение", ЭтотОбъект);
		МенеджерОборудованияКлиент.НачатьИнкассациюНаФискальномУстройстве(ОповещениеПриЗавершении, УникальныйИдентификатор, ИдентификаторУстройства, ТипИнкассации, ВносимаяСумма, ОтображатьСообщения);
	КонецЕсли;

КонецПроцедуры // ВнесениеДенег()

&НаКлиенте
Процедура ИнкассацияНаФискальномУстройствеЗавершение(РезультатВыполнения, Параметры) Экспорт
	
	ЭтаФорма.Доступность = Истина; // Разблокировка интерфейса пользователя.
	
	Если НЕ РезультатВыполнения.Результат Тогда
		ТекстСообщения = НСтр("ru='При выполнении операции произошла ошибка:""%ОписаниеОшибки%"".';uk='При виконанні операції відбулася помилка:""%ОписаниеОшибки%"".'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ОписаниеОшибки%", РезультатВыполнения.ОписаниеОшибки);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
	
КонецПроцедуры

// Процедура - обработчик команды "ИзъятиеДенег".
//
&НаКлиенте
Процедура ВыемкаДенег(Команда)
	
	Если МенеджерОборудованияКлиент.ОбновитьРабочееМестоКлиента() Тогда
		
		ИзымаемаяСумма = 0;
		
		ЗаголовокОкна = НСтр("ru='Сумма выемки, %Валюта%';uk='Сума вилучення, %Валюта%'");
		ЗаголовокОкна = СтрЗаменить(
			ЗаголовокОкна,
			"%Валюта%",
			СтруктураСостояниеКассовойСмены.ВалютаДокументаПредставление
		);
		
		ПоказатьВводЧисла(Новый ОписаниеОповещения("ВыемкаДенегЗавершение", ЭтотОбъект, Новый Структура("ИзымаемаяСумма", ИзымаемаяСумма)), ИзымаемаяСумма, ЗаголовокОкна, 15, 2);
		
	Иначе
		
		ТекстСообщения = НСтр("ru='Предварительно необходимо выбрать рабочее место внешнего оборудования текущего сеанса.';uk='Попередньо необхідно вибрати робоче місце зовнішнього обладнання поточного сеансу.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		
	КонецЕсли;
	
КонецПроцедуры

// Процедура - обработчик команды "ИзъятиеДенег".
//
&НаКлиенте
Процедура ВыемкаДенегЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	ИзымаемаяСумма = ?(Результат = Неопределено, ДополнительныеПараметры.ИзымаемаяСумма, Результат);
	
	Если (Результат <> Неопределено) Тогда
		ПараметрыКассыККМ = УправлениеНебольшойФирмойПовтИсп.ПолучитьПараметрыКассыККМ(КассаККМ);
		ИдентификаторУстройства = ПараметрыКассыККМ.ИдентификаторУстройства;
		ИспользоватьБезПодключенияОборудования = ПараметрыКассыККМ.ИспользоватьБезПодключенияОборудования;
		ЭтаФорма.Доступность = Ложь; // Блокируем интерфейс пользователя.
		ТипИнкассации = 0;   // Тип операции (0 - Выемка / 1 - Внесение)
		ОтображатьСообщения = Ложь; // Не отображать сообщения об ошибке. Сообщения на экран выводиться в процедуре обработки оповещения.
		ОповещениеПриЗавершении = Новый ОписаниеОповещения("ИнкассацияНаФискальномУстройствеЗавершение", ЭтотОбъект);
		МенеджерОборудованияКлиент.НачатьИнкассациюНаФискальномУстройстве(ОповещениеПриЗавершении, УникальныйИдентификатор, ИдентификаторУстройства, ТипИнкассации, ИзымаемаяСумма, ОтображатьСообщения);
	КонецЕсли;

КонецПроцедуры // ИзъятиеДенег()

// Функция выполняет получение состояния кассовой смены на сервере.
//
&НаСервереБезКонтекста
Функция ПолучитьСостояниеКассовойСменыНаСервере(КассаККМ)
	
	Возврат Документы.ОтчетОРозничныхПродажах.ПолучитьСостояниеКассовойСмены(КассаККМ);
	
КонецФункции // ПолучитьСостояниеКассовойСменыНаСервере()

// Процедура выполняет обновление состояния кассовой смены на клиенте.
//
&НаСервере
Процедура ОбновитьСостояниеКассовойСменыНаСервере(КассаККМ)
	
	СтруктураСостояниеКассовойСмены = ПолучитьСостояниеКассовойСменыНаСервере(КассаККМ);
	Если ЗначениеЗаполнено(СтруктураСостояниеКассовойСмены.СтатусКассовойСмены) Тогда
		
		ТекстСообщения = НСтр("ru='Смена № %НомерСмены%, Статус: %СтатусСмены% %ВремяИзменения%, В кассе %НаличностьВКассе% %Валюта%';uk='Зміна № %НомерСмены%, Статус: %СтатусСмены% %ВремяИзменения%, В касі %НаличностьВКассе% %Валюта%'");
		
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%НомерСмены%", СокрЛП(СтруктураСостояниеКассовойСмены.НомерКассовойСмены));
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%СтатусСмены%", СтруктураСостояниеКассовойСмены.СтатусКассовойСмены);
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%НаличностьВКассе%", СтруктураСостояниеКассовойСмены.НаличностьВКассе);
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Валюта%", СтруктураСостояниеКассовойСмены.ВалютаДокументаПредставление);
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ВремяИзменения%", Формат(СтруктураСостояниеКассовойСмены.ДатаИзмененияСтатуса,"ДФ='dd.MM.yy ЧЧ:мм'"));
		
		СостояниеКассовойСмены = ТекстСообщения;
		
	Иначе
		
		СостояниеКассовойСмены = НСтр("ru='Смена не открыта.';uk='Зміна не відкрита.'");
		
	КонецЕсли;
	
	// Переменная формы
	СменаОткрыта = СтруктураСостояниеКассовойСмены.СменаОткрыта;
	ТекущаяКассоваяСмена = СтруктураСостояниеКассовойСмены.КассоваяСмена;
	
	// Управление доступностью.
	Элементы.СнятьZОтчет.Видимость		  = СменаОткрыта;
	Элементы.ПробитьНулевойЧек.Видимость    = СменаОткрыта;
	Элементы.ОткрытьКассовуюСмену.Видимость = НЕ СменаОткрыта И ЗначениеЗаполнено(КассаККМ);
	
	Элементы.ЧекиККМСоздатьЧек.Доступность					= СменаОткрыта;
	Элементы.ЧекиККМДокументЧекККМВозвратСоздатьНаОсновании.Доступность = СменаОткрыта;
	Элементы.ЧекиККМСкопировать.Доступность				= СменаОткрыта;
	Элементы.КонтекстноеМенюЧекиККМСкопировать.Доступность = СменаОткрыта;
	
	Элементы.ВнесениеДенег.Доступность = ЗначениеЗаполнено(КассаККМ);
	Элементы.ВыемкаДенег.Доступность   = ЗначениеЗаполнено(КассаККМ);
	Элементы.ПробитьНулевойЧек.Доступность   = ЗначениеЗаполнено(КассаККМ);
	
КонецПроцедуры // ОбновитьСостояниеКассовойСменыИУстановитьОтборДинамическихСписков()

// Процедура - обработчик команды "ОбновитьСостояниеКассовойСмены".
//
&НаКлиенте
Процедура ОбновитьСостояниеКассовойСмены(Команда)
	
	Если НЕ ЗначениеЗаполнено(КассаККМ) Тогда
		Возврат;
	КонецЕсли;
	
	ОбновитьСостояниеКассовойСменыИУстановитьОтборДинамическихСписков(КассаККМ);
	
КонецПроцедуры // ОбновитьСостояниеКассовойСмены()

// Процедура - обработчик команды "ОткрытьУправлениеФискальнымРегистратором".
//
&НаКлиенте
Процедура ОткрытьУправлениеФискальнымРегистратором(Команда)
	
	ОткрытьФорму("Справочник.ПодключаемоеОборудование.Форма.УправлениеФискальнымРегистратором");

КонецПроцедуры // ОткрытьУправлениеФискальнымРегистратором)()

// Процедура - обработчик команды "ОткрытьУправлениеЭквайринговымТерминалом".
//
&НаКлиенте
Процедура ОткрытьУправлениеЭквайринговымТерминалом(Команда)
	
	ОткрытьФорму("Справочник.ПодключаемоеОборудование.Форма.УправлениеЭквайринговымТерминалом");

КонецПроцедуры // ОткрытьУправлениеЭквайринговымТерминалом()

// Процедура - обработчик события "ОбработкаОповещения" формы.
//
&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия  = "ОбновитьФормыПослеСнятияZОтчета" Тогда
		Элементы.ЧекиККМ.Обновить();
		ОбновитьСостояниеКассовойСменыИУстановитьОтборДинамическихСписков(КассаККМ);
		ЧекиККМПриАктивизацииСтрокиНаКлиенте();
	ИначеЕсли ИмяСобытия = "ОбновитьФормуСпискаДокументовЧекККМ" Тогда
		Элементы.ЧекиККМ.Обновить();
		ОбновитьСостояниеКассовойСменыИУстановитьОтборДинамическихСписков(КассаККМ);
		ЧекиККМПриАктивизацииСтрокиНаКлиенте();
	ИначеЕсли ИмяСобытия = "ОбновитьФормыПослеЗакрытияКассовойСмены" Тогда
		Элементы.ЧекиККМ.Обновить();
		ОбновитьСостояниеКассовойСменыИУстановитьОтборДинамическихСписков(КассаККМ);
		ЧекиККМПриАктивизацииСтрокиНаКлиенте();
	КонецЕсли;
	
КонецПроцедуры // ОбработкаОповещения()

// Процедура предназначается для обработки события "ПриАктивизацииСтроки" списка ЧекиККМ
//
&НаКлиенте
Процедура ЧекиККМПриАктивизацииСтрокиНаКлиенте()
	
	Если СтруктураСостояниеКассовойСмены = Неопределено Тогда
		ОбновитьСостояниеКассовойСменыИУстановитьОтборДинамическихСписков(КассаККМ);
	КонецЕсли;
	
	ТекущиеДанные = Элементы.ЧекиККМ.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		
		Если НЕ ТекущиеДанные.Свойство("ГруппировкаСтроки")
			И ЗначениеЗаполнено(ТекущиеДанные.НомерЧекаККМ)
			И ЗначениеЗаполнено(СтруктураСостояниеКассовойСмены)
			И ТекущиеДанные.КассоваяСмена = СтруктураСостояниеКассовойСмены.КассоваяСмена
			И НЕ ТекущиеДанные.ЕстьЧекНаВозврат
			И СменаОткрыта
			И ТекущиеДанные.Тип <> Тип("ДокументСсылка.ЧекККМВозврат") Тогда
			
			Элементы.ЧекиККМДокументЧекККМВозвратСоздатьНаОсновании.Доступность = Истина;
			Элементы.КонтекстноеМенюЧекиККМДокументЧекККМВозвратСоздатьНаОсновании.Доступность = Истина;
			
		Иначе
			
			Элементы.ЧекиККМДокументЧекККМВозвратСоздатьНаОсновании.Доступность = Ложь;
			Элементы.КонтекстноеМенюЧекиККМДокументЧекККМВозвратСоздатьНаОсновании.Доступность = Ложь;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры // ЧекиККМПриАктивизацииСтрокиНаКлиенте()

// Процедура - обработчик события "ПриАктивизацииСтроки" списка ЧекиККМ.
//
&НаКлиенте
Процедура ЧекиККМПриАктивизацииСтроки(Элемент)
	
	ЧекиККМПриАктивизацииСтрокиНаКлиенте();
	
КонецПроцедуры // ЧекиККМПриАктивизацииСтроки()

// Процедура предназначается для обработки события "ПриИзмененииНаСервере" флага ТолькоТекущаяСменаОтбор на сервере
//
&НаСервере
Процедура ТолькоТекущаяСменаОтборПриИзмененииНаСервере()
	
	УстановитьОтборДинамическихСписков();
	
КонецПроцедуры // ТолькоТекущаяСменаОтборПриИзмененииНаСервере()

// Процедура - обработчик события "ПриИзменении" флага ТолькоТекущаяСменаОтбор.
//
&НаКлиенте
Процедура ТолькоТекущаяСменаОтборПриИзменении(Элемент)

	ТолькоТекущаяСменаОтборПриИзмененииНаСервере();

КонецПроцедуры // ТолькоТекущаяСменаОтбор()

// Процедура - обработчик команды "СоздатьЧек".
//
&НаКлиенте
Процедура СоздатьЧек(Команда)
	
	Если СменаОткрыта Тогда
		ПараметрыОткрытия = Новый Структура("Основание", Новый Структура("КассаККМ", КассаККМ));
		ОткрытьФорму("Документ.ЧекККМ.ФормаОбъекта", ПараметрыОткрытия);
	КонецЕсли;
	
КонецПроцедуры // СоздатьЧек()

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ИспользоватьПодключаемоеОборудование = ПолучитьФункциональнуюОпцию("ИспользоватьПодключаемоеОборудование");
	
	// КассаККМ по умолчанию
	// Определим, сохранялись ли настройки ранее.
	ПользовательСсылка = ПараметрыСеанса.АвторизованныйПользователь;
	Если Не ЗначениеЗаполнено(ПользовательСсылка) Тогда
		ПользовательСсылка = Справочники.Пользователи.ПустаяСсылка();
		ПользовательИнформационнойБазы = "";
	Иначе
		ПользовательИнформационнойБазы = Обработки.НастройкиПользователей.ИмяПользователяИБ(ПользовательСсылка);
	КонецЕсли;
	Отбор = Новый Структура("Пользователь, КлючОбъекта", ПользовательИнформационнойБазы, "ЖурналДокументов.ЧекиККМ.Форма.ФормаСписка/КлючТекущихНастроекДанных");
	
	ВыборкаНастроек = ХранилищеСистемныхНастроек.Выбрать(Отбор);
	
	Если НЕ ВыборкаНастроек.Следующий() Тогда
		// Если не сохранялись, то установим отбор по основной кассе. Иначе отработает обработчик "ПриЗагрузкеДанныхИзНастроекНаСервере".
		КассаККМ = Справочники.КассыККМ.ПолучитьКассуККМПоУмолчанию(Перечисления.ТипыКассККМ.ФискальныйРегистратор);
		
		Если Не КассаККМ.Пустая() Тогда
			ОбновитьСостояниеКассовойСменыНаСервере(КассаККМ);
			УстановитьОтборДинамическихСписков();

			Элементы.ВнесениеДенег.Видимость = НЕ КассаККМ.ИспользоватьБезПодключенияОборудования;
			Элементы.ВыемкаДенег.Видимость = НЕ КассаККМ.ИспользоватьБезПодключенияОборудования;
			Элементы.ПробитьНулевойЧек.Видимость = НЕ КассаККМ.ИспользоватьБезПодключенияОборудования;
		КонецЕсли;
	КонецЕсли;
	// Конец КассаККМ по умолчанию
	
	// Заказы покупателей в Рознице
	Элементы.ЕстьЗаказы.Видимость = ПолучитьФункциональнуюОпцию("ИспользоватьЗаказыВРозничнойТорговле");
	// Конец Заказы покупателей в Рознице
	
	// Установим формат для текущей даты: ДФ=Ч:мм
	УправлениеНебольшойФирмойСервер.УстановитьОформлениеКолонкиДата(ЧекиККМ);
	
КонецПроцедуры

// Процедура - обработчик события "ПриЗагрузкеДанныхИзНастроекНаСервере" формы.
//
&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	КассаККМ = Настройки.Получить("КассаККМ");
	ТолькоТекущаяСмена = Настройки.Получить("ТолькоТекущаяСмена");
	
	ОбновитьСостояниеКассовойСменыНаСервере(КассаККМ);
	УстановитьОтборДинамическихСписков();
	
	Элементы.ВнесениеДенег.Видимость = НЕ КассаККМ.ИспользоватьБезПодключенияОборудования;
	Элементы.ВыемкаДенег.Видимость = НЕ КассаККМ.ИспользоватьБезПодключенияОборудования;
	Элементы.ПробитьНулевойЧек.Видимость = НЕ КассаККМ.ИспользоватьБезПодключенияОборудования;
	
КонецПроцедуры // ПриЗагрузкеДанныхИзНастроекНаСервере()

#Область ЗамерыПроизводительности

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	ОценкаПроизводительностиКлиент.НачатьЗамерВремени(Истина, "СозданиеФормыЧекККМ");
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПередНачаломИзменения(Элемент, Отказ)
	
	ОценкаПроизводительностиКлиент.НачатьЗамерВремени(Истина, "ОткрытиеФормыЧекККМ");
	
КонецПроцедуры

#КонецОбласти
