
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ВариантОтображенияИтогов = Параметры.ВариантОтображенияИтогов;
	ВалютаВедомости = Параметры.ВалютаВедомости;
	ВалютаОстатков = Параметры.ВалютаОстатков;
	
	НастроитьЭлементыНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура НастроитьЭлементыНаСервере()
	
	Если ВариантОтображенияИтогов = Перечисления.ВариантыОтображенияИтоговБанкИКасса.ВедомостьЗаПериод Тогда
		Элементы.ВалютаВедомости.Видимость = Истина;
		Элементы.ВалютаОстатков.Видимость = Ложь;
		Элементы.Декорация1.Высота = 1;
		Элементы.Декорация1.Видимость = Ложь;
		Элементы.Группа2.Высота = 3;
	ИначеЕсли ВариантОтображенияИтогов = Перечисления.ВариантыОтображенияИтоговБанкИКасса.ОстаткиПересчитанныеВВалюту Тогда
		Элементы.ВалютаВедомости.Видимость = Ложь;
		Элементы.ВалютаОстатков.Видимость = Истина;
		Элементы.Декорация1.Высота = 3;
		Элементы.Декорация1.Видимость = Истина;
		Элементы.Группа2.Высота = 0;
	Иначе
		Элементы.ВалютаВедомости.Видимость = Ложь;
		Элементы.ВалютаОстатков.Видимость = Ложь;
	КонецЕсли;
	
	УстановитьПояснительнуюНадпись();
	
КонецПроцедуры

&НаКлиенте
Процедура ОК(Команда)
	
	Если ВалютаВедомости.Пустая() И ВариантОтображенияИтогов = ПредопределенноеЗначение("Перечисление.ВариантыОтображенияИтоговБанкИКасса.ВедомостьЗаПериод") Тогда
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Выберите валюту ведомости.";
		Сообщение.Поле = "ВалютаВедомости";
		Сообщение.Сообщить();
		
		Возврат;
		
	ИначеЕсли ВалютаОстатков.Пустая() И ВариантОтображенияИтогов = ПредопределенноеЗначение("Перечисление.ВариантыОтображенияИтоговБанкИКасса.ОстаткиПересчитанныеВВалюту") Тогда
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Выберите валюту остатков.";
		Сообщение.Поле = "ВалютаОстатков";
		Сообщение.Сообщить();
		
		Возврат;
		
	КонецЕсли;
	
	ПараметрЗакрытия = Новый Структура("ВариантОтображенияИтогов, ВалютаВедомости, ВалютаОстатков", ВариантОтображенияИтогов, ВалютаВедомости, ВалютаОстатков);
	Закрыть(ПараметрЗакрытия);
	
КонецПроцедуры

&НаКлиенте
Процедура ВариантОтображенияИтоговПриИзменении(Элемент)
	
	НастроитьЭлементыНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьПояснительнуюНадпись()

	Если ВариантОтображенияИтогов = Перечисления.ВариантыОтображенияИтоговБанкИКасса.ВедомостьЗаДень Тогда
		Элементы.ДекорацияПояснительнаяНадпись.Заголовок = НСтр("ru='Дата, р/счет и валюта определяются по текущему документу.';uk='Дата, р/рахунок і валюта визначаються за поточним документом.'");
	ИначеЕсли ВариантОтображенияИтогов = Перечисления.ВариантыОтображенияИтоговБанкИКасса.ВедомостьЗаПериод Тогда
		Элементы.ДекорацияПояснительнаяНадпись.Заголовок = НСтр("ru='Будет использоваться период журнала.';uk='Використовуватиметься період журналу.'");
	ИначеЕсли ВариантОтображенияИтогов = Перечисления.ВариантыОтображенияИтоговБанкИКасса.ОстаткиПересчитанныеВВалюту Тогда
		Элементы.ДекорацияПояснительнаяНадпись.Заголовок = НСтр("ru='Пересчет выполняется по текущему курсу.';uk='Перерахунок виконується за поточним курсом.'");
	Иначе
		Элементы.ДекорацияПояснительнаяНадпись.Заголовок = НСтр("ru='Итоги в панели отображаться не будут.';uk='Підсумки в панелі відображатися не будуть.'");
	КонецЕсли;

КонецПроцедуры
