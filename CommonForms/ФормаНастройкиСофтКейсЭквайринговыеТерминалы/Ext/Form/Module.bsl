
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;

	Параметры.Свойство("Идентификатор", Идентификатор);
	Параметры.Свойство("ДрайверОборудования", ДрайверОборудования);
	
	Заголовок = НСтр("ru='Оборудование:';uk='Устаткування:'") + Символы.НПП  + Строка(Идентификатор);

	ЦветТекста = ЦветаСтиля.ЦветТекстаФормы;
	ЦветОшибки = ЦветаСтиля.ЦветОтрицательногоЧисла;

	СписокШиринаСлипЧека = Элементы.ШиринаСлипЧека.СписокВыбора;
	СписокШиринаСлипЧека.Добавить(24,  НСтр("ru='24 сим.';uk='24 сим.'"));
	СписокШиринаСлипЧека.Добавить(32,  НСтр("ru='32 сим.';uk='32 сим.'"));
	СписокШиринаСлипЧека.Добавить(36,  НСтр("ru='36 сим.';uk='36 сим.'"));
	СписокШиринаСлипЧека.Добавить(40,  НСтр("ru='40 сим.';uk='40 сим.'")); 
	СписокШиринаСлипЧека.Добавить(48,  НСтр("ru='48 сим.';uk='48 сим.'"));

	времКодВалюты                  = Неопределено;
	времШиринаСлипЧека             = Неопределено;
	времКоличествоКопийСлипЧека    = Неопределено;
	времКодСимволаЧастичногоОтреза = Неопределено;
	времДанныеМакетаСлипЧека       = Неопределено;

	Параметры.ПараметрыОборудования.Свойство("КодВалюты"                 , времКодВалюты);
	Параметры.ПараметрыОборудования.Свойство("ШиринаСлипЧека"            , времШиринаСлипЧека);
	Параметры.ПараметрыОборудования.Свойство("КоличествоКопийСлипЧека"   , времКоличествоКопийСлипЧека);
	Параметры.ПараметрыОборудования.Свойство("КодСимволаЧастичногоОтреза", времКодСимволаЧастичногоОтреза);
	Параметры.ПараметрыОборудования.Свойство("ДанныеМакетаСлипЧека"      , времДанныеМакетаСлипЧека);

	КодВалюты                  = ?(времКодВалюты                  = Неопределено, "643", времКодВалюты);
	ШиринаСлипЧека             = ?(времШиринаСлипЧека             = Неопределено,    36, времШиринаСлипЧека);
	КоличествоКопийСлипЧека    = ?(времКоличествоКопийСлипЧека    = Неопределено,     2, времКоличествоКопийСлипЧека);
	КодСимволаЧастичногоОтреза = ?(времКодСимволаЧастичногоОтреза = Неопределено,    22, времКодСимволаЧастичногоОтреза);
	ДанныеМакетаСлипЧека       = времДанныеМакетаСлипЧека;
	
	ПрочитатьДанныеМакета();
	
КонецПроцедуры

// Процедура - обработчик события "Перед открытием" формы.
//
// Параметры:
//  Отказ                - <Булево>
//                       - Признак отказа от открытия формы. Если в теле
//                         процедуры-обработчика установить данному параметру
//                         значение Истина, открытие формы выполнено не будет.
//                         Значение по умолчанию: Ложь.
//
//  СтандартнаяОбработка - <Булево>
//                       - В данный параметр передается признак выполнения
//                         стандартной (системной) обработки события. Если в
//                         теле процедуры-обработчика установить данному
//                         параметру значение Ложь, стандартная обработка
//                         события производиться не будет. Отказ от стандартной
//                         обработки не отменяет открытие формы.
//                         Значение по умолчанию: Истина.
//
&НаКлиенте
Процедура ПриОткрытии(Отказ)

	ОбновитьИнформациюОДрайвере();

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

// Процедура представляет обработчик события "Нажатие" кнопки
// "ОК" командной панели "ОсновныеДействияФормы".
//
// Параметры:
//  Кнопка - <КнопкаКоманднойПанели>
//         - Кнопка, с которой связано данное событие (кнопка "ОК").
//
&НаКлиенте
Процедура ЗаписатьИЗакрытьВыполнить()

	ЗаписатьДанныеМакета();
	
	НовыеЗначениеПараметров = Новый Структура;
	НовыеЗначениеПараметров.Вставить("ШиринаСлипЧека"            , ШиринаСлипЧека);
	НовыеЗначениеПараметров.Вставить("КоличествоКопийСлипЧека"   , КоличествоКопийСлипЧека);
	НовыеЗначениеПараметров.Вставить("КодСимволаЧастичногоОтреза", КодСимволаЧастичногоОтреза);
	НовыеЗначениеПараметров.Вставить("ДанныеМакетаСлипЧека"      , ДанныеМакетаСлипЧека);
	
	Результат = Новый Структура;
	Результат.Вставить("Идентификатор", Идентификатор);
	Результат.Вставить("ПараметрыОборудования", НовыеЗначениеПараметров);
	
	Закрыть(Результат);

КонецПроцедуры

&НаКлиенте
Процедура ТестУстройства(Команда)
	
	ОчиститьСообщения();
	
	РезультатТеста = Неопределено;

	ВходныеПараметры  = Неопределено;
	ВыходныеПараметры = Неопределено;

	времПараметрыУстройства = Новый Структура();
	времПараметрыУстройства.Вставить("ШиринаСлипЧека"            , ШиринаСлипЧека);
	времПараметрыУстройства.Вставить("КоличествоКопийСлипЧека"   , КоличествоКопийСлипЧека);
	времПараметрыУстройства.Вставить("КодСимволаЧастичногоОтреза", КодСимволаЧастичногоОтреза);
	времПараметрыУстройства.Вставить("ДанныеМакетаСлипЧека"      , ДанныеМакетаСлипЧека);

	Результат = МенеджерОборудованияКлиент.ВыполнитьДополнительнуюКоманду("CheckHealth",
	                                                                      ВходныеПараметры,
	                                                                      ВыходныеПараметры,
	                                                                      Идентификатор,
	                                                                      времПараметрыУстройства);

	Если Результат Тогда
		ТекстСообщения = НСтр("ru='Тест успешно выполнен.';uk='Тест успішно виконаний.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
	Иначе
		ДополнительноеОписание = ?(ТипЗнч(ВыходныеПараметры) = Тип("Массив")
		                           И ВыходныеПараметры.Количество() >= 2,
		                           НСтр("ru='Дополнительное описание:';uk='Додатковий опис:'") + " " + ВыходныеПараметры[1],
		                           "");


		ТекстСообщения = НСтр("ru='Тест не пройден.%ПереводСтроки%%ДополнительноеОписание%';uk='Тест не пройдено.%ПереводСтроки%%ДополнительноеОписание%'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ПереводСтроки%", ?(ПустаяСтрока(ДополнительноеОписание),
		                                                                  "",
		                                                                  Символы.ПС));
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ДополнительноеОписание%", ?(ПустаяСтрока(ДополнительноеОписание),
		                                                                           "",
		                                                                           ДополнительноеОписание));
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура УстановитьДрайвер(Команда)
	
	ОчиститьСообщения();
	Текст = НСтр("ru='Установка драйвера производиться средствами дистрибутива поставщика.';uk='Установка драйвера проводиться засобами дистрибутива постачальника.'");
	ПоказатьПредупреждение(, Текст);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОбновитьИнформациюОДрайвере()

	ВходныеПараметры  = Неопределено;
	ВыходныеПараметры = Неопределено;

	времПараметрыУстройства = Новый Структура();
	времПараметрыУстройства.Вставить("ШиринаСлипЧека"            , ШиринаСлипЧека);
	времПараметрыУстройства.Вставить("КоличествоКопийСлипЧека"   , КоличествоКопийСлипЧека);
	времПараметрыУстройства.Вставить("КодСимволаЧастичногоОтреза", КодСимволаЧастичногоОтреза);
	времПараметрыУстройства.Вставить("ДанныеМакетаСлипЧека"      , ДанныеМакетаСлипЧека);

	Если МенеджерОборудованияКлиент.ВыполнитьДополнительнуюКоманду("ПолучитьВерсиюДрайвера",
	                                                               ВходныеПараметры,
	                                                               ВыходныеПараметры,
	                                                               Идентификатор,
	                                                               времПараметрыУстройства) Тогда
		Драйвер = ВыходныеПараметры[0];
		Версия  = ВыходныеПараметры[1];
	Иначе
		Драйвер = ВыходныеПараметры[2];
		Версия  = НСтр("ru='Не определена';uk='Не визначена'");
	КонецЕсли;

	Элементы.Драйвер.ЦветТекста = ?(Драйвер = НСтр("ru='Не установлен';uk='Не встановлений'"), ЦветОшибки, ЦветТекста);
	Элементы.Версия.ЦветТекста  = ?(Версия  = НСтр("ru='Не определена';uk='Не визначена'"), ЦветОшибки, ЦветТекста);

КонецПроцедуры

Процедура ПрочитатьДанныеМакета()

	Если ДанныеМакетаСлипЧека.Количество() = 0 Тогда
		ДанныеМакетаСлипЧека.Добавить("ОАО ""Альфа Банк"""	, "Банк");
		ДанныеМакетаСлипЧека.Добавить(""       				, "Организация");
		ДанныеМакетаСлипЧека.Добавить(""       				, "Город");
		ДанныеМакетаСлипЧека.Добавить(""       				, "Адрес");
		ДанныеМакетаСлипЧека.Добавить("СПАСИБО"				, "ТекстПодвала");
	КонецЕсли;

	Банк         = ДанныеМакетаСлипЧека[0].Значение;
	Организация  = ДанныеМакетаСлипЧека[1].Значение;
	Город        = ДанныеМакетаСлипЧека[2].Значение;
	Адрес        = ДанныеМакетаСлипЧека[3].Значение;
	ТекстПодвала = ДанныеМакетаСлипЧека[4].Значение;

КонецПроцедуры

Процедура ЗаписатьДанныеМакета()

	ДанныеМакетаСлипЧека[0].Значение = Банк;
	ДанныеМакетаСлипЧека[1].Значение = Организация;
	ДанныеМакетаСлипЧека[2].Значение = Город;
	ДанныеМакетаСлипЧека[3].Значение = Адрес;
	ДанныеМакетаСлипЧека[4].Значение = ТекстПодвала;

КонецПроцедуры

#КонецОбласти