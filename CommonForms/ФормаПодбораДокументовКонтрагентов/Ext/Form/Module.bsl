
&НаСервере
// Процедура - обработчик события ПриСозданииНаСервере.
//
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Компания = Параметры.Компания;
	Договор = Параметры.ДоговорКонтрагента;
	
	УстановитьПараметрыПодбора();
	
	АдресТаблицаДокументыВХранилище = Параметры.АдресТаблицаДокументыВХранилище;
	ОтобранныеДокументы.Загрузить(ПолучитьИзВременногоХранилища(АдресТаблицаДокументыВХранилище));
	
КонецПроцедуры // ПриСозданииНаСервере()

&НаСервере
// Процедура устанавливает параметры подбора.
//
Процедура УстановитьПараметрыПодбора()
	
	СписокДокументов.Параметры.УстановитьЗначениеПараметра("Организация", Компания);
	СписокДокументов.Параметры.УстановитьЗначениеПараметра("Договор", Договор);
	
КонецПроцедуры // УстановитьПараметрыПодбора()

&НаСервере
// Процедура помещает результаты подбора в хранилище.
//
Процедура ЗаписатьПодборВХранилище()
	
	ТаблицаОтобранныеДокументы = ОтобранныеДокументы.Выгрузить();
	ПоместитьВоВременноеХранилище(ТаблицаОтобранныеДокументы, АдресТаблицаДокументыВХранилище);
	
КонецПроцедуры // ЗаписатьПодборВХранилище()

&НаКлиенте
// Процедура помещает результаты выбора в подбор.
//
Процедура СписокДокументовВыборЗначения(Элемент, СтандартнаяОбработка, Значение)
	
	СтандартнаяОбработка = Ложь;
	ТекущаяСтрока = Элемент.ТекущиеДанные;
	
	СтруктураПоиска = Новый Структура("Документ", ТекущаяСтрока.Документ);
	Строки = ОтобранныеДокументы.НайтиСтроки(СтруктураПоиска);
	
	Если Строки.Количество() > 0 Тогда
		НоваяСтрока = Строки[0];
	Иначе 
		НоваяСтрока = ОтобранныеДокументы.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущаяСтрока);
	КонецЕсли;
	
	Элементы.ОтобранныеДокументы.ТекущаяСтрока = НоваяСтрока.ПолучитьИдентификатор();
	
КонецПроцедуры // СписокДолговВыборЗначения()

&НаКлиенте
// Процедура - обработчик события НачалоПеретаскивания списка СписокДолгов.
//
Процедура СписокДокументовНачалоПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка)
	
	ТекущиеДанные = Элемент.ТекущиеДанные;
	Структура = Новый Структура;
	Структура.Вставить("Документ", ТекущиеДанные.Документ);
	
	ПараметрыПеретаскивания.Значение = Структура;
	
	ПараметрыПеретаскивания.ДопустимыеДействия = ДопустимыеДействияПеретаскивания.Копирование;
	
КонецПроцедуры // СписокДолговНачалоПеретаскивания()

&НаКлиенте
// Процедура - обработчик нажатия кнопки ОК.
//
Процедура ОК(Команда)
	
	ЗаписатьПодборВХранилище();
	Закрыть(КодВозвратаДиалога.OK);
	
КонецПроцедуры // ОК()

&НаКлиенте
// Процедура - обработчик нажатия кнопки Обновить.
//
Процедура Обновить(Команда)
	
	УстановитьПараметрыПодбора();
	
КонецПроцедуры // Обновить()

