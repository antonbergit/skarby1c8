&НаКлиенте
Перем ПрограммноеЗакрытие;

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Сертификат = Параметры.Сертификат;
	
	Результат = СервисКриптографии.ПолучитьНастройкиПолученияВременныхПаролей(Сертификат.Идентификатор);
	Телефон          = Результат.Телефон;
	Если Не ЗначениеЗаполнено(Результат.ЭлектроннаяПочта) Тогда
		ЭлектроннаяПочта = НСтр("ru='не установлена';uk='не установлена'");
	Иначе
		ЭлектроннаяПочта = Результат.ЭлектроннаяПочта;
	КонецЕсли;

	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ПрограммноеЗакрытие <> Истина Тогда
		ПрограммноеЗакрытие = Истина;
		Отказ = Истина;
		
		ПараметрыЗакрытия = Новый Структура("ТелефонИзменен,ЭлектроннаяПочтаИзменена",
			ТелефонИзменен, ЭлектроннаяПочтаИзменена);
	
		Закрыть(ПараметрыЗакрытия);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОткрытьФормуИзмененияНомераТелефона(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Сертификат", Сертификат);
	ПараметрыФормы.Вставить("НовыйТелефон", НовыйТелефон);
	ОткрытьФорму("ОбщаяФорма.ИзменениеНомераТелефона", ПараметрыФормы, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьТелефон(Команда)
	
	Если ИзменениеЭлектроннойПочты Тогда
		ПоказатьПредупреждение(, НСтр("ru='Сначала завершите изменение адреса электронной почты';uk='Сначала завершите изменение адреса электронной почты'"));
		Возврат;	
	КонецЕсли;
	
	КодПодтверждения1 = Неопределено;
	КодПодтверждения1 = Неопределено;
	
	ИзменениеТелефона = ИзменитьТелефонНаСервере();
	
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьЭлектроннуюПочту(Команда)
	
	Если ИзменениеТелефона Тогда
		ПоказатьПредупреждение(, НСтр("ru='Сначала завершите изменение номера телефона';uk='Сначала завершите изменение номера телефона'"));
		Возврат;	
	КонецЕсли;
	
	КодПодтверждения1 = Неопределено;
	КодПодтверждения1 = Неопределено;
	
	ИзменениеЭлектроннойПочты = ИзменитьЭлектроннуюПочтуНаСервере();
	
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Функция ИзменитьТелефонНаСервере()
	
	Результат = МенеджерСервисаКриптографии.НачатьИзменениеНастроекПолученияВременныхПаролей(
		Сертификат.Идентификатор, НовыйТелефон, Неопределено);
	Если Результат.Выполнено Тогда
		ИдентификаторЗаявления = Результат.ИдентификаторЗаявления;	
	Иначе
		Если Результат.КодОшибки = "НовыйТелефонРавенТекущему" Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Результат.ОписаниеОшибки,, "НовыйТелефон");
		Иначе
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Результат.ОписаниеОшибки);
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат.Выполнено;

КонецФункции

&НаСервере
Функция ИзменитьЭлектроннуюПочтуНаСервере()
	
	Результат = МенеджерСервисаКриптографии.НачатьИзменениеНастроекПолученияВременныхПаролей(
		Сертификат.Идентификатор, Неопределено, НоваяЭлектроннаяПочта);
	Если Результат.Выполнено Тогда
		ИдентификаторЗаявления = Результат.ИдентификаторЗаявления;	
	Иначе
		Если Результат.КодОшибки = "НоваяЭлектроннаяПочтаРавнаТекущему" Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Результат.ОписаниеОшибки,, "НоваяЭлектроннаяПочта");
		Иначе
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Результат.ОписаниеОшибки);
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат.Выполнено;
	
КонецФункции

&НаКлиенте
Процедура ПодтвердитьИзменение(Команда)
	
	ПодтвердитьИзменениеНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ПодтвердитьИзменениеНаСервере()
	
	Если СтрДлина(СокрЛП(КодПодтверждения1)) <> 6 Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Код подтверждения должен состоять из 6 цифр';uk='Код подтверждения должен состоять из 6 цифр'"),, "КодПодтверждения1");
		Возврат;
	КонецЕсли;
	Если СтрДлина(СокрЛП(КодПодтверждения2)) <> 6 Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Код подтверждения должен состоять из 6 цифр';uk='Код подтверждения должен состоять из 6 цифр'"),, "КодПодтверждения2");
		Возврат;
	КонецЕсли;
	
	Если ИзменениеТелефона Тогда
		Результат = МенеджерСервисаКриптографии.ЗавершитьИзменениеНастроекПолученияВременныхПаролей(
			ИдентификаторЗаявления, 
			КодПодтверждения1, 
			КодПодтверждения2);
			
		ТелефонИзменен = Результат.Выполнено;
		ИзменениеТелефона = Не Результат.Выполнено;
	Иначе 
		Результат = МенеджерСервисаКриптографии.ЗавершитьИзменениеНастроекПолученияВременныхПаролей(
			ИдентификаторЗаявления, 
			КодПодтверждения1, 
			КодПодтверждения2);
			
		ЭлектроннаяПочтаИзменена = Результат.Выполнено;
		ИзменениеЭлектроннойПочты = Не Результат.Выполнено;
	КонецЕсли;
	
	Если Не Результат.Выполнено Тогда
		Если Результат.КодОшибки = "Код1Неверный" Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Результат.ОписаниеОшибки,, "КодПодтверждения1");
		ИначеЕсли Результат.КодОшибки = "Код2Неверный" Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Результат.ОписаниеОшибки,, "КодПодтверждения2");
		Иначе
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Результат.ОписаниеОшибки);
		КонецЕсли;		
	КонецЕсли;
	
	УправлениеФормой(ЭтаФорма);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Элементы = Форма.Элементы;
	
	// Телефон
	Элементы.ГруппаИзменениеТелефона.Видимость = Форма.ИзменениеТелефона И Не Форма.ТелефонИзменен;
	Если Форма.ИзменениеТелефона Тогда
		Элементы.КодПодтверждения1Телефон.Заголовок = СтрШаблон(НСтр("ru='Код, отправленный на"
"%1';uk='Код, отправленный на"
"%1'"), Форма.Телефон);
		Элементы.КодПодтверждения2Телефон.Заголовок = СтрШаблон(НСтр("ru='Код, отправленный на"
"%1';uk='Код, отправленный на"
"%1'"), Форма.НовыйТелефон);
		
		Элементы.НовыйТелефон.Вид = ВидПоляФормы.ПолеНадписи;
	КонецЕсли;
	Элементы.ИзменитьТелефон.Видимость = Не Форма.ИзменениеТелефона И Не Форма.ТелефонИзменен;
	Если Форма.ТелефонИзменен Тогда 
		Элементы.ПодсказкаИзменениеТелефона.Заголовок = НСтр("ru='Номер телефона успешно изменен';uk='Номер телефона успешно изменен'");
	Иначе
		Элементы.ПодсказкаИзменениеТелефона.Заголовок = НСтр("ru='На оба номера высланы SMS с кодами для подтверждения';uk='На оба номера высланы SMS с кодами для подтверждения'");
	КонецЕсли;
	Элементы.ПодсказкаИзменениеТелефона.Видимость = Форма.ИзменениеТелефона ИЛИ Форма.ТелефонИзменен;
	
	// Электронная почта
	Элементы.ГруппаИзменениеЭлектроннойПочты.Видимость = Форма.ИзменениеЭлектроннойПочты И Не Форма.ЭлектроннаяПочтаИзменена;
	Если Форма.ИзменениеЭлектроннойПочты Тогда
		Элементы.КодПодтверждения1ЭлектроннаяПочта.Заголовок = СтрШаблон(НСтр("ru='Код, отправленный на"
"%1';uk='Код, отправленный на"
"%1'"), Форма.Телефон);
		Элементы.КодПодтверждения2ЭлектроннаяПочта.Заголовок = СтрШаблон(НСтр("ru='Код, отправленный на"
"%1';uk='Код, отправленный на"
"%1'"), Форма.НоваяЭлектроннаяПочта);
		Элементы.НоваяЭлектроннаяПочта.Вид = ВидПоляФормы.ПолеНадписи;
	КонецЕсли;
	Элементы.ИзменитьЭлектроннуюПочту.Видимость = Не Форма.ИзменениеЭлектроннойПочты И Не Форма.ЭлектроннаяПочтаИзменена;
	Если Форма.ЭлектроннаяПочтаИзменена Тогда 
		Элементы.ПодсказкаИзменениеЭлектроннойПочты.Заголовок = НСтр("ru='Адрес электронной почты успешно изменен';uk='Адрес электронной почты успешно изменен'");
	Иначе
		Элементы.ПодсказкаИзменениеЭлектроннойПочты.Заголовок = НСтр("ru='На номер и адрес электронной почты высланы коды для подтверждения';uk='На номер и адрес электронной почты высланы коды для подтверждения'");
	КонецЕсли;
	Элементы.ПодсказкаИзменениеЭлектроннойПочты.Видимость = Форма.ИзменениеЭлектроннойПочты ИЛИ Форма.ЭлектроннаяПочтаИзменена;
	
КонецПроцедуры

#КонецОбласти

