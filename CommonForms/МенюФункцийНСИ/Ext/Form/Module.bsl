
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ФОУчетПоНесколькимОрганизациям = ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоОрганизаций");
	Элементы.ОткрытьОрганизации.Заголовок = ?(ФОУчетПоНесколькимОрганизациям, НСтр("ru='Организации';uk='Організації'"), НСтр("ru='Организация';uk='Організація'"));
	
	ФОУчетПоНесколькимСкладам = ПолучитьФункциональнуюОпцию("УчетПоНесколькимСкладам");
	Элементы.ОткрытьСклады.Заголовок = ?(ФОУчетПоНесколькимСкладам, НСтр("ru='Склады';uk='Склади'"), "Склад");
	
	ФОУчетПоНесколькимПодразделениям = ПолучитьФункциональнуюОпцию("УчетПоНесколькимПодразделениям");
	Элементы.ОткрытьПодразделения.Заголовок = ?(ФОУчетПоНесколькимПодразделениям, НСтр("ru='Подразделения';uk='Підрозділи'"), НСтр("ru='Подразделение';uk='Підрозділ'"));
	
	ФОУчетПоНесколькимНаправлениямДеятельности = ПолучитьФункциональнуюОпцию("УчетПоНесколькимНаправлениямДеятельности");
	Элементы.ОткрытьНаправленияДеятельности.Заголовок = ?(ФОУчетПоНесколькимНаправлениямДеятельности, НСтр("ru='Направления деятельности';uk='Напрямки діяльності'"), НСтр("ru='Направление деятельности';uk='Напрямок діяльності'"));
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_НаборКонстант" Тогда 
		
		Если Источник = "ИспользоватьНесколькоОрганизаций" Тогда
			
			ФОУчетПоНесколькимОрганизациям = ПолучитьФОСервер("ИспользоватьНесколькоОрганизаций");
			Элементы.ОткрытьОрганизации.Заголовок = ?(ФОУчетПоНесколькимОрганизациям, "Организации", "Организация");
			
		ИначеЕсли Источник = "ФункциональнаяОпцияУчетПоНесколькимСкладам" Тогда
			
			ФОУчетПоНесколькимСкладам = ПолучитьФОСервер("УчетПоНесколькимСкладам");
			Элементы.ОткрытьСклады.Заголовок = ?(ФОУчетПоНесколькимСкладам, "Склады", "Склад");
			
		ИначеЕсли Источник = "ФункциональнаяОпцияУчетПоНесколькимПодразделениям" Тогда
			
			ФОУчетПоНесколькимПодразделениям = ПолучитьФОСервер("УчетПоНесколькимПодразделениям");
			Элементы.ОткрытьПодразделения.Заголовок = ?(ФОУчетПоНесколькимПодразделениям, "Подразделения", "Подразделение");
			
		ИначеЕсли Источник = "ФункциональнаяОпцияУчетПоНесколькимНаправлениямДеятельности" Тогда
			
			ФОУчетПоНесколькимНаправлениямДеятельности = ПолучитьФОСервер("УчетПоНесколькимНаправлениямДеятельности");
			Элементы.ОткрытьНаправленияДеятельности.Заголовок = ?(ФОУчетПоНесколькимНаправлениямДеятельности, "Направления деятельности", "Направление деятельности");
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОткрытьОрганизации(Команда)
	
	Если ФОУчетПоНесколькимОрганизациям Тогда
		ОткрытьФорму("Справочник.Организации.ФормаСписка");
	Иначе
		ПараметрОрганизация = Новый Структура("Ключ", ПредопределенноеЗначение("Справочник.Организации.ОсновнаяОрганизация"));
		ОткрытьФорму("Справочник.Организации.ФормаОбъекта", ПараметрОрганизация);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьСклады(Команда)
	
	Если ФОУчетПоНесколькимСкладам Тогда
		
		МассивОтбора = Новый Массив;
		МассивОтбора.Добавить(ПредопределенноеЗначение("Перечисление.ТипыСтруктурныхЕдиниц.Склад"));
		МассивОтбора.Добавить(ПредопределенноеЗначение("Перечисление.ТипыСтруктурныхЕдиниц.Розница"));
		МассивОтбора.Добавить(ПредопределенноеЗначение("Перечисление.ТипыСтруктурныхЕдиниц.РозницаСуммовойУчет"));
		
		СтруктураОтбора = Новый Структура("ТипСтруктурнойЕдиницы", МассивОтбора);
		
		ОткрытьФорму("Справочник.СтруктурныеЕдиницы.ФормаСписка", Новый Структура("Отбор", СтруктураОтбора));
		
	Иначе
		
		ПараметрыФормы = Новый Структура("Ключ", ПредопределенноеЗначение("Справочник.СтруктурныеЕдиницы.ОсновнойСклад"));
		ОткрытьФорму("Справочник.СтруктурныеЕдиницы.ФормаОбъекта", ПараметрыФормы);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьПодразделения(Команда)
	
	Если ФОУчетПоНесколькимПодразделениям Тогда
		
		СтруктураОтбора = Новый Структура("ТипСтруктурнойЕдиницы", ПредопределенноеЗначение("Перечисление.ТипыСтруктурныхЕдиниц.Подразделение"));
		ОткрытьФорму("Справочник.СтруктурныеЕдиницы.ФормаСписка", Новый Структура("Отбор", СтруктураОтбора));
		
	Иначе
		
		ПараметрыФормы = Новый Структура("Ключ", ПредопределенноеЗначение("Справочник.СтруктурныеЕдиницы.ОсновноеПодразделение"));
		ОткрытьФорму("Справочник.СтруктурныеЕдиницы.ФормаОбъекта", ПараметрыФормы);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьНаправленияДеятельности(Команда)
	
	Если ФОУчетПоНесколькимНаправлениямДеятельности Тогда
		ОткрытьФорму("Справочник.НаправленияДеятельности.ФормаСписка");
	Иначе
		ПараметрНаправлениеДеятельности = Новый Структура("Ключ", ПредопределенноеЗначение("Справочник.НаправленияДеятельности.ОсновноеНаправление"));
		ОткрытьФорму("Справочник.НаправленияДеятельности.ФормаОбъекта", ПараметрНаправлениеДеятельности);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция ПолучитьФОСервер(ИмяФункциональнойОпции)
	
	Возврат ПолучитьФункциональнуюОпцию(ИмяФункциональнойОпции);
	
КонецФункции

#КонецОбласти