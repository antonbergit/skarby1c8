
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("Дата") Тогда
		Объект.Дата = Параметры.Дата;
	КонецЕсли;
	
	Если Параметры.Свойство("Организация") тогда
		Объект.Организация = Параметры.Организация;
		//Компания = УправлениеНебольшойФирмойСервер.ПолучитьОрганизацию(Объект.Организация);	
		Компания = Объект.Организация;	
	КонецЕсли;
	
	Объект.РасчетыВозврат = Перечисления.РасчетыВозврат.Расчеты;
	
	Если Параметры.Свойство("ВидЦен") И ЗначениеЗаполнено(Параметры.ВидЦен) Тогда
		Объект.ВидЦен = Параметры.ВидЦен
	Иначе
		Объект.ВидЦен = Справочники.ВидыЦен.Оптовая;
	КонецЕсли;	
	
	Если Параметры.Свойство("Договор") тогда
		Для Каждого Строка Из Параметры.Договор цикл
			СтрокаОтборДоговоровКонтрагентов = Объект.ОтборДоговоровКонтрагентов.Добавить();
			СтрокаОтборДоговоровКонтрагентов.ДоговорКонтрагента = Строка.Значение;
			СтрокаОтборДоговоровКонтрагентов.Контрагент = Строка.Значение.Владелец;
			
		КонецЦикла;
	КонецЕсли;
	
	Обработка = РеквизитФормыВЗначение("Объект");
	Обработка.ОбновитьДоговораНаДату();
	ЗначениеВРеквизитФормы(Обработка, "Объект");
	ОбновитьТаблицаДокументов();
	ОбновитьТаблицаДоговоров();
КонецПроцедуры

&НаСервере
// Функция помещает результаты подбора в хранилище
Функция ЗаписатьПодборВХранилище() 
	Обработка = РеквизитФормыВЗначение("Объект");
	
	СтруктураОтбора = Новый Структура();
	
	Если Объект.Договора.Количество() = 0 тогда
		
		ТекстИсключения = "По одной и/или нескольким ставкам НДС сумма выбранной номенклатуры превышает сумму неподтвержденных налоговых обязательств!" + Символы.ПС;
		ТекстИсключения = ТекстИсключения  + "Заполнение номенклатурным составом отменено";
		
		ВызватьИсключение ТекстИсключения;
		
	КонецЕсли;
	
	Для каждого СтрокаТаблицыДоговоров Из Объект.Договора Цикл
		СтруктураОтбора.Вставить("Дата", СтрокаТаблицыДоговоров.Дата);	
		СтруктураОтбора.Вставить("ДоговорКонтрагента", СтрокаТаблицыДоговоров.ДоговорКонтрагента);	
		СтруктураОтбора.Вставить("РасчетыВозврат", СтрокаТаблицыДоговоров.РасчетыВозврат);	
		
		Если НЕ Обработка.МожноФормироватьНалоговыеНакладные(СтруктураОтбора) Тогда
			ТекстИсключения = "Контрагент: " + СтруктураОтбора.ДоговорКонтрагента.Владелец + "," + Символы.ПС;
			ТекстИсключения = ТекстИсключения  + "Дата: " + Формат(СтруктураОтбора.Дата,"ДФ=dd.MM.yyyy") + "," + Символы.ПС;				
			ТекстИсключения = ТекстИсключения  + "Договор: " + СтруктураОтбора.ДоговорКонтрагента + "," + Символы.ПС;
			ТекстИсключения = ТекстИсключения  + Символы.ПС;
			ТекстИсключения = ТекстИсключения  + "По одной и/или нескольким ставкам НДС сумма выбранной номенклатуры превышает сумму неподтвержденных налоговых обязательств!" + Символы.ПС;
			ТекстИсключения = ТекстИсключения  + "Заполнение номенклатурным составом отменено";
			
			ВызватьИсключение ТекстИсключения;
			
		КонецЕсли;
		
	КонецЦикла; 

	
	Возврат ПоместитьВоВременноеХранилище(Объект.Запасы.Выгрузить());
	
КонецФункции

&НаКлиенте
Процедура ОК(Команда)
	
	Закрыть(ЗаписатьПодборВХранилище());
	
КонецПроцедуры

&НаСервере
Функция ПоместитьТаблицаДокументыВХранилище()
	
	ТаблицаВыгрузки = ЭтаФорма.ТаблицаДокументов.Выгрузить(,"Документ");
	ТаблицаВыгрузки.Удалить(0);
	
	Возврат ПоместитьВоВременноеХранилище(
		ТаблицаВыгрузки,
		УникальныйИдентификатор
	);
	
КонецФункции 

&НаСервере
Процедура ПолучитьТаблицаДокументыИзХранилища(АдресТаблицаДокументыВХранилище, ПараметрыПодбора)
	
	ТаблицаДокументы = ПолучитьИзВременногоХранилища(АдресТаблицаДокументыВХранилище);
	
	ОбратныйИндекс = Объект.ИсточникиНоменклатуры.Количество() - 1;
	Пока ОбратныйИндекс >= 0 Цикл
		Если ЗначениеЗаполнено(Объект.ИсточникиНоменклатуры[ОбратныйИндекс].Документ) тогда
			Объект.ИсточникиНоменклатуры.Удалить(ОбратныйИндекс);	
		КонецЕсли;
		ОбратныйИндекс = ОбратныйИндекс -1;
	КонецЦикла;
	
	ОбратныйИндекс = Объект.Запасы.Количество() - 1;
	Пока ОбратныйИндекс >= 0 Цикл
		Если ЗначениеЗаполнено(Объект.Запасы[ОбратныйИндекс].Документ) тогда
			Объект.Запасы.Удалить(ОбратныйИндекс);
		КонецЕсли;
		ОбратныйИндекс = ОбратныйИндекс -1;
	КонецЦикла;
	
	ПараметрыПодбора.Удалить("АдресТаблицаДокументыВХранилище");
	ПараметрыПодбора.Удалить("Компания");
	ПараметрыПодбора.Вставить("Документ");
	
	Для каждого СтрокаДокументы Из ТаблицаДокументы Цикл
		
		ПараметрыПодбора.Документ = СтрокаДокументы.Документ;
		
		Обработка = РеквизитФормыВЗначение("Объект");
		Обработка.ЗаполнитьТабличныеЧасти(ПараметрыПодбора);
		ЗначениеВРеквизитФормы(Обработка, "Объект");
		
	КонецЦикла;
	
	ОбновитьТаблицаДокументов();
КонецПроцедуры 

&НаКлиенте
Процедура ПодборДокументов(Команда)
	
	ТекущиеДанныеДоговоров = Элементы.ТаблицаДоговоров.ТекущиеДанные;
	Если ТекущиеДанныеДоговоров = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	АдресТаблицаДокументыВХранилище = ПоместитьТаблицаДокументыВХранилище();
	
	ПараметрыПодбора = Новый Структура(
		"АдресТаблицаДокументыВХранилище,
		|Компания,
		|ДоговорКонтрагента,
		|РасчетыВозврат,
		|Дата",
		АдресТаблицаДокументыВХранилище,
		Компания,
		ТекущиеДанныеДоговоров.ДоговорКонтрагента,
		ТекущиеДанныеДоговоров.РасчетыВозврат,
		ТекущиеДанныеДоговоров.Дата
	);
	
	ОткрытьФорму("ОбщаяФорма.ФормаПодбораДокументовКонтрагентов", ПараметрыПодбора,,,,, Новый ОписаниеОповещения("ПодборДокументовЗавершение", ЭтотОбъект, Новый Структура("АдресТаблицаДокументыВХранилище, ПараметрыПодбора", АдресТаблицаДокументыВХранилище, ПараметрыПодбора)),РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодборДокументовЗавершение(Результат1, ДополнительныеПараметры) Экспорт

    АдресТаблицаДокументыВХранилище = ДополнительныеПараметры.АдресТаблицаДокументыВХранилище;
	ПараметрыПодбора = ДополнительныеПараметры.ПараметрыПодбора;
	Результат = Результат1;
	
	Если Результат = КодВозвратаДиалога.OK Тогда
		ПолучитьТаблицаДокументыИзХранилища(АдресТаблицаДокументыВХранилище, ПараметрыПодбора );
	КонецЕсли;

КонецПроцедуры

&НаСервере
// Функция получает список товаров из временного хранилища
//
Процедура ПолучитьЗапасыИзХранилища(АдресЗапасовВХранилище, ИмяТабличнойЧасти)
	
	ТаблицаДляЗагрузки = ПолучитьИзВременногоХранилища(АдресЗапасовВХранилище);
	Объект[ИмяТабличнойЧасти].Очистить();
	 
	Для каждого СтрокаЗагрузки Из ТаблицаДляЗагрузки Цикл
		
		НоваяСтрока = Объект[ИмяТабличнойЧасти].Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаЗагрузки);
		
	КонецЦикла;
	
	Для каждого СтрокаТабДок Из Объект.ИсточникиНоменклатуры Цикл
		
		СтруктураОтбора = Новый Структура("Документ, СтавкаНДС",СтрокаТабДок.Документ, СтрокаТабДок.СтавкаНДС);
		Строки = Объект.Запасы.НайтиСтроки(СтруктураОтбора);
		
		СуммаНДС = 0;
		Сумма = 0;
		Для каждого Строка Из Строки Цикл
			СуммаНДС = СуммаНДС + Строка.СуммаНДСВДокумент;		
			Сумма = Сумма + Строка.ВсегоВДокумент;		
		
		КонецЦикла; 
		СтрокаТабДок.СуммаНДС  = СуммаНДС;
		СтрокаТабДок.Сумма  = Сумма;
		
	КонецЦикла; 

	
КонецПроцедуры // ПолучитьЗапасыИзХранилища()

&НаКлиенте
Процедура ДокументыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если Поле.Имя = "ДокументыДокумент" Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура("Документ, Запасы, Организация, Контрагент, Договор, Дата, ВидЦен",
		Элемент.ТекущиеДанные.Документ,
		Объект.Запасы,
		Компания,
		Объект.ОтборДоговоровКонтрагентов[0].Контрагент,
		Объект.ОтборДоговоровКонтрагентов[0].ДоговорКонтрагента,
		Объект.Дата,
		Объект.ВидЦен
		);
		
	Если ЗначениеЗаполнено(ПараметрыФормы.Документ) Тогда
		ОткрытьФорму("Обработка.ЗаполнениеНоменклатурнымСоставом.Форма.ФормаНоменклатурногоСостава", ПараметрыФормы, ЭтаФорма,,,,Новый ОписаниеОповещения("ФормаНоменклатурногоСоставаЗавершение", ЭтотОбъект));
	Иначе
		ОткрытьФорму("Обработка.ЗаполнениеНоменклатурнымСоставом.Форма.ФормаНоменклатурногоСоставаБезДокументаОснования", ПараметрыФормы, ЭтаФорма,,,,Новый ОписаниеОповещения("ФормаНоменклатурногоСоставаБезДокументаОснованияЗавершение", ЭтотОбъект));
	КонецЕсли; 	
	
КонецПроцедуры

&НаКлиенте
Процедура ФормаНоменклатурногоСоставаЗавершение(Результат, ДополнительныеПараметры) Экспорт

	АдресЗапасовВХранилище = Результат;
	
	Если ЗначениеЗаполнено(АдресЗапасовВХранилище) Тогда
		
		ПолучитьЗапасыИзХранилища(АдресЗапасовВХранилище, "Запасы");
		
		ОбновитьТаблицаДокументов();
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ФормаНоменклатурногоСоставаБезДокументаОснованияЗавершение(Результат, ДополнительныеПараметры) Экспорт

	АдресЗапасовВХранилище = Результат;
	
	Если ЗначениеЗаполнено(АдресЗапасовВХранилище) Тогда
		
		ПолучитьЗапасыИзХранилища(АдресЗапасовВХранилище, "Запасы");
		
		ОбновитьТаблицаДокументов();
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ИзменитьКоличествоПропорциональноБезУчетаВыбранного(Команда)
	
	ДанныеСтрокиДоговоров = Элементы.ТаблицаДоговоров.ТекущиеДанные;

	Если ДанныеСтрокиДоговоров <> Неопределено Тогда
		
		СтруктураСтрокиДоговоров = Новый Структура();
		СтруктураСтрокиДоговоров.Вставить("Дата",ДанныеСтрокиДоговоров.Дата);
		СтруктураСтрокиДоговоров.Вставить("ДоговорКонтрагента",ДанныеСтрокиДоговоров.ДоговорКонтрагента);
		СтруктураСтрокиДоговоров.Вставить("РасчетыВозврат",ДанныеСтрокиДоговоров.РасчетыВозврат);
		
		РаспределитьКоличество(СтруктураСтрокиДоговоров, Ложь);
		
	КонецЕсли; 
	
	ОбновитьТаблицаДокументов();
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьКоличествоПропорциональноСУчетомВыбранного(Команда)
	
	ДанныеСтрокиДоговоров = Элементы.ТаблицаДоговоров.ТекущиеДанные;

	Если ДанныеСтрокиДоговоров <> Неопределено Тогда
		
		СтруктураСтрокиДоговоров = Новый Структура();
		СтруктураСтрокиДоговоров.Вставить("Дата",ДанныеСтрокиДоговоров.Дата);
		СтруктураСтрокиДоговоров.Вставить("ДоговорКонтрагента",ДанныеСтрокиДоговоров.ДоговорКонтрагента);
		СтруктураСтрокиДоговоров.Вставить("РасчетыВозврат",ДанныеСтрокиДоговоров.РасчетыВозврат);
		
		РаспределитьКоличество(СтруктураСтрокиДоговоров, Истина);
		
	КонецЕсли; 
		
	ОбновитьТаблицаДокументов();
	
КонецПроцедуры

&НаСервере
Процедура РаспределитьКоличество(СтруктураСтрокиДоговоров, ТолькоВыделенныеПозиции=Ложь)

	Обработка = РеквизитФормыВЗначение("Объект");
	Обработка.РаспределитьКоличество(СтруктураСтрокиДоговоров, ТолькоВыделенныеПозиции);
	ЗначениеВРеквизитФормы(Обработка, "Объект");
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьТаблицаДокументов()
	ЭтаФорма.ТаблицаДокументов.Очистить();
	
	ТабДок = Объект.ИсточникиНоменклатуры.Выгрузить(,"Документ");
	ТабДок.Свернуть("Документ");
	Для каждого СтрокаТабДок Из ТабДок Цикл
		НоваяСтрока = ЭтаФорма.ТаблицаДокументов.Добавить();
		НоваяСтрока.Документ = СтрокаТабДок.Документ; 
		Если ЗначениеЗаполнено(СтрокаТабДок.Документ) Тогда
			НоваяСтрока.Представление = СтрокаТабДок.Документ; 
		Иначе
			НоваяСтрока.Представление = "Строки, добавленные вручную"; 
		КонецЕсли; 
		
		СтруктураОтбора = Новый Структура("Документ",СтрокаТабДок.Документ);
		Строки = Объект.Запасы.НайтиСтроки(СтруктураОтбора);
		
		СуммаНДС0 = 0;
		СуммаБезНДС = 0;
		Сумма = 0;
		СуммаНДС = 0;
		
		Для каждого Строка Из Строки Цикл
			СуммаНДС = СуммаНДС + Строка.СуммаНДСВДокумент;		
			Если Строка.СтавкаНДС.Ставка = 0 И Строка.СтавкаНДС.НеОблагается Тогда
				СуммаБезНДС = СуммаБезНДС + Строка.ВсегоВДокумент;		
			ИначеЕсли Строка.СтавкаНДС.Ставка = 0 И НЕ Строка.СтавкаНДС.НеОблагается Тогда
				СуммаНДС0 = СуммаНДС0 + Строка.ВсегоВДокумент;		
			Иначе
				Сумма = Сумма + Строка.ВсегоВДокумент;		
			КонецЕсли; 
		
		КонецЦикла; 
		НоваяСтрока.СуммаНДС  = СуммаНДС;
		НоваяСтрока.Сумма  = Сумма;
		НоваяСтрока.СуммаБезНДС  = СуммаБезНДС;
		НоваяСтрока.СуммаНДС0  = СуммаНДС0;
		
	КонецЦикла; 

КонецПроцедуры // ОбновитьТаблицаДокументов()
 
&НаСервере
Процедура ОбновитьТаблицаДоговоров()
	ЭтаФорма.ТаблицаДоговоров.Очистить();
	
	ТабДок = Объект.Договора.Выгрузить(,"Дата, ДоговорКонтрагента, РасчетыВозврат");
	ТабДок.Свернуть("Дата, ДоговорКонтрагента, РасчетыВозврат");
	Для каждого СтрокаТабДок Из ТабДок Цикл
		НоваяСтрока = ЭтаФорма.ТаблицаДоговоров.Добавить();
		НоваяСтрока.Дата = СтрокаТабДок.Дата; 
		НоваяСтрока.ДоговорКонтрагента = СтрокаТабДок.ДоговорКонтрагента; 
		НоваяСтрока.РасчетыВозврат = СтрокаТабДок.РасчетыВозврат; 
		НоваяСтрока.Контрагент = СтрокаТабДок.ДоговорКонтрагента.Владелец; 
		
		СтруктураОтбора = Новый Структура("Дата, ДоговорКонтрагента, РасчетыВозврат",СтрокаТабДок.Дата, СтрокаТабДок.ДоговорКонтрагента, СтрокаТабДок.РасчетыВозврат);
		Строки = Объект.Договора.НайтиСтроки(СтруктураОтбора);
		
		СуммаНДС0 = 0;
		СуммаБезНДС = 0;
		Сумма = 0;
		СуммаНДС = 0;
		Аванс =0;
		
		Для каждого Строка Из Строки Цикл
			СуммаНДС = СуммаНДС + Строка.СуммаНДС;	
			Если НЕ ЗначениеЗаполнено(Строка.СтавкаНДС) Тогда
				Аванс = Аванс + Строка.Сумма;		
			ИначеЕсли Строка.СтавкаНДС.Ставка = 0 И Строка.СтавкаНДС.НеОблагается Тогда
				СуммаБезНДС = СуммаБезНДС + Строка.Сумма;		
			ИначеЕсли Строка.СтавкаНДС.Ставка = 0 И НЕ Строка.СтавкаНДС.НеОблагается Тогда
				СуммаНДС0 = СуммаНДС0 + Строка.Сумма;		
			Иначе
				Сумма = Сумма + Строка.Сумма;		
			КонецЕсли; 
		
		КонецЦикла; 
		НоваяСтрока.СуммаНДС 	 = СуммаНДС;
		НоваяСтрока.Сумма 		 = Сумма;
		НоваяСтрока.СуммаБезНДС  = СуммаБезНДС;
		НоваяСтрока.СуммаНДС0 	 = СуммаНДС0;
		НоваяСтрока.Аванс 		 = Аванс;
		
	КонецЦикла; 

КонецПроцедуры // ОбновитьТаблицаДоговоров()
 

