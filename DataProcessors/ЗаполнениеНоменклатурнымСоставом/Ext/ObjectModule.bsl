Перем мИтоговаяТаблица Экспорт;

Перем мВалютаРегламентированногоУчета Экспорт;

Перем ДопустимыеТипыДокументов Экспорт;

Перем НеДопустимыеТипыДокументов Экспорт;


// Эта функция пересчитывает сумму из валюты ВалютаНач по курсу ПоКурсуНач 
// в валюту ВалютаКон по курсу ПоКурсуКон
//
// Параметры:      
//  Сумма          - сумма, которую следует пересчитать;
//  ВалютаНач      - ссылка на элемент справочника Валют;
//                   определяет валюты из которой надо пересчитвать;
//  ВалютаКон      - ссылка на элемент справочника Валют;
//                   определяет валюты в которую надо пересчитвать;
//  ПоКурсуНач     - курс из которого надо пересчитать;
//  ПоКурсуКон     - курс в который надо пересчитать;
//  ПоКратностьНач - кратность из которого надо пересчитать (по умолчанию = 1);
//  ПоКратностьКон - кратность в который надо пересчитать  (по умолчанию = 1);
//
// Возвращаемое значение: 
//  Сумма, пересчитанная в другую валюту
//
Функция ПересчитатьИзВалютыВВалюту(Сумма, ВалютаНач, ВалютаКон, ПоКурсуНач, ПоКурсуКон, 
			   ПоКратностьНач =1, ПоКратностьКон = 1, 
			   Погрешность = 0, СоответствиеПогрешностей = Неопределено, Ключ = Неопределено) Экспорт

	Если (ВалютаНач = ВалютаКон) Тогда

		// Считаем, что пересчет не нужен.
		Возврат Сумма;
	КонецЕсли;

	Если (ПоКурсуНач = ПоКурсуКон) 
	   и (ПоКратностьНач = ПоКратностьКон) Тогда

		// ну, тут и считать нечего...
		Возврат Сумма;
	КонецЕсли;

	Если ПоКурсуНач     = 0 
	 или ПоКурсуКон     = 0 
	 или ПоКратностьНач = 0 
	 или ПоКратностьКон = 0 Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("ПересчитатьИзВалютыВВалюту(): при пересчете обнаружен нулевой курс.");
		Возврат 0;
	КонецЕсли;
	
	НоваяСумма = Окр((Сумма * ПоКурсуНач * ПоКратностьКон) / (ПоКурсуКон * ПоКратностьНач), 2);
	Возврат НоваяСумма;

КонецФункции //ПересчитатьИзВалютыВВалюту()

// Возвращает курс валюты на дату.
//
Функция ПолучитьКурсВалюты(Валюта, ДатаКурса) Экспорт
	
	Структура = РегистрыСведений.КурсыВалют.ПолучитьПоследнее(ДатаКурса, Новый Структура("Валюта", Валюта));
	
	Возврат Структура;
	
КонецФункции // ПолучитьКурсВалюты()

// Выполняет округление числовых значений с накоплением погрешностей округления, образовавшихся
//		в результате предыдущих вызовов функции
//
// Параметры
//  Число 		– Число. Округляемое значение
//  Точность	– Число. Точность округления
//	Погрешность	- Число. Переменная, в которой накапливается погрешность с предыдущих вызовов
//
// Возвращаемое значение:
//   Число   – округленное значение
//
Функция ОкруглитьСУчетомПогрешности(Число, Точность, Погрешность = 0, 
	               СоответствиеПогрешностей = Неопределено, Ключ = Неопределено) Экспорт
				   
	Если НЕ СоответствиеПогрешностей = Неопределено И ЗначениеЗаполнено(Ключ) Тогда
	
		// считываем погрешность округления, накопленную ранее при расчетах
		Погрешность = СоответствиеПогрешностей[Ключ];
		// погрешности округления еще нет -- первая сумма
		Если Погрешность = Неопределено Тогда
			Погрешность = 0;
		КонецЕсли;
		// округлим с учетом погрешности
		Округленное = ОкруглитьСУчетомПогрешности(Число, Точность, Погрешность);
		// сохраним погрешность округления
		СоответствиеПогрешностей.Вставить(Ключ, Погрешность);
	
	Иначе
		
		Если Число = 0 Тогда
			Возврат 0;
		КонецЕсли; 
	
		// выравнивание разрядности
		Число = Окр(Число, 27, ?(Число<0, РежимОкругления.Окр15как10, РежимОкругления.Окр15как20));
		
		// сумма с учетом погрешности предыдущих вычислений
		Округляемое = Число + Погрешность;

		// для отрицательного числа меняем направление округления, чтобы избежать ошибки Окр(-0.5) = -1
		Округленное	= Окр(Округляемое, Точность, ?(Округляемое<0, РежимОкругления.Окр15как10, РежимОкругления.Окр15как20));
		
		// рассчитаем новую погрешность округления
		Погрешность	= Округляемое - Округленное;
		
	КонецЕсли;
	
	Возврат Округленное;

КонецФункции // ОкруглитьСУчетомПогрешности()


Процедура ОбновитьДоговораНаДату(ПараметрОтборДоговоровКонтрагентов = Неопределено) Экспорт

	Запрос = Новый Запрос();
	
	Запрос.УстановитьПараметр("ПустаяСтавкаНДС",Справочники.СтавкиНДС.ПустаяСсылка());
	Запрос.УстановитьПараметр("Отгрузка", 		Перечисления.РасчетыВозврат.Расчеты);
	Запрос.УстановитьПараметр("Возврат", 		Перечисления.РасчетыВозврат.Возврат);
	
	СобытияНДСПродажОтгрузка = Новый СписокЗначений();
	СобытияНДСПродажОтгрузка.Добавить(Перечисления.СобытияНДСПродаж.РеализацияПокупателю);
	СобытияНДСПродажОтгрузка.Добавить(Перечисления.СобытияНДСПродаж.ОплатаПокупателем);
	Запрос.УстановитьПараметр("СобытияНДСПродажОтгрузка", СобытияНДСПродажОтгрузка);
	
	СобытияНДСПриобретенийВозврат = Новый СписокЗначений();
	СобытияНДСПриобретенийВозврат.Добавить(Перечисления.СобытияНДСПродаж.ВозвратОплатыПокупателю);
	СобытияНДСПриобретенийВозврат.Добавить(Перечисления.СобытияНДСПродаж.ВозвратОтПокупателя);
	Запрос.УстановитьПараметр("СобытияНДСПродажВозврат", СобытияНДСПриобретенийВозврат);
	
	Запрос.УстановитьПараметр("Организация", Организация);
	
	ДополнительныеУсловия = "";
	Если Не ПараметрОтборДоговоровКонтрагентов = Неопределено Тогда
		
		ДополнительныеУсловия =  ДополнительныеУсловия + " И ДоговорКонтрагента В (&ДоговораКонтрагентов) ";
		Запрос.УстановитьПараметр("ДоговораКонтрагентов", ПараметрОтборДоговоровКонтрагентов.ВыгрузитьКолонку("ДоговорКонтрагента"));
		
	Иначе
		
		ОтборДоговоров = ОтборДоговоровКонтрагентов.Выгрузить();
		ОтборДоговоров.Свернуть("ДоговорКонтрагента", "");
		СтрокаСПустымДоговором = ОтборДоговоров.Найти(Справочники.ДоговорыКонтрагентов.ПустаяСсылка());
		Если НЕ СтрокаСПустымДоговором = Неопределено Тогда 
			ОтборДоговоров.Удалить(СтрокаСПустымДоговором);	
		КонецЕсли;
		
		// если остались строки, значит устанавливаем фильтр по договорам,
		// иначе - по контрагентам
		Если ОтборДоговоров.Количество() > 0 Тогда
			
			ДополнительныеУсловия =  ДополнительныеУсловия + " И ДоговорКонтрагента В (&ДоговораКонтрагентов) ";
			Запрос.УстановитьПараметр("ДоговораКонтрагентов", ОтборДоговоров);
			
		Иначе 
			
			ОтборКонтрагентов = ОтборДоговоровКонтрагентов.Выгрузить();
			ОтборКонтрагентов.Свернуть("Контрагент", "");
			Если ОтборКонтрагентов.Количество() > 0 Тогда
				ДополнительныеУсловия =  ДополнительныеУсловия + " И ДоговорКонтрагента.Владелец В ИЕРАРХИИ(&Контрагенты) ";
				Запрос.УстановитьПараметр("Контрагенты", ОтборКонтрагентов);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;		
	
	Если НЕ ЗначениеЗаполнено(Дата) Тогда
		Запрос.УстановитьПараметр("МоментВремени", 	'000101010000');
	Иначе		
		Запрос.УстановитьПараметр("МоментВремени", 	Новый Граница(Дата, ВидГраницы.Включая));
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Дата", Дата);
	
	ТекстЗапросаРасчеты=
				  "	&Дата КАК Дата,
	               |	&Отгрузка КАК РасчетыВозврат,
	               |	НДСПродажОстатки.ДоговорКонтрагента.Владелец КАК Контрагент,
	               |	НДСПродажОстатки.ДоговорКонтрагента КАК ДоговорКонтрагента,
	               |	НДСПродажОстатки.СтавкаНДС КАК СтавкаНДС,
	               |	ВЫБОР
	               |		КОГДА НДСПродажОстатки.СтавкаНДС = &ПустаяСтавкаНДС
	               |			ТОГДА 0
	               |		ИНАЧЕ НДСПродажОстатки.СуммаОстаток
	               |	КОНЕЦ КАК СуммаОтгрузки,
	               |	ВЫБОР
	               |		КОГДА НДСПродажОстатки.СтавкаНДС = &ПустаяСтавкаНДС
	               |			ТОГДА 0
	               |		ИНАЧЕ НДСПродажОстатки.СуммаНДСОстаток
	               |	КОНЕЦ КАК СуммаНДСОтгрузки,
	               |	ВЫБОР
	               |		КОГДА НДСПродажОстатки.СтавкаНДС = &ПустаяСтавкаНДС
	               |			ТОГДА НДСПродажОстатки.СуммаОстаток
	               |		ИНАЧЕ 0
	               |	КОНЕЦ КАК СуммаОплаты
	               |ИЗ
	               |	РегистрНакопления.НДСПродаж.Остатки(&МоментВремени, Организация = &Организация И СобытиеНДС В (&СобытияНДСПродажОтгрузка) " + ДополнительныеУсловия + ") КАК НДСПродажОстатки
	               |";
	 ТекстЗапросаВозврат=
				  "	&Дата КАК Дата,
	               |	&Возврат КАК РасчетыВозврат,
	               |	НДСПродажОстатки.ДоговорКонтрагента.Владелец КАК Контрагент,
	               |	НДСПродажОстатки.ДоговорКонтрагента КАК ДоговорКонтрагента,
	               |	НДСПродажОстатки.СтавкаНДС КАК СтавкаНДС,
	               |	ВЫБОР
	               |		КОГДА НДСПродажОстатки.СтавкаНДС = &ПустаяСтавкаНДС
	               |			ТОГДА 0
	               |		ИНАЧЕ НДСПродажОстатки.СуммаОстаток
	               |	КОНЕЦ  СуммаОтгрузки,
	               |	ВЫБОР
	               |		КОГДА НДСПродажОстатки.СтавкаНДС = &ПустаяСтавкаНДС
	               |			ТОГДА 0
	               |		ИНАЧЕ НДСПродажОстатки.СуммаНДСОстаток
	               |	КОНЕЦ КАК СуммаНДСОтгрузки,
	               |	ВЫБОР
	               |		КОГДА НДСПродажОстатки.СтавкаНДС = &ПустаяСтавкаНДС
	               |			ТОГДА НДСПродажОстатки.СуммаОстаток
	               |		ИНАЧЕ 0
	               |	КОНЕЦ КАК СуммаОплаты
	               |ИЗ
	               |	РегистрНакопления.НДСПродаж.Остатки(&МоментВремени, Организация = &Организация И СобытиеНДС В (&СобытияНДСПродажВозврат) " + ДополнительныеУсловия + ") КАК НДСПродажОстатки
				   |";
	
	Если  НЕ ЗначениеЗаполнено(РасчетыВозврат) = Ложь
		И ПараметрОтборДоговоровКонтрагентов = Неопределено Тогда
		
		Если РасчетыВозврат = Перечисления.РасчетыВозврат.Расчеты Тогда
			Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
							|" +ТекстЗапросаРасчеты;	
		Иначе
			Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
							|" +ТекстЗапросаВозврат;	
		КонецЕсли; 
		
	Иначе
		Запрос.Текст = 	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
						|" + ТекстЗапросаРасчеты + "
						| ОБЪЕДИНИТЬ ВСЕ
						|
						|ВЫБРАТЬ
						|" + ТекстЗапросаВозврат;
	КонецЕсли;
					
	Запрос.Текст = Запрос.Текст + 
				   "ИТОГИ
				   |	СУММА(СуммаОтгрузки),
				   |	СУММА(СуммаНДСОтгрузки),
				   |	СУММА(СуммаОплаты)
				   |ПО
				   |	Контрагент,
				   |	РасчетыВозврат,
				   |	ДоговорКонтрагента,
				   |	СтавкаНДС";

	
	
	Договора.Очистить();

	Выборка1 = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока Выборка1.Следующий() Цикл // Контрагент
		Выборка2 = Выборка1.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока Выборка2.Следующий() Цикл //РасчетыВозврат
			Выборка3 = Выборка2.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока Выборка3.Следующий() Цикл  //ДоговорКонтрагента
				// последний уровень итогов
				ВыборкаДоговора = Выборка3.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				Пока ВыборкаДоговора.Следующий() Цикл  
					
					СуммаНеподтверденнойПредоплаты = ВыборкаДоговора.СуммаОплаты;
					
					// по ставкам НДС
					ВыборкаСтавка = ВыборкаДоговора.Выбрать();
					Пока ВыборкаСтавка.Следующий() Цикл
						Если ВыборкаСтавка.СуммаОтгрузки > 0 Тогда
							ДобавитьСтрокуВДоговора(ВыборкаСтавка);
							СуммаНеподтверденнойПредоплаты = СуммаНеподтверденнойПредоплаты - ВыборкаСтавка.СуммаОтгрузки 
						КонецЕсли;
					КонецЦикла;
					
					// дополнительно имеется неподтверженная НД предоплата
					Если СуммаНеподтверденнойПредоплаты > 0 Тогда
						ДобавитьСтрокуВДоговора(ВыборкаДоговора, СуммаНеподтверденнойПредоплаты);
					КонецЕсли;

				КонецЦикла; 	
				
			КонецЦикла; 	
		КонецЦикла; 	
	КонецЦикла; 	
	
	// Добавим в Договора строки, по которым нет остатков
	// в случае, если данная процедура вызвана в режиме обновления таблицы договоров (заполнен параметр ПараметрОтборДоговоровКонтрагентов)
	// И удалим  лишние (в запросе фильтр по Ресчету/Возврату не накладывался)
	Если Не ПараметрОтборДоговоровКонтрагентов = Неопределено Тогда
		
		// Добавляем новые
		СтруктураПоиска = Новый Структура("ДоговорКонтрагента, РасчетыВозврат");
		Для каждого СтрокаТаблицыДокументов Из ПараметрОтборДоговоровКонтрагентов Цикл
			
			СтруктураПоиска.ДоговорКонтрагента  = СтрокаТаблицыДокументов.ДоговорКонтрагента;		
			СтруктураПоиска.РасчетыВозврат 		= СтрокаТаблицыДокументов.РасчетыВозврат;		
			
			Если Договора.НайтиСтроки(СтруктураПоиска).Количество() = 0 Тогда
				СтрокаДоговоров = Договора.Добавить();
				СтрокаДоговоров.Дата = Дата;		
				СтрокаДоговоров.ДоговорКонтрагента  = СтрокаТаблицыДокументов.ДоговорКонтрагента;		
				СтрокаДоговоров.РасчетыВозврат 		= СтрокаТаблицыДокументов.РасчетыВозврат;		
			КонецЕсли;
			
		КонецЦикла; 
		
		// Удаляем Лишние
		СтруктураПоиска = Новый Структура();
		Сч = 0;
		Пока Сч <= Договора.Количество() - 1 Цикл
			
			СтрокаДоговоров = Договора.Получить(Сч);
			СтруктураПоиска.Вставить("ДоговорКонтрагента",	СтрокаДоговоров.ДоговорКонтрагента);		
			СтруктураПоиска.Вставить("РасчетыВозврат",		СтрокаДоговоров.РасчетыВозврат);		
			
			Если ПараметрОтборДоговоровКонтрагентов.НайтиСтроки(СтруктураПоиска).Количество() = 0 Тогда
				Договора.Удалить(СтрокаДоговоров);
			Иначе
				Сч = Сч + 1
			КонецЕсли;
			
		КонецЦикла; 
		
	КонецЕсли;
	
	// добавим строки-ссылки на номенклатуру, добавляемую пользователем.
	ДоговораКопия = Договора.Выгрузить();
	ДоговораКопия.Свернуть("ДоговорКонтрагента","");
	
	СтруктураПоиска = Новый Структура("ДоговорКонтрагента, РасчетыВозврат, Документ");
	// если "возврат" (формируем Приложения 2) - необходимо всегда указывать документ-основание: налоговую накладную - основание!
	СтруктураПоиска.РасчетыВозврат 	= Перечисления.РасчетыВозврат.Расчеты;
	СтруктураПоиска.Документ 		= Неопределено;
	Для каждого СтрокаДоговоров Из ДоговораКопия Цикл
		
		СтруктураПоиска.ДоговорКонтрагента 	= СтрокаДоговоров.ДоговорКонтрагента;
		СтрокиИсточниковНоменклатуры = ИсточникиНоменклатуры.НайтиСтроки(СтруктураПоиска);
		Если СтрокиИсточниковНоменклатуры.Количество() = 0 Тогда
			ВыборкаНДС = Справочники.СтавкиНДС.Выбрать();
			Пока ВыборкаНДС.Следующий() Цикл
				СтрокаИсточниковНоменклатуры = ИсточникиНоменклатуры.Добавить();
				СтрокаИсточниковНоменклатуры.Дата 					= Дата;		
				СтрокаИсточниковНоменклатуры.ДоговорКонтрагента 	= СтруктураПоиска.ДоговорКонтрагента;
				СтрокаИсточниковНоменклатуры.РасчетыВозврат 		= СтруктураПоиска.РасчетыВозврат;
				СтрокаИсточниковНоменклатуры.Документ	 	 		= Неопределено;	
				СтрокаИсточниковНоменклатуры.СтавкаНДС	 	 		= ВыборкаНДС.Ссылка;	
				
			КонецЦикла; 
		КонецЕсли;
	
	КонецЦикла;
КонецПроцедуры

Процедура ДобавитьСтрокуВДоговора(Выборка, СуммаНеподтверденнойПредоплаты=Неопределено) Экспорт 
	
	СтрокаДоговоров = Договора.Добавить();
	
	СтрокаДоговоров.Дата		 		= Выборка.Дата;
	Если СтрокаДоговоров.Дата = '00010101' Тогда
		СтрокаДоговоров.Дата = Дата;	
	КонецЕсли;
	СтрокаДоговоров.РасчетыВозврат 		= Выборка.РасчетыВозврат;
	СтрокаДоговоров.ДоговорКонтрагента 	= Выборка.ДоговорКонтрагента;
	
	Если НЕ ЗначениеЗаполнено(Выборка.СтавкаНДС) Тогда
		
		СтрокаДоговоров.СтавкаНДС = Справочники.СтавкиНДС.ПустаяСсылка();
		Если СуммаНеподтверденнойПредоплаты=Неопределено Тогда
			СтрокаДоговоров.Сумма = Выборка.СуммаОплаты;
		Иначе
			СтрокаДоговоров.Сумма = СуммаНеподтверденнойПредоплаты;
		КонецЕсли;
		
	Иначе
		
		СтрокаДоговоров.СтавкаНДС 	= Выборка.СтавкаНДС;
		СтрокаДоговоров.Сумма 		= Выборка.СуммаОтгрузки;
		СтрокаДоговоров.СуммаНДС	= Выборка.СуммаНДСОтгрузки;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьТабличныеЧасти(СтруктураОтбора) Экспорт 
	
	//проверим наличие необходимого отбора
	Если НЕ СтруктураОтбора.Свойство("СуществующийДокумент") Тогда
	
		СтруктураОтбора.Вставить("СуществующийДокумент", Ложь);
	
	КонецЕсли;
	
	ДокументОснование = СтруктураОтбора.Документ; 
	ЗаполнитьТабличнуюЧасть("Запасы", 			СтруктураОтбора);
	
	ОбновитьИсточникиНоменклатуры(СтруктураОтбора);
	
КонецПроцедуры


Процедура ОбновитьИсточникиНоменклатуры(СтруктураОтбораНоменклатуры) Экспорт

	мИтоговаяТаблица.Очистить();
	
	ВыгрузитьТабличнуюЧастьВИтоговуюТаблицу(СтруктураОтбораНоменклатуры, мИтоговаяТаблица, "Запасы");
	
	
	// получим реквизиты документа, связанные с НДС
	Документ = СтруктураОтбораНоменклатуры.Документ;
	Если Документ = Неопределено Тогда
		// Без документа- основания
		Если СтруктураОтбораНоменклатуры.ДоговорКонтрагента.РасчетыВУсловныхЕдиницах Тогда
			ВалютаДокумента  = СтруктураОтбораНоменклатуры.ДоговорКонтрагента.ВалютаРасчетов;
		Иначе	
			ВалютаДокумента  = мВалютаРегламентированногоУчета;
		КонецЕсли;
		НалогообложениеНДС 	 = Перечисления.ТипыНалогообложенияНДС.ОблагаетсяНДС;
		СуммаВключаетНДС = Истина;
	
	Иначе
		
		ВалютаДокумента  = Документ.ВалютаДокумента;
		НалогообложениеНДС 	 = Документ.НалогообложениеНДС;
		СуммаВключаетНДС = Документ.СуммаВключаетНДС;
	
	КонецЕсли; 
	
	// пересчитаем суммы с учетом ошибок округления и реквизитов учета НДС в документе в гривны
	ПогрешностиОкругления 	  = Новый Соответствие();
	
	ДанныеВалДок  = ПолучитьКурсВалюты(ВалютаДокумента, Дата);
	ДанныеВалРегл = ПолучитьКурсВалюты(мВалютаРегламентированногоУчета, Дата);
	
	Для каждого СтрокаТаблицы Из мИтоговаяТаблица Цикл
		
		// Рассчитаем суммы в документе в валюте документа
		СуммаСНДСВал    = СтрокаТаблицы.ВсегоВДокумент;
		СуммаНДСВал     = СтрокаТаблицы.СуммаНДСВДокумент;
		
		// Рассчитаем суммы в документе в валюте регл. учета
		Если ВалютаДокумента = мВалютаРегламентированногоУчета Тогда
			СтрокаТаблицы.ВсегоВДокумент 	 = СуммаСНДСВал;
			СтрокаТаблицы.СуммаНДСВДокумент  = СуммаНДСВал;
		Иначе
										 	
			СтрокаТаблицы.ВсегоВДокумент 	= ПересчитатьИзВалютыВВалюту(СуммаСНДСВал, 
																		ВалютаДокумента,
																		мВалютаРегламентированногоУчета, 
																		ДанныеВалДок.Курс, 
																		ДанныеВалРегл.Курс,
																		ДанныеВалДок.Кратность,
																		ДанныеВалРегл.Кратность,
																		, ПогрешностиОкругления , "СуммаСНДСРегл");
			
			СтрокаТаблицы.СуммаНДСВДокумент  = УчетНДССервер.РассчитатьСуммуНДСсУчетомПогрешности(СтрокаТаблицы.ВсегоВДокумент,
																					Истина,
																					Истина,
																					СтрокаТаблицы.СтавкаНДС,
																					ПогрешностиОкругления);
		КонецЕсли;
		
	КонецЦикла;	
	
	мИтоговаяТаблица.Свернуть("СтавкаНДС","ВсегоВДокумент, СуммаНДСВДокумент");
	
	
	Если мИтоговаяТаблица.Количество() = 0 Тогда
		// добавим единственную "пустую" строку
		СтрокаИсточниковНоменклатуры = ИсточникиНоменклатуры.Вставить(0);
		СтрокаИсточниковНоменклатуры.СуществующийДокумент	= СтруктураОтбораНоменклатуры.СуществующийДокумент;
		СтрокаИсточниковНоменклатуры.Дата				 	= СтруктураОтбораНоменклатуры.Дата;
		СтрокаИсточниковНоменклатуры.ДоговорКонтрагента 	= СтруктураОтбораНоменклатуры.ДоговорКонтрагента;
		СтрокаИсточниковНоменклатуры.Документ 				= СтруктураОтбораНоменклатуры.Документ;
		СтрокаИсточниковНоменклатуры.РасчетыВозврат 		= СтруктураОтбораНоменклатуры.РасчетыВозврат;
		
	Иначе
		СтруктураОтбораДоговоров = Новый Структура();
		СтруктураОтбораДоговоров.Вставить("Дата", 				СтруктураОтбораНоменклатуры.Дата);
		СтруктураОтбораДоговоров.Вставить("ДоговорКонтрагента", СтруктураОтбораНоменклатуры.ДоговорКонтрагента);
		СтруктураОтбораДоговоров.Вставить("РасчетыВозврат",		СтруктураОтбораНоменклатуры.РасчетыВозврат);
		
		Для каждого СтрокаСумм Из мИтоговаяТаблица Цикл
			СтрокаИсточниковНоменклатуры = ИсточникиНоменклатуры.Добавить();
			СтрокаИсточниковНоменклатуры.СуществующийДокумент	= СтруктураОтбораНоменклатуры.СуществующийДокумент;
			СтрокаИсточниковНоменклатуры.Дата				    = СтруктураОтбораНоменклатуры.Дата;
			СтрокаИсточниковНоменклатуры.ДоговорКонтрагента 	= СтруктураОтбораНоменклатуры.ДоговорКонтрагента;
			СтрокаИсточниковНоменклатуры.Документ 				= СтруктураОтбораНоменклатуры.Документ;
			СтрокаИсточниковНоменклатуры.Представление 			= Строка(СтруктураОтбораНоменклатуры.Документ);
			
			СтрокаИсточниковНоменклатуры.РасчетыВозврат	 		= СтруктураОтбораНоменклатуры.РасчетыВозврат;
			СтрокаИсточниковНоменклатуры.СтавкаНДС      		= СтрокаСумм.СтавкаНДС;
			СтрокаИсточниковНоменклатуры.СуммаНДС       		= СтрокаСумм.СуммаНДСВДокумент;
			СтрокаИсточниковНоменклатуры.Сумма		  			= СтрокаСумм.ВсегоВДокумент;
			
			СтруктураОтбораДоговоров.Вставить("СтавкаНДС",  СтрокаСумм.СтавкаНДС);
			
			СтрокиДоговоров = Договора.НайтиСтроки(СтруктураОтбораДоговоров);
			Если СтрокиДоговоров.Количество() = 0 Тогда
				СтрокаДоговоров = Договора.Добавить();
				Для каждого Параметр Из СтруктураОтбораДоговоров Цикл
					СтрокаДоговоров[Параметр.Ключ] = Параметр.Значение;
				КонецЦикла; 
			КонецЕсли;
			
		КонецЦикла; 
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ВыгрузитьТабличнуюЧастьВИтоговуюТаблицу(СтруктураОтбора, Таблица, ИмяТабличнойЧасти)
	
	КолонкиТабличнойЧасти = ЭтотОбъект.Метаданные().ТабличныеЧасти.Найти(ИмяТабличнойЧасти).Реквизиты;
	
	// выгружаем строки табличных частей документов в таблицу
	СтрокиПоДокументу = ЭтотОбъект[ИмяТабличнойЧасти].НайтиСтроки(СтруктураОтбора);
	Для каждого СтрокаТЧ Из СтрокиПоДокументу Цикл
		
		СтрокаИтоговойТаблицы = Таблица.Добавить();
		
		СтрокаИтоговойТаблицы.ВсегоВДокумент = СтрокаТЧ.ВсегоВДокумент;
		
		Если  КолонкиТабличнойЧасти.Найти("СтавкаНДС") = Неопределено 
		  ИЛИ НЕ ЗначениеЗаполнено(СтрокаТЧ.СтавкаНДС) Тогда
		  // если ставка в документе не заполнена считаем это ставкой НеНДС.
		  //СтрокаИтоговойТаблицы.СтавкаНДС = Перечисления.СтавкиНДС.НеНДС;
		Иначе 
			СтрокаИтоговойТаблицы.СтавкаНДС = СтрокаТЧ.СтавкаНДС;
		КонецЕсли;
		
		Если  НЕ КолонкиТабличнойЧасти.Найти("СуммаНДСВДокумент") = Неопределено Тогда
			СтрокаИтоговойТаблицы.СуммаНДСВДокумент = СтрокаТЧ.СуммаНДСВДокумент;
		КонецЕсли; 
	
	КонецЦикла; 

КонецПроцедуры

Процедура ЗаполнитьТабличнуюЧасть(ИмяТабличнойЧастиДокумента, СтруктураОтбора)
	Перем ИмяТабличнойЧастиОбработки;
	
	Документ = СтруктураОтбора.Документ;
	
	ИмяТабличнойЧастиОбработки = ИмяТабличнойЧастиДокумента;
	
	Если Документ.Метаданные().ТабличныеЧасти.Найти(ИмяТабличнойЧастиДокумента) = Неопределено Тогда
		// нет такой табличной части
		Возврат
	КонецЕсли;
	
	КолокниТЧОбработки = ЭтотОбъект.Метаданные().ТабличныеЧасти.Найти(ИмяТабличнойЧастиОбработки).Реквизиты;		
	КолокниТЧДокумента = Документ.Метаданные().ТабличныеЧасти.Найти(ИмяТабличнойЧастиДокумента).Реквизиты;
	
	
	Для каждого СтрокаТЧДокумента Из Документ[ИмяТабличнойЧастиДокумента] Цикл 
		
		СтрокаТЧОбработки = ЭтотОбъект[ИмяТабличнойЧастиОбработки].Добавить();
		
		Для каждого Колонка Из КолокниТЧОбработки Цикл
			
			ИмяКолонки = Колонка.Имя; 
			
			Если ИмяКолонки = "Документ" Тогда
				
				СтрокаТЧОбработки[ИмяКолонки] = СтруктураОтбора.Документ;	
				
			ИначеЕсли ИмяКолонки = "Дата" Тогда
				
				СтрокаТЧОбработки[ИмяКолонки] = СтруктураОтбора.Дата;	
				
			ИначеЕсли ИмяКолонки = "СуществующийДокумент" Тогда
				
				СтрокаТЧОбработки[ИмяКолонки] = СтруктураОтбора.СуществующийДокумент;	
				
			ИначеЕсли ИмяКолонки = "РасчетыВозврат" Тогда
				
				СтрокаТЧОбработки[ИмяКолонки] = СтруктураОтбора.РасчетыВозврат;	
				
			ИначеЕсли ИмяКолонки = "ДоговорКонтрагента" Тогда
				
				СтрокаТЧОбработки[ИмяКолонки] = СтруктураОтбора.ДоговорКонтрагента;
				
			ИначеЕсли ИмяКолонки = "КодУКТВЭД" Тогда
				
				Если  НЕ КолокниТЧДокумента.Найти("КодУКТВЭД") = Неопределено Тогда
					СтрокаТЧОбработки.КодУКТВЭД = СтрокаТЧДокумента.КодУКТВЭД;
					
				ИначеЕсли НЕ КолокниТЧДокумента.Найти("Номенклатура") = Неопределено Тогда
					
					НоменклатураГТДНоменклатуры = СтрокаТЧДокумента.Номенклатура.НоменклатураГТД;
					
					Если НЕ КолокниТЧДокумента.Найти("ХарактеристикаНоменклатуры") = Неопределено Тогда
						
						НоменклатураГТДХарактеристики = СтрокаТЧДокумента.ХарактеристикаНоменклатуры.НоменклатураГТД;
						
						Если СтрокаТЧДокумента.ХарактеристикаНоменклатуры.ОтечественныйНеподакцизныйТовар = Истина Тогда
							СтрокаТЧОбработки.КодУКТВЭД = Неопределено;
						ИначеЕсли ЗначениеЗаполнено(НоменклатураГТДХарактеристики) Тогда
							Если ТипЗнч(НоменклатураГТДХарактеристики) = Тип("СправочникСсылка.НоменклатураГТД") Тогда
								СтрокаТЧОбработки.КодУКТВЭД = НоменклатураГТДХарактеристики.КодУКТВЭД;
							ИначеЕсли ТипЗнч(НоменклатураГТДХарактеристики) = Тип("СправочникСсылка.КлассификаторУКТВЭД") Тогда
								СтрокаТЧОбработки.КодУКТВЭД = НоменклатураГТДХарактеристики;
							КонецЕсли; 
						Иначе
							Если ТипЗнч(НоменклатураГТДНоменклатуры) = Тип("СправочникСсылка.НоменклатураГТД") Тогда
								СтрокаТЧОбработки.КодУКТВЭД = НоменклатураГТДНоменклатуры.КодУКТВЭД;
							ИначеЕсли ТипЗнч(НоменклатураГТДНоменклатуры) = Тип("СправочникСсылка.КлассификаторУКТВЭД") Тогда
								СтрокаТЧОбработки.КодУКТВЭД = НоменклатураГТДНоменклатуры;
							КонецЕсли; 
						КонецЕсли;
					Иначе
						Если ТипЗнч(НоменклатураГТДНоменклатуры) = Тип("СправочникСсылка.НоменклатураГТД") Тогда
							СтрокаТЧОбработки.КодУКТВЭД = НоменклатураГТДНоменклатуры.КодУКТВЭД;
						ИначеЕсли ТипЗнч(НоменклатураГТДНоменклатуры) = Тип("СправочникСсылка.КлассификаторУКТВЭД") Тогда
							СтрокаТЧОбработки.КодУКТВЭД = НоменклатураГТДНоменклатуры;
						КонецЕсли; 
					КонецЕсли;	
					
				КонецЕсли;
				
			ИначеЕсли ИмяКолонки = "НомерГТД" Тогда
				
				Если  НЕ КолокниТЧДокумента.Найти("НомерГТД") = Неопределено Тогда
					
					СтрокаТЧОбработки.НомерГТД = СтрокаТЧДокумента.НомерГТД;
					
				ИначеЕсли НЕ КолокниТЧДокумента.Найти("Номенклатура") = Неопределено Тогда
					
					НоменклатураГТДНоменклатуры = СтрокаТЧДокумента.Номенклатура.НоменклатураГТД;	
					
					Если НЕ КолокниТЧДокумента.Найти("ХарактеристикаНоменклатуры") = Неопределено Тогда
					
						НоменклатураГТДХарактеристики = СтрокаТЧДокумента.ХарактеристикаНоменклатуры.НоменклатураГТД;
						
						Если СтрокаТЧДокумента.ХарактеристикаНоменклатуры.ОтечественныйНеподакцизныйТовар = Истина Тогда
							СтрокаТЧОбработки.НомерГТД = Неопределено;
						ИначеЕсли ЗначениеЗаполнено(НоменклатураГТДХарактеристики) Тогда
							Если ТипЗнч(НоменклатураГТДХарактеристики) = Тип("СправочникСсылка.НоменклатураГТД") Тогда
								СтрокаТЧОбработки.НомерГТД = НоменклатураГТДХарактеристики.НомерГТД;
							ИначеЕсли ТипЗнч(НоменклатураГТДХарактеристики) = Тип("СправочникСсылка.КлассификаторУКТВЭД") Тогда
								СтрокаТЧОбработки.НомерГТД = НоменклатураГТДХарактеристики;
							КонецЕсли; 
						Иначе
							Если ТипЗнч(НоменклатураГТДНоменклатуры) = Тип("СправочникСсылка.НоменклатураГТД") Тогда
								СтрокаТЧОбработки.НомерГТД = НоменклатураГТДНоменклатуры.НомерГТД;
							ИначеЕсли ТипЗнч(НоменклатураГТДНоменклатуры) = Тип("СправочникСсылка.КлассификаторУКТВЭД") Тогда
								СтрокаТЧОбработки.НомерГТД = НоменклатураГТДНоменклатуры;
							КонецЕсли; 
						КонецЕсли;
					Иначе
						Если ТипЗнч(НоменклатураГТДНоменклатуры) = Тип("СправочникСсылка.НоменклатураГТД") Тогда
							СтрокаТЧОбработки.НомерГТД = НоменклатураГТДНоменклатуры.НомерГТД;
						ИначеЕсли ТипЗнч(НоменклатураГТДНоменклатуры) = Тип("СправочникСсылка.КлассификаторУКТВЭД") Тогда
							СтрокаТЧОбработки.НомерГТД = НоменклатураГТДНоменклатуры;
						КонецЕсли; 
					КонецЕсли;	
					
				КонецЕсли;	
				
			ИначеЕсли НЕ КолокниТЧДокумента.Найти(ИмяКолонки) = Неопределено Тогда
				
				СтрокаТЧОбработки[ИмяКолонки] = СтрокаТЧДокумента[ИмяКолонки]
				
			ИначеЕсли Прав(ИмяКолонки, 9) = "ВДокумент" Тогда
				
				ИмяКолонкиИсходногоЗначения = Лев(ИмяКолонки, СтрДлина(ИмяКолонки) - 9); 
				
				Если  НЕ КолокниТЧДокумента.Найти(ИмяКолонкиИсходногоЗначения) = Неопределено Тогда
					
					СтрокаТЧОбработки[ИмяКолонки] = СтрокаТЧДокумента[ИмяКолонкиИсходногоЗначения];
					
				КонецЕсли;
				
			ИначеЕсли ИмяКолонки = "Флаг" Тогда
				
				СтрокаТЧОбработки[ИмяКолонки] = Истина;	

			КонецЕсли;
		
		КонецЦикла; 
		
	КонецЦикла; 
	
КонецПроцедуры

Процедура ОчиститьТабличныеЧасти() Экспорт
		
	ОтборДоговоровКонтрагентов.Очистить();
	Договора.Очистить();
	ИсточникиНоменклатуры.Очистить();

КонецПроцедуры

Функция БудутСформированыНалоговыеНакладные(ТаблицаЧастичнойОтгрузки) Экспорт
	ТаблицаКопия = ТаблицаЧастичнойОтгрузки.Скопировать();
	ТаблицаКопия.Свернуть("ЧастичнаяОтгрузка, ЗаСчетАванса");
	
	Результат = Ложь;
	Для каждого Строка Из ТаблицаКопия Цикл
		
		Если  Строка.ЧастичнаяОтгрузка = Ложь
			И Строка.ЗаСчетАванса	   = Истина Тогда
			Продолжить;
		Иначе
			Результат = Истина;
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
		
	Возврат Результат;
КонецФункции

// Функция проверяет, сумма неподтвержденных налоговых обязательств с учетом аванса не превышает ли
// сумму выбранной в ИсточникахНоменклатуры номенклатуры. При отрицательном результате возвращает таблицу
// содержащую суммы частичных отгрузок в разрезе ставок (суммы превышения разносятся по ставкам пропорционально)
Функция МожноФормироватьНалоговыеНакладные(СтуктураОтбораИсточниковНоменклатуры, ТаблицаЧастичнойОтгрузки = Неопределено) Экспорт
	
	// Пока не будет установлено обратное
	Результат = Истина;
	
	мИтоговаяТаблица.Очистить();
	
	ТаблицаНеподтвержденныхОбязательств = мИтоговаяТаблица.Скопировать();
	ТаблицаИтоговПоНоменклатуре 		= мИтоговаяТаблица.Скопировать();
	ТаблицаЧастичнойОтгрузки 			= мИтоговаяТаблица.Скопировать();
	ТаблицаЧастичнойОтгрузки.Колонки.Добавить("ЧастичнаяОтгрузка", 	Новый ОписаниеТипов("Булево"));
	ТаблицаЧастичнойОтгрузки.Колонки.Добавить("ЗаСчетАванса", 	   	Новый ОписаниеТипов("Булево"));
	// комбинации признаков означают следующее: ЧастичнаяОтгрузка/ЗаСчетАванса
	// Ложь	 /Ложь	 - Номенклатура по ставке переносятся в налоговый документ полностью
	// Ложь	 /Истина - Номенклатура по ставке НЕ переносятся в налоговый документ 
	// Истина/Истина - Образуется частичная отгрузка на сумму пропорционально распределенного между ставками аванса
	// Истина/Ложь   - Образуется частичная отгрузка на указанную сумму
	
	// Занесем в таблицу суммы неподтвержденных налоговых обязательств по каждой ставке и определим сумму
	// аванса, не подтвержденного налоговыми документами: ее можно будет закрыть по произвольной ставке.
	СуммаАванса = 0;
	СтруктураОбораДоговоров = Новый Структура();
	СтруктураОбораДоговоров.Вставить("Дата", СтуктураОтбораИсточниковНоменклатуры.Дата);	
	СтруктураОбораДоговоров.Вставить("ДоговорКонтрагента", СтуктураОтбораИсточниковНоменклатуры.ДоговорКонтрагента);	
	СтруктураОбораДоговоров.Вставить("РасчетыВозврат", СтуктураОтбораИсточниковНоменклатуры.РасчетыВозврат);	
	ДанныеДоговоров = Договора.НайтиСтроки(СтруктураОбораДоговоров);
	Для каждого СтрокаДоговоров Из ДанныеДоговоров Цикл
		
		Если НЕ ЗначениеЗаполнено(СтрокаДоговоров.СтавкаНДС) Тогда
			
			СуммаАванса = СуммаАванса + СтрокаДоговоров.Сумма;
			
		Иначе	
			
			СтрокаИтоговойТаблицы = ТаблицаНеподтвержденныхОбязательств.Добавить();
			СтрокаИтоговойТаблицы.СтавкаНДС 		= СтрокаДоговоров.СтавкаНДС;
			СтрокаИтоговойТаблицы.ВсегоВДокумент	= СтрокаДоговоров.Сумма;
			СтрокаИтоговойТаблицы.СуммаНДСВДокумент	= СтрокаДоговоров.СуммаНДС;
			
		КонецЕсли;
		
	КонецЦикла;
	ТаблицаНеподтвержденныхОбязательств.Свернуть("СтавкаНДС", "ВсегоВДокумент, СуммаНДСВДокумент");
		
	// Определим суммы по номенклатуре
	ДанныеИсточниковНоменклатуры = ИсточникиНоменклатуры.НайтиСтроки(СтуктураОтбораИсточниковНоменклатуры);
	Для каждого СтрокаИсточниковНоменклатуры Из ДанныеИсточниковНоменклатуры Цикл
		Если НЕ ЗначениеЗаполнено(СтрокаИсточниковНоменклатуры.СтавкаНДС) Тогда
			Продолжить;
		КонецЕсли;
		
		СтрокаИтоговойТаблицы = ТаблицаИтоговПоНоменклатуре.Добавить();
		СтрокаИтоговойТаблицы.СтавкаНДС 		= СтрокаИсточниковНоменклатуры.СтавкаНДС;
		СтрокаИтоговойТаблицы.ВсегоВДокумент	= СтрокаИсточниковНоменклатуры.Сумма;
		СтрокаИтоговойТаблицы.СуммаНДСВДокумент	= СтрокаИсточниковНоменклатуры.СуммаНДС;
		
	КонецЦикла; 
	ТаблицаИтоговПоНоменклатуре.Свернуть("СтавкаНДС", "ВсегоВДокумент, СуммаНДСВДокумент");
	
	ОстатокАванса = СуммаАванса;
	// пройдемся по таблице итогов по номенклатуре и определим имеются ли частичные отгрузки
	СтруктураОтбора = новый Структура();
	Для каждого СтрокаПоНоменклатуре Из ТаблицаИтоговПоНоменклатуре Цикл
		
		СтруктураОтбора.Вставить("СтавкаНДС", СтрокаПоНоменклатуре.СтавкаНДС);
		СтрокиНеподтвержденныхОбязательств = ТаблицаНеподтвержденныхОбязательств.НайтиСтроки(СтруктураОтбора);
		Если СтрокиНеподтвержденныхОбязательств.Количество() = 0 Тогда
			// может попасть в НН за счет аванса
			СтрокаЧастичнойОтгрузки = ТаблицаЧастичнойОтгрузки.Добавить();
			СтрокаЧастичнойОтгрузки.СтавкаНДС 			= СтруктураОтбора.СтавкаНДС;
			СтрокаЧастичнойОтгрузки.ВсегоВДокумент 		= СтрокаПоНоменклатуре.ВсегоВДокумент;
			СтрокаЧастичнойОтгрузки.СуммаНДСВДокумент 	= СтрокаПоНоменклатуре.СуммаНДСВДокумент;
			СтрокаЧастичнойОтгрузки.ЧастичнаяОтгрузка   = Истина;
			СтрокаЧастичнойОтгрузки.ЗаСчетАванса		= Истина;
			ОстатокАванса = ОстатокАванса - СтрокаПоНоменклатуре.ВсегоВДокумент;

		Иначе
			// строка - единственная
			СтрокаНеподтвержденныхОбязательств = СтрокиНеподтвержденныхОбязательств[0];
			Если СтрокаНеподтвержденныхОбязательств.ВсегоВДокумент > СтрокаПоНоменклатуре.ВсегоВДокумент Тогда
				// эти данные должны попасть в налоговые документы полностью
				СтрокаЧастичнойОтгрузки = ТаблицаЧастичнойОтгрузки.Добавить();
				СтрокаЧастичнойОтгрузки.СтавкаНДС 			= СтруктураОтбора.СтавкаНДС;
				СтрокаЧастичнойОтгрузки.ВсегоВДокумент 		= СтрокаПоНоменклатуре.ВсегоВДокумент;
				СтрокаЧастичнойОтгрузки.СуммаНДСВДокумент 	= СтрокаПоНоменклатуре.СуммаНДСВДокумент;
				СтрокаЧастичнойОтгрузки.ЧастичнаяОтгрузка   = Ложь;
				СтрокаЧастичнойОтгрузки.ЗаСчетАванса		= Ложь;				
				
			ИначеЕсли СтрокаНеподтвержденныхОбязательств.ВсегоВДокумент < СтрокаПоНоменклатуре.ВсегоВДокумент Тогда
				// на сумму неподтвержденных обязательств возникает частичная отгрузка, остальное может попасть
				// в налоговый документ за счет аванса
				СтрокаЧастичнойОтгрузки = ТаблицаЧастичнойОтгрузки.Добавить();
				СтрокаЧастичнойОтгрузки.СтавкаНДС 			= СтруктураОтбора.СтавкаНДС;
				СтрокаЧастичнойОтгрузки.ВсегоВДокумент 		= СтрокаНеподтвержденныхОбязательств.ВсегоВДокумент;
				СтрокаЧастичнойОтгрузки.СуммаНДСВДокумент 	= СтрокаНеподтвержденныхОбязательств.СуммаНДСВДокумент;
				СтрокаЧастичнойОтгрузки.ЧастичнаяОтгрузка   = Истина;
				СтрокаЧастичнойОтгрузки.ЗаСчетАванса		= Ложь;
				
				СтрокаЧастичнойОтгрузки = ТаблицаЧастичнойОтгрузки.Добавить();
				СтрокаЧастичнойОтгрузки.СтавкаНДС 			= СтруктураОтбора.СтавкаНДС;
				СтрокаЧастичнойОтгрузки.ВсегоВДокумент 		= СтрокаПоНоменклатуре.ВсегоВДокумент - СтрокаНеподтвержденныхОбязательств.ВсегоВДокумент;
				СтрокаЧастичнойОтгрузки.СуммаНДСВДокумент 	= СтрокаПоНоменклатуре.СуммаНДСВДокумент - СтрокаНеподтвержденныхОбязательств.СуммаНДСВДокумент;
				СтрокаЧастичнойОтгрузки.ЧастичнаяОтгрузка   = Истина;
				СтрокаЧастичнойОтгрузки.ЗаСчетАванса		= Истина;
				ОстатокАванса = ОстатокАванса - (СтрокаПоНоменклатуре.ВсегоВДокумент - СтрокаНеподтвержденныхОбязательств.ВсегоВДокумент);
				
			ИначеЕсли НЕ СтрокаНеподтвержденныхОбязательств.СуммаНДСВДокумент = СтрокаПоНоменклатуре.СуммаНДСВДокумент Тогда
				// ситуация с зависшими копейками по НДС (может возникнуть при частичных оплатах/отгрузках
				Результат = Ложь;
				
				СтрокаЧастичнойОтгрузки = ТаблицаЧастичнойОтгрузки.Добавить();
				СтрокаЧастичнойОтгрузки.СтавкаНДС 			= СтруктураОтбора.СтавкаНДС;
				СтрокаЧастичнойОтгрузки.ВсегоВДокумент 		= СтрокаНеподтвержденныхОбязательств.ВсегоВДокумент;
				СтрокаЧастичнойОтгрузки.СуммаНДСВДокумент 	= СтрокаНеподтвержденныхОбязательств.СуммаНДСВДокумент;
				СтрокаЧастичнойОтгрузки.ЧастичнаяОтгрузка	= Истина;
				СтрокаЧастичнойОтгрузки.ЗаСчетАванса   		= Ложь;
			Иначе
				// эти данные должны попасть в налоговые документы полностью
				СтрокаЧастичнойОтгрузки = ТаблицаЧастичнойОтгрузки.Добавить();
				СтрокаЧастичнойОтгрузки.СтавкаНДС 			= СтруктураОтбора.СтавкаНДС;
				СтрокаЧастичнойОтгрузки.ВсегоВДокумент 		= СтрокаПоНоменклатуре.ВсегоВДокумент;
				СтрокаЧастичнойОтгрузки.СуммаНДСВДокумент 	= СтрокаПоНоменклатуре.СуммаНДСВДокумент;
				СтрокаЧастичнойОтгрузки.ЧастичнаяОтгрузка   = Ложь;
				СтрокаЧастичнойОтгрузки.ЗаСчетАванса		= Ложь;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	//удалим строки с нулевыми суммами:
	НульСтроки = ТаблицаЧастичнойОтгрузки.НайтиСтроки(Новый Структура("ВсегоВДокумент",0));
	Для каждого НульСтрока Из НульСтроки Цикл
		ТаблицаЧастичнойОтгрузки.удалить(НульСтрока);
	КонецЦикла; 

	Если ОстатокАванса < 0  ИЛИ (ТаблицаЧастичнойОтгрузки.Количество() = 0 И ТаблицаИтоговПоНоменклатуре.Количество() > 0) Тогда
		Результат = Ложь;
	Иначе
		// суммы неподтвержденного аванса достаточно для того, чтобы по данным номенклатурного состава документа сформировать налогвые документы по определенным ставкам
	КонецЕсли;
		
	Если Результат = Ложь Тогда
		// попытаемся распределить авансы
		СтрокиТоваров= ТаблицаЧастичнойОтгрузки.НайтиСтроки(Новый Структура("ЧастичнаяОтгрузка, ЗаСчетАванса", Истина, Истина));
		
		// За Товары
		Если СуммаАванса = 0 Тогда
			// аванса и не было
			Для каждого Строка Из СтрокиТоваров Цикл
				// Установим признак, означающий, что строки товаров по данной ставке не попадут в налоговую накладную.
				Строка.ЧастичнаяОтгрузка	= Ложь;
			КонецЦикла; 
		Иначе
			СуммаОтгрузокВсего = 0;
			Для каждого Строка Из СтрокиТоваров Цикл
				СуммаОтгрузокВсего = СуммаОтгрузокВсего + Строка.ВсегоВДокумент;
			КонецЦикла;
			
			КоэффЧастичнойОтгрузки = ?(СуммаОтгрузокВсего = 0, 0, СуммаАванса / СуммаОтгрузокВсего);
			Если КоэффЧастичнойОтгрузки < 1 Тогда
				ПоследняяСтрока = Неопределено;
				ЗачтеноАванса 	= 0;
				Для каждого Строка Из СтрокиТоваров Цикл
					
					Строка.ВсегоВДокумент  = Строка.ВсегоВДокумент * КоэффЧастичнойОтгрузки;
					// пропорционально определим НДС (НДС в т. ч. - как в регистре НДС Продаж)
					Строка.СуммаНДСВДокумент =  УчетНДССервер.РассчитатьСуммуНДСсУчетомПогрешности(Строка.ВсегоВДокумент, Истина, Истина, Строка.СтавкаНДС);
					
					ЗачтеноАванса 	= ЗачтеноАванса + Строка.ВсегоВДокумент;
					ПоследняяСтрока = Строка;
				КонецЦикла;
				Если Не ПоследняяСтрока = Неопределено Тогда
					// ошибки округления отнесем на последнюю строку:
					ПоследняяСтрока.ВсегоВДокумент = ПоследняяСтрока.ВсегоВДокумент + СуммаАванса - ЗачтеноАванса;
					ПоследняяСтрока.СуммаНДСВДокумент =  УчетНДССервер.РассчитатьСуммуНДСсУчетомПогрешности(ПоследняяСтрока.ВсегоВДокумент, Истина, Истина, ПоследняяСтрока.СтавкаНДС)
				КонецЕсли;
			КонецЕсли;
			
		КонецЕсли;	
		
	КонецЕсли;
	
	Возврат Результат;

КонецФункции

Процедура ВыделитьВсюНоменклатуру(СтруктураОтбора, ТолькоВыделенныеПозиции=Ложь)
	
	// устанавливаем количество и суммы в документ равными количеству и суммам документа основания
	НайденныеСтроки = Запасы.НайтиСтроки(СтруктураОтбора);
	Для каждого Строка Из НайденныеСтроки Цикл
		// выделяем только позиции по документам-основаниям.
		// строки введенные пользователем вручную не рассматриваем
		Если НЕ ЗначениеЗаполнено(Строка.Документ) Тогда
			Продолжить
		КонецЕсли;
		Если ТолькоВыделенныеПозиции Тогда
			Если НЕ Строка.Флаг Тогда
				Строка.КоличествоВДокумент 	= 0;
				Строка.ВсегоВДокумент		= 0;
				Строка.СуммаНДСВДокумент = 0;
			Иначе
				Строка.КоличествоВДокумент 	= Строка.Количество;
				Строка.ВсегоВДокумент		= Строка.Всего;
				Строка.СуммаНДСВДокумент = Строка.СуммаНДС;
			КонецЕсли;
		Иначе
			Строка.Флаг = Истина;
			Строка.КоличествоВДокумент 	= Строка.Количество;
			Строка.ВсегоВДокумент		= Строка.Всего;
			Строка.СуммаНДСВДокумент = Строка.СуммаНДС;
		КонецЕсли; 
		
	КонецЦикла;     

КонецПроцедуры

Процедура РаспределитьКоличество(Данные, ТолькоВыделенныеПозиции=Ложь) Экспорт

	СтруктураОтбораНоменклатуры = Новый Структура("Дата, СуществующийДокумент, ДоговорКонтрагента, РасчетыВозврат, СуществующийДокумент", Данные.Дата, Ложь, Данные.ДоговорКонтрагента, Данные.РасчетыВозврат, Ложь);
	СтруктураОтбораДокументов   = Новый Структура("Дата, СуществующийДокумент, ДоговорКонтрагента, РасчетыВозврат", Данные.Дата, Ложь, Данные.ДоговорКонтрагента, Данные.РасчетыВозврат);
	
	
	// получим суммы частичных отгрузок
	//  с учетом введенных вручную позиций номенклатуры
	ТаблицаЧастичнойОтгрузки = Неопределено;
	
	Если МожноФормироватьНалоговыеНакладные(СтруктураОтбораДокументов, ТаблицаЧастичнойОтгрузки) Тогда
		// ничего более делать не нужно, выделив все количество в документах мы не вышли за сумм неподтвержденных обязательтв.
		Сообщить("Суммы указанных документов не превышают сумм неподтвержденных налоговых обязательств");
		Возврат;
	КонецЕсли;
	
	ВыделитьВсюНоменклатуру(СтруктураОтбораДокументов, ТолькоВыделенныеПозиции); 
	
	ДанныеВалРегл = ПолучитьКурсВалюты(Константы.НациональнаяВалюта.Получить(), Данные.Дата);
	
	
	// В таблице ИсточниковНоменклатуры содержатся актуальные суммы документов
	// В таблице ТаблицаЧастичнойОтгрузки - суммы, на которые можно выписать налоговые.
	// получим коэффициент по каждой из ставок и пропорционально изменим количество в документах.
	
	// в начале удалим строки означющие тот факт, что по ставке/таре номенклатура не переносится в налоговый документ
	УдаляемыеСтроки = ТаблицаЧастичнойОтгрузки.НайтиСтроки(Новый Структура("ЧастичнаяОтгрузка, 	ЗаСчетАванса",
																			Ложь, 				Истина));
	Для каждого УдаляемаяСтрока Из УдаляемыеСтроки Цикл
		ТаблицаЧастичнойОтгрузки.Удалить(УдаляемаяСтрока);
	КонецЦикла; 
	ТаблицаЧастичнойОтгрузки.Свернуть("СтавкаНДС", "ВсегоВДокумент");
	
	
	
    ВыборкаСтавкиНДС = Справочники.СтавкиНДС.Выбрать();
	Пока ВыборкаСтавкиНДС.Следующий() Цикл
		СтавкаНДС = ВыборкаСтавкиНДС.Ссылка;
		Если ТолькоВыделенныеПозиции Тогда
			СтруктураОтбора = Новый Структура("СтавкаНДС, Флаг",СтавкаНДС, Истина);	
		Иначе	
			СтруктураОтбора = Новый Структура("СтавкаНДС",СтавкаНДС);	
		КонецЕсли; 
		
		СтрокиЗапасы = Запасы.НайтиСтроки(СтруктураОтбора);
		СуммаПоДокументам = 0;
		НеизменяемаяСумма = 0;
		Для каждого СтрокаЗапасы Из СтрокиЗапасы Цикл
			Если НЕ ЗначениеЗаполнено(СтрокаЗапасы.Документ) Тогда
				НеизменяемаяСумма = НеизменяемаяСумма + СтрокаЗапасы.Всего;
			Иначе
				СуммаПоДокументам = СуммаПоДокументам + СтрокаЗапасы.Всего;
			КонецЕсли;
		КонецЦикла; 
		
		
		// эта строка должна быть единственной
		СтрокиТаблицыЧастичнойОтгрузки = ТаблицаЧастичнойОтгрузки.НайтиСтроки(Новый Структура("СтавкаНДС", 
																							   СтавкаНДС));
		Если    СтрокиТаблицыЧастичнойОтгрузки.Количество() = 0
			ИЛИ СуммаПоДокументам = 0 
			ИЛИ СтрокиТаблицыЧастичнойОтгрузки[0].ВсегоВДокумент <= НеизменяемаяСумма Тогда
			Коэфф = 0;
		Иначе 
			Коэфф = (СтрокиТаблицыЧастичнойОтгрузки[0].ВсегоВДокумент - НеизменяемаяСумма) / СуммаПоДокументам;
		КонецЕсли;
		
		
		
		ПогрешностиОкругления 	  = Новый Соответствие();
		
		// изменим количество во всех документах
		Для каждого ТекущийДокумент Из ИсточникиНоменклатуры Цикл 
			
			Если НЕ ЗначениеЗаполнено(ТекущийДокумент.Документ) Тогда
				Продолжить;
			КонецЕсли;

			СтруктураОтбораНоменклатуры.Вставить("Документ", ТекущийДокумент.Документ);
			СтруктураОтбораНоменклатуры.Вставить("СтавкаНДС", СтавкаНДС);
			
			Если ТолькоВыделенныеПозиции Тогда
				СтруктураОтбораНоменклатуры.Вставить("Флаг", Истина);
			КонецЕсли;
			
			СтрокиНоменклатуры = Запасы.НайтиСтроки(СтруктураОтбораНоменклатуры);
			
			Документ = СтруктураОтбораНоменклатуры.Документ;
			ВалютаДокумента  = Документ.ВалютаДокумента;
			
			ДанныеВалДок  = ПолучитьКурсВалюты(ВалютаДокумента, Данные.Дата);
			
			// пересчитаем накопленную погрешность в валюту документа без округления
			ТекущаяПогрешность = ПогрешностиОкругления["Всего"];
			Если НЕ ТекущаяПогрешность = Неопределено Тогда
				Попытка
					ПогрешностиОкругления.Вставить("Всего", ТекущаяПогрешность*(ДанныеВалРегл.Курс/ДанныеВалРегл.Кратность)/(ДанныеВалДок.Курс/ДанныеВалДок.Кратность)); 
				Исключение
					ПогрешностиОкругления.Вставить("Всего", 0);
				КонецПопытки;								
			КонецЕсли;
			ТекущаяПогрешность = ПогрешностиОкругления["СуммаНДС"];
			Если НЕ ТекущаяПогрешность = Неопределено Тогда
				Попытка
					ПогрешностиОкругления.Вставить("СуммаНДС", ТекущаяПогрешность*(ДанныеВалРегл.Курс/ДанныеВалРегл.Кратность)/(ДанныеВалДок.Курс/ДанныеВалДок.Кратность)); 
				Исключение
					ПогрешностиОкругления.Вставить("СуммаНДС", 0);
				КонецПопытки;								
			КонецЕсли;
			
			Для каждого СтрокаНоменклатуры Из СтрокиНоменклатуры Цикл
				СтрокаНоменклатуры.КоличествоВДокумент = СтрокаНоменклатуры.Количество * Коэфф;
				СтрокаНоменклатуры.ВсегоВДокумент 	   = ОкруглитьСУчетомПогрешности(СтрокаНоменклатуры.Всего * Коэфф, 2, ,ПогрешностиОкругления, "Всего");
				СтрокаНоменклатуры.СуммаНДСВДокумент   = ОкруглитьСУчетомПогрешности(СтрокаНоменклатуры.СуммаНДС * Коэфф, 2, ,ПогрешностиОкругления, "СуммаНДС");	
			КонецЦикла;
			
			// пересчитаем накопленную погрешность в гривни без округления
			ТекущаяПогрешность = ПогрешностиОкругления["Всего"];
			Если НЕ ТекущаяПогрешность = Неопределено Тогда
				Попытка
					ПогрешностиОкругления.Вставить("Всего", ТекущаяПогрешность*(ДанныеВалДок.Курс/ДанныеВалДок.Кратность)/(ДанныеВалРегл.Курс/ДанныеВалРегл.Кратность)); 
				Исключение
					ПогрешностиОкругления.Вставить("Всего", 0);	
				КонецПопытки;								
			КонецЕсли;
			ТекущаяПогрешность = ПогрешностиОкругления["СуммаНДС"];
			Если НЕ ТекущаяПогрешность = Неопределено Тогда
				Попытка
					ПогрешностиОкругления.Вставить("СуммаНДС", ТекущаяПогрешность*(ДанныеВалДок.Курс/ДанныеВалДок.Кратность)/(ДанныеВалРегл.Курс/ДанныеВалРегл.Кратность)); 
				Исключение
					ПогрешностиОкругления.Вставить("СуммаНДС", 0);	
				КонецПопытки;								
			КонецЕсли;
			
		КонецЦикла;		
	КонецЦикла; 
	
	Для каждого ТекущийДокумент Из ИсточникиНоменклатуры Цикл 
		
		Если НЕ ЗначениеЗаполнено(ТекущийДокумент.Документ) Тогда
			Продолжить;
		КонецЕсли;

		СтруктураОтбораНоменклатуры = Новый Структура("Документ, СтавкаНДС", ТекущийДокумент.Документ, ТекущийДокумент.СтавкаНДС);
		
		СтрокиНоменклатуры = Запасы.НайтиСтроки(СтруктураОтбораНоменклатуры);
		ВсегоВДокумент = 0;
		СуммаНДСВДокумент = 0;
		Для каждого СтрокаНоменклатуры Из СтрокиНоменклатуры Цикл
			ВсегоВДокумент 	   = ВсегоВДокумент + СтрокаНоменклатуры.ВсегоВДокумент;
			СуммаНДСВДокумент   = СуммаНДСВДокумент + СтрокаНоменклатуры.СуммаНДСВДокумент;	
		КонецЦикла;
		ТекущийДокумент.Сумма = ВсегоВДокумент;
		ТекущийДокумент.СуммаНДС = СуммаНДСВДокумент;
	КонецЦикла;	
	
КонецПроцедуры


мВалютаРегламентированногоУчета   = Константы.НациональнаяВалюта.Получить();

ДопустимыеТипыДокументов = Новый Массив;
ДопустимыеТипыДокументов.Добавить(Тип("ДокументСсылка.РасходнаяНакладная"));	
ДопустимыеТипыДокументов.Добавить(Тип("ДокументСсылка.АктВыполненныхРабот"));	
ДопустимыеТипыДокументов.Добавить(Тип("ДокументСсылка.СчетНаОплату"));	
ДопустимыеТипыДокументов.Добавить(Тип("ДокументСсылка.ЗаказПокупателя"));	
ДопустимыеТипыДокументов.Добавить(Тип("ДокументСсылка.НалоговаяНакладная"));	

НеДопустимыеТипыДокументов = Новый Массив;
НеДопустимыеТипыДокументов.Добавить(Тип("ДокументСсылка.ОтчетКомитенту"));	
НеДопустимыеТипыДокументов.Добавить(Тип("ДокументСсылка.ОтчетКомиссионера"));	
НеДопустимыеТипыДокументов.Добавить(Тип("ДокументСсылка.ОтчетОПереработке"));	
НеДопустимыеТипыДокументов.Добавить(Тип("ДокументСсылка.ПриходнаяНакладная"));	
НеДопустимыеТипыДокументов.Добавить(Тип("ДокументСсылка.ОтчетОРозничныхПродажах"));	
НеДопустимыеТипыДокументов.Добавить(Тип("ДокументСсылка.ПередачаВА"));	


КЧ = Новый КвалификаторыЧисла(15,2);

мИтоговаяТаблица = Новый ТаблицаЗначений();
мИтоговаяТаблица.Колонки.Добавить("СтавкаНДС", 		Новый ОписаниеТипов("СправочникСсылка.СтавкиНДС"));
мИтоговаяТаблица.Колонки.Добавить("СуммаНДСВДокумент", Новый ОписаниеТипов("Число", КЧ));
мИтоговаяТаблица.Колонки.Добавить("ВсегоВДокумент", Новый ОписаниеТипов("Число", КЧ));

