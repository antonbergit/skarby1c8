
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	СписокДействий = Новый СписокЗначений;
	СписокДействий.Добавить(НСтр("ru='Подготовка ...';uk='Підготовка ...'"));
	СписокДействий.Добавить(НСтр("ru='Программируем ...';uk='Програмуємо ...'"));
	СписокДействий.Добавить(НСтр("ru='Оформляем документы ...';uk='Оформляємо документи ...'"));
	СписокДействий.Добавить(НСтр("ru='Регистрируем сайт ...';uk='Реєструємо сайт ...'"));
	СписокДействий.Добавить(НСтр("ru='Делаем вывеску и печать ...';uk='Робимо вивіску і печатку ...'"));
	
	СписокДействий.Добавить(НСтр("ru='Арендуем магазин ...';uk='Орендуємо магазин ...'"));
	СписокДействий.Добавить(НСтр("ru='Нанимаем продавцов ...';uk='Наймаємо продавців ...'"));
	СписокДействий.Добавить(НСтр("ru='Везем товары ...';uk='Веземо товари ...'"));
	СписокДействий.Добавить(НСтр("ru='Раскладываем по полкам ...';uk='Розкладаємо по полицях ...'"));
	СписокДействий.Добавить(НСтр("ru='Клеим ценники ...';uk='Клеїмо цінники ...'"));
	СписокДействий.Добавить(НСтр("ru='Открываем магазин ...';uk='Відкриваємо магазин ...'"));
	СписокДействий.Добавить(НСтр("ru='Продаем, сдачу не забываем давать ...';uk='Продаємо, здачу не забуваємо давати ...'"));
	СписокДействий.Добавить(НСтр("ru='Считаем выручку ...';uk='Рахуємо виручку ...'"));
	СписокДействий.Добавить(НСтр("ru='Кладем деньги на счет ...';uk='Кладемо гроші на рахунок ...'"));
	
	СопровождающийТекст = СписокДействий[0];
	НомерДействия = 1;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если Выполнено Тогда 
		Отказ = Истина;
	Иначе
		ИнтервалПроверки = 7;
		ПодключитьОбработчикОжидания("ПроверитьВыполнение", ИнтервалПроверки, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;
	
	ПриЗакрытииНаСервере()
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ЗавершениеРаботы Тогда
		ЗакрытиеПодтверждено = Истина;
	КонецЕсли;
	
	Если НЕ ЗакрытиеПодтверждено Тогда

		Если НЕ Выполнено Тогда
			СтандартнаяОбработка = Ложь;
			Отказ = Истина;
			Отменить(Неопределено);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Отменить(Команда)
	ТекстВопроса = НСтр("ru='Создание сайта еще не завершено.';uk='Створення сайту ще не завершено.'");
	
	Кнопки = Новый СписокЗначений;
	Кнопки.Добавить(1, НСтр("ru='Подождать еще';uk='Почекати ще'"));
	Кнопки.Добавить(КодВозвратаДиалога.Прервать);
	
	ОтключитьОбработчикОжидания("ПроверитьВыполнение");
	
	Обработчик = Новый ОписаниеОповещения("ОтменитьЗавершение", ЭтотОбъект);
	ПоказатьВопрос(Обработчик, ТекстВопроса, Кнопки, 60,1);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПроверитьВыполнение()
	
	Если НомерДействия = (СписокДействий.Количество()) Тогда
		НомерДействия = 1;
	КонецЕсли;
	СопровождающийТекст = СписокДействий[НомерДействия];
	НомерДействия = НомерДействия + 1;
	
	ПодключитьОбработчикОжидания("ПроверитьВыполнение", ИнтервалПроверки, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтменитьЗавершение(Ответ, ДополнительныеПараметры) Экспорт
	
	ПрерватьЕслиНеВыполнено = (Ответ = КодВозвратаДиалога.Прервать);
	Если ПрерватьЕслиНеВыполнено Тогда
		ЗакрытиеПодтверждено = Истина;
		Закрыть();
	КонецЕсли;

	ПодключитьОбработчикОжидания("ПроверитьВыполнение", ИнтервалПроверки, Истина);
КонецПроцедуры

&НаСервере
Процедура ПриЗакрытииНаСервере()
	
	ДлительныеОперации.ОтменитьВыполнениеЗадания(ФоновоеЗаданиеИдентификатор);
	Сообщить(НСтр("ru='Создание сайта прервано';uk='Створення сайту перервано'"));
	
КонецПроцедуры

#КонецОбласти
