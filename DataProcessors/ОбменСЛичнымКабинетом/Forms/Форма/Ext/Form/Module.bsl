
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ИдентификаторКоманды = "";
	Параметры.Свойство("ИдентификаторКоманды", ИдентификаторКоманды);
	
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	
	// Инициализация вспомогательных констант
	//
	ПараметрыВызова = Новый Структура();
	ПараметрыВызова.Вставить("ИдентификаторКоманды", ИдентификаторКоманды);
	
	ОбработкаОбъект._ИнициализироватьКонстантыОбмена(КонстантыОбмена, ПараметрыВызова);
	
	// Инициализация реквизитов объекта
	//
	ОбработкаОбъект._ИнициализироватьРеквизитыОбъекта(КонстантыОбмена);
		
	// Инициализация реквизитов формы
	//
	Схема = ОбработкаОбъект.прПолучитьСхемуКомпоновкиДанныхТоваров();
	КомпоновщикТоваров.Инициализировать(
		Новый ИсточникДоступныхНастроекКомпоновкиДанных(
			ПоместитьВоВременноеХранилище(Схема, УникальныйИдентификатор)
		)
	); 
	КомпоновщикТоваров.ЗагрузитьНастройки(Схема.НастройкиПоУмолчанию);
	
	// Заполнение формы дополнительными данными
	//		
	_СоздатьРеквизитыШапкиЗаказа(КонстантыОбмена);
	
	_СоздатьРеквизитыТЧЗаказа(КонстантыОбмена);	
	
	ЗначениеВРеквизитФормы(ОбработкаОбъект, "Объект");
		
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	_ВыполнитьКомандуНаКлиенте();
	
	_ОбновитьДоступностьПолейШапкиЗаказа();
	
	прУстановитьДоступностьИВидимостьЭлементовФормы();
	
	прРазвернутьСвернутьГруппы(прПолучитьГруппыДополнительныхНастроек());
	
	//прОбновитьЗаголовкиКнопок();
	
	ИнформацияОКонтексте = прПолучитьИнформациюОКонтекстеВыполненияНаСервере();
	
	прЗаполнитьАдресаСайтов(ИнформацияОКонтексте);
	
	//Заголовок = ИнформацияОКонтексте["ВерсияДрайвера"];
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "СопоставлениеСвойств" Тогда
		
		ТекущаяСтрокаСвойств = Элементы.Свойства.ДанныеСтроки(Элементы.Свойства.ТекущаяСтрока);		
		ЗаполнитьЗначенияСвойств(ТекущаяСтрокаСвойств, Параметр);
		
		Для каждого ОбновляемыйРеквизит Из Параметр.ОбновляемыеРеквизиты Цикл
			Объект[ОбновляемыйРеквизит.Ключ] = ОбновляемыйРеквизит.Значение;
		КонецЦикла;
		
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьПапкуКэша(Команда)

	Если КонстантыОбмена.ВнутренняяОбработка Тогда
		_ВывестиСообщение(НСтр("ru='Каталог кэша находится на сервере 1С в папке:';uk='Каталог кеша знаходиться на сервері 1С в папці:'")+Символы.ПС+КонстантыОбмена.КаталогКэша);
	Иначе
		
		ИнформацияОКонтексте = прПолучитьИнформациюОКонтекстеВыполненияНаСервере();
		Если ИнформацияОКонтексте["ВариантБазы"] = "FILE" Тогда
			ЗапуститьПриложение(КонстантыОбмена.КаталогКэша);
		ИначеЕсли ИнформацияОКонтексте["ВариантБазы"] = "SRVR" Тогда
			_ВывестиСообщение(НСтр("ru='Каталог кэша находится на сервере 1С в папке:';uk='Каталог кеша знаходиться на сервері 1С в папці:'")+Символы.ПС+КонстантыОбмена.КаталогКэша);
		Иначе
			_ВывестиСообщение(НСтр("ru='Не удалось определить вариант соединения с информационной базой!';uk='Не вдалося визначити варіант з`єднання з інформаційною базою!'"));
		КонецЕсли;
		
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьЛогВФайл(Команда)
	
	ИнформацияОКонтексте = прПолучитьИнформациюОКонтекстеВыполненияНаСервере();
	
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	Диалог.Заголовок = "Сохранить логирование";
	Диалог.Фильтр = "Файл txt|*.txt";
	Если Не Диалог.Выбрать() Тогда
		Возврат;
	КонецЕсли;
	
	ТекстовыйФайл = Новый ТекстовыйДокумент;
	
	Для каждого СтрокаТаблицы Из Объект.Логи Цикл
		ТекстовыйФайл.ДобавитьСтроку(
			""+СтрокаТаблицы.Контекст+";"+
			""+Строка(СтрокаТаблицы.ДатаВремя)+";"+
			""+СтрокаТаблицы.Статус+";"+
			""+СтрокаТаблицы.Сообщение
		);
	КонецЦикла;
	
	ТекстовыйФайл.Записать(Диалог.ПолноеИмяФайла, КодировкаТекста.UTF8);
	
	прПредупреждение(НСтр("ru='Логирование сохранено.';uk='Логування збережено.'"), 30, НСтр("ru='Информация';uk='Інформація'"), ИнформацияОКонтексте);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьЕдиницыИзмерения(Команда)
	
	прСформироватьЕдиницыИзмеренияНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьВалюты(Команда)
	
	прСформироватьВалютыНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьТовары(Команда)
	
	_ВывестиСообщение(НСтр("ru='Действие: ""ЗаполнитьТовары"" [Начало]';uk='Дія: ""ЗаполнитьТовары"" [Начало]'"), , Ложь);
	
	прЗаполнитьТоварыНаСервере();
	
	//прОбновитьЗаголовкиКнопок();
	
	_ВывестиСообщение(НСтр("ru='Действие: ""ЗаполнитьТовары"" [Конец]';uk='Дія: ""ЗаполнитьТовары"" [Конец]'"), , Ложь);	
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьНастройки(Команда)

	СписокВнутреннихНастроек = Новый Структура;
	СписокВнутреннихНастроек.Вставить("ТекстНастроек", прСериализоватьНастройкиXMLНаСервере());
	_ХранилищеСохранитьНастройкиНаСервере(СписокВнутреннихНастроек);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьНастройки(Команда)
	
	ТекстНастроек = Неопределено;
	ЗначенияВнутреннихНастроек = _ХранилищеЗагрузитьНастройкиНаСервере();
	ЗначенияВнутреннихНастроек.Свойство("ТекстНастроек", ТекстНастроек);
	
	прДесериализоватьНастройкиXMLНаСервере(ТекстНастроек);
	
	
	// Обновление полей после загрузки настроек
	_ОбновитьДоступностьПолейШапкиЗаказа();		
	
	прУстановитьЛоготипИнтернетРесурса();
	
	прУстановитьДоступностьИВидимостьЭлементовФормы();
	
	//прОбновитьЗаголовкиКнопок();
	
	Объект.Логи.Очистить();
	
	_ПриИзмененииЛогироватьДействия();
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьНастройкиФайл(Команда)
	
	ДиалогВыбораФайла =	Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);

	ДиалогВыбораФайла.Фильтр = "Файл данных (*.xml)|*.xml";
	ДиалогВыбораФайла.Расширение = "xml";
	ДиалогВыбораФайла.Заголовок = "Выберите файл";
	ДиалогВыбораФайла.ПредварительныйПросмотр = Ложь;
	ДиалогВыбораФайла.ИндексФильтра = 0;
	ДиалогВыбораФайла.ПолноеИмяФайла = "";
	ДиалогВыбораФайла.ПроверятьСуществованиеФайла = Ложь;
	
	Если ДиалогВыбораФайла.Выбрать() Тогда		
		
		// Чтение настроек
		ТекстНастроек = Неопределено;
		ЗначенияВнутреннихНастроек = _ХранилищеЗагрузитьНастройкиНаСервере();
		ЗначенияВнутреннихНастроек.Свойство("ТекстНастроек", ТекстНастроек);
		
		// Сохранение настроек
		ТекстовыйДокумент = Новый ТекстовыйДокумент;
		ТекстовыйДокумент.ДобавитьСтроку(ТекстНастроек);
		ТекстовыйДокумент.Записать(ДиалогВыбораФайла.ПолноеИмяФайла);
		
		// Сообщение
		ИнформацияОКонтексте = прПолучитьИнформациюОКонтекстеВыполненияНаСервере();
		прПредупреждение(НСтр("ru='Настройки успешно сохранены';uk='Налаштування успішно збережені'"), , НСтр("ru='Сохранение настроек';uk='Збереження налаштувань'"), ИнформацияОКонтексте);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьНастройкиФайл(Команда)
	
	ДиалогВыбораФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);

	ДиалогВыбораФайла.Фильтр = "Файл данных (*.xml)|*.xml";
	ДиалогВыбораФайла.Расширение = "xml";
	ДиалогВыбораФайла.Заголовок = "Выберите файл";
	ДиалогВыбораФайла.ПредварительныйПросмотр = Ложь;
	ДиалогВыбораФайла.ИндексФильтра = 0;
	ДиалогВыбораФайла.ПолноеИмяФайла = "";
	ДиалогВыбораФайла.ПроверятьСуществованиеФайла = Ложь;
	
	Если ДиалогВыбораФайла.Выбрать() Тогда
		
		// Чтение настроек
		ТекстовыйДокумент = Новый ТекстовыйДокумент;
		ТекстовыйДокумент.Прочитать(ДиалогВыбораФайла.ПолноеИмяФайла);
		
		// Сохранение настроек в хранилище
		СписокВнутреннихНастроек = Новый Структура;
		СписокВнутреннихНастроек.Вставить("ТекстНастроек", ТекстовыйДокумент.ПолучитьТекст());
		_ХранилищеСохранитьНастройкиНаСервере(СписокВнутреннихНастроек);
		
		// Загрузка настроек
		ЗагрузитьНастройки(Неопределено);
		
		// Сообщение
		ИнформацияОКонтексте = прПолучитьИнформациюОКонтекстеВыполненияНаСервере();
		прПредупреждение(НСтр("ru='Настройки успешно загружены';uk='Налаштування успішно завантажені'"), , НСтр("ru='Загрузка настроек';uk='Завантаження налаштувань'"), ИнформацияОКонтексте);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьТовары(Команда)
	
	ИнформацияОКонтексте = прПолучитьИнформациюОКонтекстеВыполненияНаСервере();
	
	прВопрос(НСтр("ru='Выполнить выгрузку товаров?';uk='Виконати вивантаження товарів?'"), РежимДиалогаВопрос.ДаНет, 60, КодВозвратаДиалога.Да, НСтр("ru='Выгрузка товаров';uk='Вивантаження товарів'"), , "прВыгрузитьТоварыНаКлиенте", ИнформацияОКонтексте);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьЗаказы(Команда)

	ИнформацияОКонтексте = прПолучитьИнформациюОКонтекстеВыполненияНаСервере();
	
	прВопрос(НСтр("ru='Выполнить загрузку заказов?';uk='Виконати завантаження замовлень?'"), РежимДиалогаВопрос.ДаНет, 60, КодВозвратаДиалога.Да, НСтр("ru='Загрузка заказов';uk='Завантаження замовлень'"), , "прЗагрузитьЗаказыНаКлиенте", ИнформацияОКонтексте);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьСвойства(Команда)
	
	ИнформацияОКонтексте = прПолучитьИнформациюОКонтекстеВыполненияНаСервере();
	
	Если Объект.Свойства.Количество() > 0 Тогда			
		прВопрос(НСтр("ru='Выполнить заполнение свойств?';uk='Виконати заповнення властивостей?'"), РежимДиалогаВопрос.ДаНет, 60, КодВозвратаДиалога.Да, НСтр("ru='Заполнение свойств';uk='Заповнення властивостей'"), , "прЗаполнитьСвойстваНаКлиенте", ИнформацияОКонтексте);		
	Иначе
		прЗаполнитьСвойстваНаКлиенте(КодВозвратаДиалога.Да, ИнформацияОКонтексте);		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьОбновление(Команда)
	
	ПараметрыВызова = Новый Структура;
	ПараметрыВызова.Вставить("КонстантыОбмена", КонстантыОбмена);
	
	_ПроверитьОбновленияДрайвераОбменаНаСервере(ПараметрыВызова);
	
КонецПроцедуры

&НаКлиенте
Процедура СвойстваВключитьВсе(Команда)
	прУстановитьСнятьПризнак("Свойства", "Выгружать", Истина);
КонецПроцедуры

&НаКлиенте
Процедура СвойстваВыключитьВсе(Команда)
    прУстановитьСнятьПризнак("Свойства", "Выгружать", Ложь);	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьНаличие(Команда)
	прУстановитьСнятьПризнак("Товары", "Наличие", Истина);
КонецПроцедуры

&НаКлиенте
Процедура СнятьНаличие(Команда)
	прУстановитьСнятьПризнак("Товары", "Наличие", Ложь);
КонецПроцедуры

&НаКлиенте
Процедура ТоварыВключитьВсе(Команда)
	прУстановитьСнятьПризнак("Товары", "Выгружать", Истина);
	//прОбновитьЗаголовкиКнопок();
КонецПроцедуры

&НаКлиенте
Процедура ТоварыВыключитьВсе(Команда)
	прУстановитьСнятьПризнак("Товары", "Выгружать", Ложь);
	//прОбновитьЗаголовкиКнопок();
КонецПроцедуры

&НаКлиенте
Процедура УдалитьРегистрациюИзменений(Команда)
	прУдалитьРегистрациюИзмененийСервер();	
КонецПроцедуры

// : РАБОТА С НАСТРОЙКАМИ

&НаСервере
Функция прСериализоватьНастройкиXMLНаСервере() 
	
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	
	ПараметрыВызова = Новый Структура;
	ПараметрыВызова.Вставить("КомпоновщикТоваров", КомпоновщикТоваров);
	ПараметрыВызова.Вставить("РеквизитыШапкиЗаказа", _ПолучитьВыбранныеРеквизитыШапкиЗаказаРучногоЗаполнения(КонстантыОбмена));
	ПараметрыВызова.Вставить("РеквизитыТЧЗаказа", _ПолучитьВыбранныеРеквизитыТЧЗаказаРучногоЗаполнения(КонстантыОбмена));
	ПараметрыВызова.Вставить("РеквизитыИсключения", Новый Массив);
	
	ПараметрыВызова.РеквизитыИсключения.Добавить("АдресФайлаДанныхКатегорийАтрибутов");
	ПараметрыВызова.РеквизитыИсключения.Добавить("АдресФайлаКатегорийАтрибутов");
	ПараметрыВызова.РеквизитыИсключения.Добавить("Заказ_ДатаЗагрузки");
	
	ТекстНастроек = ОбработкаОбъект._СериализоватьНастройкиXML(ПараметрыВызова);
	
	ЗначениеВРеквизитФормы(ОбработкаОбъект, "Объект");
	
	Возврат ТекстНастроек;
	
КонецФункции

&НаСервере
Процедура прДесериализоватьНастройкиXMLНаСервере(ТекстНастроек) 
	
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	
	ПараметрыВызова = Новый Структура("КомпоновщикТоваров", КомпоновщикТоваров);
	
	Настройки = ОбработкаОбъект._ДесериализоватьНастройкиXML(ТекстНастроек, ПараметрыВызова);
	
	// Значение по умолчанию
	
	// Реквизиты шапки заказа
	Если Настройки.Свойство("РеквизитыШапкиЗаказа") Тогда
		ЗначениеРеквизита = Неопределено;
		Для каждого ИмяРеквизита Из _ПолучитьРеквизитыШапкиЗаказаРучногоЗаполнения(КонстантыОбмена) Цикл
			РеквизитВыбран = Настройки["РеквизитыШапкиЗаказа"].Свойство(ИмяРеквизита, ЗначениеРеквизита);
			ЭтаФорма[КонстантыОбмена.Заказ.ПрефиксФлага+ИмяРеквизита] = РеквизитВыбран;
			ЭтаФорма[КонстантыОбмена.Заказ.ПрефиксПоля+ИмяРеквизита] = ЗначениеРеквизита;							
		КонецЦикла;
	КонецЕсли;
	
	// Реквизиты ТЧ заказа
	Если Настройки.Свойство("РеквизитыТЧЗаказа") Тогда		
		
		ИмяТаблицыФормы = _ПолучитьИмяТаблицыФормы(КонстантыОбмена);
		
		РеквизитыТЧЗаказа = ЭтаФорма[ИмяТаблицыФормы];
		
		РеквизитыТЧ = РеквизитыТЧЗаказа[0];
		
		ЗаполнитьЗначенияСвойств(РеквизитыТЧ, Настройки.РеквизитыТЧЗаказа);
		
	КонецЕсли;
	
	ЗначениеВРеквизитФормы(ОбработкаОбъект, "Объект");
	
КонецПроцедуры

// : 

&НаСервере
Процедура прЗаполнитьТоварыНаСервере() 
	
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	
	ПараметрыВызова = Новый Структура;
	ПараметрыВызова.Вставить("КомпоновщикТоваров", КомпоновщикТоваров);
	ПараметрыВызова.Вставить("АдресЛога", АдресЛога);
	ПараметрыВызова.Вставить("КонстантыОбмена", КонстантыОбмена);
	
	ТаблицаТовары = ОбработкаОбъект.прПолучитьТовары(ПараметрыВызова);
	
	ОбработкаОбъект.Товары.Загрузить(ТаблицаТовары);
	
	ЗначениеВРеквизитФормы(ОбработкаОбъект, "Объект");
	
КонецПроцедуры

&НаКлиенте
Процедура прВыгрузитьТоварыНаКлиенте(Результат, ИнформацияОКонтексте) Экспорт
	
	Если Результат <> КодВозвратаДиалога.Да Тогда
		_ВывестиСообщение(НСтр("ru='Отказ пользователя от выгрузки товаров.';uk='Відмова користувача від вивантаження товарів.'"), СтатусСообщения.Внимание, Ложь);
		Возврат;
	КонецЕсли;
	
	_ВывестиСообщение(НСтр("ru='Действие: ""ВыгрузитьТовары"" [Начало]';uk='Дія: ""ВыгрузитьТовары"" [Начало]'"), , Ложь);
	
	ПараметрыВызова = Новый Структура;
	ПараметрыВызова.Вставить("КонстантыОбмена", КонстантыОбмена);
	ПараметрыВызова.Вставить("АдресЛога", АдресЛога);
	ПараметрыВызова.Вставить("КомпоновщикТоваров", КомпоновщикТоваров);
		
	прВыгрузитьТоварыНаСервере(ПараметрыВызова);
	
	_ВывестиСообщение(НСтр("ru='Действие: ""ВыгрузитьТовары"" [Конец]';uk='Дія: ""ВыгрузитьТовары"" [Конец]'"), , Ложь);
	
	прПредупреждение(НСтр("ru='Выгрузка товаров завершена.';uk='Вивантаження товарів завершене.'"), 30, НСтр("ru='Выгрузка товаров';uk='Вивантаження товарів'"), ИнформацияОКонтексте); 
	
КонецПроцедуры

&НаСервере
Процедура прВыгрузитьТоварыНаСервере(Параметры) 
	
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	
	ОбработкаОбъект._ВыгрузитьТовары(Параметры);
	
	ЗначениеВРеквизитФормы(ОбработкаОбъект, "Объект");
	
КонецПроцедуры

&НаКлиенте
Процедура прЗагрузитьЗаказыНаКлиенте(Результат, ИнформацияОКонтексте) Экспорт
	
	Если Результат <> КодВозвратаДиалога.Да Тогда
		_ВывестиСообщение(НСтр("ru='Отказ пользователя от загрузки заказов.';uk='Відмова користувача від завантаження замовлень.'"), СтатусСообщения.Внимание, Ложь);
		Возврат;
	КонецЕсли; 
	
	_ВывестиСообщение("Действие: ""ЗагрузитьЗаказы"" [Начало]", , Ложь);
	
	ПараметрыВызова = Новый Структура;
	ПараметрыВызова.Вставить("КонстантыОбмена", КонстантыОбмена);
	ПараметрыВызова.Вставить("ЗначенияРеквизитовШапкиЗаказаПоУмолчанию", _ПолучитьВыбранныеРеквизитыШапкиЗаказаРучногоЗаполнения(КонстантыОбмена));
	ПараметрыВызова.Вставить("ЗначенияРеквизитовТЧЗаказаПоУмолчанию", _ПолучитьВыбранныеРеквизитыТЧЗаказаРучногоЗаполнения(КонстантыОбмена));
		
	прЗагрузитьЗаказыНаСервере(ПараметрыВызова);	
	
	_ВывестиСообщение(НСтр("ru='Действие: ""ЗагрузитьЗаказы"" [Конец]';uk='Дія: ""ЗагрузитьЗаказы"" [Конец]'"), , Ложь);
	
	прПредупреждение(НСтр("ru='Загрузка заказов завершена.';uk='Завантаження замовлень завершено.'"), 30, НСтр("ru='Загрузка заказов';uk='Завантаження замовлень'"), ИнформацияОКонтексте);
	
КонецПроцедуры

&НаСервере
Процедура прЗагрузитьЗаказыНаСервере(Параметры) 
	
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	
	ОбработкаОбъект._ЗагрузитьЗаказы(Параметры);
	
	ЗначениеВРеквизитФормы(ОбработкаОбъект, "Объект");
	
КонецПроцедуры

&НаКлиенте
Процедура прЗаполнитьСвойстваНаКлиенте(Результат, ИнформацияОКонтексте) Экспорт
	
	Если Результат <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	Объект.Свойства.Очистить();
	
	прЗаполнитьТаблицуСвойствНаСервере();	
	
КонецПроцедуры

&НаСервере
Функция прПолучитьМетаданныеРеквизитовШапкиЗаказаРучногоЗаполнения(Параметры)	
	
	Результат = Новый Массив();
	
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	
	РеквизитыЗаказаАвтоЗаполнения = ОбработкаОбъект.прПолучитьРеквизитыЗаказаАвтоЗаполнения("Шапка");
	
	ЗакрытыеОбъектыМетаданных = _ПолучитьЗакрытыеОбъектыМетаданных();
	
	Для каждого мдРеквизита Из Метаданные.Документы[Параметры.Заказ.ИмяДокумента].Реквизиты Цикл
		
		Если РеквизитыЗаказаАвтоЗаполнения.Найти(мдРеквизита.Имя) <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Если _ЗакрытыйОбъектМетаданных(мдРеквизита, ЗакрытыеОбъектыМетаданных) Тогда
			Продолжить;
		КонецЕсли;
		
		Результат.Добавить(мдРеквизита);
		
	КонецЦикла;	
	
	ЗначениеВРеквизитФормы(ОбработкаОбъект, "Объект");
	
	Возврат Результат;
		
КонецФункции

&НаСервере
Функция прПолучитьМетаданныеРеквизитовТЧЗаказаРучногоЗаполнения(Параметры)
	
	Результат = Новый Массив();
	
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	
	РеквизитыЗаказаАвтоЗаполнения = ОбработкаОбъект.прПолучитьРеквизитыЗаказаАвтоЗаполнения("ТЧ");
	
	ЗакрытыеОбъектыМетаданных = _ПолучитьЗакрытыеОбъектыМетаданных();
	
	Для каждого мдРеквизита Из Метаданные.Документы[Параметры.Заказ.ИмяДокумента].ТабличныеЧасти[Параметры.Заказ.ИмяТЧТовары].Реквизиты Цикл
		
		Если РеквизитыЗаказаАвтоЗаполнения.Найти(мдРеквизита.Имя) <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Если _ЗакрытыйОбъектМетаданных(мдРеквизита, ЗакрытыеОбъектыМетаданных) Тогда
			Продолжить;
		КонецЕсли;
		
		Результат.Добавить(мдРеквизита);
		
	КонецЦикла;
	
	ЗначениеВРеквизитФормы(ОбработкаОбъект, "Объект");
	
	Возврат Результат;
		
КонецФункции

&НаКлиенте
Процедура прУстановитьСнятьПризнак(ТабличноеПоле, Поле, Значение)
	
	Если Объект[ТабличноеПоле].Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого ТекСтрока Из Объект[ТабличноеПоле] Цикл
		ТекСтрока[Поле] = Значение;	
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура прУдалитьРегистрациюИзмененийСервер()
	
	Отказ = Ложь;
	
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	
	тзТовары = Новый ТаблицаЗначений;
	Если ОбработкаОбъект.Товары.Количество() = 0 Тогда
		
		ПараметрыВызова = Новый Структура;
		ПараметрыВызова.Вставить("КонстантыОбмена", КонстантыОбмена);
		ПараметрыВызова.Вставить("АдресЛога", АдресЛога);
		ПараметрыВызова.Вставить("КомпоновщикТоваров", КомпоновщикТоваров);
		
		тзТовары = ОбработкаОбъект.прПолучитьТовары(ПараметрыВызова);

	Иначе
		тзТовары = ОбработкаОбъект.Товары.Выгрузить(ОбработкаОбъект.Товары.НайтиСтроки(Новый Структура("Выгружать", Истина)));
	КонецЕсли;
	
	ОбработкаОбъект.прУдалитьРегистрациюИзменений(Отказ, тзТовары);
	
	ЗначениеВРеквизитФормы(ОбработкаОбъект, "Объект");
	
КонецПроцедуры


// : ОБЩИЕ

&НаКлиенте
Процедура прУстановитьДоступностьИВидимостьЭлементовФормы()
	
	Если Объект.РежимОбмена = КонстантыОбмена.РежимыОбмена.Сайт Тогда
		Элементы.ГруппаСайт.Видимость = Истина;
		Элементы.ГруппаФайлы.Видимость = Ложь;
	Иначе
		Элементы.ГруппаСайт.Видимость = Объект.ИспользоватьПодключениеКСайту;
		Элементы.ГруппаФайлы.Видимость = Истина;
	КонецЕсли;
	
	Элементы.ГруппаПроксиСервер.Видимость = Объект.Прокси_Использование;
	
	Если Объект.ВыгружатьТолькоИзменения Тогда
		Элементы.УзелРегистрацииИзменений.Доступность = Истина;
		Элементы.ТоварыУдалитьРегистрациюИзменений.Доступность = Истина;
	Иначе
		Элементы.УзелРегистрацииИзменений.Доступность = Ложь;
		Элементы.ТоварыУдалитьРегистрациюИзменений.Доступность = Ложь;
	КонецЕсли;
	
	Элементы.Заказ_ГруппаНовойНоменклатуры.Видимость = КонстантыОбмена.СправочникНоменклатураИерархический;
	Элементы.Заказ_ГруппаНовыхКонтрагентов.Видимость = КонстантыОбмена.СправочникКонтрагентыИерархический;
	
Конецпроцедуры

&НаКлиенте
Процедура прЗаполнитьАдресаСайтов(ИнформацияОКонтексте) 
	
	АдресаСайтов = Новый Соответствие;
	АдресаСайтов.Вставить("http://my.prom.ua/", "http://my.prom.ua/");
	Для каждого _Адрес Из АдресаСайтов Цикл
		Элементы.Интернет_АдресСайта.СписокВыбора.Добавить(_Адрес.Значение);
	КонецЦикла;
	
	ИмяСвойства = "";
	Если ИнформацияОКонтексте["ВерсияПлатформы"] = "8.2" Тогда
		
		ИмяСвойства = "КнопкаСпискаВыбора";
		
	ИначеЕсли ИнформацияОКонтексте["ВерсияПлатформы"] = "8.3" Тогда
		
		Если ИнформацияОКонтексте["РежимСовместимости"] = "Версия8_1"
			ИЛИ ИнформацияОКонтексте["РежимСовместимости"] = "Версия8_2_13"
			ИЛИ ИнформацияОКонтексте["РежимСовместимости"] = "Версия8_2_16"
			ИЛИ ИнформацияОКонтексте["РежимСовместимости"] = "Версия8_3_1"
			ИЛИ ИнформацияОКонтексте["РежимСовместимости"] = "Версия8_3_2" Тогда
			ИмяСвойства = "КнопкаСпискаВыбора";
		Иначе
			ИмяСвойства = "КнопкаВыпадающегоСписка";
		КонецЕсли;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ИмяСвойства) Тогда
		Элементы.Интернет_АдресСайта[ИмяСвойства] = Истина;
	КонецЕсли;	
	
КонецПроцедуры

&НаСервере
Функция прПолучитьИнформациюОКонтекстеВыполненияНаСервере() 
	
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	
	Возврат ОбработкаОбъект._ПолучитьИнформациюОКонтекстеВыполнения(КонстантыОбмена);
	
КонецФункции

&НаСервере
Процедура прЗаполнитьТаблицуСвойствНаСервере()
	
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");	
		
	ОбработкаОбъект.прЗаполнитьДополнительныеРеквизитыИСведения();
	
	ЗначениеВРеквизитФормы(ОбработкаОбъект, "Объект");
	
КонецПроцедуры

&НаСервере
Процедура прСформироватьВалютыНаСервере() 
	
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	
	ТабличныйДокумент = ДопустимыеВалюты;
	
	Макет = ОбработкаОбъект.ПолучитьМакет("ДопустимыеВалюты");
	
	ТабличныйДокумент.Очистить();
	
	ТабличныйДокумент.Вывести(Макет);
	
	Для НомерСтроки = 2 По ТабличныйДокумент.ВысотаТаблицы Цикл
		
		ЯчейчаКод = ТабличныйДокумент.Область(НомерСтроки, 1); 
		ЯчейкаСокращение = ТабличныйДокумент.Область(НомерСтроки, 2); 
		ЯчейкаНаименование = ТабличныйДокумент.Область(НомерСтроки, 3); 
		
		КодВалюты = СокрЛП(ЯчейчаКод.Текст);
		Если ЗначениеЗаполнено(КодВалюты) Тогда 
			
			Если Справочники.Валюты.НайтиПоКоду(КодВалюты) <> Справочники.Валюты.ПустаяСсылка() Тогда 
				
				ЯчейчаКод.ЦветФона = WebЦвета.СветлоЗеленый;
				ЯчейкаСокращение.ЦветФона = WebЦвета.СветлоЗеленый;
				ЯчейкаНаименование.ЦветФона = WebЦвета.СветлоЗеленый;
				
			Иначе
				
				ЯчейкаРасшифровка = Новый Структура;
				ЯчейкаРасшифровка.Вставить("Код", КодВалюты);
				ЯчейкаРасшифровка.Вставить("Сокращение", СокрЛП(ЯчейкаСокращение.Текст));
				ЯчейкаРасшифровка.Вставить("Наименование", СокрЛП(ЯчейкаНаименование.Текст));
				
				ЯчейчаКод.Расшифровка = ЯчейкаРасшифровка;
				ЯчейкаСокращение.Расшифровка = ЯчейкаРасшифровка;
				ЯчейкаНаименование.Расшифровка = ЯчейкаРасшифровка;
				
			КонецЕсли;
			
		КонецЕсли;
					
	КонецЦикла;
	
	ОбработкаОбъект = ЗначениеВРеквизитФормы(ОбработкаОбъект, "Объект");
	
КонецПроцедуры

&НаСервере
Процедура прСформироватьЕдиницыИзмеренияНаСервере() 
	
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	
	ТаблицаВсехЕдиниц = Новый ТаблицаЗначений;
	ТаблицаВсехЕдиниц.Колонки.Добавить("Наименование");
	ТаблицаВсехЕдиниц.Колонки.Добавить("ИспользуетсяВ1С");
	ТаблицаВсехЕдиниц.Колонки.Добавить("ИспользуетсяНаСайте");
	
	
	ДанныеЕдиницИзмерений = ОбработкаОбъект.прПолучитьДанныеИспользуемыхЕдиницИзмерений(
		КонстантыОбмена.ДопустимыеЕдиницыИзмерения
	);
	
	Для каждого _Имя Из ДанныеЕдиницИзмерений.ЕдиницыИспользуемыеНаСайте Цикл
		НоваяСтрока = ТаблицаВсехЕдиниц.Добавить();
		НоваяСтрока.Наименование = _Имя;
		НоваяСтрока.ИспользуетсяНаСайте = 1;
	КонецЦикла;
	
	Для каждого _Имя Из ДанныеЕдиницИзмерений.ЕдиницыИспользуемыеВ1С Цикл
		НоваяСтрока = ТаблицаВсехЕдиниц.Добавить();
		НоваяСтрока.Наименование = _Имя;
		НоваяСтрока.ИспользуетсяВ1С = 1;
	КонецЦикла;
	
	ТаблицаВсехЕдиниц.Свернуть("Наименование", "ИспользуетсяВ1С, ИспользуетсяНаСайте");
	ТаблицаВсехЕдиниц.Сортировать("Наименование");
	
	
	ЕдиницыИзмерения.Очистить();
	
	Макет = ОбработкаОбъект.ПолучитьМакет("АнализЕдиницИзмерения");	
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	ОбластьСтрока = Макет.ПолучитьОбласть("Строка");
	
	ЕдиницыИзмерения.Вывести(ОбластьШапка);
	
	Для каждого СтрокаТаблицы Из ТаблицаВсехЕдиниц Цикл
		
		ОбластьСтрока.Параметры.Наименование = СтрокаТаблицы.Наименование;
		ОбластьСтрока.Параметры.ИспользуетсяВ1С = ?(СтрокаТаблицы.ИспользуетсяВ1С > 0, Истина, Ложь);
		ОбластьСтрока.Параметры.ИспользуетсяНаСайте = ?(СтрокаТаблицы.ИспользуетсяНаСайте > 0, Истина, Ложь);
		
		ЕдиницыИзмерения.Вывести(ОбластьСтрока);
		
		ТекущиеЯчейки = ЕдиницыИзмерения.Область(ЕдиницыИзмерения.ВысотаТаблицы, 1, ЕдиницыИзмерения.ВысотаТаблицы, 3);		
		Если СтрокаТаблицы.ИспользуетсяВ1С > 0 и СтрокаТаблицы.ИспользуетсяНаСайте > 0 Тогда
			ТекущиеЯчейки.ЦветФона = WebЦвета.СветлоЗеленый;
		ИначеЕсли СтрокаТаблицы.ИспользуетсяВ1С > 0 и СтрокаТаблицы.ИспользуетсяНаСайте = 0 Тогда
			ТекущиеЯчейки.ЦветФона = WebЦвета.Розовый;
		ИначеЕсли СтрокаТаблицы.ИспользуетсяВ1С = 0 и СтрокаТаблицы.ИспользуетсяНаСайте = 0 Тогда
			ТекущиеЯчейки.ЦветФона = WebЦвета.СеребристоСерый;
		ИначеЕсли СтрокаТаблицы.ИспользуетсяВ1С = 0 и СтрокаТаблицы.ИспользуетсяНаСайте > 0 Тогда
			
			ЯчейкаРасшифровка = Новый Структура;
			ЯчейкаРасшифровка.Вставить("Наименование", СтрокаТаблицы.Наименование);
			
			ТекущиеЯчейки.Расшифровка = ЯчейкаРасшифровка;
			
		КонецЕсли;		
				
	КонецЦикла;
	
	ОбработкаОбъект = ЗначениеВРеквизитФормы(ОбработкаОбъект, "Объект");
		
КонецПроцедуры

&НаКлиенте
Процедура прСоздатьВалютуПоРасшифровке(Расшифровка) 
	
	ИмяСправочника = КонстантыОбмена.Валюты.ИмяСправочника;
	
	НовыеДанные = Новый Структура;
	НовыеДанные.Вставить("Код", Расшифровка.Код);
	НовыеДанные.Вставить("Наименование", Расшифровка.Сокращение);
	НовыеДанные.Вставить("НаименованиеПолное", Расшифровка.Наименование);
			
	Форма = ПолучитьФорму("Справочник."+ИмяСправочника+".ФормаОбъекта", НовыеДанные);
	
	Форма.Объект.Код = Расшифровка.Код;
	Форма.Объект.Наименование = Расшифровка.Сокращение;
	
	Форма.Открыть();
	
	Форма.Модифицированность = Истина;

КонецПроцедуры

&НаКлиенте
Процедура прСоздатьЕдиницуИзмеренияПоРасшифровке(Расшифровка) 
	
	ИмяСправочника = КонстантыОбмена.ЕдиницыИзмерения.ИмяСправочника;
	
	НовыеДанные = Новый Структура;
	НовыеДанные.Вставить("Наименование", Расшифровка.Наименование);
			
	Форма = ПолучитьФорму("Справочник."+ИмяСправочника+".ФормаОбъекта", НовыеДанные);
	
	Форма.Объект.Наименование = Расшифровка.Наименование;
	
	Форма.Открыть();
	
	Форма.Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура прОбновитьЗаголовкиКнопок()
	
	// Кнопка "Выгрузить товары"
	ЗаголовокКнопкиВыгрузитьТовары = "Выгрузить товары %РежимОбмена% (%РежимВыгрузки%)";
	
	РежимОбмена = ?(Объект.РежимОбмена = КонстантыОбмена.РежимыОбмена.Сайт, "на сайт", "в файл");
	
	РежимВыгрузки = "";	
	Если Объект.Товары.Количество() = 0 Тогда
		РежимВыгрузки = "по условиям отбора";
	Иначе
		КолвоТоваровДляВыгрузки = Формат(Объект.Товары.НайтиСтроки(Новый Структура("Выгружать", Истина)).Количество(), "ЧН=0; ЧГ=0");
		РежимВыгрузки = "по заполенным товарам, кол-во товаров: "+КолвоТоваровДляВыгрузки;
	КонецЕсли;	
	
	ЗаголовокКнопкиВыгрузитьТовары = СтрЗаменить(ЗаголовокКнопкиВыгрузитьТовары, "%РежимОбмена%", РежимОбмена);
	ЗаголовокКнопкиВыгрузитьТовары = СтрЗаменить(ЗаголовокКнопкиВыгрузитьТовары, "%РежимВыгрузки%", РежимВыгрузки);
	
	Элементы.ФормаВыгрузитьТовары.Заголовок = ЗаголовокКнопкиВыгрузитьТовары;
	
	// Кнопка "Загрузить заказы"
	ЗаголовокКнопкиЗагрузитьЗаказы = "Загрузить заказы %РежимОбмена%";
	
	РежимОбмена = ?(Объект.РежимОбмена = КонстантыОбмена.РежимыОбмена.Сайт, "с сайта", "с файла");
	
	ЗаголовокКнопкиЗагрузитьЗаказы = СтрЗаменить(ЗаголовокКнопкиЗагрузитьЗаказы, "%РежимОбмена%", РежимОбмена);
	
	Элементы.ФормаЗагрузитьЗаказы.Заголовок = ЗаголовокКнопкиЗагрузитьЗаказы;
	
КонецПроцедуры

&НаКлиенте
Процедура прУстановитьЛоготипИнтернетРесурса()
	
	Логотип = _ПолучитьАдресЛоготипаИнтернетРесурса(КонстантыОбмена.АдресаСайтов);
	
КонецПроцедуры


// : РАБОТА С ГРУППАМИ ДОПОЛНИТЕЛЬНЫХ НАСТРОЕК

&НаКлиенте
Функция прПолучитьГруппыДополнительныхНастроек()
	
	Группы = Новый Массив;
	Группы.Добавить("ГруппаНастройкиДополнительныеНастройки");
	Группы.Добавить("ГруппаНастройкиВариантыВыгрузки");
	Группы.Добавить("ГруппаНастройкиПредопределенныеСвойства");
	Группы.Добавить("ГруппаЗагрузкаДополнительныеНастройки");
	Группы.Добавить("ГруппаЗагрузкаСправочники");
	Группы.Добавить("ГруппаЗагрузкаКэшированниеДанных");
	Группы.Добавить("ГруппаТовары");
	
	Возврат Группы;
	
КонецФункции

&НаКлиенте
Процедура прРазвернутьСвернутьГруппы(Группы)
	Для Каждого Группа Из Группы Цикл
		прРазвернутьСвернутьГруппу(Группа);
	КонецЦикла;	
КонецПроцедуры

&НаКлиенте
Процедура прРазвернутьСвернутьГруппу(Группа)	
	Элементы[Группа].Видимость = Не Элементы[Группа].Видимость;	
КонецПроцедуры

&НаКлиенте
Процедура НастройкиДополнительныеНастройки(Команда)	
	прРазвернутьСвернутьГруппу("ГруппаНастройкиДополнительныеНастройки");	
КонецПроцедуры

&НаКлиенте
Процедура НастройкиДополнительныеНастройкиВариантыВыгрузки(Команда)	
	прРазвернутьСвернутьГруппу("ГруппаНастройкиВариантыВыгрузки");	
КонецПроцедуры

&НаКлиенте
Процедура НастройкиДополнительныеНастройкиСвойства(Команда)	
	прРазвернутьСвернутьГруппу("ГруппаНастройкиПредопределенныеСвойства");	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузкаДополнительныеНастройки(Команда)
	прРазвернутьСвернутьГруппу("ГруппаЗагрузкаДополнительныеНастройки");	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузкаДополнительныеНастройкиСправочники(Команда)
	прРазвернутьСвернутьГруппу("ГруппаЗагрузкаСправочники");
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузкаДополнительныеНастройкиКэширование(Команда)
	прРазвернутьСвернутьГруппу("ГруппаЗагрузкаКэшированниеДанных");
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузкаТовары(Команда)
	прРазвернутьСвернутьГруппу("ГруппаТовары");	
КонецПроцедуры


// : МОДАЛЬНЫЕ И НЕМОДАЛЬНЫЕ МЕТОДЫ

&НаКлиенте
Процедура прВопрос(ТекстВопроса, Кнопки, Таймаут = 0, КнопкаПоУмолчанию = "", Заголовок = "", КнопкаТаймаута = Неопределено, ИмяПроцедуры, ИнформацияОКонтексте)
	
	//Код = "";
	//
	//Если ИнформацияОКонтексте["ВерсияПлатформы"] = "8.2" Тогда
	//	
	//	Код = "Ответ = Вопрос(НСтр(""ru = '""+ТекстВопроса+""'""), Кнопки, Таймаут, КнопкаПоУмолчанию, Заголовок, КнопкаТаймаута);
	//		|"+ИмяПроцедуры+"(Ответ, ИнформацияОКонтексте);";
	//	
	//ИначеЕсли ИнформацияОКонтексте["ВерсияПлатформы"] = "8.3" Тогда
	//	
	//	Если ИнформацияОКонтексте["РежимИспользованияМодальности"] = "НеИспользовать"
	//		ИЛИ ИнформацияОКонтексте["РежимИспользованияМодальности"] = "ИспользоватьСПредупреждениями" Тогда
	//		
	//		Код = "Оповещение = Новый ОписаниеОповещения(ИмяПроцедуры, ЭтаФорма, ИнформацияОКонтексте);
	//			|ПоказатьВоп
	//			
	//			
	//			
	//			рос(Оповещение, НСтр(""ru = '""+ТекстВопроса+""'""), Кнопки, Таймаут, КнопкаПоУмолчанию, Заголовок, КнопкаТаймаута);"
	//		
	//	Иначе
	//		
	//		Код = "Ответ = Вопрос(НСтр(""ru = '""+ТекстВопроса+""'""), Кнопки, Таймаут, КнопкаПоУмолчанию, Заголовок, КнопкаТаймаута);
	//			|"+ИмяПроцедуры+"(Ответ, ИнформацияОКонтексте);";
	//		
	//	КонецЕсли;
	//	
	//КонецЕсли;
	//
	//Выполнить(Код);
	
	Оповещение = Новый ОписаниеОповещения(ИмяПроцедуры, ЭтаФорма, ИнформацияОКонтексте);
	ПоказатьВопрос(Оповещение, ТекстВопроса, Кнопки, Таймаут, КнопкаПоУмолчанию, Заголовок, КнопкаТаймаута);  
	
КонецПроцедуры

&НаКлиенте
Процедура прПредупреждение(ТекстПредупреждения, Таймаут = 0, Заголовок = "", ИнформацияОКонтексте)
	
	//Код = "";
	//
	//Если ИнформацияОКонтексте["ВерсияПлатформы"] = "8.2" Тогда
	//	
	//	Код = "Предупреждение(ТекстПредупреждения, Таймаут, Заголовок)";
	//	
	//ИначеЕсли ИнформацияОКонтексте["ВерсияПлатформы"] = "8.3" Тогда
	//	
	//	Если ИнформацияОКонтексте["РежимИспользованияМодальности"] = "НеИспользовать"
	//		ИЛИ ИнформацияОКонтексте["РежимИспользованияМодальности"] = "ИспользоватьСПредупреждениями" Тогда
	//		
	//		Код = "ПоказатьПредупреждение(, ТекстПредупреждения, Таймаут, Заголовок)";
	//		
	//	Иначе
	//		
	//		Код = "Предупреждение(ТекстПредупреждения, Таймаут, Заголовок)";
	//		
	//	КонецЕсли;
	//	
	//КонецЕсли;
	//
	//Выполнить(Код);
	
	ПоказатьПредупреждение(, ТекстПредупреждения, Таймаут, Заголовок);
	
КонецПроцедуры

// : ОБЩИЕ

&НаКлиенте
Процедура _ВывестиСообщение(Текст, Состояние=Неопределено, ВыводитьСообщение=Истина, ЛогироватьСообщение=Истина)
	
	Если Не ВыводитьСообщение и Не ЛогироватьСообщение Тогда
		Возврат;
	КонецЕсли;
	
	СостояниеСообщения = ?(Состояние=Неопределено, СтатусСообщения.Информация, Состояние);
	
	ВременнаяМетка = ТекущаяДата();
	
	Если ВыводитьСообщение Тогда
		
		ВременнаяМеткаСтрокой = "[ "+Формат(ВременнаяМетка, "ДЛФ=DT")+"]";
		
		Сообщить("К: "+ВременнаяМеткаСтрокой +": "+ Текст, СостояниеСообщения);
		
	КонецЕсли;	

	Если Объект.ЛогироватьДействия и ЛогироватьСообщение Тогда
			
		НоваяСтрока = Объект.Логи.Добавить();
		
		НоваяСтрока.Контекст = "К";
		
		НоваяСтрока.ДатаВремя = ВременнаяМетка;
		
		НоваяСтрока.Сообщение = Текст;
		
		НоваяСтрока.Статус = Строка(СостояниеСообщения);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура _ПриИзмененииЛогироватьДействия()
	
	Если Объект.ЛогироватьДействия Тогда
		ИнформацияОКонтексте = прПолучитьИнформациюОКонтекстеВыполненияНаСервере();
		Для каждого ЭлементКонтекста Из ИнформацияОКонтексте Цикл
			_ВывестиСообщение(ЭлементКонтекста.Ключ+": "+Строка(ЭлементКонтекста.Значение), , Ложь);
		КонецЦикла;
	Иначе
		Объект.Логи.Очистить();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция _ПолучитьПолноеИмяФормы(ИмяФормы)
	
	СимволТочка = ".";
	ПозицияТочки = СтрДлина(ЭтаФорма.ИмяФормы);
	
	Пока Сред(ЭтаФорма.ИмяФормы, ПозицияТочки, 1) <> СимволТочка Цикл
		ПозицияТочки = ПозицияТочки - 1;
	КонецЦикла;
	
	Возврат Лев(ЭтаФорма.ИмяФормы, ПозицияТочки) + ИмяФормы;
	
КонецФункции

&НаСервере
Функция _ПолучитьАдресЛоготипаИнтернетРесурса(ИменаРесурсов) 
	
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	
	Макет = ОбработкаОбъект.ПолучитьМакет("ЛогоИнтернетРесурса");	
	
	ИмяРесурса = "";
	Для каждого ЭлементРесурс Из ИменаРесурсов Цикл
		Если Найти(ОбработкаОбъект.Интернет_АдресСайта, ЭлементРесурс.Ключ) > 0 Тогда
			ИмяРесурса = ЭлементРесурс.Значение;
			Прервать;
		КонецЕсли;	
	КонецЦикла;
	
	Картинка = Неопределено;
	Если ЗначениеЗаполнено(ИмяРесурса) Тогда
			
		Для Счетчик=1 По Макет.Рисунки.Количество() Цикл
			
			Рисунок = Макет.Рисунки.Получить(Счетчик-1);
			Если НРег(Рисунок.Имя) = НРег(ИмяРесурса) Тогда
				Картинка = Рисунок.Картинка;
				Прервать;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат ПоместитьВоВременноеХранилище(Картинка, ЭтаФорма.УникальныйИдентификатор);
		
КонецФункции

&НаСервере
Процедура _ПроверитьОбновленияДрайвераОбменаНаСервере(Параметры) 
	
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	
	ТекстНастроек = ОбработкаОбъект._ПроверитьОбновленияДрайвераОбмена(Параметры);
	
	ЗначениеВРеквизитФормы(ОбработкаОбъект, "Объект");
	
КонецПроцедуры


// : ЗАКАЗ : ШАПКА

&НаСервере
Функция _ПолучитьРеквизитыШапкиЗаказаРучногоЗаполнения(Параметры)
	
	Результат = Новый Массив;
	
	Для каждого мдРеквизита Из прПолучитьМетаданныеРеквизитовШапкиЗаказаРучногоЗаполнения(Параметры) Цикл
		Результат.Добавить(мдРеквизита.Имя);
	КонецЦикла;
	
	Возврат Результат;
		
КонецФункции

&НаСервере
Процедура _СоздатьРеквизитыШапкиЗаказа(Параметры) 
		
	// Реквизиты
	ДобавляемыеРеквизиты = Новый Массив;
	мдРеквизитов = Новый Соответствие;
	
	Для каждого мдРеквизита Из прПолучитьМетаданныеРеквизитовШапкиЗаказаРучногоЗаполнения(Параметры) Цикл
		
		// Флаг
		РеквизитФормы = Новый РеквизитФормы(
			Параметры.Заказ.ПрефиксФлага+мдРеквизита.Имя,
			Новый ОписаниеТипов("Булево"),
			,
			"Выбор " + мдРеквизита.Синоним
		);			
		ДобавляемыеРеквизиты.Добавить(РеквизитФормы);
		
		// Поле
		РеквизитФормы = Новый РеквизитФормы(
			Параметры.Заказ.ПрефиксПоля+мдРеквизита.Имя,
			мдРеквизита.Тип,
			,
			мдРеквизита.Синоним
		);			
		ДобавляемыеРеквизиты.Добавить(РеквизитФормы);
		
		мдРеквизитов.Вставить(РеквизитФормы, мдРеквизита);

	КонецЦикла;
		
	ИзменитьРеквизиты(ДобавляемыеРеквизиты);
	
	
	// Элементы
	
	Элементы["РеквизитыШапки"].Заголовок = "Заказ";
	
	Номер = 0;
	ПоловинаЭлементов = Окр(ДобавляемыеРеквизиты.Количество()/4, 0, РежимОкругления.Окр15как20);
	Для Номер=1 По ДобавляемыеРеквизиты.Количество()/2 Цикл
		
		Сторона = ?(Номер>ПоловинаЭлементов, "Право", "Лево");
		
		// Флаг
		РеквизитФормы = ДобавляемыеРеквизиты[(Номер-1)*2];
		Элемент = Элементы.Добавить(РеквизитФормы.Имя, Тип("ПолеФормы"), Элементы["ГруппаВыборГруппа"+Сторона+"ЗначенияЗаказовПоУмолчанию"]);
		Элемент.Вид = ВидПоляФормы.ПолеФлажка;
		Элемент.ПутьКДанным = РеквизитФормы.Имя;
		Элемент.ТолькоПросмотр = Ложь;
		Элемент.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
		Элемент.УстановитьДействие("ПриИзменении", "ПриИзмененииВыбораПоляШапкиЗаказа");
		
		// Поле
		РеквизитФормы = ДобавляемыеРеквизиты[(Номер*2)-1];
		
		мдРеквизита = мдРеквизитов[РеквизитФормы];
		
		Элемент = Элементы.Добавить(РеквизитФормы.Имя, Тип("ПолеФормы"), Элементы["ГруппаПолеГруппа"+Сторона+"ЗначенияЗаказовПоУмолчанию"]);
		Элемент.Вид = ВидПоляФормы.ПолеВвода;
		Элемент.ПутьКДанным = РеквизитФормы.Имя;
		Элемент.ТолькоПросмотр = Ложь;
		Элемент.Доступность = Ложь;		
		Элемент.Подсказка = мдРеквизита.Подсказка;
							
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура _ОбновитьДоступностьПоляШапкиЗаказа(ИмяРеквизита)
	
	ЗначениеВыбора = ЭтаФорма[КонстантыОбмена.Заказ.ПрефиксФлага+ИмяРеквизита];
	ЭтаФорма.Элементы[КонстантыОбмена.Заказ.ПрефиксПоля+ИмяРеквизита].Доступность = ЗначениеВыбора;
	
КонецПроцедуры

&НаКлиенте
Процедура _ОбновитьДоступностьПолейШапкиЗаказа()
	
	Для каждого ИмяРеквизита Из _ПолучитьРеквизитыШапкиЗаказаРучногоЗаполнения(КонстантыОбмена) Цикл
		_ОбновитьДоступностьПоляШапкиЗаказа(ИмяРеквизита);	
	КонецЦикла;
		
КонецПроцедуры

&НаСервере
Функция _ПолучитьВыбранныеРеквизитыШапкиЗаказаРучногоЗаполнения(Параметры) 
 
	Результат = Новый Структура;

	Для каждого ИмяРеквизита Из _ПолучитьРеквизитыШапкиЗаказаРучногоЗаполнения(Параметры) Цикл

		ПолеВыбрано = ЭтаФорма[Параметры.Заказ.ПрефиксФлага+ИмяРеквизита];
		Если Не ПолеВыбрано Тогда
			Продолжить;		
		КонецЕсли;

		ЗначениеПоля = ЭтаФорма[Параметры.Заказ.ПрефиксПоля+ИмяРеквизита];
		Результат.Вставить(ИмяРеквизита, ЗначениеПоля);

	КонецЦикла;

	Возврат Результат;
 
КонецФункции

&НаСервере
Функция _ПолучитьЗакрытыеОбъектыМетаданных()
		
	ЗакрытыеОбъектыМетаданных = Новый Массив;
	
	ОбъектыМетаданных = Новый ТаблицаЗначений;
	ОбъектыМетаданных.Колонки.Добавить("Объект");
	ОбъектыМетаданных.Колонки.Добавить("Состояние"); // 1 - включен, 0 - выключен
	
	Для Каждого ФункциональнаяОпция Из Метаданные.ФункциональныеОпции Цикл
		
		Если ФункциональнаяОпция.Состав.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		Если ФункциональнаяОпция.Хранение.Тип <> Новый ОписаниеТипов("Булево") Тогда
			Продолжить;
		КонецЕсли;
				
		Состояние = ?(ПолучитьФункциональнуюОпцию(ФункциональнаяОпция.Имя), 1, 0);
		
		Для Каждого ЭлементСостава Из ФункциональнаяОпция.Состав Цикл
			Если ЭлементСостава.Объект = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			НовСтрока = ОбъектыМетаданных.Добавить();
			НовСтрока.Объект = ЭлементСостава.Объект;
			НовСтрока.Состояние = Состояние;
		КонецЦикла;
		
	КонецЦикла;
	
	ОбъектыМетаданных.Свернуть("Объект", "Состояние");
	
	НайденныеСтроки = ОбъектыМетаданных.НайтиСтроки(Новый Структура("Состояние", 0));
	
	ЗакрытыеОбъектыМетаданных = ОбъектыМетаданных.Скопировать(НайденныеСтроки, "Объект").ВыгрузитьКолонку("Объект");
	
	Возврат ЗакрытыеОбъектыМетаданных;
	
КонецФункции

&НаСервере
Функция _ЗакрытыйОбъектМетаданных(мдРеквизита, ЗакрытыеОбъектыМетаданных)
	
	ЗакрытыйТип = Истина;
		
	ТипыРеквизита = мдРеквизита.Тип.Типы();		
	Для Каждого ТипРеквизита Из ТипыРеквизита Цикл
		ОбъектМетаданных = Метаданные.НайтиПоТипу(ТипРеквизита);
		Если ЗакрытыеОбъектыМетаданных.Найти(ОбъектМетаданных) = Неопределено Тогда
			ЗакрытыйТип = Ложь;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ЗакрытыйТип;
	
КонецФункции


// : ЗАКАЗ : ТЧ

&НаСервере
Процедура _СоздатьРеквизитыТЧЗаказа(Параметры)
	
	ИмяТаблицыФормы = _ПолучитьИмяТаблицыФормы(Параметры);
	
	// Реквизиты
	ДобавляемыеРеквизиты = Новый Массив;
	мдРеквизитов = Новый Соответствие;
	
	// ТЗ
	РеквизитФормы = Новый РеквизитФормы(ИмяТаблицыФормы, Новый ОписаниеТипов("ТаблицаЗначений"));
	ДобавляемыеРеквизиты.Добавить(РеквизитФормы);
	
	Для каждого мдРеквизита Из прПолучитьМетаданныеРеквизитовТЧЗаказаРучногоЗаполнения(Параметры) Цикл
		
		// ТЗ колонка
		РеквизитФормы = Новый РеквизитФормы(
			мдРеквизита.Имя,
			мдРеквизита.Тип,
			ИмяТаблицыФормы,
			мдРеквизита.Синоним
		);			
		
		ДобавляемыеРеквизиты.Добавить(РеквизитФормы);
		
		мдРеквизитов.Вставить(РеквизитФормы, мдРеквизита);

	КонецЦикла;
		
	ИзменитьРеквизиты(ДобавляемыеРеквизиты);
	
	
	// Элементы
	
	Элементы["РеквизитыТЧ"].Заголовок = "Заказ " + "(" + Параметры.Заказ.ИмяТЧТовары + ")";
	
	// ТЗ
	Элемент = Элементы.Добавить(ИмяТаблицыФормы, Тип("ТаблицаФормы"), Элементы["РеквизитыТЧ"]);
	Элемент.ПутьКДанным = ИмяТаблицыФормы;
	Элемент.ПоложениеКоманднойПанели = ПоложениеКоманднойПанелиЭлементаФормы.Нет;
	Элемент.ИзменятьСоставСтрок = Ложь;
	
	Для Номер = 1 По ДобавляемыеРеквизиты.Количество() - 1 Цикл
		
		РеквизитФормы = ДобавляемыеРеквизиты[Номер];
		РеквизитФормыИмя = РеквизитФормы.Имя;
		РеквизитФормыЗаголовок = РеквизитФормы.Заголовок;
		
		мдРеквизита = мдРеквизитов[РеквизитФормы];
		
		// ТЗ Колонка
		Элемент = Элементы.Добавить(ИмяТаблицыФормы + РеквизитФормыИмя, Тип("ПолеФормы"), Элементы[ИмяТаблицыФормы]);
		Элемент.Заголовок = РеквизитФормыЗаголовок;
		Элемент.ПутьКДанным = ИмяТаблицыФормы + "." + РеквизитФормыИмя;
		Элемент.Вид = ВидПоляФормы.ПолеВвода;
		Элемент.КнопкаОчистки = Истина;
		Элемент.Подсказка = мдРеквизита.Подсказка;
		
	КонецЦикла;
	
	// Пустая строка
	ЭтаФорма[ИмяТаблицыФормы].Добавить();
	
КонецПроцедуры

&НаСервере
Функция _ПолучитьВыбранныеРеквизитыТЧЗаказаРучногоЗаполнения(Параметры)
	
	Результат = Новый Структура;
	
	ИмяТаблицыФормы = _ПолучитьИмяТаблицыФормы(Параметры);
	
	РеквизитыТЧЗаказа = ЭтаФорма[ИмяТаблицыФормы].Выгрузить();
	
	РеквизитыТЧ = РеквизитыТЧЗаказа[0];
	
	Для Каждого ТекРеквизит Из РеквизитыТЧЗаказа.Колонки Цикл
		РеквизитИмя = ТекРеквизит.Имя;
		РеквизитЗначение = РеквизитыТЧ[РеквизитИмя];
		Если ЗначениеЗаполнено(РеквизитЗначение) Тогда
			Результат.Вставить(РеквизитИмя, РеквизитЗначение);
		КонецЕсли;		
	КонецЦикла;
	
	Возврат Результат;
 
КонецФункции

&НаСервере
Функция _ПолучитьИмяТаблицыФормы(Параметры)
	
	ТаблицаФормы = Параметры.Заказ.ИмяТЧТовары;
	ТаблицаФормыПрефикс = Параметры.Заказ.ПрефиксТаблицы;
	ТаблицыФормыИмя = ТаблицаФормыПрефикс + ТаблицаФормы;
	
	Возврат ТаблицыФормыИмя;
	
КонецФункции


// : ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ

&НаКлиенте
Процедура ПриИзмененииВыбораПоляШапкиЗаказа(Элемент)
		
	_ОбновитьДоступностьПоляШапкиЗаказа(СтрЗаменить(Элемент.Имя, КонстантыОбмена.Заказ.ПрефиксФлага, ""));
		
КонецПроцедуры

&НаКлиенте
Процедура РежимОбменаПриИзменении(Элемент)
	
	прУстановитьДоступностьИВидимостьЭлементовФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгружатьТолькоИзмененныеПриИзменении(Элемент)
	
	прУстановитьДоступностьИВидимостьЭлементовФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьПодключениеКСайтуПриИзменении(Элемент)
	
	прУстановитьДоступностьИВидимостьЭлементовФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ПроксиИспользованиеПриИзменении(Элемент)
	
	прУстановитьДоступностьИВидимостьЭлементовФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ЛогироватьДействияПриИзменении(Элемент)
	_ПриИзмененииЛогироватьДействия();
КонецПроцедуры

&НаКлиенте
Процедура ИмяФайлаОбменаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	Диалог.Заголовок = "Документ CML";
	Диалог.Фильтр = "Документ CML|*.cml";
	Если Диалог.Выбрать() Тогда
		Объект.ИмяФайлаОбмена = Диалог.ПолноеИмяФайла;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура СвойстваСвойствоСайтНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;	
						
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ЗакрыватьПриЗакрытииВладельца", Истина);
	ПараметрыФормы.Вставить("ЗакрыватьПриВыборе", Истина);
	ПараметрыФормы.Вставить("ОсновнаяФормаУИ", ЭтаФорма.УникальныйИдентификатор);
	ПараметрыФормы.Вставить("КонстантыОбмена", КонстантыОбмена);
	
	ФормаВыбораСвойства = ПолучитьФорму(_ПолучитьПолноеИмяФормы("ФормаСопоставленияСвойств"), ПараметрыФормы, ЭтаФорма, "ВыборСвойствСайта", ВариантОткрытияОкна.ОтдельноеОкно);
   			
	КопироватьДанныеФормы(Объект, ФормаВыбораСвойства.Объект);
	
	ФормаВыбораСвойства.Открыть();
	
КонецПроцедуры

&НаКлиенте
Процедура АдресСайтаПриИзменении(Элемент)	
	
	прУстановитьЛоготипИнтернетРесурса();
	
КонецПроцедуры

&НаКлиенте
Процедура ДопустимыеВалютыОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка)
	
	Если ТипЗнч(Расшифровка) = Тип("Структура") Тогда 
		
		СтандартнаяОбработка = Ложь;
		
		прСоздатьВалютуПоРасшифровке(Расшифровка);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЕдиницыИзмеренияОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка)
	
	Если ТипЗнч(Расшифровка) = Тип("Структура") Тогда 
		
		СтандартнаяОбработка = Ложь;
		
		прСоздатьЕдиницуИзмеренияПоРасшифровке(Расшифровка);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПриИзменении(Элемент)
	//прОбновитьЗаголовкиКнопок();	
КонецПроцедуры

&НаКлиенте
Процедура ПанельСтраницПриСменеСтраницы(Элемент, ТекущаяСтраница)
	//прОбновитьЗаголовкиКнопок();	
КонецПроцедуры

&НаКлиенте
Процедура СвойстваСвойствоСайтОчистка(Элемент, СтандартнаяОбработка)
	
	СвойстваТекущиеДанные = Элементы.Свойства.ТекущиеДанные;
	Если СвойстваТекущиеДанные <> Неопределено Тогда
		СвойстваТекущиеДанные.Наименование = "";
		СвойстваТекущиеДанные.Категория = "";
	КонецЕсли;	
	
КонецПроцедуры


// : ХРАНИЛИЩЕ НАСТРОЕК

&НаСервере
Процедура _ХранилищеСохранитьНастройкиНаСервере(СписокНастроек)
	
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	
	ОбработкаОбъект._ЗаписатьЗначенияВнутреннихНастроек(СписокНастроек);
	
	ЗначениеВРеквизитФормы(ОбработкаОбъект, "Объект");
	
КонецПроцедуры

&НаСервере
Функция _ХранилищеЗагрузитьНастройкиНаСервере()
	
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	
	ЗначенияНастроек = ОбработкаОбъект._ПолучитьЗначенияВнутреннихНастроек();
	
	ЗначениеВРеквизитФормы(ОбработкаОбъект, "Объект");	
	
	Возврат ЗначенияНастроек;
	
КонецФункции

&НаСервере
Процедура _ХранилищеУдалитьНастройкиНаСервере()
	
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	
	ОбработкаОбъект._ОчиститьЗначенияВнутреннихНастроек();
	
	ЗначениеВРеквизитФормы(ОбработкаОбъект, "Объект");
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьХранилищеНастроек(Команда)
	
	_ХранилищеУдалитьНастройкиНаСервере();
	
КонецПроцедуры


// : ИНТЕГРАЦИЯ В КОНФИГУРАЦИЮ

&НаКлиенте
Функция _ВыполнитьКомандуНаКлиенте()
	
	Если _ВыполнитьКомандуНаСервере(ПараметрЗапуска) Тогда
		ЗавершитьРаботуСистемы(Ложь);
	КонецЕсли;
		
КонецФункции

&НаСервере
Функция _ВыполнитьКомандуНаСервере(ПараметрЗапуска)
		
	Результат = Ложь;
	
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	
	Результат = ОбработкаОбъект._ВыполнитьКоманду(ПараметрЗапуска);
	
	ЗначениеВРеквизитФормы(ОбработкаОбъект, "Объект");
	
	Возврат Результат;
	
КонецФункции
