
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОсновнаяФормаУИ = Параметры.ОсновнаяФормаУИ;
	КонстантыОбмена = Параметры.КонстантыОбмена;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)		
	
	_ЗаполнитьДанныеНаСервере();		
	
КонецПроцедуры

&НаКлиенте
Процедура КнопкаНайтиКатегорию(Команда)
	
	_ВыполнитьПоискКатегорийНаКлиенте();
	
КонецПроцедуры

// : ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ

&НаКлиенте
Процедура КатегорииВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ДанныеСтрокиТаблицы = Элемент.ДанныеСтроки(ВыбраннаяСтрока);
	
	Если ДанныеСтрокиТаблицы.ЭтоРезультатПоиска Тогда
		
		СтандартнаяОбработка = Ложь;
		
		ИДСтроки = НайтиСтрокуКатегорииПоИдНаКлиенте(Категории.ПолучитьЭлементы(), ДанныеСтрокиТаблицы.ИД_Категории);
		Если ИДСтроки <> Неопределено Тогда
			Элементы.Категории.ТекущаяСтрока = ИДСтроки;
		КонецЕсли;				
				
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СтрокаПоискаКатегорииПриИзменении(Элемент)
	
	_ВыполнитьПоискКатегорийНаКлиенте();
		
КонецПроцедуры

&НаКлиенте
Процедура КатегорииПриАктивизацииСтроки(Элемент)
		
	ТекущиеДанные = Элементы.Категории.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыСтроки = Новый Структура;
	ПараметрыСтроки.Вставить("Категория", ТекущиеДанные.Категория);
	ПараметрыСтроки.Вставить("ИД_Категории", ТекущиеДанные.ИД_Категории);
	ПараметрыСтроки.Вставить("ЭтоРезультатПоиска", ТекущиеДанные.ЭтоРезультатПоиска);
	
	_КатегорииПриАктивизацииСтрокиНаСервере(ПараметрыСтроки);
		
КонецПроцедуры

&НаКлиенте
Процедура АтрибутыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекущиеДанныеКатегории = Элементы.Категории.ТекущиеДанные;
	Если ТекущиеДанныеКатегории = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеСтрокиТаблицы = Элемент.ДанныеСтроки(ВыбраннаяСтрока);
	
	ОбновляемыеРеквизиты = Новый Структура;
	ОбновляемыеРеквизиты.Вставить("АдресФайлаКатегорийАтрибутов", Объект.АдресФайлаКатегорийАтрибутов);
	ОбновляемыеРеквизиты.Вставить("АдресФайлаДанныхКатегорийАтрибутов", Объект.АдресФайлаДанныхКатегорийАтрибутов);
	
	ПараметрыВыбора = Новый Структура;
	ПараметрыВыбора.Вставить("ОбновляемыеРеквизиты", ОбновляемыеРеквизиты);
	ПараметрыВыбора.Вставить("СвойствоСайт", ДанныеСтрокиТаблицы.ИД_Атрибута);
	ПараметрыВыбора.Вставить("Наименование", ДанныеСтрокиТаблицы.Атрибут);
	ПараметрыВыбора.Вставить("Категория", ТекущиеДанныеКатегории.Категория);
	
	Оповестить("СопоставлениеСвойств", ПараметрыВыбора);
	
	Закрыть(Истина);
	
КонецПроцедуры

&НаКлиенте
Функция НайтиСтрокуКатегорииПоИдНаКлиенте(Элементы, ИД_Категории) 

	Результат = Неопределено;
	
	Для каждого Строка Из Элементы Цикл
		
		Если Строка.ЭтоРезультатПоиска Тогда
			Продолжить;
		КонецЕсли;		
		Если Строка.ИД_Категории = ИД_Категории Тогда			
			Результат = Строка.ПолучитьИдентификатор();
		КонецЕсли;
		
		Если Результат <> Неопределено Тогда
			Прервать;
		КонецЕсли;
		
		Результат = НайтиСтрокуКатегорииПоИдНаКлиенте(Строка.ПолучитьЭлементы(), ИД_Категории);
				
	КонецЦикла;
	
	Возврат Результат;
		
КонецФункции

&НаСервере
Процедура _ЗаполнитьДанныеНаСервере() 		
	
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
		
	Если ЗначениеЗаполнено(ОбработкаОбъект.АдресФайлаДанныхКатегорийАтрибутов) Тогда
		
		Кэш = ПолучитьИзВременногоХранилища(ОбработкаОбъект.АдресФайлаДанныхКатегорийАтрибутов);
		
		ЗначениеВРеквизитФормы(Кэш["Категории"], "Категории");
		
		Возврат;
		
	Иначе
		
		// если кэш записан в файл на сервере, то считываем его и помещаем во временно хранилище
		ФайлКэша = Новый Файл(КонстантыОбмена.КаталогКэша+"cache_ca_data.dat");
		Если ФайлКэша.Существует() Тогда
			
			ТекстовыйДокумент = Новый ТекстовыйДокумент;
			
			ТекстовыйДокумент.Прочитать(ФайлКэша.ПолноеИмя, КодировкаТекста.UTF8);
			
			Кэш = ЗначениеИзСтрокиВнутр(ТекстовыйДокумент.ПолучитьТекст());
			
			ОбработкаОбъект.АдресФайлаДанныхКатегорийАтрибутов = ПоместитьВоВременноеХранилище(Кэш, ОсновнаяФормаУИ);
			
			ЗначениеВРеквизитФормы(Кэш["Категории"], "Категории");

		Иначе
						
			Если Не ЗначениеЗаполнено(ОбработкаОбъект.АдресФайлаКатегорийАтрибутов) Тогда
				СлужебныеДанные = ОбработкаОбъект._ПолучитьДанныеССайта();
			КонецЕсли;
			
			НастройкиЗаполнения = Новый Структура;
			НастройкиЗаполнения.Вставить("УИОсновнойФоры", ОсновнаяФормаУИ);
			НастройкиЗаполнения.Вставить("АдресФайлаКатегорийАтрибутов", ОбработкаОбъект.АдресФайлаКатегорийАтрибутов);
			НастройкиЗаполнения.Вставить("КаталогКэша", КонстантыОбмена.КаталогКэша);
			
			ТаблицаСвойств = Новый ТаблицаЗначений;
			ОбработкаОбъект._ЗаполнитьТаблицыСвойствКатегорий(ТаблицаСвойств, НастройкиЗаполнения);
			
			Если ОбработкаОбъект.АдресФайлаКатегорийАтрибутов <> НастройкиЗаполнения.АдресФайлаКатегорийАтрибутов Тогда
				ОбработкаОбъект.АдресФайлаКатегорийАтрибутов = НастройкиЗаполнения.АдресФайлаКатегорийАтрибутов;
			КонецЕсли;
			
			ДеревоКатегорий = Новый ДеревоЗначений;
			ДеревоКатегорий.Колонки.Добавить("Категория");
			ДеревоКатегорий.Колонки.Добавить("ИД_Категории");
			ДеревоКатегорий.Колонки.Добавить("ЭтоРезультатПоиска");
			
			_ЗаполнитьСтроки(ДеревоКатегорий, ТаблицаСвойств, "0");
			
			Если ТаблицаСвойств.Количество() > 0 и ДеревоКатегорий.Строки.Количество() > 0 Тогда
				
				Кэш = Новый Структура;
				Кэш.Вставить("Свойства", ТаблицаСвойств);
				Кэш.Вставить("Категории", ДеревоКатегорий);
				
				ОбработкаОбъект.АдресФайлаДанныхКатегорийАтрибутов = ПоместитьВоВременноеХранилище(Кэш, ОсновнаяФормаУИ);
				
				// записываем файл кэша данных на сервер
				Файл = Новый ТекстовыйДокумент;
				Файл.УстановитьТекст(ЗначениеВСтрокуВнутр(Кэш));
				Попытка
					Файл.Записать(ФайлКэша.ПолноеИмя, КодировкаТекста.UTF8);
				Исключение
					Ошибка = ОписаниеОшибки();
				КонецПопытки;	
				
			КонецЕсли;
			
			ЗначениеВРеквизитФормы(ДеревоКатегорий, "Категории");
			
		КонецЕсли;		
		
	КонецЕсли;
		
	ЗначениеВРеквизитФормы(ОбработкаОбъект, "Объект");
	
КонецПроцедуры

&НаСервере
Функция _ЗаполнитьСтроки(Дерево, _Свойства, ИД)
	
	ТаблицаКатегорий = _Свойства.Скопировать(_Свойства.НайтиСтроки(Новый Структура("ИД_Категория_Родитель", ИД)), "ИД_Категории,Категория");
	ТаблицаКатегорий.Свернуть("ИД_Категории,Категория", "");
	
	Для каждого СтрокаКА Из ТаблицаКатегорий Цикл
		
		НоваяСтрока = Дерево.Строки.Добавить();
		
		НоваяСтрока.ИД_Категории = СтрокаКА.ИД_Категории;
		НоваяСтрока.Категория = СтрокаКА.Категория;
		НоваяСтрока.ЭтоРезультатПоиска = Ложь;
		
		_ЗаполнитьСтроки(НоваяСтрока, _Свойства, СтрокаКА.ИД_Категории);
		
	КонецЦикла;	
	
КонецФункции

&НаКлиенте
Процедура _ВыполнитьПоискКатегорийНаКлиенте() 
	
	_ВыполнитьПоискКатегорийНаСервере();
	
	Для Каждого Строка Из Категории.ПолучитьЭлементы() Цикл
		Элементы.Категории.Свернуть(Строка.ПолучитьИдентификатор());	
	КонецЦикла;	
	Для Каждого Строка Из Категории.ПолучитьЭлементы() Цикл    
		Если Строка.ЭтоРезультатПоиска Тогда
			Элементы.Категории.Развернуть(Строка.ПолучитьИдентификатор(), Истина);	
		КонецЕсли;		
		Прервать;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура _ВыполнитьПоискКатегорийНаСервере() 
	
	_Категории = РеквизитФормыВЗначение("Категории");
	
	_ПоискКатегории(_Категории);
	
	ЗначениеВРеквизитФормы(_Категории, "Категории");
	
КонецПроцедуры

&НаСервере
Процедура _СформироватьСтрокиНайденныхКатегорий(_Категории, НайденныеКатегории, ИскомаяКатегория) 
					
	Для каждого НайденнаяСтрока Из _ВыполнитьПоискКатегории(_Категории, ИскомаяКатегория) Цикл
		
		СтрокаНайденннойКатегории = НайденныеКатегории.Строки.Найти(НайденнаяСтрока.ИД_Категории, "ИД_Категории", Истина);
		Если СтрокаНайденннойКатегории = Неопределено Тогда
			
			РодителиСтроки = Новый Массив;
			
			Родитель = НайденнаяСтрока.Родитель;
			Пока Родитель <> Неопределено Цикл
				РодителиСтроки.Добавить(Родитель);
				Родитель = Родитель.Родитель;
			КонецЦикла;
			
			СтрокиДерева = НайденныеКатегории.Строки;
			ИндексРодителя = РодителиСтроки.Количество()-1;
			
			Пока ИндексРодителя <> -1 Цикл
				
				СтрокаРодителя = РодителиСтроки[ИндексРодителя];					
				
				НоваяСтрока = _СоздатьКатегориюПоискаПриОтсутствии(НайденныеКатегории, СтрокиДерева, СтрокаРодителя.Категория, СтрокаРодителя.ИД_Категории);
				
				СтрокиДерева = НоваяСтрока.Строки;
				
				ИндексРодителя = ИндексРодителя - 1;
				
			КонецЦикла;
							
			НоваяСтрока = _СоздатьКатегориюПоискаПриОтсутствии(НайденныеКатегории, СтрокиДерева, НайденнаяСтрока.Категория, НайденнаяСтрока.ИД_Категории);
							
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция _СоздатьКатегориюПоискаПриОтсутствии(Дерево, Строки, Категория, ИД_Категории) 
	
	СтрокаНайденннойКатегории = Дерево.Строки.Найти(ИД_Категории, "ИД_Категории", Истина);
	Если СтрокаНайденннойКатегории = Неопределено Тогда
		
		НоваяСтрока = Строки.Добавить();										
		
		НоваяСтрока.Категория = Категория;
		НоваяСтрока.ИД_Категории = ИД_Категории;
		НоваяСтрока.ЭтоРезультатПоиска = Истина;
		
		Возврат НоваяСтрока;
		
	Иначе
		Возврат СтрокаНайденннойКатегории;		
	КонецЕсли;
	
КонецФункции

&НаСервере
Процедура _ПоискКатегории(_Категории) 
	
	ИмяКатегорииПоиска = "-= РЕЗУЛЬТАТ ПОИСКА =-";
	
	СтрокаПоиска = СтрокаПоискаКатегории;
		
	НайденныеСтроки = _Категории.Строки.НайтиСтроки(Новый Структура("Категория,ИД_Категории", ИмяКатегорииПоиска, ""));	
	Пока НайденныеСтроки.Количество() <> 0 Цикл
		НайденныеСтроки[0].Строки.Очистить();
		_Категории.Строки.Удалить(НайденныеСтроки[0]);
		НайденныеСтроки.Удалить(0);
	КонецЦикла;	
	
	Если ПустаяСтрока(СтрокаПоиска) Тогда
		Возврат;
	КонецЕсли;
	
	Если СтрДлина(СтрокаПоиска) < 3 Тогда
		Возврат;
	КонецЕсли;
		
	СтрокаРезультатаПоиска = _Категории.Строки.Вставить(0);
		
	СтрокаРезультатаПоиска.Категория = ИмяКатегорииПоиска;
	СтрокаРезультатаПоиска.ИД_Категории = Неопределено;
	СтрокаРезультатаПоиска.ЭтоРезультатПоиска = Истина;
	
	
	_СформироватьСтрокиНайденныхКатегорий(_Категории, СтрокаРезультатаПоиска, СтрокаПоиска);
	
КонецПроцедуры

&НаСервере
Функция _ВыполнитьПоискКатегории(Дерево, ИскомаяКатегория)
	
	Результат = Новый Массив;
	
	нрИскомаяКатегория = НРег(ИскомаяКатегория);
	
	Для каждого СтрокаДерева  Из Дерево.Строки Цикл
		
		Если Найти(НРег(СтрокаДерева.Категория), нрИскомаяКатегория) > 0 Тогда
			Результат.Добавить(СтрокаДерева);
		КонецЕсли;
		
		Если СтрокаДерева.Строки.Количество() > 0 Тогда
			
			Для каждого НайденнаяСтрока Из _ВыполнитьПоискКатегории(СтрокаДерева, нрИскомаяКатегория) Цикл
				Результат.Добавить(НайденнаяСтрока);
			КонецЦикла;
			
		КонецЕсли;
				
	КонецЦикла;
		
	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура _КатегорииПриАктивизацииСтрокиНаСервере(Параметры)
	
	_Атрибуты = РеквизитФормыВЗначение("Атрибуты");	
	
	Кэш = ПолучитьИзВременногоХранилища(Объект.АдресФайлаДанныхКатегорийАтрибутов);	
	Если Кэш = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	_Свойства = Кэш["Свойства"];
	_Категории = Кэш["Категории"];
	
	
	_Атрибуты.Очистить();
			
	НайденныеКатегории = _Категории.Строки.НайтиСтроки(Новый Структура("ИД_Категории, ЭтоРезультатПоиска", Параметры.ИД_Категории, Ложь), Истина);
	Если НайденныеКатегории.Количество() = 0 Тогда
		Возврат;
	Иначе
		СтрокаКатегории = НайденныеКатегории[0];
	КонецЕсли;
				
	Если СтрокаКатегории.Строки.Количество() = 0 Тогда
		
		Для каждого СтрокаТаблицы Из _Свойства.НайтиСтроки(Новый Структура("ИД_Категории", СтрокаКатегории.ИД_Категории)) Цикл
			
			НоваяСтрока = _Атрибуты.Добавить();
			
			НоваяСтрока.Атрибут = СтрокаТаблицы.Атрибут;
			НоваяСтрока.ИД_Атрибута = СтрокаТаблицы.ИД_Атрибута;
			
		КонецЦикла;
		
		_Атрибуты.Сортировать("Атрибут");
		
	КонецЕсли;		
		
	ЗначениеВРеквизитФормы(_Атрибуты, "Атрибуты");
	
КонецПроцедуры



