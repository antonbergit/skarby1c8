
// ПЕРЕОПРЕДЕЛЯЕМЫЕ: ФОРМИРОВАНИЕ ДАННЫХ

Процедура прИнициализироватьКонстантыОбмена(Источник)
	
	Источник.Вставить("Заказ", Новый Структура);
	Источник["Заказ"].Вставить("ИмяДокумента", "ЗаказПокупателя");
	Источник["Заказ"].Вставить("ИмяРеквизитаНомерЗаказаВходящий", "НомерВходящегоДокумента");
	Источник["Заказ"].Вставить("ИмяТЧТовары", "Запасы");
	Источник["Заказ"].Вставить("ИмяКонстантыВалютаУчета", "ВалютаУчета");
	Источник["Заказ"].Вставить("ПрефиксФлага", "_вр_");
	Источник["Заказ"].Вставить("ПрефиксПоля", "_зп_");
	Источник["Заказ"].Вставить("ПрефиксТаблицы", "_тч_");
	Источник["Заказ"].Вставить("СтавкиНДС", прПолучитьСтавкиНДС());
	
	Источник.Вставить("Номенклатура", Новый Структура);
	Источник["Номенклатура"].Вставить("ТипыНоменклатуры", прПолучитьТипыНоменклатуры());
	Источник["Номенклатура"].Вставить("ВидыНоменклатуры", прПолучитьВидыНоменклатуры());
	
	Источник.Вставить("Характеристика", Новый Структура);
	Источник["Характеристика"].Вставить("ИмяКонстантыИспользоватьХарактеристики", "ФункциональнаяОпцияИспользоватьХарактеристики");
	
	Источник.Вставить("ЕдиницыИзмерения", Новый Структура);
	Источник["ЕдиницыИзмерения"].Вставить("ИмяСправочника", "КлассификаторЕдиницИзмерения");
	
	Источник.Вставить("Валюты", Новый Структура);
	Источник["Валюты"].Вставить("ИмяСправочника", "Валюты");
	
КонецПроцедуры

Процедура прПроверитьКолонкиТЗТовары(тзТовары, Параметры)
	
	СоответствиеОбязательныхКолонок = Новый Соответствие;
	СоответствиеОбязательныхКолонок.Вставить("Номенклатура", Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	СоответствиеОбязательныхКолонок.Вставить("Характеристика", Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
	СоответствиеОбязательныхКолонок.Вставить("ТипЦен", Новый ОписаниеТипов("СправочникСсылка.ВидыЦен"));
	СоответствиеОбязательныхКолонок.Вставить("ТипЦенСайт", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(15)));
	СоответствиеОбязательныхКолонок.Вставить("Цена", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 2)));
	СоответствиеОбязательныхКолонок.Вставить("Валюта", Новый ОписаниеТипов("СправочникСсылка.Валюты"));
	СоответствиеОбязательныхКолонок.Вставить("Количество", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 3)));
	
	Для Каждого Колонка Из СоответствиеОбязательныхКолонок Цикл
		Если тзТовары.Колонки.Найти(Колонка.Ключ) = Неопределено Тогда
			тзТовары.Колонки.Добавить(Колонка.Ключ, Колонка.Значение);
		КонецЕсли;	
	КонецЦикла;	
	
КонецПроцедуры

Функция прСформироватьЗапросОсновныеДанные() 
	
	ТекстЗапроса = "
	|// -= 0 -=
	|ВЫБРАТЬ
	|	Т.Валюта,
	|	Т.Наличие,
	|	Т.Количество,
	|	Т.ТипЦен,
	|	Т.ТипЦенСайт,
	|	Т.Цена,	
	|	Т.Характеристика,
	|	Т.Номенклатура
	|ПОМЕСТИТЬ втТовары
	|ИЗ
	|	&Товары КАК Т
	|;
	|
	|// -= 1 -=
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Т.Характеристика,
	|	Т.Номенклатура	
	|ПОМЕСТИТЬ втТоварыБезДублей
	|ИЗ
	|	втТовары КАК Т
	|;	
	|
	|////////////////////////////////////////////////////////////////////////////////
	|// -= 2 -=
	|ВЫБРАТЬ
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.Характеристика КАК Характеристика,
	|	НоменклатураСправочник.Код КАК Код,
	|	НоменклатураСправочник.Артикул КАК Артикул,
	|	НоменклатураСправочник.Наименование КАК Наименование,
	|	НоменклатураСправочник.НаименованиеПолное КАК НаименованиеПолное,
	|	НоменклатураСправочник.Комментарий КАК Описание,
	|	НоменклатураСправочник.Родитель КАК Родитель,
	|	НоменклатураСправочник.ЭтоГруппа КАК ЭтоГруппа,
	|	ПРЕДСТАВЛЕНИЕ(НоменклатураСправочник.ЕдиницаИзмерения) КАК ЕдиницаИзмерения
	|ИЗ
	|	втТоварыБезДублей КАК Товары
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК НоменклатураСправочник
	|		ПО Товары.Номенклатура = НоменклатураСправочник.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|// -= 3 -=
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Т.Валюта,
	|	Т.Валюта.Код КАК ВалютаКод,
	|	Т.Валюта.Наименование КАК ВалютаСокращение,	
	|	Т.ТипЦен,
	|	Т.Цена,
	|	Т.ТипЦенСайт,
	|	Т.Характеристика,
	|	Т.Номенклатура
	|ИЗ
	|	втТовары КАК Т
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|// -= 4 -=
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Т.Номенклатура,
	|	Т.Характеристика,
	|	Т.Наличие,
	|	Т.Количество	
	|ИЗ
	|	втТовары КАК Т
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|// -= 5 -=
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Т.ТипЦен,
	|	Т.ТипЦен.Наименование КАК ТипЦенНаименование,
	|	Т.ТипЦенСайт,
	|	Т.Валюта,
	|	Т.Валюта.Код КАК ВалютаКод,
	|	Т.Валюта.Наименование КАК ВалютаСокращение	
	|ИЗ
	|	втТовары КАК Т
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|// -= 6 -=
	|ВЫБРАТЬ
	|	Номенклатура.Ссылка КАК Ссылка,
	|	Номенклатура.Наименование,
	|	Номенклатура.Родитель,
	|	Номенклатура.ЭтоГруппа
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура
	|ГДЕ
	|	Номенклатура.Ссылка В
	|			(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|				Т.Номенклатура
	|			ИЗ
	|				втТовары КАК Т)
	|	И Номенклатура.Родитель <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|ИТОГИ ПО
	|	Ссылка ТОЛЬКО ИЕРАРХИЯ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|// -= 7 -=
	|ВЫБРАТЬ
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	СправочникНоменклатура.Родитель
	|ИЗ
	|	втТоварыБезДублей КАК Товары
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|	ПО Товары.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	СправочникНоменклатура.Родитель <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|// -= 8 -=
	|ВЫБРАТЬ
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.Характеристика КАК Характеристика,
	|	СправочникНоменклатура.Наименование КАК ТоварНаименование,
	|	СправочникНоменклатура.Код КАК ТоварКод,
	|	НоменклатураПрисоединенныеФайлы.Ссылка КАК Хранилище,
	|	НоменклатураПрисоединенныеФайлы.Наименование КАК ХранилищеНаименование,
	|	НоменклатураПрисоединенныеФайлы.ТипХраненияФайла КАК ТипХраненияФайла,
	|	НоменклатураПрисоединенныеФайлы.ПутьКФайлу КАК ПутьКФайлу,
	|	НоменклатураПрисоединенныеФайлы.Том КАК Том,
	|	НоменклатураПрисоединенныеФайлы.Размер КАК Размер,
	|	ПрисоединенныеФайлы.ХранимыйФайл КАК ДанныеХранилища,
	|	ВЫБОР
	|		КОГДА СправочникНоменклатура.ФайлКартинки = НоменклатураПрисоединенныеФайлы.Ссылка
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЭтоОсновноеИзображение
	|ИЗ
	|	втТоварыБезДублей КАК Товары
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.НоменклатураПрисоединенныеФайлы КАК НоменклатураПрисоединенныеФайлы
	|		ПО (НоменклатураПрисоединенныеФайлы.ВладелецФайла = Товары.Номенклатура)
	|			И (НЕ НоменклатураПрисоединенныеФайлы.ПометкаУдаления)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПрисоединенныеФайлы КАК ПрисоединенныеФайлы
	|		ПО (ПрисоединенныеФайлы.ПрисоединенныйФайл = НоменклатураПрисоединенныеФайлы.Ссылка)
	|			И (ПрисоединенныеФайлы.ПрисоединенныйФайл ССЫЛКА Справочник.НоменклатураПрисоединенныеФайлы)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО Товары.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	&ВыгружатьКартинки
	|	И ВЫБОР
	|			КОГДА НоменклатураПрисоединенныеФайлы.ТипХраненияФайла = ЗНАЧЕНИЕ(Перечисление.ТипыХраненияФайлов.ВИнформационнойБазе)
	|				ТОГДА НЕ ПрисоединенныеФайлы.ХранимыйФайл ЕСТЬ NULL 
	|			КОГДА НоменклатураПрисоединенныеФайлы.ТипХраненияФайла = ЗНАЧЕНИЕ(Перечисление.ТипыХраненияФайлов.ВТомахНаДиске)
	|				ТОГДА НЕ НоменклатураПрисоединенныеФайлы.Том = ЗНАЧЕНИЕ(Справочник.ТомаХраненияФайлов.ПустаяСсылка)
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ
	|;
	|	
	|////////////////////////////////////////////////////////////////////////////////
	|// -= 9 -=
	|ВЫБРАТЬ
	|	""Н"" КАК Источник,
	|	""Р"" КАК ТипИсточника,
	|	1 КАК Вес,
	|	втТоварыБезДублей.Номенклатура,
	|	втТоварыБезДублей.Номенклатура.Наименование КАК НоменклатураНаименование,
	|	втТоварыБезДублей.Характеристика,
	|	НоменклатураДополнительныеРеквизиты.Свойство,
	|	НоменклатураДополнительныеРеквизиты.Свойство.Заголовок КАК Наименование,
	|	НоменклатураДополнительныеРеквизиты.Значение
	|ПОМЕСТИТЬ втТоварыСвойства
	|ИЗ
	|	втТоварыБезДублей КАК втТоварыБезДублей
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура.ДополнительныеРеквизиты КАК НоменклатураДополнительныеРеквизиты
	|		ПО втТоварыБезДублей.Номенклатура = НоменклатураДополнительныеРеквизиты.Ссылка
	|ГДЕ
	|	НЕ НоменклатураДополнительныеРеквизиты.Ссылка ЕСТЬ NULL
	|	И НоменклатураДополнительныеРеквизиты.Свойство В(&СвойстваВыбранные)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	""Х"",
	|	""Р"",
	|	2,
	|	втТоварыБезДублей.Номенклатура,
	|	втТоварыБезДублей.Номенклатура.Наименование,
	|	втТоварыБезДублей.Характеристика,
	|	ХарактеристикиДополнительныеРеквизиты.Свойство,
	|	ХарактеристикиДополнительныеРеквизиты.Свойство.Заголовок,
	|	ХарактеристикиДополнительныеРеквизиты.Значение
	|ИЗ
	|	втТоварыБезДублей КАК втТоварыБезДублей
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ХарактеристикиНоменклатуры.ДополнительныеРеквизиты КАК ХарактеристикиДополнительныеРеквизиты
	|		ПО втТоварыБезДублей.Характеристика = ХарактеристикиДополнительныеРеквизиты.Ссылка
	|ГДЕ
	|	НЕ ХарактеристикиДополнительныеРеквизиты.Ссылка ЕСТЬ NULL
	|	И ХарактеристикиДополнительныеРеквизиты.Свойство В(&СвойстваВыбранные)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	""Н"",
	|	""С"",
	|	1,
	|	втТоварыБезДублей.Номенклатура,
	|	втТоварыБезДублей.Номенклатура.Наименование,
	|	втТоварыБезДублей.Характеристика,
	|	НоменклатураДополнительныеСведения.Свойство,
	|	НоменклатураДополнительныеСведения.Свойство.Заголовок,
	|	НоменклатураДополнительныеСведения.Значение
	|ИЗ
	|	втТоварыБезДублей КАК втТоварыБезДублей
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДополнительныеСведения КАК НоменклатураДополнительныеСведения
	|		ПО втТоварыБезДублей.Номенклатура = НоменклатураДополнительныеСведения.Объект
	|ГДЕ
	|	НЕ НоменклатураДополнительныеСведения.Объект ЕСТЬ NULL
	|	И НоменклатураДополнительныеСведения.Свойство В(&СвойстваВыбранные)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|// -= 10 -=
	|ВЫБРАТЬ
	|	втТоварыСвойства.Источник,
	|	втТоварыСвойства.ТипИсточника,
	|	втТоварыСвойства.Номенклатура КАК Номенклатура,
	|	втТоварыСвойства.НоменклатураНаименование,
	|	втТоварыСвойства.Характеристика,
	|	втТоварыСвойства.Свойство КАК Свойство,
	|	втТоварыСвойства.Наименование,
	|	втТоварыСвойства.Значение
	|ИЗ
	|	втТоварыСвойства КАК втТоварыСвойства
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			МАКСИМУМ(втТоварыСвойства.Вес) КАК Вес,
	|			втТоварыСвойства.Номенклатура КАК Номенклатура,
	|			втТоварыСвойства.Характеристика КАК Характеристика,
	|			втТоварыСвойства.Свойство КАК Свойство
	|		ИЗ
	|			втТоварыСвойства КАК втТоварыСвойства
	|		
	|		СГРУППИРОВАТЬ ПО
	|			втТоварыСвойства.Номенклатура,
	|			втТоварыСвойства.Характеристика,
	|			втТоварыСвойства.Свойство) КАК ПриоритетныеСвойства
	|		ПО втТоварыСвойства.Номенклатура = ПриоритетныеСвойства.Номенклатура
	|			И втТоварыСвойства.Характеристика = ПриоритетныеСвойства.Характеристика
	|			И втТоварыСвойства.Свойство = ПриоритетныеСвойства.Свойство
	|			И втТоварыСвойства.Вес = ПриоритетныеСвойства.Вес
	|ИТОГИ ПО
	|	Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|// -= 11 -=
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	втТоварыСвойства.Свойство,
	|	втТоварыСвойства.Наименование
	|ИЗ
	|	втТоварыСвойства КАК втТоварыСвойства
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ втТовары
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ втТоварыБезДублей
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ втТоварыСвойства
	|;
	|
	|";
			   
	Результат = Новый Структура;
	Результат.Вставить("ТекстЗапроса", ТекстЗапроса);
	Результат.Вставить("Индексы", Новый Структура);	
	Результат.Индексы.Вставить("Товары", 2);
	Результат.Индексы.Вставить("ТоварыЦены", 3);
	Результат.Индексы.Вставить("ТоварыОстатки", 4);
	Результат.Индексы.Вставить("ТоварыТипыЦен", 5);	
	Результат.Индексы.Вставить("ТоварыИерархия", 6);	
	Результат.Индексы.Вставить("ТоварыРодители", 7);
	Результат.Индексы.Вставить("ТоварыКартинки", 8);
	Результат.Индексы.Вставить("ТоварыСвойства", 10);
	Результат.Индексы.Вставить("ТоварыСвойстваРазличные", 11);
	
	Возврат Результат;
	
КонецФункции

Процедура прЗаполнитьДанныеТоваров(Выборка, Пакет, Параметры=Неопределено) 
	
	ИспользоватьАлиасы = Параметры.АлиасыЕдиницИзмеренийСайта.Количество() > 0;
	
	Выборка.Сбросить();
	Пока Выборка.Следующий() Цикл
		
		ТоварУИД = _ПолучитьОбъединенныйУИД(Выборка.Номенклатура, Выборка.Характеристика);
		Ключ = _СформироватьКлючИзУИД(ТоварУИД);
		НоменклатураУИД = _УИД(Выборка.Номенклатура, Истина);
		
		НоваяЗапись = Новый Структура;
		
		// Служебные поля
		НоваяЗапись.Вставить("ТоварУИД", ТоварУИД);
		// Поля данных
		НоваяЗапись.Вставить("НоменклатураУИД", НоменклатураУИД);
		НоваяЗапись.Вставить("Номенклатура", Выборка.Номенклатура);
		НоваяЗапись.Вставить("Код", Выборка.Код);
		НоваяЗапись.Вставить("Характеристика", Выборка.Характеристика);
		НоваяЗапись.Вставить("ХарактеристикаУИД", _УИД(Выборка.Характеристика));
		НоваяЗапись.Вставить("Артикул", Выборка.Артикул);
       	НоваяЗапись.Вставить("Наименование", Выборка.Наименование);
       	НоваяЗапись.Вставить("НаименованиеПолное", Выборка.НаименованиеПолное);
       	НоваяЗапись.Вставить("Описание", Выборка.Описание);
       	НоваяЗапись.Вставить("Родитель", Выборка.Родитель);
       	НоваяЗапись.Вставить("РодительУИД", _УИД(Выборка.Родитель));
       	НоваяЗапись.Вставить("ЭтоГруппа", Выборка.ЭтоГруппа);
		ЕдиницаИзмерения = Выборка.ЕдиницаИзмерения;
		Если ИспользоватьАлиасы Тогда
			Значение = Параметры.АлиасыЕдиницИзмеренийСайта[Выборка.ЕдиницаИзмерения];
			Если Значение <> Неопределено Тогда
				ЕдиницаИзмерения = Значение;
			КонецЕсли;
		КонецЕсли;
		НоваяЗапись.Вставить("ЕдиницаИзмерения", ЕдиницаИзмерения);
		
		Пакет[Ключ] = НоваяЗапись;

	КонецЦикла;
	
КонецПроцедуры

Процедура прЗаполнитьДанныеТоварыЦены(Выборка, Пакет, Параметры=Неопределено) 
	
	КлассификаторВалют = Параметры.КонстантыОбмена.ДопустимыеВалюты;
	
	НенайденныеВалюты = Новый Соответствие;
	
	Выборка.Сбросить();
	Пока Выборка.Следующий() Цикл 
		
		ТоварУИД = _ПолучитьОбъединенныйУИД(Выборка.Номенклатура, Выборка.Характеристика);
		Ключ = _СформироватьКлючИзУИД(ТоварУИД);
		КлючТипЦен = _ПолучитьКлючТипаЦены(Выборка.ТипЦен);
				
		НоваяЗапись = Новый Структура;
		
		// Служебные поля
		НоваяЗапись.Вставить("ТоварУИД", ТоварУИД);
		// Поля данных
		НоваяЗапись.Вставить("Номенклатура", Выборка.Номенклатура);			
		НоваяЗапись.Вставить("Характеристика", Выборка.Характеристика);
		НоваяЗапись.Вставить("Валюта", Выборка.Валюта);
		НоваяЗапись.Вставить("ВалютаКод", Выборка.ВалютаКод);
		НоваяЗапись.Вставить("ВалютаСокращение", КлассификаторВалют[Выборка.ВалютаКод]);
		НоваяЗапись.Вставить("ТипЦен", Выборка.ТипЦен);
		НоваяЗапись.Вставить("ТипЦенУИД", _УИД(Выборка.ТипЦен));
		НоваяЗапись.Вставить("Цена", Выборка.Цена);
		НоваяЗапись.Вставить("ТипЦенСайт", Выборка.ТипЦенСайт);
		
		Если Не ЗначениеЗаполнено(НоваяЗапись.ВалютаСокращение) 
			И ЗначениеЗаполнено(Выборка.ВалютаКод) Тогда
			НенайденныеВалюты.Вставить(Выборка.ВалютаКод);
		КонецЕсли;
				
		Если Пакет[Ключ] = Неопределено Тогда
			Пакет[Ключ] = Новый Структура;
		КонецЕсли;		
		Пакет[Ключ].Вставить(КлючТипЦен, НоваяЗапись);
		
	КонецЦикла;

	Для Каждого Валюта Из НенайденныеВалюты Цикл
		_ВывестиСообщение(НСтр("ru='В классификаторе валют не найдена валюта по коду "".';uk='У класифікаторі валют не знайдена валюта за кодом "".'") + Валюта.Ключ + """.", СтатусСообщения.Внимание);
	КонецЦикла;
		
КонецПроцедуры

Процедура прЗаполнитьДанныеТоварыОстатки(Выборка, Пакет, Параметры=Неопределено) 
	
	Выборка.Сбросить();
	Пока Выборка.Следующий() Цикл
		
		ТоварУИД = _ПолучитьОбъединенныйУИД(Выборка.Номенклатура, Выборка.Характеристика);
		Ключ = _СформироватьКлючИзУИД(ТоварУИД);
		
		НоваяЗапись = Новый Структура;
		
		// Служебные поля
		НоваяЗапись.Вставить("ТоварУИД", ТоварУИД);
		// Поля данных
		НоваяЗапись.Вставить("Наличие", Выборка.Наличие);
		НоваяЗапись.Вставить("Количество", Выборка.Количество);
		
		Пакет[Ключ] = НоваяЗапись;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура прЗаполнитьДанныеТоварыТипыЦен(Выборка, Пакет, Параметры=Неопределено) 
	
	КлассификаторВалют = Параметры.КонстантыОбмена.ДопустимыеВалюты;
	
	НенайденныеВалюты = Новый Соответствие;
	
	Выборка.Сбросить();
	Пока Выборка.Следующий() Цикл 
		
		Если Не ЗначениеЗаполнено(Выборка.ТипЦен) Тогда
			Продолжить;
		КонецЕсли;
		
		НоваяЗапись = Новый Структура;		
		
		// Поля данных
		НоваяЗапись.Вставить("УИД", _УИД(Выборка.ТипЦен));
		НоваяЗапись.Вставить("ТипЦен", Выборка.ТипЦен);
		НоваяЗапись.Вставить("Наименование", Выборка.ТипЦенНаименование);
		НоваяЗапись.Вставить("ТипЦенСайт", Выборка.ТипЦенСайт);
		НоваяЗапись.Вставить("Валюта", Выборка.Валюта);
		НоваяЗапись.Вставить("ВалютаКод", Выборка.ВалютаКод);
		НоваяЗапись.Вставить("ВалютаСокращение", КлассификаторВалют[Выборка.ВалютаКод]);
		
		Если Не ЗначениеЗаполнено(НоваяЗапись.ВалютаСокращение) 
			И ЗначениеЗаполнено(Выборка.ВалютаКод) Тогда
			НенайденныеВалюты.Вставить(Выборка.ВалютаКод);
		КонецЕсли;
		
		Пакет.Добавить(НоваяЗапись);
				
	КонецЦикла;
	
	Для Каждого Валюта Из НенайденныеВалюты Цикл
		_ВывестиСообщение(НСтр("ru='В классификаторе валют не найдена валюта по коду "".';uk='У класифікаторі валют не знайдена валюта за кодом "".'") + Валюта.Ключ + """.", СтатусСообщения.Внимание);
	КонецЦикла;

КонецПроцедуры

Процедура прЗаполнитьДанныеИерархияТоваров(Выборка, Пакет, Параметры=Неопределено) 
	
	ДобавленныеГруппы = Новый Соответствие;
	
	Выборка.Сбросить();
	Пока Выборка.Следующий() Цикл 
				
		Если ДобавленныеГруппы[Выборка.Ссылка] = Неопределено Тогда
			ДобавленныеГруппы[Выборка.Ссылка] = Истина;
		Иначе
			Продолжить;
		КонецЕсли;
		
		ГруппаУИД = _ПолучитьОбъединенныйУИД(Выборка.Ссылка);
		Ключ = _СформироватьКлючИзУИД(ГруппаУИД);
		
		НоваяЗапись = Новый Структура;
		
		// Служебные поля
		НоваяЗапись.Вставить("ГруппаУИД", ГруппаУИД);
		// Поля данных
		НоваяЗапись.Вставить("УИД", _УИД(Выборка.Ссылка));
		НоваяЗапись.Вставить("Наименование", Выборка.Наименование);
		НоваяЗапись.Вставить("РодительУИД", ?(ЗначениеЗаполнено(Выборка.Родитель), _УИД(Выборка.Родитель), ""));
		
		Пакет[Ключ] = НоваяЗапись;
				
	КонецЦикла;

КонецПроцедуры

Процедура прЗаполнитьДанныеТоварыРодители(Выборка, Пакет, Параметры=Неопределено) 
	
	Выборка.Сбросить();
	Пока Выборка.Следующий() Цикл 
						
		ТоварУИД = _ПолучитьОбъединенныйУИД(Выборка.Номенклатура, Выборка.Характеристика);
		Ключ = _СформироватьКлючИзУИД(ТоварУИД);
		
		НоваяЗапись = Новый Структура;
		
		// Служебные поля
		НоваяЗапись.Вставить("ТоварУИД", ТоварУИД);
		// Поля данных
		НоваяЗапись.Вставить("РодительУИД", _УИД(Выборка.Родитель));
		
		Пакет[Ключ] = НоваяЗапись;
				
	КонецЦикла;

КонецПроцедуры

Процедура прЗаполнитьДанныеТоварыКартинки(Выборка, Пакет, Параметры=Неопределено) 
	
	ИнформацияОКонтексте = _ПолучитьИнформациюОКонтекстеВыполнения(Параметры.КонстантыОбмена);
	ТипПлатформыСервера = ИнформацияОКонтексте.ТипПлатформы;
	
	ТомаНаДисках = Новый Соответствие;
	ТомаНаДисках.Вставить(Строка(ТипПлатформы.Windows_x86), "ПолныйПутьWindows");
	ТомаНаДисках.Вставить(Строка(ТипПлатформы.Windows_x86_64), "ПолныйПутьWindows");
	ТомаНаДисках.Вставить(Строка(ТипПлатформы.Linux_x86), "ПолныйПутьLinux");
	ТомаНаДисках.Вставить(Строка(ТипПлатформы.Linux_x86_64), "ПолныйПутьLinux");
	
	ИмяПоляСПолнымПутем = ТомаНаДисках[ТипПлатформыСервера];
	
	Выборка.Сбросить();
	Пока Выборка.Следующий() Цикл 
						
		ТоварУИД = _ПолучитьОбъединенныйУИД(Выборка.Номенклатура, Выборка.Характеристика);
		Ключ = _СформироватьКлючИзУИД(ТоварУИД);
		
		НоваяЗапись = Новый Структура;
		
		// Служебные поля
		НоваяЗапись.Вставить("ТоварУИД", ТоварУИД);
		НоваяЗапись.Вставить("КартинкаУИД", _УИД(Выборка.Хранилище, Истина, Истина));
		// Поля данных
		НоваяЗапись.Вставить("ТоварНаименование", Выборка.ТоварНаименование);
		НоваяЗапись.Вставить("ТоварКод", СокрЛП(Выборка.ТоварКод));
		НоваяЗапись.Вставить("Хранилище", Выборка.Хранилище);
		НоваяЗапись.Вставить("ХранилищеНаименование", Выборка.ХранилищеНаименование);
		НоваяЗапись.Вставить("ЭтоОсновноеИзображение", Выборка.ЭтоОсновноеИзображение);
		НоваяЗапись.Вставить("ТипХраненияФайла", Выборка.ТипХраненияФайла);
		НоваяЗапись.Вставить("ДанныеХранилища", Выборка.ДанныеХранилища);
		НоваяЗапись.Вставить("ПолныйПутьКФайлу", ?(ЗначениеЗаполнено(ИмяПоляСПолнымПутем), "" + Выборка.Том[ИмяПоляСПолнымПутем] + Выборка.ПутьКФайлу, ""));
		НоваяЗапись.Вставить("Размер", Выборка.Размер);	
		
		Если Пакет[Ключ] = Неопределено Тогда
			Пакет[Ключ] = Новый Массив;
		КонецЕсли;
		Пакет[Ключ].Добавить(НоваяЗапись);
		
	КонецЦикла;

КонецПроцедуры

Процедура прЗаполнитьДанныеТоварыСвойства(Выборка, Пакет, Параметры=Неопределено) 

	СвойстваЗаменяемые = Параметры.СвойстваЗаменяемые;
	
	СвойстваСайта = Параметры.СвойстваСайта;
	
	Выборка.Сбросить();
	Пока Выборка.Следующий() Цикл
		
		ВыборкаДетальная = Выборка.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаДетальная.Следующий() Цикл
			
			Выгружать = Истина;
			
			ТоварУИД = _ПолучитьОбъединенныйУИД(ВыборкаДетальная.Номенклатура, ВыборкаДетальная.Характеристика);
			Ключ = _СформироватьКлючИзУИД(ТоварУИД);
			
			НоваяЗапись = Новый Структура;
			
			// Служебные поля
			НоваяЗапись.Вставить("ТоварУИД", ТоварУИД);
			// Поля данных
			НоваяЗапись.Вставить("УИД", _УИД(ВыборкаДетальная.Свойство));
			НоваяЗапись.Вставить("Свойство", ВыборкаДетальная.Свойство);
			НоваяЗапись.Вставить("Значение", ВыборкаДетальная.Значение);
			НоваяЗапись.Вставить("Множественное", Истина);
			НоваяЗапись.Вставить("Обязательное", Истина);
			НоваяЗапись.Вставить("ТипЗначения", "Строка");
			НоваяЗапись.Вставить("ДляТоваров", Истина);
			
			// Поле Наименование
			СвойствоЗаменяемое = СвойстваЗаменяемые[ВыборкаДетальная.Свойство];
			Если ЗначениеЗаполнено(СвойствоЗаменяемое) Тогда
				Выгружать = СвойствоЗаменяемое.Выгружать;
				НоваяЗапись.Вставить("Наименование", СвойствоЗаменяемое.Свойство);
			Иначе
				НоваяЗапись.Вставить("Наименование", ВыборкаДетальная.Наименование);
			КонецЕсли;
			
			// Поле СвойствоСайт
			СвойствоСайта = СвойстваСайта[ВыборкаДетальная.Свойство];
			Если ЗначениеЗаполнено(СвойствоСайта) Тогда
				НоваяЗапись.Вставить("СвойствоСайт", Число(СвойствоСайта));
			Иначе
				НоваяЗапись.Вставить("СвойствоСайт", Неопределено);
			КонецЕсли;
			
			// Добавление свойства
			Если Пакет[Ключ] = Неопределено Тогда
				Пакет[Ключ] = Новый Массив;
			КонецЕсли;
			
			Если Выгружать Тогда
				Пакет[Ключ].Добавить(НоваяЗапись);
			КонецЕсли;
						
		КонецЦикла;
				
	КонецЦикла;
	
КонецПроцедуры

Процедура прЗаполнитьДанныеТоварыСвойстваРазличные(Выборка, Пакет, Параметры=Неопределено) 

	СвойстваЗаменяемые = Параметры.СвойстваЗаменяемые;
	
	СвойстваСайта = Параметры.СвойстваСайта;
	
	Выборка.Сбросить();
	Пока Выборка.Следующий() Цикл
		
		Выгружать = Истина;
		
		НоваяЗапись = Новый Структура;
		
		// Поля данных
		НоваяЗапись.Вставить("УИД", _УИД(Выборка.Свойство));
		НоваяЗапись.Вставить("Свойство", Выборка.Свойство);
		НоваяЗапись.Вставить("Множественное", Истина);
		НоваяЗапись.Вставить("Обязательное", Истина);
		НоваяЗапись.Вставить("ТипЗначения", "Строка");
		НоваяЗапись.Вставить("ДляТоваров", Истина);
		
		// Поле Наименование
		СвойствоЗаменяемое = СвойстваЗаменяемые[Выборка.Свойство];
		Если ЗначениеЗаполнено(СвойствоЗаменяемое) Тогда
			Выгружать = СвойствоЗаменяемое.Выгружать;
			НоваяЗапись.Вставить("Наименование", СвойствоЗаменяемое.Свойство);			
		Иначе
			НоваяЗапись.Вставить("Наименование", Выборка.Наименование);
		КонецЕсли;
		
		// Поле СвойствоСайт
		СвойствоСайта = СвойстваСайта[Выборка.Свойство];
		Если ЗначениеЗаполнено(СвойствоСайта) Тогда
			НоваяЗапись.Вставить("СвойствоСайт", Число(СвойствоСайта));
		Иначе
			НоваяЗапись.Вставить("СвойствоСайт", Неопределено);
		КонецЕсли;
		
		// Добавление свойства
		Если Выгружать Тогда
			Пакет.Добавить(НоваяЗапись);
		КонецЕсли;
									
	КонецЦикла;
	
КонецПроцедуры

Функция прВыгрузитьКартинкуНаДиск(Путь, ДанныеКартинки) 
			
	Картинка = Неопределено;
	
	Попытка
	
		Если ДанныеКартинки.ТипХраненияФайла = Перечисления.ТипыХраненияФайлов.ВИнформационнойБазе Тогда
			
			ХранилищеКартинки = ДанныеКартинки.ДанныеХранилища;	
			
			Картинка = ХранилищеКартинки.Получить();	
			
		ИначеЕсли ДанныеКартинки.ТипХраненияФайла = Перечисления.ТипыХраненияФайлов.ВТомахНаДиске Тогда
			
			ПолныйПутьКФайлу = ДанныеКартинки.ПолныйПутьКФайлу;	
			
			Картинка = Новый ДвоичныеДанные(ПолныйПутьКФайлу);	
			
		КонецЕсли;
	
	Исключение
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		_ВывестиСообщение(ПодробноеПредставлениеОшибки(ИнформацияОбОшибке), СтатусСообщения.Внимание);
	КонецПопытки;
		
	Если ТипЗнч(Картинка) <> Тип("ДвоичныеДанные") и ТипЗнч(Картинка) <> Тип("Картинка") Тогда
		Возврат Неопределено;
	КонецЕсли;
			
	РасширениеФайлаКартинки = "";
	ФорматКартинкиРазрешен = Ложь;	
	ОпределятьПоСигнатуре = Истина;
	
	РазрешенныеСигнатуры = Новый Соответствие;
	РазрешенныеСигнатуры.Вставить("JPEG", "FF D8 FF");
	РазрешенныеСигнатуры.Вставить("PNG", "89 50 4E 47 0D 0A 1A 0A");
	РазрешенныеСигнатуры.Вставить("GIF", "47 49 46 38");
		
	Попытка
		Если ТипЗнч(Картинка) = Тип("ДвоичныеДанные") Тогда
			СигнатураФайла = Лев(Строка(Картинка), (8*2)+(8-1)); // первые 8 байт
		Иначе
			СигнатураФайла = Лев(Строка(Картинка.ПолучитьДвоичныеДанные()), (8*2)+(8-1)); // первые 8 байт
		КонецЕсли;
		Если ПустаяСтрока(СигнатураФайла) Тогда
			ВызватьИсключение "ОшибкаСигнатурыФайла";
		КонецЕсли;		
	Исключение
		ОпределятьПоСигнатуре = Ложь;
	КонецПопытки;
	
	Если ОпределятьПоСигнатуре Тогда
		
		Для каждого Сигнатура Из РазрешенныеСигнатуры Цикл
			Если Сигнатура.Значение = ВРег(Лев(СигнатураФайла, СтрДлина(Сигнатура.Значение))) Тогда
				ФорматКартинкиРазрешен = Истина;
				РасширениеФайлаКартинки = Сигнатура.Ключ;
				Прервать;
			КонецЕсли;	
		КонецЦикла;
				
		Если Не ФорматКартинкиРазрешен Тогда
			
			ТекстСообщения = НСтр("ru='Запрещенный формат картинки товара: ';uk='Заборонений формат картинки товару: '") +
				ДанныеКартинки.ТоварНаименование +" [ "+ ДанныеКартинки.ТоварКод + " ]; image id: " +
				ДанныеКартинки.КартинкаУИД;
				
			_ВывестиСообщение(ТекстСообщения, СтатусСообщения.Важное);				
			
			Возврат Неопределено;
			
		КонецЕсли;	
		
		ИмяКартинки = ДанныеКартинки.КартинкаУИД + "." + НРег(РасширениеФайлаКартинки);
		
	Иначе
		ИмяКартинки = ДанныеКартинки.КартинкаУИД + ".jpeg";
	КонецЕсли;
	
	СоздатьКаталог(Путь);
	
	ПолноеИмяФайлаКартинки = Путь + ?(Прав(Путь, 1)="\", "", "\") + ИмяКартинки;
	ФайлКартинкиНаДиске = Новый Файл(ПолноеИмяФайлаКартинки);
	
	Попытка
		Если НЕ ФайлКартинкиНаДиске.Существует() Тогда
			Картинка.Записать(ПолноеИмяФайлаКартинки);		
		КонецЕсли;		
	Исключение
		_ВывестиСообщение(НСтр("ru='Не удалось записать файл картинки на диск. Номенклатура: ';uk='Не вдалося записати файл картинки на диск. Номенклатура: '") +
							ДанныеКартинки.ТоварНаименование +" [ "+ ДанныеКартинки.ТоварКод + " ]");
		Возврат Неопределено;
	КонецПопытки;
	
	Результат = Новый Структура;	
	Результат.Вставить("Адрес", ИмяКартинки);
	Результат.Вставить("ПолныйПуть", ФайлКартинкиНаДиске.ПолноеИмя);
	Результат.Вставить("Размер", Формат(ФайлКартинкиНаДиске.Размер(), "ЧГ="));
	
	Возврат Результат;
	
КонецФункции

Функция прОбработатьНачалоЭлемента(ОбъектCML, Знач ИмяЭлемента, ДеревоДокументов);
	
	Успешно = Истина;
	
	ТекущаяСтрокаДерева  	  = Неопределено;
	ТекущаяСтрокаТоваровУслуг = Неопределено;
	
	КоличествоДокументов 	  = ДеревоДокументов.Строки.Количество();
	Если КоличествоДокументов > 0 Тогда
		
		ТекущаяСтрокаДерева    = ДеревоДокументов.Строки[КоличествоДокументов - 1];
		КоличествоТоваровУслуг = ТекущаяСтрокаДерева.Строки.Количество();
		
		Если КоличествоТоваровУслуг > 0 Тогда
			ТекущаяСтрокаТоваровУслуг = ТекущаяСтрокаДерева.Строки[КоличествоТоваровУслуг - 1];
		КонецЕсли;	
		
	КонецЕсли;	
	
	Если ИмяЭлемента = "Документ" Тогда
		
		НовСтрокаДерева 			   = ДеревоДокументов.Строки.Добавить();
		НовСтрокаДерева.СтавкаНДС 	   = 0;
		НовСтрокаДерева.СтруктураДанныхКонтрагента = Новый Структура;
		
	ИначеЕсли ИмяЭлемента = "Документ.Контрагенты.Контрагент" Тогда
		
		ДеревоАдресов = Новый ДеревоЗначений;
		ДеревоАдресов.Колонки.Добавить("ВидАдреса");
		ДеревоАдресов.Колонки.Добавить("Представление");
		ДеревоАдресов.Колонки.Добавить("Комментарий");
		ДеревоАдресов.Колонки.Добавить("ПолеТип");
		ДеревоАдресов.Колонки.Добавить("ПолеЗначение");
		ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Вставить("ДеревоАдресов", ДеревоАдресов);
		
		ТаблицаКонтактов = Новый ТаблицаЗначений;
		ТаблицаКонтактов.Колонки.Добавить("КонтактТип");
		ТаблицаКонтактов.Колонки.Добавить("КонтактЗначение");
		ТаблицаКонтактов.Колонки.Добавить("КонтактКомментарий");
		ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Вставить("ТаблицаКонтактов", ТаблицаКонтактов);
		
		ТаблицаКонтактныхЛиц = Новый ТаблицаЗначений;
		ТаблицаКонтактныхЛиц.Колонки.Добавить("Наименование");
		ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Вставить("ТаблицаКонтактныхЛиц", ТаблицаКонтактныхЛиц);
		
		ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Вставить("Адрес","");
		ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Вставить("ИД","");
		ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Вставить("Наименование","");
		ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Вставить("НаименованиеПолное","");		
		ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Вставить("Роль","");
		ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Вставить("ЮрЛицо","");
		
	ИначеЕсли ИмяЭлемента = "Документ.Контрагенты.Контрагент.АдресРегистрации" Тогда
		
		ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Свойство("ДеревоАдресов", ДеревоАдресов);
		НовСтрокаДерева	= ДеревоАдресов.Строки.Добавить();
		НовСтрокаДерева.ВидАдреса = "АдресРегистрации";
		ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Вставить("ДеревоАдресов", ДеревоАдресов);
		
	ИначеЕсли ИмяЭлемента = "Документ.Контрагенты.Контрагент.АдресРегистрации.АдресноеПоле" Тогда
		
		ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Свойство("ДеревоАдресов", ДеревоАдресов);
		СтрокаДерева 	= ДеревоАдресов.Строки.Найти("АдресРегистрации", "ВидАдреса");
		НовСтрокаДерева = СтрокаДерева.Строки.Добавить();
		ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Вставить("ДеревоАдресов", ДеревоАдресов);
				
	ИначеЕсли ИмяЭлемента = "Документ.Контрагенты.Контрагент.ЮридическийАдрес" Тогда
		
		ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Свойство("ДеревоАдресов", ДеревоАдресов);
		НовСтрокаДерева	= ДеревоАдресов.Строки.Добавить();
		НовСтрокаДерева.ВидАдреса = "ЮридическийАдрес";
		ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Вставить("ДеревоАдресов", ДеревоАдресов);
		
	ИначеЕсли ИмяЭлемента = "Документ.Контрагенты.Контрагент.ЮридическийАдрес.АдресноеПоле" Тогда
		
		ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Свойство("ДеревоАдресов", ДеревоАдресов);
		СтрокаДерева 	= ДеревоАдресов.Строки.Найти("ЮридическийАдрес", "ВидАдреса");
		НовСтрокаДерева = СтрокаДерева.Строки.Добавить();
		ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Вставить("ДеревоАдресов", ДеревоАдресов);
		
	ИначеЕсли ИмяЭлемента = "Документ.Контрагенты.Контрагент.РасчетныеСчета.РасчетныйСчет" Тогда
	ИначеЕсли ИмяЭлемента = "Документ.Контрагенты.Контрагент.РасчетныеСчета.РасчетныйСчет.БанкКорреспондент.Банк" Тогда	
	ИначеЕсли ИмяЭлемента = "Документ.Контрагенты.Контрагент.РасчетныеСчета.РасчетныйСчет.БанкКорреспондент.Банк.Адрес" Тогда	
	ИначеЕсли ИмяЭлемента = "Документ.Контрагенты.Контрагент.РасчетныеСчета.РасчетныйСчет.БанкКорреспондент.Банк.Адрес.АдресноеПоле" Тогда	
	ИначеЕсли ИмяЭлемента = "Документ.Контрагенты.Контрагент.РасчетныеСчета.РасчетныйСчет.Банк" Тогда
	ИначеЕсли ИмяЭлемента = "Документ.Контрагенты.Контрагент.РасчетныеСчета.РасчетныйСчет.Банк.Адрес" Тогда	
	ИначеЕсли ИмяЭлемента = "Документ.Контрагенты.Контрагент.РасчетныеСчета.РасчетныйСчет.Банк.Адрес.АдресноеПоле" Тогда		
		
	ИначеЕсли ИмяЭлемента = "Документ.Контрагенты.Контрагент.Адрес" Тогда	
		
		ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Свойство("ДеревоАдресов", ДеревоАдресов);
		НовСтрокаДерева	= ДеревоАдресов.Строки.Добавить();
		НовСтрокаДерева.ВидАдреса = "Адрес";
		ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Вставить("ДеревоАдресов", ДеревоАдресов);
		
	ИначеЕсли ИмяЭлемента = "Документ.Контрагенты.Контрагент.Адрес.АдресноеПоле" Тогда		
		
		ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Свойство("ДеревоАдресов", ДеревоАдресов);
		СтрокаДерева 	= ДеревоАдресов.Строки.Найти("Адрес", "ВидАдреса");
		НовСтрокаДерева = СтрокаДерева.Строки.Добавить();
		ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Вставить("ДеревоАдресов", ДеревоАдресов);
		
	ИначеЕсли ИмяЭлемента = "Документ.Контрагенты.Контрагент.Контакты.Контакт" Тогда
		
		ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Свойство("ТаблицаКонтактов", ТаблицаКонтактов);
		ТаблицаКонтактов.Добавить();
		ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Вставить("ТаблицаКонтактов", ТаблицаКонтактов);
		
	ИначеЕсли ИмяЭлемента = "Документ.Контрагенты.Контрагент.Представители.Представитель.Контрагент" Тогда	
		
		ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Свойство("ТаблицаКонтактныхЛиц", ТаблицаКонтактныхЛиц);
		ТаблицаКонтактныхЛиц.Добавить();
		ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Вставить("ТаблицаКонтактныхЛиц", ТаблицаКонтактныхЛиц);
		
	ИначеЕсли ИмяЭлемента = "Документ.Товары.Товар" Тогда
		
		НовСтрокаДерева = ТекущаяСтрокаДерева.Строки.Добавить();
		
	ИначеЕсли ИмяЭлемента = "Документ.Товары.Товар.БазоваяЕдиница" Тогда		
		
		Пока ОбъектCML.ПрочитатьАтрибут() Цикл
			Если ОбъектCML.Имя = "Код" Тогда
				КодБазовойЕдиницы = ОбъектCML.Значение;
				ТекущаяСтрокаТоваровУслуг.ТоварУслугаБазоваяЕдиницаКод = КодБазовойЕдиницы;
				Прервать;
			КонецЕсли;	
		КонецЦикла
		
	ИначеЕсли ИмяЭлемента = "Документ.Товары.Товар.ЗначенияРеквизитов.ЗначениеРеквизита" Тогда
		
		ТекущаяСтрокаТоваровУслуг.Строки.Добавить();
		
	ИначеЕсли ИмяЭлемента = "Документ.Товары.Товар.Скидки.Скидка" Тогда
		
		ТекущаяСтрокаТоваровУслуг.Строки.Добавить();
		
	ИначеЕсли ИмяЭлемента = "Документ.ЗначенияРеквизитов.ЗначениеРеквизита" Тогда
		
		НовСтрокаДерева = ТекущаяСтрокаДерева.Строки.Добавить();
		
	КонецЕсли;
	
	Возврат Успешно;
	
КонецФункции

Функция прОбработатьЗначениеЭлемента(Знач ИмяЭлемента, Знач ЗначениеЭлемента, ДеревоДокументов);
	
	Успешно 			 = Истина;
	КоличествоДокументов = ДеревоДокументов.Строки.Количество();
	ИмяТекущегоЭлемента  = _ПолучитьИмяЭлементаИзПоследовательности(ИмяЭлемента);
	ДеревоАдресов		 = Неопределено;
	ТаблицаКонтактов	 = Неопределено;
	ТаблицаКонтактныхЛиц = Неопределено;
	
	БулевоЗначениеCML_Истина = "true";	
	
	Если КоличествоДокументов > 0 Тогда
		
		ТекущаяСтрокаДерева       = ДеревоДокументов.Строки[КоличествоДокументов - 1];
		
		ТекущаяСтрокаТоваровУслуг = Неопределено;
		ТекущаяСтрокаСкидок 	  = Неопределено;
		
		КоличествоТоваровУслуг    = ТекущаяСтрокаДерева.Строки.Количество();
		
		Если КоличествоТоваровУслуг > 0 Тогда
			
			ТекущаяСтрокаТоваровУслуг = ТекущаяСтрокаДерева.Строки[КоличествоТоваровУслуг - 1];	
			
			КоличествоСкидок    	  = ТекущаяСтрокаТоваровУслуг.Строки.Количество();
			
			Если КоличествоСкидок > 0 Тогда
				ТекущаяСтрокаСкидок = ТекущаяСтрокаТоваровУслуг.Строки[КоличествоСкидок - 1];	
			КонецЕсли;
			
		КонецЕсли;	
		
		Если ИмяЭлемента = "Документ.Номер" Тогда
			
			ТекущаяСтрокаДерева.НомерВходящий = ЗначениеЭлемента;			
			ТекущаяСтрокаДерева.Номер = ЗначениеЭлемента;			
						
		ИначеЕсли ИмяЭлемента = "Документ.Ид" Тогда	
			
			ТекущаяСтрокаДерева.ИД = ЗначениеЭлемента;			
			
		ИначеЕсли ИмяЭлемента = "Документ.Дата" Тогда
			
			ДатаВремя = _ПривестиСтрокуКДате(ЗначениеЭлемента);
			
			ТекущаяСтрокаДерева.Дата = ДатаВремя;
			
		ИначеЕсли ИмяЭлемента = "Документ.Время" Тогда
			
			ТекущаяСтрокаДерева.Время = ЗначениеЭлемента;
			
		ИначеЕсли ИмяЭлемента = "Документ.ХозОперация" Тогда
						
			ТекущаяСтрокаДерева.ХозОперация = ЗначениеЭлемента;
			
		ИначеЕсли ИмяЭлемента = "Документ.Валюта" Тогда
						
			ТекущаяСтрокаДерева.Валюта = ЗначениеЭлемента;
			
		ИначеЕсли ИмяЭлемента = "Документ.Курс" Тогда
			
			КурсCML = Число(ЗначениеЭлемента);
			ТекущаяСтрокаДерева.Курс      = КурсCML;
			
		ИначеЕсли ИмяЭлемента = "Документ.ПримечаниеКлиента" Тогда
			
			ТекущаяСтрокаДерева.ПримечаниеКлиента = СтрЗаменить(ЗначениеЭлемента, Символы.ПС, " ");
			
		ИначеЕсли ИмяЭлемента = "Документ.Контрагенты.Контрагент.Наименование" Тогда
			
			ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Вставить("Наименование", ЗначениеЭлемента); 
			
		ИначеЕсли ИмяЭлемента = "Документ.Контрагенты.Контрагент.Комментарий" Тогда
			
			ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Вставить("Комментарий", ЗначениеЭлемента); 
			
		ИначеЕсли ИмяЭлемента = "Документ.Контрагенты.Контрагент.Ид" Тогда
			
			ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Вставить("ИД", ЗначениеЭлемента); 
			
		ИначеЕсли ИмяЭлемента = "Документ.Контрагенты.Контрагент.Роль" Тогда
			
			ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Вставить("Роль", ЗначениеЭлемента);
			
		ИначеЕсли ИмяЭлемента = "Документ.Контрагенты.Контрагент.ПолноеНаименование"
			или ИмяЭлемента = "Документ.Контрагенты.Контрагент.ОфициальноеНаименование" Тогда
			
			ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Вставить("НаименованиеПолное", ЗначениеЭлемента);
			
		ИначеЕсли ИмяЭлемента = "Документ.Контрагенты.Контрагент.КПП" тогда
			
			ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Вставить("КПП", ЗначениеЭлемента);
						
		ИначеЕсли ИмяЭлемента = "Документ.Контрагенты.Контрагент.ДатаРождения" Тогда
			
			ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Вставить("ДатаРождения", ЗначениеЭлемента); 
			
		ИначеЕсли ИмяЭлемента = "Документ.Контрагенты.Контрагент.Адрес.Представление" Тогда 
			
			ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Вставить("Адрес", ЗначениеЭлемента); 
			
		ИначеЕсли ИмяЭлемента = "Документ.Контрагенты.Контрагент.МестоРождения" Тогда	
			
			ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Вставить("МестоРождения", ЗначениеЭлемента); 
			
		ИначеЕсли ИмяЭлемента = "Документ.Контрагенты.Контрагент.Пол" Тогда
			
			ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Вставить("Пол", ЗначениеЭлемента); 
			
		ИначеЕсли ИмяЭлемента = "Документ.Контрагенты.Контрагент.ИНН" Тогда
			
			ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Вставить("ИНН", ЗначениеЭлемента); 
			
		ИначеЕсли ИмяЭлемента = "Документ.Контрагенты.Контрагент.УдостоверениеЛичности.ВидДокумента"
			или ИмяЭлемента = "Документ.Контрагенты.Контрагент.УдостоверениеЛичности.Серия"
			или ИмяЭлемента = "Документ.Контрагенты.Контрагент.УдостоверениеЛичности.Номер"
			или ИмяЭлемента = "Документ.Контрагенты.Контрагент.УдостоверениеЛичности.ДатаВыдачи"
			или ИмяЭлемента = "Документ.Контрагенты.Контрагент.УдостоверениеЛичности.КемВыдан" Тогда		
			
			СохраненноеЗначение = "";
			ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Свойство("ДокументУдостоверяющийЛичность", СохраненноеЗначение); 
			ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Вставить("ДокументУдостоверяющийЛичность", СохраненноеЗначение + ЗначениеЭлемента + " "); 
			
		ИначеЕсли ИмяЭлемента = "Документ.Контрагенты.Контрагент.АдресРегистрации.Представление"
			или ИмяЭлемента = "Документ.Контрагенты.Контрагент.АдресРегистрации.Комментарий" Тогда
			
			ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Свойство("ДеревоАдресов", ДеревоАдресов);
			СтрокаДерева = ДеревоАдресов.Строки.Найти("АдресРегистрации", "ВидАдреса");
			СтрокаДерева[ИмяТекущегоЭлемента] = ЗначениеЭлемента;
			ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Вставить("ДеревоАдресов", ДеревоАдресов);
			
		ИначеЕсли ИмяЭлемента = "Документ.Контрагенты.Контрагент.АдресРегистрации.АдресноеПоле.Тип"
			или ИмяЭлемента = "Документ.Контрагенты.Контрагент.АдресРегистрации.АдресноеПоле.Значение" Тогда
			
			ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Свойство("ДеревоАдресов", ДеревоАдресов);
			СтрокаДерева = ДеревоАдресов.Строки.Найти("АдресРегистрации", "ВидАдреса");
			СтрокаАдресногоПоля = СтрокаДерева.Строки[СтрокаДерева.Строки.Количество() - 1];
			СтрокаАдресногоПоля["Поле" + ИмяТекущегоЭлемента] = ЗначениеЭлемента;
			ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Вставить("ДеревоАдресов", ДеревоАдресов);
						
		ИначеЕсли ИмяЭлемента = "Документ.Контрагенты.Контрагент.ЮридическийАдрес.Представление"
			или ИмяЭлемента = "Документ.Контрагенты.Контрагент.ЮридическийАдрес.Комментарий" Тогда
			
			ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Свойство("ДеревоАдресов", ДеревоАдресов);
			СтрокаДерева = ДеревоАдресов.Строки.Найти("ЮридическийАдрес", "ВидАдреса");
			СтрокаДерева[ИмяТекущегоЭлемента] = ЗначениеЭлемента;
			ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Вставить("ДеревоАдресов", ДеревоАдресов);
			
		ИначеЕсли ИмяЭлемента = "Документ.Контрагенты.Контрагент.ЮридическийАдрес.АдресноеПоле.Тип"
			или ИмяЭлемента = "Документ.Контрагенты.Контрагент.ЮридическийАдрес.АдресноеПоле.Значение" Тогда
			
			ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Свойство("ДеревоАдресов", ДеревоАдресов);
			СтрокаДерева = ДеревоАдресов.Строки.Найти("ЮридическийАдрес", "ВидАдреса");
			СтрокаАдресногоПоля = СтрокаДерева.Строки[СтрокаДерева.Строки.Количество() - 1];
			СтрокаАдресногоПоля["Поле" + ИмяТекущегоЭлемента] = ЗначениеЭлемента;
			ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Вставить("ДеревоАдресов", ДеревоАдресов);
			
		ИначеЕсли ИмяЭлемента = "Документ.Контрагенты.Контрагент.ОсновнойВидДеятельности" Тогда
			
			ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Вставить("ОсновнойВидДеятельности", ЗначениеЭлемента); 
						
		ИначеЕсли ИмяЭлемента = "Документ.Контрагенты.Контрагент.Адрес.Представление"
			или ИмяЭлемента = "Документ.Контрагенты.Контрагент.Адрес.Комментарий" Тогда	
			
			ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Свойство("ДеревоАдресов", ДеревоАдресов);
			СтрокаДерева = ДеревоАдресов.Строки.Найти("Адрес", "ВидАдреса");
			СтрокаДерева[ИмяТекущегоЭлемента] = ЗначениеЭлемента;
			ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Вставить("ДеревоАдресов", ДеревоАдресов);
			
		ИначеЕсли ИмяЭлемента = "Документ.Контрагенты.Контрагент.Адрес.АдресноеПоле.Тип"	
			или ИмяЭлемента = "Документ.Контрагенты.Контрагент.Адрес.АдресноеПоле.Значение" Тогда	
			
			ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Свойство("ДеревоАдресов", ДеревоАдресов);
			СтрокаДерева = ДеревоАдресов.Строки.Найти("Адрес", "ВидАдреса");
			СтрокаАдресногоПоля = СтрокаДерева.Строки[СтрокаДерева.Строки.Количество() - 1];
			СтрокаАдресногоПоля["Поле" + ИмяТекущегоЭлемента] = ЗначениеЭлемента;
			ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Вставить("ДеревоАдресов", ДеревоАдресов);
			
		ИначеЕсли ИмяЭлемента = "Документ.Контрагенты.Контрагент.Контакты.Контакт.Тип" 
			или ИмяЭлемента = "Документ.Контрагенты.Контрагент.Контакты.Контакт.Значение"
			или ИмяЭлемента = "Документ.Контрагенты.Контрагент.Контакты.Контакт.Комментарий" Тогда		
			
			ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Свойство("ТаблицаКонтактов", ТаблицаКонтактов);
			СтрокаТаблицы = ТаблицаКонтактов[ТаблицаКонтактов.Количество() - 1];
			СтрокаТаблицы["Контакт" + ИмяТекущегоЭлемента] = ЗначениеЭлемента;
			ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Вставить("ТаблицаКонтактов", ТаблицаКонтактов);
			
		ИначеЕсли ИмяЭлемента = "Документ.Контрагенты.Контрагент.Представители.Представитель.Контрагент.Наименование" Тогда		
			
			ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Свойство("ТаблицаКонтактныхЛиц", ТаблицаКонтактныхЛиц);
			СтрокаТаблицы = ТаблицаКонтактныхЛиц[ТаблицаКонтактныхЛиц.Количество() - 1];
			СтрокаТаблицы["Наименование"] = ЗначениеЭлемента;
			ТекущаяСтрокаДерева.СтруктураДанныхКонтрагента.Вставить("ТаблицаКонтактныхЛиц", ТаблицаКонтактныхЛиц);
			
		ИначеЕсли ИмяЭлемента = "Документ.Комментарий" Тогда	
			
			ТекущаяСтрокаДерева.Комментарий = СтрЗаменить(ЗначениеЭлемента, Символы.ПС, " ");
			
		ИначеЕсли ИмяЭлемента = "Документ.Налоги.Налог.УчтеноВСумме" Тогда
			
			ТекущаяСтрокаДерева.СуммаВключаетНДС = Ложь;
			Если ЗначениеЭлемента = БулевоЗначениеCML_Истина Тогда
				ТекущаяСтрокаДерева.СуммаВключаетНДС = Истина;
			КонецЕсли;	
			
		ИначеЕсли ИмяЭлемента = "Документ.Налоги.Налог.Ставка" Тогда
			
			ТекущаяСтрокаДерева.УчитыватьНДС = ЗначениеЗаполнено(ЗначениеЭлемента);
			
		ИначеЕсли ИмяЭлемента = "Документ.Скидки.Скидка.Сумма" Тогда
			
			СуммаЧисло = Число(ЗначениеЭлемента);
			ТекущаяСтрокаДерева.СуммаСкидки = СуммаЧисло;
			
		ИначеЕсли ИмяЭлемента = "Документ.Скидки.Скидка.УчтеноВСумме" Тогда
			
			ТекущаяСтрокаДерева.СкидкаВСумме = Ложь;
			Если ЗначениеЭлемента = БулевоЗначениеCML_Истина Тогда
				ТекущаяСтрокаДерева.СкидкаВСумме = Истина;
			КонецЕсли;
			
		ИначеЕсли ИмяЭлемента = "Документ.Товары.Товар.Ид"
			или ИмяЭлемента = "Документ.Товары.Товар.БазоваяЕдиница"
			или ИмяЭлемента = "Документ.Товары.Товар.ИдКаталога"
			или ИмяЭлемента = "Документ.Товары.Товар.Валюта"
			или ИмяЭлемента = "Документ.Товары.Товар.Наименование" Тогда
			
			ТекущаяСтрокаТоваровУслуг["ТоварУслуга" + ИмяТекущегоЭлемента] = ЗначениеЭлемента;
			
		ИначеЕсли ИмяЭлемента = "Документ.Товары.Товар.ЦенаЗаЕдиницу"
			или ИмяЭлемента = "Документ.Товары.Товар.Сумма"
			или ИмяЭлемента = "Документ.Товары.Товар.Количество" Тогда
			
			ТекущаяСтрокаТоваровУслуг["ТоварУслуга" + ИмяТекущегоЭлемента] = Число(ЗначениеЭлемента);			  
			
		ИначеЕсли ИмяЭлемента = "Документ.Товары.Товар.ЗначенияРеквизитов.ЗначениеРеквизита.Наименование"
			или ИмяЭлемента = "Документ.Товары.Товар.ЗначенияРеквизитов.ЗначениеРеквизита.Значение" Тогда
			
			ТекущаяСтрокаСкидок["ЗначениеРеквизита" + ИмяТекущегоЭлемента] = ЗначениеЭлемента;
			
		ИначеЕсли ИмяЭлемента = "Документ.Товары.Товар.Скидки.Скидка.Сумма" Тогда
			
			СуммаЧисло = Число(ЗначениеЭлемента);
			ТекущаяСтрокаСкидок.СуммаСкидки = СуммаЧисло;
			
		ИначеЕсли ИмяЭлемента = "Документ.Товары.Товар.Налоги.Налог.Сумма" Тогда
			
			СуммаЧисло = Число(ЗначениеЭлемента);
			ТекущаяСтрокаТоваровУслуг.СуммаНДС = СуммаЧисло;
			
		ИначеЕсли ИмяЭлемента = "Документ.Товары.Товар.Налоги.Налог.Ставка" Тогда
			
			СуммаЧисло = Число(ЗначениеЭлемента);
			ТекущаяСтрокаТоваровУслуг.СтавкаНДС = СуммаЧисло;
			
			Если СуммаЧисло <> 0 Тогда 
				ТекущаяСтрокаДерева.УчитыватьНДС = Истина;
			КонецЕсли;
			
		ИначеЕсли ИмяЭлемента = "Документ.Товары.Товар.Скидки.Скидка.УчтеноВСумме" Тогда
			
			ТекущаяСтрокаСкидок.СкидкаВСумме = Ложь;
			Если ЗначениеЭлемента = БулевоЗначениеCML_Истина Тогда
				ТекущаяСтрокаСкидок.СкидкаВСумме = Истина;
			КонецЕсли;	
			
		ИначеЕсли ИмяЭлемента = "Документ.ЗначенияРеквизитов.ЗначениеРеквизита.Наименование"
			или ИмяЭлемента = "Документ.ЗначенияРеквизитов.ЗначениеРеквизита.Значение" Тогда
			
			ТекущаяСтрокаТоваровУслуг["Свойство" + ИмяТекущегоЭлемента] = ЗначениеЭлемента;
			
		КонецЕсли;
		
	КонецЕсли; 
	
	Возврат Успешно;
	
КонецФункции

Функция прСформироватьДанныеЗаказов(ДанныеCML)

	Результат = Новый Структура;
	
	ОбъектCML = Новый ЧтениеXML;
	Попытка
		ОбъектCML.УстановитьСтроку(ДанныеCML);
	Исключение
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		_ВывестиСообщение(ПодробноеПредставлениеОшибки(ИнформацияОбОшибке), СтатусСообщения.ОченьВажное);
		Возврат Ложь;
	КонецПопытки;
	
	ДеревоДокументов = Новый ДеревоЗначений;
	ДеревоДокументов.Колонки.Добавить("СтруктураДанныхКонтрагента");
	// Заказы
	ДеревоДокументов.Колонки.Добавить("ИД");
	ДеревоДокументов.Колонки.Добавить("Номер");
	ДеревоДокументов.Колонки.Добавить("НомерВходящий");
	ДеревоДокументов.Колонки.Добавить("Дата");
	ДеревоДокументов.Колонки.Добавить("ХозОперация");
	ДеревоДокументов.Колонки.Добавить("Валюта");
	ДеревоДокументов.Колонки.Добавить("Курс");
	ДеревоДокументов.Колонки.Добавить("Сумма");
	ДеревоДокументов.Колонки.Добавить("Время");
	ДеревоДокументов.Колонки.Добавить("Комментарий");
	ДеревоДокументов.Колонки.Добавить("ПримечаниеКлиента");
	ДеревоДокументов.Колонки.Добавить("ИДКонтрагента");
	ДеревоДокументов.Колонки.Добавить("УчитыватьНДС");
	ДеревоДокументов.Колонки.Добавить("СуммаВключаетНДС");
	ДеревоДокументов.Колонки.Добавить("СуммаСкидки");
	ДеревоДокументов.Колонки.Добавить("СкидкаВСумме");
	ДеревоДокументов.Колонки.Добавить("СуммаНДС");
	ДеревоДокументов.Колонки.Добавить("СтавкаНДС");
	// Подчинение документу	
	ДеревоДокументов.Колонки.Добавить("ТоварУслугаНаименование");
	ДеревоДокументов.Колонки.Добавить("ТоварУслугаБазоваяЕдиницаКод");
	ДеревоДокументов.Колонки.Добавить("ТоварУслугаБазоваяЕдиница");
	ДеревоДокументов.Колонки.Добавить("ТоварУслугаСумма");
	ДеревоДокументов.Колонки.Добавить("ТоварУслугаКомментарий");
	ДеревоДокументов.Колонки.Добавить("ТоварУслугаЦенаЗаЕдиницу");
	ДеревоДокументов.Колонки.Добавить("ТоварУслугаКоличество");
	ДеревоДокументов.Колонки.Добавить("ТоварУслугаИд");
	ДеревоДокументов.Колонки.Добавить("ТоварУслугаВалюта");
	ДеревоДокументов.Колонки.Добавить("ТоварУслугаИдКаталога");
	// Свойства
	ДеревоДокументов.Колонки.Добавить("СвойствоНаименование");
	ДеревоДокументов.Колонки.Добавить("СвойствоЗначение");	
	// Подчинение товарам/услугам	
	ДеревоДокументов.Колонки.Добавить("ЗначениеРеквизитаНаименование");
	ДеревоДокументов.Колонки.Добавить("ЗначениеРеквизитаЗначение");
	
	СписокАтрибутов = Новый Массив;
	СписокАтрибутов.Добавить("ДатаФормирования");
	КоммерческаяИнформация = _ПолучитьЗначенияКоммерческойИнформации(ДанныеCML, СписокАтрибутов);
	Если КоммерческаяИнформация = Неопределено Тогда
		_ВывестиСообщение(НСтр("ru='Не удалось прочитать значения коммерческой информации.';uk='Не вдалося прочитати значення комерційної інформації.'"), СтатусСообщения.ОченьВажное);
		Возврат Ложь;
	КонецЕсли;

	Результат.Вставить("ДеревоЗаказов", ДеревоДокументов);
	Результат.Вставить("ДатаФормирования", XMLЗначение(Тип("Дата"), КоммерческаяИнформация.ДатаФормирования));
	
	Успешно = Истина;
	ПоследовательностьЭлементов = "";
	
	Пока Истина Цикл
		
		ОчереднойУзелCMLПрочитан = Ложь;
		
		Попытка
			ОчереднойУзелCMLПрочитан = ОбъектCML.Прочитать();
		Исключение
			Успешно = Ложь;
			ИнформацияОбОшибке = ИнформацияОбОшибке();
			_ВывестиСообщение(ПодробноеПредставлениеОшибки(ИнформацияОбОшибке), СтатусСообщения.ОченьВажное);
			Прервать;
		КонецПопытки;	
		
		Если Не ОчереднойУзелCMLПрочитан Тогда
			Прервать;
		КонецЕсли;	
		
		ТипУзла = ОбъектCML.ТипУзла;
		ИмяУзла = ОбъектCML.Имя;
		
		Если ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
			
			ПоследовательностьЭлементов = _ДобавитьЭлементКПоследовательности(ПоследовательностьЭлементов, ИмяУзла);
			
			Успешно = прОбработатьНачалоЭлемента(ОбъектCML, ПоследовательностьЭлементов, ДеревоДокументов);
			Если Не Успешно Тогда
				
				_ВывестиСообщение(НСтр("ru='Не удалось обработать начало элемента (';uk='Не вдалося обробити початок елементу ('") + ПоследовательностьЭлементов + ").", Ложь);
				Прервать;
				
			КонецЕсли;	
			
		ИначеЕсли ТипУзла = ТипУзлаXML.КонецЭлемента Тогда
			
			ПоследовательностьЭлементов = _УдалитьПоследнийЭлементИзПоследовательности(ПоследовательностьЭлементов);
			
		ИначеЕсли ТипУзла = ТипУзлаXML.Текст Тогда	
			
			ЗначениеЭлемента = ОбъектCML.Значение;
			
			Успешно = прОбработатьЗначениеЭлемента(ПоследовательностьЭлементов, ЗначениеЭлемента, ДеревоДокументов);			
			Если Не Успешно Тогда
				
				_ВывестиСообщение(НСтр("ru='Не удалось обработать значение элемента (';uk='Не вдалося обробити значення елементу ('") + ПоследовательностьЭлементов + ") = (" + ЗначениеЭлемента + ").", Ложь);
				Прервать;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	ОбъектCML.Закрыть();
	
	Возврат ?(Успешно, Результат, Неопределено);
	
КонецФункции

Функция прОбработатьДеревоЗаказов(ДеревоЗаказов)

	ЗаказыДанные = Новый Структура;
	
	ЗаказыДанные.Вставить("Заказы", Новый ТаблицаЗначений);
	ЗаказыДанные["Заказы"].Колонки.Добавить("ИД");
	ЗаказыДанные["Заказы"].Колонки.Добавить("Номер");
	ЗаказыДанные["Заказы"].Колонки.Добавить("НомерВходящий");
	ЗаказыДанные["Заказы"].Колонки.Добавить("Дата");
	ЗаказыДанные["Заказы"].Колонки.Добавить("Время");
	ЗаказыДанные["Заказы"].Колонки.Добавить("Комментарий");
	ЗаказыДанные["Заказы"].Колонки.Добавить("ПримечаниеКлиента");
	ЗаказыДанные["Заказы"].Колонки.Добавить("ХозОперация");
	ЗаказыДанные["Заказы"].Колонки.Добавить("Валюта");
	ЗаказыДанные["Заказы"].Колонки.Добавить("Курс");
	ЗаказыДанные["Заказы"].Колонки.Добавить("Сумма");
	ЗаказыДанные["Заказы"].Колонки.Добавить("ИДКонтрагента");	
	ЗаказыДанные["Заказы"].Колонки.Добавить("УчитыватьНДС");
	ЗаказыДанные["Заказы"].Колонки.Добавить("СтавкаНДС");
	ЗаказыДанные["Заказы"].Колонки.Добавить("СуммаВключаетНДС");
	ЗаказыДанные["Заказы"].Колонки.Добавить("СуммаСкидки");
	ЗаказыДанные["Заказы"].Колонки.Добавить("СкидкаВСумме");
	ЗаказыДанные["Заказы"].Колонки.Добавить("СуммаНДС");
		
	ЗаказыДанные.Вставить("Контрагенты", Новый ТаблицаЗначений);
	ЗаказыДанные["Контрагенты"].Колонки.Добавить("ИД");
	ЗаказыДанные["Контрагенты"].Колонки.Добавить("ИДЗаказа");
	ЗаказыДанные["Контрагенты"].Колонки.Добавить("Наименование");
	ЗаказыДанные["Контрагенты"].Колонки.Добавить("ПолноеНаименование");
	ЗаказыДанные["Контрагенты"].Колонки.Добавить("Адрес");
	ЗаказыДанные["Контрагенты"].Колонки.Добавить("Роль");	
	
	ЗаказыДанные.Вставить("КонтактныеЛица", Новый ТаблицаЗначений);	
	ЗаказыДанные["КонтактныеЛица"].Колонки.Добавить("ИДЗаказа");
	ЗаказыДанные["КонтактныеЛица"].Колонки.Добавить("ИДКонтрагента");
	ЗаказыДанные["КонтактныеЛица"].Колонки.Добавить("Наименование");
	
	ЗаказыДанные.Вставить("КонтактнаяИнформация", Новый ТаблицаЗначений);
	ЗаказыДанные["КонтактнаяИнформация"].Колонки.Добавить("ИД");
	ЗаказыДанные["КонтактнаяИнформация"].Колонки.Добавить("ИДЗаказа");
	ЗаказыДанные["КонтактнаяИнформация"].Колонки.Добавить("ТипКонтактнойИнформации");
	ЗаказыДанные["КонтактнаяИнформация"].Колонки.Добавить("Значение");
	
	ЗаказыДанные.Вставить("РеквизитыЗаказов", Новый ТаблицаЗначений);
	ЗаказыДанные["РеквизитыЗаказов"].Колонки.Добавить("ИД");
	ЗаказыДанные["РеквизитыЗаказов"].Колонки.Добавить("ТипРеквизита");
	ЗаказыДанные["РеквизитыЗаказов"].Колонки.Добавить("Значение");
	
	ЗаказыДанные.Вставить("РеквизитыТоваров", Новый ТаблицаЗначений);
	ЗаказыДанные["РеквизитыТоваров"].Колонки.Добавить("ИД");	
	ЗаказыДанные["РеквизитыТоваров"].Колонки.Добавить("ИДЗаказа");
	ЗаказыДанные["РеквизитыТоваров"].Колонки.Добавить("ТипРеквизита");
	ЗаказыДанные["РеквизитыТоваров"].Колонки.Добавить("Значение");
	
	ЗаказыДанные.Вставить("ТоварыЗаказов", Новый ТаблицаЗначений);
	ЗаказыДанные["ТоварыЗаказов"].Колонки.Добавить("ИД");	
	ЗаказыДанные["ТоварыЗаказов"].Колонки.Добавить("ИДКаталога");
	ЗаказыДанные["ТоварыЗаказов"].Колонки.Добавить("ИДЗаказа");
	ЗаказыДанные["ТоварыЗаказов"].Колонки.Добавить("Наименование");
	ЗаказыДанные["ТоварыЗаказов"].Колонки.Добавить("БазоваяЕдиница");
	ЗаказыДанные["ТоварыЗаказов"].Колонки.Добавить("Цена");
	ЗаказыДанные["ТоварыЗаказов"].Колонки.Добавить("Количество");
	ЗаказыДанные["ТоварыЗаказов"].Колонки.Добавить("Сумма");
	ЗаказыДанные["ТоварыЗаказов"].Колонки.Добавить("СуммаНДС");
	ЗаказыДанные["ТоварыЗаказов"].Колонки.Добавить("СтавкаНДС");
	ЗаказыДанные["ТоварыЗаказов"].Колонки.Добавить("ПроцентСкидкиНаценки");
	ЗаказыДанные["ТоварыЗаказов"].Колонки.Добавить("Валюта");
	
	Для Каждого СтрокаДокумент из ДеревоЗаказов.Строки Цикл
		
		// Заказ
		НоваяСтрокаЗаказ = ЗаказыДанные["Заказы"].Добавить();		
		ЗаполнитьЗначенияСвойств(НоваяСтрокаЗаказ, СтрокаДокумент);
		НоваяСтрокаЗаказ.ИДКонтрагента = СтрокаДокумент.СтруктураДанныхКонтрагента.ИД;
		
		// Контрагент
		НоваяСтрокаКонтрагент = ЗаказыДанные["Контрагенты"].Добавить();		
		НоваяСтрокаКонтрагент.ИД = НоваяСтрокаЗаказ.ИДКонтрагента;
		НоваяСтрокаКонтрагент.ИДЗаказа = НоваяСтрокаЗаказ.ИД;
		НоваяСтрокаКонтрагент.Наименование = СтрокаДокумент.СтруктураДанныхКонтрагента.Наименование;
		НоваяСтрокаКонтрагент.ПолноеНаименование = СтрокаДокумент.СтруктураДанныхКонтрагента.НаименованиеПолное;
		НоваяСтрокаКонтрагент.Адрес = СтрокаДокумент.СтруктураДанныхКонтрагента.Адрес;
		НоваяСтрокаКонтрагент.Роль = СтрокаДокумент.СтруктураДанныхКонтрагента.Роль;
		
		// Контактные лица
		Для Каждого СтрокаКонтактногоЛица из СтрокаДокумент.СтруктураДанныхКонтрагента.ТаблицаКонтактныхЛиц Цикл
			НоваяСтрокаКонтактногоЛица = ЗаказыДанные["КонтактныеЛица"].Добавить();			
			НоваяСтрокаКонтактногоЛица.ИДКонтрагента = НоваяСтрокаКонтрагент.ИД;
			НоваяСтрокаКонтактногоЛица.Наименование = СтрокаКонтактногоЛица.Наименование;
			НоваяСтрокаКонтактногоЛица.ИДЗаказа = НоваяСтрокаЗаказ.ИД;
		КонецЦикла;
		
		// Контактная информация
		Для Каждого СтрокаКонтакта из СтрокаДокумент.СтруктураДанныхКонтрагента.ТаблицаКонтактов Цикл
			НоваяСтрокаКонтактнойИнформации = ЗаказыДанные["КонтактнаяИнформация"].Добавить();			
			НоваяСтрокаКонтактнойИнформации.ИД = НоваяСтрокаКонтрагент.ИД;
			НоваяСтрокаКонтактнойИнформации.ТипКонтактнойИнформации = СтрокаКонтакта.КонтактТип;
			НоваяСтрокаКонтактнойИнформации.Значение = СтрокаКонтакта.КонтактЗначение;
			НоваяСтрокаКонтактнойИнформации.ИДЗаказа = НоваяСтрокаЗаказ.ИД;
		КонецЦикла;
		
		//реквизиты заказа
		Для Каждого СтрокаТЧ из СтрокаДокумент.Строки Цикл
			Если Не ЗначениеЗаполнено(СтрокаТЧ.ТоварУслугаИд) Тогда 
				// Реквизит заказа
				НоваяСтрокаРеквизитЗаказа = ЗаказыДанные["РеквизитыЗаказов"].Добавить();
				НоваяСтрокаРеквизитЗаказа.ИД = НоваяСтрокаЗаказ.ИД;
				НоваяСтрокаРеквизитЗаказа.ТипРеквизита = СтрокаТЧ.СвойствоНаименование;
				НоваяСтрокаРеквизитЗаказа.Значение = СтрокаТЧ.СвойствоЗначение;
			Иначе
				// Товар / услуга
				НоваяСтрокаТовараЗаказа = ЗаказыДанные["ТоварыЗаказов"].Добавить();
				НоваяСтрокаТовараЗаказа.ИДЗаказа = НоваяСтрокаЗаказ.ИД;
				НоваяСтрокаТовараЗаказа.ИД = СтрокаТЧ.ТоварУслугаИд;
				НоваяСтрокаТовараЗаказа.БазоваяЕдиница = СтрокаТЧ.ТоварУслугаБазоваяЕдиница;
				НоваяСтрокаТовараЗаказа.Сумма = СтрокаТЧ.ТоварУслугаСумма;
				НоваяСтрокаТовараЗаказа.СуммаНДС = СтрокаТЧ.СуммаНДС;
				НоваяСтрокаТовараЗаказа.СтавкаНДС = СтрокаТЧ.СтавкаНДС;
				НоваяСтрокаТовараЗаказа.Цена = СтрокаТЧ.ТоварУслугаЦенаЗаЕдиницу;
				НоваяСтрокаТовараЗаказа.Количество = СтрокаТЧ.ТоварУслугаКоличество;
				НоваяСтрокаТовараЗаказа.Наименование = СтрокаТЧ.ТоварУслугаНаименование;
				НоваяСтрокаТовараЗаказа.ИДКаталога = СтрокаТЧ.ТоварУслугаИдКаталога;
				НоваяСтрокаТовараЗаказа.Валюта = СтрокаТЧ.ТоварУслугаВалюта;
				
				Для каждого СтрокаРеквизитаТовара из СтрокаТЧ.Строки Цикл
					Если ЗначениеЗаполнено(СтрокаРеквизитаТовара.СуммаСкидки) И СтрокаРеквизитаТовара.СуммаСкидки <> 0 Тогда 
						НоваяСтрокаТовараЗаказа.ПроцентСкидкиНаценки = ?(НоваяСтрокаТовараЗаказа.Цена=0 ИЛИ НоваяСтрокаТовараЗаказа.Количество=0,0,100-Окр(СтрокаРеквизитаТовара.СуммаСкидки/(НоваяСтрокаТовараЗаказа.Цена*НоваяСтрокаТовараЗаказа.Количество)*100,2));
					Иначе
						НоваяСтрокаРеквизитвТоваров = ЗаказыДанные["РеквизитыТоваров"].Добавить();
						НоваяСтрокаРеквизитвТоваров.ИД = НоваяСтрокаТовараЗаказа.ИД;
						НоваяСтрокаРеквизитвТоваров.ТипРеквизита = СтрокаРеквизитаТовара.ЗначениеРеквизитаНаименование;
						НоваяСтрокаРеквизитвТоваров.Значение = СтрокаРеквизитаТовара.ЗначениеРеквизитаЗначение;
						НоваяСтрокаРеквизитвТоваров.ИДЗаказа = НоваяСтрокаЗаказ.ИД;
					КонецЕсли;
				КонецЦикла;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат ЗаказыДанные;	
	
КонецФункции

Функция прНайтиСоздатьНоменклатуру(Данные, РеквизитыТоваров, Параметры, КэшТовары, КэшЕдИзм)
	
	НоменклатураСсылка = Неопределено;
	
	// 1. Пытаемся найти по ГУИД
	ИДКаталога = Новый Массив;
	УИДНоменклатуры = Неопределено;
	Если НЕ ЗначениеЗаполнено(НоменклатураСсылка) И ЗначениеЗаполнено(Данные.ИДКаталога) Тогда
		
		// Пример значения ИДКаталога
		// Номеклатура 
		// "00000000-0000-0000-0000-000000000000"
		// Номеклатура#Характеристика 
		// "00000000-0000-0000-0000-000000000000#00000000-0000-0000-0000-000000000000"
		
		// Номенклатура
		УИД = Неопределено;
		ИДКаталога = _РазложитьСтрокуВМассив(Данные.ИДКаталога, "#");
		Если ИДКаталога.Количество() >= 1 Тогда
			УИД = ИДКаталога[0];
		КонецЕсли;
		
		Если ЗначениеЗаполнено(УИД) Тогда
			УИДНоменклатуры = _ПолучитьУИДПоСтроке(УИД);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(УИДНоменклатуры) Тогда
			НоменклатураСсылка = прПолучитьСсылкуПоИД(
				Справочники,
				"Номенклатура",
				УИДНоменклатуры,
				Параметры
			);
		КонецЕсли;
		
	КонецЕсли;
	
	// 2. Пытаемся найти по внешнему ИД номенклатуры
	ИДНоменклатуры = "";
	Если НЕ ЗначениеЗаполнено(НоменклатураСсылка) И ЗначениеЗаполнено(Заказ_СвойствоНоменклатурыВнешнийИД) И _СтрокаТолькоИзЦифр(Данные.ИД) Тогда		
		
		ИДНоменклатуры = СокрЛП(Данные.ИД);
		Если Заказ_КэшироватьДанные Тогда			
			НоменклатураСсылка = _ПолучитьЗначениеИзКэша(КэшТовары, "ИД", ИДНоменклатуры);
		Иначе			
			НоменклатураСсылка = прПолучитьСсылкуПоВнешнемуИД("Номенклатура", Заказ_СвойствоНоменклатурыВнешнийИД, ИДНоменклатуры);
		КонецЕсли;		
		
	КонецЕсли;
	
	// 3. Пытаемся найти по наименованию
	НаименованиеНоменклатуры = "";
	Если НЕ ЗначениеЗаполнено(НоменклатураСсылка) Тогда		
		
		НаименованиеНоменклатуры = СокрЛП(Данные.Наименование);
		Если Заказ_КэшироватьДанные Тогда
			НоменклатураСсылка = _ПолучитьЗначениеИзКэша(КэшТовары, "Наименования", НаименованиеНоменклатуры);
		Иначе		
			НоменклатураСсылка = Справочники.Номенклатура.НайтиПоНаименованию(НаименованиеНоменклатуры, Истина);
			Если ЗначениеЗаполнено(НоменклатураСсылка) И НоменклатураСсылка.ЭтоГруппа Тогда
				_ВывестиСообщение(НСтр("ru='Товар: ""';uk='Товар: ""'")+НаименованиеНоменклатуры+НСтр("ru='"" определен, как группа и будет пропущен.';uk='"" визначений, як група і буде пропущений.'"), СтатусСообщения.Внимание);
				НоменклатураСсылка = Неопределено;
			КонецЕсли;			
		КонецЕсли;
		
		// Проверка найденного значения
		Если ЗначениеЗаполнено(НоменклатураСсылка) И ЗначениеЗаполнено(ИДНоменклатуры) Тогда
			// если нашли по наименованию, а по ИД не нашли, стоит проверить, не другая ли это позиция
			ВнешнийИД = НоменклатураСсылка.ДополнительныеРеквизиты.НайтиСтроки(Новый Структура("Свойство", Заказ_СвойствоНоменклатурыВнешнийИД));
			Если ВнешнийИД.Количество() <> 0 Тогда
				НоменклатураСсылка = Неопределено;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;	
	
	Если ЗначениеЗаполнено(НоменклатураСсылка) Тогда
		Возврат НоменклатураСсылка;
	КонецЕсли;
	
	// Создаем новую номенклатуру	
	НоменклатураОбъект = Справочники["Номенклатура"].СоздатьЭлемент();
	Если ЗначениеЗаполнено(УИДНоменклатуры) Тогда
		НоменклатураОбъект.УстановитьСсылкуНового(Справочники["Номенклатура"].ПолучитьСсылку(УИДНоменклатуры));
	КонецЕсли;	
	
	НоменклатураОбъект.УстановитьНовыйКод();
	
	Если ЗначениеЗаполнено(Заказ_ГруппаНовойНоменклатуры) Тогда
		НоменклатураОбъект.Родитель = Заказ_ГруппаНовойНоменклатуры;
	КонецЕсли;
	
	НоменклатураОбъект.Наименование = СокрЛП(Данные.Наименование);
	НоменклатураОбъект.НаименованиеПолное = НоменклатураОбъект.Наименование;
	
	// Дополнительные реквизиты
	ДополнительныеРеквизиты = _ТаблицуВСоответствие(
									РеквизитыТоваров.НайтиСтроки(Новый Структура("ИД, ИДЗаказа", Данные.ИД, Данные.ИДЗаказа)),
									"ТипРеквизита",
									"Значение");

	
	// Тип номеклатуры
	ТипНоменклатуры = прПолучитьТипНоменклатуры(ДополнительныеРеквизиты["ТипНоменклатуры"], Параметры);
	Если ЗначениеЗаполнено(ТипНоменклатуры) Тогда
		НоменклатураОбъект.ТипНоменклатуры = ТипНоменклатуры;		
	Иначе
		_ВывестиСообщение(НСтр("ru='Не найден тип номенклатуры ""';uk='Не знайдений тип номенклатури ""'")+ДополнительныеРеквизиты["ТипНоменклатуры"]+""".", СтатусСообщения.Внимание);
	КонецЕсли;
	
	// Категория номенклатуры
	НоменклатураОбъект.КатегорияНоменклатуры = Справочники.КатегорииНоменклатуры.БезКатегории;
		
	// Единица измерения
	ЕдиницаИзмерения = прПолучитьЕдиницуИзмерения(Данные.БазоваяЕдиница, КэшЕдИзм, Параметры);
	Если ЗначениеЗаполнено(ЕдиницаИзмерения) Тогда		
		НоменклатураОбъект.ЕдиницаИзмерения = ЕдиницаИзмерения;
	Иначе
		_ВывестиСообщение(НСтр("ru='Не найдена ед. измерения ""';uk='Не знайдена од. виміру ""'")+Данные.БазоваяЕдиница+НСтр("ru='"". Внесите ее в классификатор.';uk='"". Внесіть її в класифікатор.'"), СтатусСообщения.Внимание);
	КонецЕсли;
	
	// Использовать характеристики
	Если ИДКаталога.Количество() >= 2 Тогда
		УИД = ИДКаталога[1];
		УИДХарактеристики = Неопределено;
		Если ЗначениеЗаполнено(УИД) Тогда
			УИДХарактеристики = _ПолучитьУИДПоСтроке(УИД);
		КонецЕсли;
		Если ЗначениеЗаполнено(УИДХарактеристики) Тогда
			НоменклатураОбъект.ИспользоватьХарактеристики = Истина;
		КонецЕсли;		
	КонецЕсли;	
	
	Если ЗначениеЗаполнено(ИДНоменклатуры) Тогда
		
		НовыйРеквизит = НоменклатураОбъект.ДополнительныеРеквизиты.Добавить();
		НовыйРеквизит.Свойство = Заказ_СвойствоНоменклатурыВнешнийИД;
		НовыйРеквизит.Значение = ИДНоменклатуры;
		
	КонецЕсли;
	
	НоменклатураОбъект.ОбменДанными.Загрузка = Истина;
	НоменклатураОбъект.Записать();
	
	НоменклатураСсылка = НоменклатураОбъект.Ссылка;
	
	Если Заказ_КэшироватьДанные Тогда
		Если ЗначениеЗаполнено(ИДНоменклатуры) Тогда
			_УстановитьЗначениеВКэш(КэшТовары , "ИД", ИДНоменклатуры, НоменклатураСсылка);
		КонецЕсли;
		Если ЗначениеЗаполнено(НаименованиеНоменклатуры) Тогда
			_УстановитьЗначениеВКэш(КэшТовары , "Наименования", НаименованиеНоменклатуры, НоменклатураСсылка);
		КонецЕсли;
		_ДобавитьЗначениеВКэш(КэшТовары, "Все", НоменклатураСсылка);
	КонецЕсли;
	
	_ВывестиСообщение(НСтр("ru='Создана новая номенклатура для заказа, наименование: ""';uk='Створена нова номенклатура для замовлення, найменування: ""'")+НоменклатураСсылка.Наименование+""".", СтатусСообщения.Информация);
	
	Возврат НоменклатураСсылка;
	
КонецФункции

Функция прНайтиСоздатьХарактеристикуНоменклатуры(Данные, Номенклатура, Параметры) 
	
	ХарактеристикаСсылка = Неопределено;
	
	ИспользоватьХарактеристики = Номенклатура.ИспользоватьХарактеристики;
	
	Если НЕ ИспользоватьХарактеристики Тогда
		Возврат ХарактеристикаСсылка;
	КонецЕсли;
		
	// 1. Пытаемся найти по ГУИД
	УИДХарактеристики = Неопределено;
	Если НЕ ЗначениеЗаполнено(ХарактеристикаСсылка) И ЗначениеЗаполнено(Данные.ИДКаталога) Тогда
		
		// Пример значения ИДКаталога
		// Номеклатура 
		// "00000000-0000-0000-0000-000000000000"
		// Номеклатура#Характеристика 
		// "00000000-0000-0000-0000-000000000000#00000000-0000-0000-0000-000000000000"
		
		// Характеристика
		УИД = Неопределено;		
		ИДКаталога = _РазложитьСтрокуВМассив(Данные.ИДКаталога, "#");
		Если ИДКаталога.Количество() >= 2 Тогда
			УИД = ИДКаталога[1];
		КонецЕсли;		
		
		Если ЗначениеЗаполнено(УИД) Тогда
			УИДХарактеристики = _ПолучитьУИДПоСтроке(УИД);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(УИДХарактеристики) Тогда
			ХарактеристикаСсылка = прПолучитьСсылкуПоИД(
				Справочники,
				"ХарактеристикиНоменклатуры",
				УИДХарактеристики,
				Параметры
			);
		КонецЕсли;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ХарактеристикаСсылка) Тогда
		Возврат ХарактеристикаСсылка;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(УИДХарактеристики) Тогда
		Возврат ХарактеристикаСсылка;
	КонецЕсли;
	
	// Создаем новую характеристику	
	ХаракетристикаОбъект = Справочники["ХарактеристикиНоменклатуры"].СоздатьЭлемент();
	ХаракетристикаОбъект.УстановитьСсылкуНового(Справочники["ХарактеристикиНоменклатуры"].ПолучитьСсылку(УИДХарактеристики));
	
	// Владелец
	ХаракетристикаОбъект.Владелец = Номенклатура;
	
	ХаракетристикаОбъект.Наименование = СокрЛП(Данные.Наименование);
	
	ХаракетристикаОбъект.ОбменДанными.Загрузка = Истина;
	ХаракетристикаОбъект.Записать();
	
	ХарактеристикаСсылка = ХаракетристикаОбъект.Ссылка;
	
	_ВывестиСообщение(НСтр("ru='Создана новая характеристика для заказа, наименование: ""';uk='Створена нова характеристика для замовлення, найменування: ""'")+ХарактеристикаСсылка.Наименование+""".", СтатусСообщения.Информация);
	
	Возврат ХарактеристикаСсылка;
	
КонецФункции

Функция прНайтиСоздатьКонтрагента(ДанныеКонтрагент, ДанныеКонтактнойИнформации, КэшКонтрагенты) 
	
	КонтрагентСсылка = Неопределено;
	
	ЭлектроннаяПочтаКонтрагента = Неопределено;
	ТелефонКонтрагента = Неопределено;
	Для каждого СтрокаКонтактнойИнформации Из ДанныеКонтактнойИнформации Цикл
		Если НРег(СтрокаКонтактнойИнформации.ТипКонтактнойИнформации) = "почта" Тогда
			ЭлектроннаяПочтаКонтрагента = ?(ЗначениеЗаполнено(СтрокаКонтактнойИнформации.Значение), СокрЛП(СтрокаКонтактнойИнформации.Значение), Неопределено);
		КонецЕсли;
		Если НРег(СтрокаКонтактнойИнформации.ТипКонтактнойИнформации) = "телефонрабочий" Тогда
			ТелефонКонтрагента = ?(ЗначениеЗаполнено(СтрокаКонтактнойИнформации.Значение), СокрЛП(СтрокаКонтактнойИнформации.Значение), Неопределено);
		КонецЕсли;		
	КонецЦикла;
	
	// 1. Пытаемся найти по внешнему ИД контрагента
	ИДКонтрагента = "";
	Если НЕ ЗначениеЗаполнено(КонтрагентСсылка) И ЗначениеЗаполнено(Заказ_СвойствоКонтрагентаВнешнийИД) И _СтрокаТолькоИзЦифр(ДанныеКонтрагент.ИД) Тогда
			
		// ИД пользователя
		ИДКонтрагента = СокрЛП(ДанныеКонтрагент.ИД);
		Если Заказ_КэшироватьДанные Тогда			
			КонтрагентСсылка = _ПолучитьЗначениеИзКэша(КэшКонтрагенты, "ИД", ИДКонтрагента);
		Иначе			
			КонтрагентСсылка = прПолучитьСсылкуПоВнешнемуИД("Контрагенты", Заказ_СвойствоКонтрагентаВнешнийИД, ИДКонтрагента);
		КонецЕсли;		
					
	КонецЕсли;
	
	// 2. Пытаемся найти по email
	Если НЕ ЗначениеЗаполнено(КонтрагентСсылка) И ЗначениеЗаполнено(ЭлектроннаяПочтаКонтрагента) Тогда
		
		// email
		Если Заказ_КэшироватьДанные Тогда
			КонтрагентСсылка = _ПолучитьЗначениеИзКэша(КэшКонтрагенты, "ЭлектроннаяПочта", ЭлектроннаяПочтаКонтрагента);
		Иначе
			Запрос = Новый Запрос;
			Запрос.Текст = "ВЫБРАТЬ
			|	КонтрагентыКонтактнаяИнформация.Ссылка
			|ИЗ
			|	Справочник.Контрагенты.КонтактнаяИнформация КАК КонтрагентыКонтактнаяИнформация
			|ГДЕ
			|	КонтрагентыКонтактнаяИнформация.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.АдресЭлектроннойПочты)
			|	И КонтрагентыКонтактнаяИнформация.Вид = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.EmailКонтрагента)
			|	И КонтрагентыКонтактнаяИнформация.Представление = &ЭлектроннаяПочта";
			
			Запрос.УстановитьПараметр("ЭлектроннаяПочта", ЭлектроннаяПочтаКонтрагента);
			
			Результат = Запрос.Выполнить();
			
			Выборка = Результат.Выбрать();		
			Если Выборка.Следующий() Тогда 
				КонтрагентСсылка = Выборка.Ссылка;
			КонецЕсли;
		КонецЕсли;
				
	КонецЕсли;
	
	// 3. Пытаемся найти по телефону
	Если НЕ ЗначениеЗаполнено(КонтрагентСсылка) И ЗначениеЗаполнено(ТелефонКонтрагента) Тогда
		
		// телефон
		Если Заказ_КэшироватьДанные Тогда
			КонтрагентСсылка = _ПолучитьЗначениеИзКэша(КэшКонтрагенты, "Телефоны", ТелефонКонтрагента);
		Иначе
			Запрос = Новый Запрос;
			Запрос.Текст = "ВЫБРАТЬ
			|	КонтрагентыКонтактнаяИнформация.Ссылка
			|ИЗ
			|	Справочник.Контрагенты.КонтактнаяИнформация КАК КонтрагентыКонтактнаяИнформация
			|ГДЕ
			|	КонтрагентыКонтактнаяИнформация.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.Телефон)
			|	И КонтрагентыКонтактнаяИнформация.Вид = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.ТелефонКонтрагента)
			|	И КонтрагентыКонтактнаяИнформация.Представление = &НомерТелефона";
			
			Запрос.УстановитьПараметр("НомерТелефона", ТелефонКонтрагента);
			
			Результат = Запрос.Выполнить();
			
			Выборка = Результат.Выбрать();		
			Если Выборка.Следующий() Тогда 
				КонтрагентСсылка = Выборка.Ссылка;
			КонецЕсли;
		КонецЕсли;		
		
	КонецЕсли;
		
	Если ЗначениеЗаполнено(КонтрагентСсылка) Тогда
		Возврат КонтрагентСсылка;
	КонецЕсли;
	
	// Создаем нового контрагента
	КонтрагентОбъект = Справочники.Контрагенты.СоздатьЭлемент();
	КонтрагентОбъект.УстановитьНовыйКод();
	
	Если ЗначениеЗаполнено(Заказ_ГруппаНовыхКонтрагентов) Тогда
		КонтрагентОбъект.Родитель = Заказ_ГруппаНовыхКонтрагентов;
	КонецЕсли;
	
	КонтрагентОбъект.Наименование = СокрЛП(ДанныеКонтрагент.Наименование);
	КонтрагентОбъект.НаименованиеПолное = СокрЛП(ДанныеКонтрагент.ПолноеНаименование);
	
	КонтрагентОбъект.ВидКонтрагента = Перечисления.ВидыКонтрагентов.ФизическоеЛицо;
	
	// Контактная информация
	Если ЗначениеЗаполнено(ЭлектроннаяПочтаКонтрагента) Тогда		
		НоваяСтрока = КонтрагентОбъект.КонтактнаяИнформация.Добавить();
		НоваяСтрока.Тип = Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты;
		НоваяСтрока.Вид = Справочники.ВидыКонтактнойИнформации.EmailКонтрагента;
		НоваяСтрока.ЗначенияПолей = ЭлектроннаяПочтаКонтрагента;
		НоваяСтрока.Представление = ЭлектроннаяПочтаКонтрагента;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТелефонКонтрагента) Тогда
		НоваяСтрока = КонтрагентОбъект.КонтактнаяИнформация.Добавить();
		НоваяСтрока.Тип = Перечисления.ТипыКонтактнойИнформации.Телефон;
		НоваяСтрока.Вид = Справочники.ВидыКонтактнойИнформации.ТелефонКонтрагента;
		НоваяСтрока.ЗначенияПолей = ТелефонКонтрагента;
		НоваяСтрока.Представление = ТелефонКонтрагента;
	КонецЕсли;
	
	// Дополнительные свойства
	Если ЗначениеЗаполнено(ИДКонтрагента) Тогда
		НоваяСтрока = КонтрагентОбъект.ДополнительныеРеквизиты.Добавить();
		НоваяСтрока.Свойство = Заказ_СвойствоКонтрагентаВнешнийИД;
		НоваяСтрока.Значение = ИДКонтрагента;
	КонецЕсли;
	
	КонтрагентОбъект.ОбменДанными.Загрузка = Истина;
	КонтрагентОбъект.Записать();
	
	КонтрагентСсылка = КонтрагентОбъект.Ссылка;
	
	Если Заказ_КэшироватьДанные Тогда
		Если ЗначениеЗаполнено(ИДКонтрагента) Тогда
			_УстановитьЗначениеВКэш(КэшКонтрагенты, "ИД", ИДКонтрагента, КонтрагентСсылка);
		КонецЕсли;
		Если ЗначениеЗаполнено(ЭлектроннаяПочтаКонтрагента) Тогда
			_УстановитьЗначениеВКэш(КэшКонтрагенты, "ЭлектроннаяПочта", ЭлектроннаяПочтаКонтрагента, КонтрагентСсылка);
		КонецЕсли;
		Если ЗначениеЗаполнено(ТелефонКонтрагента) Тогда
			_УстановитьЗначениеВКэш(КэшКонтрагенты, "Телефоны", ТелефонКонтрагента, КонтрагентСсылка);
		КонецЕсли;
		_ДобавитьЗначениеВКэш(КэшКонтрагенты, "Все", КонтрагентСсылка);
	КонецЕсли;
	
	_ВывестиСообщение(НСтр("ru='Создан новый контрагент: ""';uk='Створений новий контрагент: ""'")+КонтрагентСсылка.Наименование+""".", СтатусСообщения.Информация);	
	
	Возврат КонтрагентСсылка;
	
КонецФункции

Функция прНайтиСоздатьДоговорКонтрагента(ДокументОбъект, КэшДоговораКонтрагентов, Параметры)
	
	ДоговорСсылка = Неопределено;
	
	Если НЕ ДокументОбъект.Контрагент.ВестиРасчетыПоДоговорам Тогда
		Возврат ДоговорСсылка;
	КонецЕсли;
	
	КлассификаторВалют = Параметры.КонстантыОбмена.ДопустимыеВалюты;
	
	//1. Пытаемся найти по Контрагенту и Валюте
	Если Не ЗначениеЗаполнено(ДоговорСсылка) И ЗначениеЗаполнено(ДокументОбъект.Контрагент) И ЗначениеЗаполнено(ДокументОбъект.ВалютаДокумента) Тогда
		
		Если Заказ_КэшироватьДанные Тогда			
			ДоговорСсылка = _ПолучитьЗначениеИзКэша(КэшДоговораКонтрагентов, КлассификаторВалют[ДокументОбъект.ВалютаДокумента.Код], ДокументОбъект.Контрагент);
		Иначе
			Запрос = Новый Запрос;
			Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
			               |	ДоговорыКонтрагентов.Ссылка,
			               |	ДоговорыКонтрагентов.ДатаДоговора КАК ДатаНачалаДействия
			               |ИЗ
			               |	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
			               |ГДЕ
			               |	ДоговорыКонтрагентов.ВидДоговора = ЗНАЧЕНИЕ(Перечисление.ВидыДоговоров.СПокупателем)
			               |	И ДоговорыКонтрагентов.ВалютаРасчетов = &Валюта
			               |	И ДоговорыКонтрагентов.Владелец = &Контрагент
			               |
			               |УПОРЯДОЧИТЬ ПО
			               |	ДатаНачалаДействия УБЫВ";
			
			Запрос.УстановитьПараметр("Валюта", ДокументОбъект.ВалютаДокумента);
			Запрос.УстановитьПараметр("Контрагент", ДокументОбъект.Контрагент);
			
			Результат = Запрос.Выполнить();
			
			Выборка = Результат.Выбрать();		
			Если Выборка.Следующий() Тогда 
				ДоговорСсылка = Выборка.Ссылка;
			КонецЕсли;
		КонецЕсли;		
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДоговорСсылка) Тогда
		Возврат ДоговорСсылка;
	КонецЕсли;
	
	// Создаем новый договор контрагента
	ДоговорОбъект = Справочники.ДоговорыКонтрагентов.СоздатьЭлемент();
	
	ЗаполнитьЗначенияСвойств(ДоговорОбъект, ДокументОбъект, "Организация");
	
	ДоговорОбъект.Владелец = ДокументОбъект.Контрагент;
	
	ДоговорОбъект.Наименование = "Договор с "+ДокументОбъект.Контрагент+" ("+ДокументОбъект.ВалютаДокумента+")";
	ДоговорОбъект.НомерДоговора = "";
	ДоговорОбъект.ДатаДоговора = ТекущаяДата();
	ДоговорОбъект.ВалютаРасчетов = ДокументОбъект.ВалютаДокумента;
	
	ДоговорОбъект.ВидДоговора = Перечисления.ВидыДоговоров.СПокупателем;
	
	ДоговорОбъект.ОбменДанными.Загрузка = Истина;
	ДоговорОбъект.Записать();
	
	ДоговорСсылка = ДоговорОбъект.Ссылка;
	
	Если Заказ_КэшироватьДанные Тогда
		Если ЗначениеЗаполнено(ДоговорСсылка.ВалютаРасчетов) И ЗначениеЗаполнено(ДоговорСсылка.Владелец) Тогда
			_УстановитьЗначениеВКэш(КэшДоговораКонтрагентов , КлассификаторВалют[ДоговорСсылка.ВалютаРасчетов.Код], ДоговорСсылка.Владелец, ДоговорСсылка);
		КонецЕсли;
		_ДобавитьЗначениеВКэш(КэшДоговораКонтрагентов, "Все", ДоговорСсылка);
	КонецЕсли;
	
	_ВывестиСообщение(НСтр("ru='Создан новый договор с: ""';uk='Створений новий договір з: ""'")+ДокументОбъект.Контрагент+""".", СтатусСообщения.Информация);	
	
	Возврат ДоговорСсылка;
	
КонецФункции

Функция прСоздатьЗаказы(Отказ, Параметры, СтруктураЗаказов)
	
	ВсегоЗаказов = СтруктураЗаказов["Заказы"].Количество();	
	Если ВсегоЗаказов = 0 Тогда
		Возврат 0;
	КонецЕсли;
		
	_ИмяДокументаЗаказ = Параметры.КонстантыОбмена.Заказ.ИмяДокумента;	
	_ИмяРеквизитаНомерЗаказаВходящий = Параметры.КонстантыОбмена.Заказ.ИмяРеквизитаНомерЗаказаВходящий;
	_ИмяТЧТовары = Параметры.КонстантыОбмена.Заказ.ИмяТЧТовары;
	_ИмяКонстантыВалютаУчета = Параметры.КонстантыОбмена.Заказ.ИмяКонстантыВалютаУчета;
	
	// Кэш
	Кэш = прКэшироватьДанные(СтруктураЗаказов, Параметры);
	
	КэшЗаказы = Кэш.КэшЗаказы;
	КэшТовары = Кэш.КэшТовары;
	КэшКонтрагенты = Кэш.КэшКонтрагенты;
	КэшВалюты = Кэш.КэшВалюты;
	КэшЕдИзм = Кэш.КэшЕдИзм;
	КэшДоговораКонтрагентов = Кэш.КэшДоговораКонтрагентов;
	
	// Алиасы единиц измерений
	Параметры.Вставить("АлиасыЕдиницИзмеренийСайта", _ПолучитьАлиасыЕдиницИзмеренийССайта(Истина, Параметры));
		
	// Основная валюта
	ОсновнаяВалюта = Константы[_ИмяКонстантыВалютаУчета].Получить();
	КодОсновнойВалюты = СокрЛП(ОсновнаяВалюта.Код);
	НаименованиеОсновнойВалюты = СокрЛП(ОсновнаяВалюта.Наименование);	
	
	// Классификатор валют
	КлассификаторВалют = Параметры.КонстантыОбмена.ДопустимыеВалютыИнвертированные;
	НенайденныеВалюты = Новый Соответствие;
	
	// Запрос для поиска заказов
	Запрос = Новый Запрос("
			|ВЫБРАТЬ
			|	Т.Ссылка
			|ИЗ
			|	Документ."+_ИмяДокументаЗаказ+" КАК Т
			|ГДЕ
			|	Т.Номер = &НомерСВалютой И
			|	Т."+_ИмяРеквизитаНомерЗаказаВходящий+" = &НомерВходящий");	
		
	ОбработаноЗаказов 	= 0;
	
	Для каждого СтрокаЗаказ Из СтруктураЗаказов["Заказы"] Цикл
		
		ЗаказОбработанБезОшибок = Истина;
		
		ВалютыЗаказа = Новый Соответствие;
		Для каждого СтрокаТоварЗаказа Из СтруктураЗаказов["ТоварыЗаказов"].НайтиСтроки(Новый Структура("ИДЗаказа", СтрокаЗаказ.Ид)) Цикл
			КодВалюты = КлассификаторВалют[СтрокаТоварЗаказа.Валюта];
			Если ЗначениеЗаполнено(КодВалюты) Тогда
				ВалютыЗаказа.Вставить(КодВалюты, СтрокаТоварЗаказа.Валюта);
			Иначе
				НенайденныеВалюты.Вставить(СтрокаТоварЗаказа.Валюта);
			КонецЕсли;			
		КонецЦикла;
		
		ИспользуемОсновнуюВалюту = (ВалютыЗаказа.Количество() = 0);
		Если ИспользуемОсновнуюВалюту Тогда
			ВалютыЗаказа.Вставить(КодОсновнойВалюты, НаименованиеОсновнойВалюты);
		КонецЕсли;
		
		// Создаем и ищем заказы в разрере валют
		Для каждого ЭлементВалюты Из ВалютыЗаказа Цикл
			
			_КодВалюты = ЭлементВалюты.Ключ;
			_НаименованиеВалюты = СокрЛП(ЭлементВалюты.Значение);
			_НомерЗаказаВходящий = СокрЛП(СтрокаЗаказ.НомерВходящий);
			_НомерЗаказаСВалютой = _НомерЗаказаВходящий + _НаименованиеВалюты;						
			
			// Поиск заказа
			ЗаказСсылка = Неопределено;			
			Если Заказ_КэшироватьДанные Тогда
				ЗаказСсылка = _ПолучитьЗначениеИзКэша(КэшЗаказы, "ИД", _НомерЗаказаСВалютой);
			Иначе				
				Запрос.УстановитьПараметр("НомерСВалютой", _НомерЗаказаСВалютой);
				Запрос.УстановитьПараметр("НомерВходящий", _НомерЗаказаВходящий);
				
				ВыборкаЗаказ = Запрос.Выполнить().Выбрать();
				Если ВыборкаЗаказ.Следующий() Тогда 
					ЗаказСсылка = ВыборкаЗаказ.Ссылка;
				КонецЕсли;
			КонецЕсли;
						
			Если ЗначениеЗаполнено(ЗаказСсылка) Тогда
				
				Если Заказ_СоздаватьТолькоНовые Тогда
					_ВывестиСообщение(НСтр("ru='Заказ № ';uk='Замовлення № '") + _НомерЗаказаСВалютой + НСтр("ru=' не был изменен т.к. он уже существует.';uk=' не був змінений оскільки він вже існує.'"), СтатусСообщения.Информация);
					Продолжить;
				КонецЕсли;
				
				ДокументОбъект = ЗаказСсылка.ПолучитьОбъект();
				
				Если ДокументОбъект.Проведен Тогда
					_ВывестиСообщение(НСтр("ru='Заказ № ';uk='Замовлення № '") + _НомерЗаказаСВалютой + НСтр("ru=' не был изменен т.к. он проведен.';uk=' не був змінений оскільки він проведений.'"), СтатусСообщения.Информация);
					Продолжить;
				КонецЕсли;
								
				// Очистка данных документа
				мдДокумента = Метаданные.Документы[_ИмяДокументаЗаказ];
				Для каждого мдРеквизита Из мдДокумента.Реквизиты Цикл
					ДокументОбъект[мдРеквизита.Имя] = Неопределено;
				КонецЦикла;
				Для каждого мдТабличнойЧасти Из мдДокумента.ТабличныеЧасти Цикл
					ДокументОбъект[мдТабличнойЧасти.Имя].Очистить();
				КонецЦикла;
				ДокументОбъект.ПометкаУдаления = Ложь;
				
			Иначе
				ДокументОбъект = Документы[_ИмяДокументаЗаказ].СоздатьДокумент();				
			КонецЕсли;			
			
			// Заполнение заказа
			_Строки = СтруктураЗаказов["Контрагенты"].НайтиСтроки(Новый Структура("ИДЗаказа", СтрокаЗаказ.Ид));
			СтрокаКонтрагент = ?(_Строки.Количество() > 0, _Строки[0], Неопределено);
			СтрокиКонтактнойИнформации = Неопределено;
			Если СтрокаКонтрагент <> Неопределено Тогда
				СтрокиКонтактнойИнформации = СтруктураЗаказов["КонтактнаяИнформация"].НайтиСтроки(Новый Структура("ИД, ИДЗаказа", СтрокаКонтрагент.ИД, СтрокаЗаказ.Ид));
			КонецЕсли;			
			
			// Шапка вручную
			прИнициализироватьДокументЗаказ(ДокументОбъект);
			
			// Шапка значения заполнения по умолчанию
			Для каждого ПолеПоУмолчанию Из Параметры.ЗначенияРеквизитовШапкиЗаказаПоУмолчанию Цикл
				ДокументОбъект[ПолеПоУмолчанию.Ключ] = ПолеПоУмолчанию.Значение;
			КонецЦикла;
			
			ДокументОбъект[_ИмяРеквизитаНомерЗаказаВходящий] = _НомерЗаказаВходящий;
			ДокументОбъект.Номер = _НомерЗаказаСВалютой;
			ДокументОбъект.Дата = Дата(Формат(СтрокаЗаказ.Дата, "ДЛФ=D")+" "+СтрокаЗаказ.Время);
			ДокументОбъект.Организация = Организация;
			ДокументОбъект.ВалютаДокумента = прПолучитьВалюту(_КодВалюты, КэшВалюты);
			ДокументОбъект.Контрагент = прНайтиСоздатьКонтрагента(СтрокаКонтрагент, СтрокиКонтактнойИнформации, КэшКонтрагенты);
			ДокументОбъект.Договор = прНайтиСоздатьДоговорКонтрагента(ДокументОбъект, КэшДоговораКонтрагентов, Параметры);
			
			// Товары
			ОтборТовары = Новый Структура("ИДЗаказа", СтрокаЗаказ.Ид);
			Если Не ИспользуемОсновнуюВалюту Тогда
				ОтборТовары.Вставить("Валюта", _НаименованиеВалюты);
			КонецЕсли;
			
			Для каждого СтрокаТоварЗаказа Из СтруктураЗаказов["ТоварыЗаказов"].НайтиСтроки(ОтборТовары) Цикл
				
				НоваяСтрокаТовар = ДокументОбъект[_ИмяТЧТовары].Добавить();
				
				// ТЧ значения заполнения по умолчанию
				ЗаполнитьЗначенияСвойств(НоваяСтрокаТовар, Параметры.ЗначенияРеквизитовТЧЗаказаПоУмолчанию);
				
				НоваяСтрокаТовар.Номенклатура = прНайтиСоздатьНоменклатуру(СтрокаТоварЗаказа, СтруктураЗаказов["РеквизитыТоваров"], Параметры, КэшТовары, КэшЕдИзм);
				НоваяСтрокаТовар.Характеристика = прНайтиСоздатьХарактеристикуНоменклатуры(СтрокаТоварЗаказа, НоваяСтрокаТовар.Номенклатура, Параметры);
				НоваяСтрокаТовар.ЕдиницаИзмерения = ?(ЗначениеЗаполнено(НоваяСтрокаТовар.Номенклатура.ЕдиницаИзмерения), НоваяСтрокаТовар.Номенклатура.ЕдиницаИзмерения, прПолучитьЕдиницуИзмерения(СтрокаТоварЗаказа.БазоваяЕдиница, КэшЕдИзм, Параметры));
				
				// Количество, цена, сумма
				НоваяСтрокаТовар.Количество = СтрокаТоварЗаказа.Количество;				
				НоваяСтрокаТовар.Цена = СтрокаТоварЗаказа.Цена;
				НоваяСтрокаТовар.Сумма = СтрокаТоварЗаказа.Сумма;
				
				// Ручная скидки
				Если ЗначениеЗаполнено(НоваяСтрокаТовар.ПроцентСкидкиНаценки) Тогда
					
					СуммаРучнойСкидки = Окр(СтрокаТоварЗаказа.Количество * СтрокаТоварЗаказа.Цена * НоваяСтрокаТовар.ПроцентСкидкиНаценки / 100, 2);
					
					// Сумма после ручной скидки
					НоваяСтрокаТовар.Сумма = СтрокаТоварЗаказа.Сумма - СуммаРучнойСкидки;
					
				КонецЕсли;				
				
				// НДС
				Если ЗначениеЗаполнено(НоваяСтрокаТовар.СтавкаНДС) Тогда
					
					ПроцентНДС = прПолучитьСтавкуНДСЧислом(НоваяСтрокаТовар.СтавкаНДС, Параметры);
					
					НоваяСтрокаТовар.СуммаНДС = ?(ДокументОбъект.СуммаВключаетНДС, НоваяСтрокаТовар.Сумма * ПроцентНДС / (ПроцентНДС + 1), НоваяСтрокаТовар.Сумма * ПроцентНДС);
					НоваяСтрокаТовар.Всего = НоваяСтрокаТовар.Сумма + ?(ДокументОбъект.СуммаВключаетНДС, 0, НоваяСтрокаТовар.СуммаНДС);
					
				КонецЕсли;			
				
			КонецЦикла;
			
			// Комментарий
			Комментарий = "";
			Для каждого СтрокаРеквизитаЗаказа Из СтруктураЗаказов["РеквизитыЗаказов"].НайтиСтроки(Новый Структура("ИД", СтрокаЗаказ.Ид)) Цикл
				Комментарий = СокрЛП(Комментарий + " " + СтрокаРеквизитаЗаказа.ТипРеквизита + ": " + СтрокаРеквизитаЗаказа.Значение) + ",";
			КонецЦикла;
			
			ПримечаниеКлиента = ?(ЗначениеЗаполнено(СтрокаЗаказ.ПримечаниеКлиента), НСтр("ru=' Комментарий: ';uk=' Коментар: '") + СтрокаЗаказ.ПримечаниеКлиента, "");
						
			ДокументОбъект.Комментарий = Комментарий + ПримечаниеКлиента;
			
			СостояниеЗаказа = "";
			Причина = "";
			ЭтоНовый = ДокументОбъект.ЭтоНовый();
			
			Попытка				
				
				ДокументОбъект.Записать();
								
				НоваяСтрокаСозданныеЗаказы = СозданныеЗаказы.Добавить();
				НоваяСтрокаСозданныеЗаказы.Заказ = ДокументОбъект.Ссылка;
				
				Если Заказ_КэшироватьДанные И ЭтоНовый Тогда
					_УстановитьЗначениеВКэш(КэшЗаказы, "ИД", _НомерЗаказаСВалютой, ДокументОбъект.Ссылка);
					_ДобавитьЗначениеВКэш(КэшЗаказы, "Все", ДокументОбъект.Ссылка);
				КонецЕсли;				
				
				СостояниеЗаказа = ?(ЭтоНовый, "Создан новый", "Обновлен");
				
			Исключение
				
				ЗаказОбработанБезОшибок = Ложь;
							
				СостояниеЗаказа = ?(ЭтоНовый, "Не создан", "Не обновлен");
				Причина = ОписаниеОшибки();
				
			КонецПопытки;
			
			_ВывестиСообщение(СостояниеЗаказа + НСтр("ru=' заказ № ';uk=' замовлення № '")+ДокументОбъект.Номер+НСтр("ru=' от ';uk=' від '")+ДокументОбъект.Дата+"."+?(ЗначениеЗаполнено(Причина), НСтр("ru=' Причина: ';uk=' Причина: '")+Причина, ""), СтатусСообщения.Информация);
						
		КонецЦикла;		
		
		Если ЗаказОбработанБезОшибок Тогда
			ОбработаноЗаказов = ОбработаноЗаказов + 1;
		КонецЕсли;
				
	КонецЦикла;
	
	Для Каждого Валюта Из НенайденныеВалюты Цикл
		_ВывестиСообщение(НСтр("ru='В классификаторе валют не найдена валюта по наименованию ""';uk='У класифікаторі валют не знайдена валюта по найменуванню ""'") + Валюта.Ключ + """.", СтатусСообщения.Внимание);
	КонецЦикла;
	
	Возврат ОбработаноЗаказов;
	
КонецФункции

Функция прИнициализироватьДокументЗаказ(ДокументОбъект)
	
	ДокументОбъект.Заполнить(Неопределено);
	
	Возврат Истина;
	
КонецФункции

Функция прПолучитьСсылкуПоВнешнемуИД(ИмяСправочника, СвойствоВнешнийИД, ВнешнийИД) 
	
	Результат = Неопределено;
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("ВнешнийИД", ВнешнийИД);
	Запрос.УстановитьПараметр("СвойствоВнешнийИД", СвойствоВнешнийИД);
	
	Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
	|	ДополнительныеРеквизиты.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник."+ИмяСправочника+".ДополнительныеРеквизиты КАК ДополнительныеРеквизиты
	|ГДЕ
	|	ДополнительныеРеквизиты.Свойство = &СвойствоВнешнийИД
	|	И ДополнительныеРеквизиты.Значение = &ВнешнийИД";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	Если Выборка.Следующий() Тогда 
		Результат = Выборка.Ссылка;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция прПолучитьСсылкуПоИД(Менеджер, Имя, ИД, Параметры) 
	
	Результат = Неопределено;
	
	Попытка
		
		Результат = Менеджер[Имя].ПолучитьСсылку(ИД);
		
		// Фикс для: <Объект не найден> (42:80e83a24fa5f0e4711e48b8162258550)
		// определяем битую ссылку поиском подстроки, ПолучитьОбъект() - "тяжелый" метод
		ПараметрыБитойСсылки = Параметры.КонстантыОбмена.ПараметрыБитойСсылки;
		
		ПредставлениеСсылки = Лев(ВРег(СокрЛП(Строка(Результат))), ПараметрыБитойСсылки.Длина);
		Если Найти(ПредставлениеСсылки, ПараметрыБитойСсылки.Представление) <> 0 Тогда
			Результат = Неопределено;
		КонецЕсли;
		
	Исключение
		ОписаниеОшибки = НСтр("ru='Не удалось получить ссылку по уникальному идентификатору ""';uk='Не вдалося отримати посилання по унікальному ідентифікатору ""'")+Строка(ИД)+""". " + ОписаниеОшибки();
		_ВывестиСообщение(ОписаниеОшибки, СтатусСообщения.Внимание);
	КонецПопытки;
		
	Возврат Результат;
	
КонецФункции

Функция прПолучитьВалюту(Код, КэшВалюты) 
		
	Если Заказ_КэшироватьДанные Тогда
		Возврат _ПолучитьЗначениеИзКэша(КэшВалюты, "ИД", Код);
	Иначе
		Возврат Справочники.Валюты.НайтиПоКоду(Код, Истина);
	КонецЕсли;
	
КонецФункции

Функция прПолучитьЕдиницуИзмерения(Наименование, КэшЕдИзм, Параметры) 
		
	Результат = Неопределено;
	
	АлиасыЕдиницИзмеренийСайта = Параметры.АлиасыЕдиницИзмеренийСайта;
	
	СписокВариантов = АлиасыЕдиницИзмеренийСайта[Наименование];	
	Если СписокВариантов = Неопределено Тогда
		Возврат Результат;	
	КонецЕсли;
	
	Для Каждого ЕдИзм Из СписокВариантов Цикл
		Если Заказ_КэшироватьДанные Тогда
			Результат = _ПолучитьЗначениеИзКэша(КэшЕдИзм, "ИД", ЕдИзм);
		Иначе
			Результат = Справочники.КлассификаторЕдиницИзмерения.НайтиПоНаименованию(ЕдИзм, Истина);
		КонецЕсли;		
		Если ЗначениеЗаполнено(Результат) Тогда
			Прервать;
		КонецЕсли;		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Функция прПолучитьЕдиницыИзмерения()
	
	Запрос = Новый Запрос;	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Т.Ссылка,
	|	Т.Наименование
	|ИЗ
	|	Справочник.КлассификаторЕдиницИзмерения КАК Т";
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

Функция прПолучитьСтавкуНДСЧислом(СтавкаНДС, Параметры)
		
	Возврат Параметры.КонстантыОбмена.Заказ.СтавкиНДС[СтавкаНДС];
	
КонецФункции

Функция прПолучитьСтавкиНДС()
	
	Результат = Новый Соответствие;
	
	Если Метаданные.Перечисления.Найти("СтавкиНДС") <> Неопределено Тогда			
		Для Каждого СтавкаНДС Из Метаданные.Перечисления["СтавкиНДС"].ЗначенияПеречисления Цикл			
			Если СтавкаНДС.Имя = "НДС20" ИЛИ СтавкаНДС.Имя = "НДС20_120" Тогда
				
				Результат.Вставить(Перечисления["СтавкиНДС"][СтавкаНДС.Имя], 0.2);
				
			ИначеЕсли СтавкаНДС.Имя = "НДС6" ИЛИ СтавкаНДС.Имя = "НДС6_106" Тогда
				
				Результат.Вставить(Перечисления["СтавкиНДС"][СтавкаНДС.Имя], 0.06);
				
			ИначеЕсли СтавкаНДС.Имя = "НДС7" Тогда
				
				Результат.Вставить(Перечисления["СтавкиНДС"][СтавкаНДС.Имя], 0.07);
				
			ИначеЕсли СтавкаНДС.Имя = "НДС8" Тогда
				
				Результат.Вставить(Перечисления["СтавкиНДС"][СтавкаНДС.Имя], 0.08);
				
			ИначеЕсли СтавкаНДС.Имя = "НДС10" ИЛИ СтавкаНДС.Имя = "НДС10_110" Тогда
				
				Результат.Вставить(Перечисления["СтавкиНДС"][СтавкаНДС.Имя], 0.1);
				
			ИначеЕсли СтавкаНДС.Имя = "НДС18" ИЛИ СтавкаНДС.Имя = "НДС18_118" Тогда
				
				Результат.Вставить(Перечисления["СтавкиНДС"][СтавкаНДС.Имя], 0.18);
				
			ИначеЕсли СтавкаНДС.Имя = "НДС909" Тогда
				
				Результат.Вставить(Перечисления["СтавкиНДС"][СтавкаНДС.Имя], 0.0909);
				
			ИначеЕсли СтавкаНДС.Имя = "НДС1525" Тогда
				
				Результат.Вставить(Перечисления["СтавкиНДС"][СтавкаНДС.Имя], 0.1525);
				
			ИначеЕсли СтавкаНДС.Имя = "НДС1667" Тогда
				
				Результат.Вставить(Перечисления["СтавкиНДС"][СтавкаНДС.Имя], 0.1667);
				
			ИначеЕсли СтавкаНДС.Имя = "НДС24" ИЛИ СтавкаНДС.Имя = "НДС24_124" Тогда
				
				Результат.Вставить(Перечисления["СтавкиНДС"][СтавкаНДС.Имя], 0.24);
				
			Иначе
				
				Результат.Вставить(Перечисления["СтавкиНДС"][СтавкаНДС.Имя], 0);
				
			КонецЕсли;			
		КонецЦикла;		
	КонецЕсли;
	
	Если Метаданные.Справочники.Найти("СтавкиНДС") <> Неопределено Тогда		
		
		Выборка = Справочники["СтавкиНДС"].Выбрать();
		Пока Выборка.Следующий() Цикл
			Результат.Вставить(Выборка.Ссылка, Выборка.Ставка/100);
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция прПолучитьТипНоменклатуры(ТипНоменклатуры, Параметры)
		
	Возврат Параметры.КонстантыОбмена.Номенклатура.ТипыНоменклатуры[ТипНоменклатуры];
	
КонецФункции

Функция прПолучитьТипыНоменклатуры()
	
	Результат = Новый Соответствие;
	
	Если Метаданные.Перечисления.Найти("ТипыНоменклатуры") <> Неопределено Тогда
		Для Каждого ТипНоменклатуры Из Метаданные.Перечисления["ТипыНоменклатуры"].ЗначенияПеречисления Цикл
			Если ТипНоменклатуры.Имя = "Услуга"	Тогда
				Результат.Вставить("Услуга", Перечисления.ТипыНоменклатуры[ТипНоменклатуры.Имя]);
			ИначеЕсли ТипНоменклатуры.Имя = "Товар" Тогда
				Результат.Вставить("Товар", Перечисления.ТипыНоменклатуры[ТипНоменклатуры.Имя]);
			ИначеЕсли ТипНоменклатуры.Имя = "Запас" Тогда
				Результат.Вставить("Товар", Перечисления.ТипыНоменклатуры[ТипНоменклатуры.Имя]);
			КонецЕсли;		
		КонецЦикла;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция прПолучитьВидНоменклатуры(ВидНоменклатуры, Параметры)
		
	Возврат Параметры.КонстантыОбмена.Номенклатура.ВидыНоменклатуры[ВидНоменклатуры];
	
КонецФункции

Функция прПолучитьВидыНоменклатуры()
	
	Результат = Новый Соответствие;
	
	Если Метаданные.Справочники.Найти("ВидыНоменклатуры") <> Неопределено Тогда
		Выборка = Справочники["ВидыНоменклатуры"].Выбрать();
		Пока Выборка.Следующий() Цикл
			Результат.Вставить(Выборка.Наименование, Выборка.Ссылка);
		КонецЦикла;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция прКартинкаКорректная(Источник) 
	Возврат Источник.Свойство("ПолныйПуть");
КонецФункции

Процедура прУдалитьРегистрациюИзменений(Отказ, тзТовары) Экспорт
	
	Если НЕ ВыгружатьТолькоИзменения Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(УзелРегистрацииИзменений) Тогда
		Отказ = Истина;
		_ВывестиСообщение(НСтр("ru='Не заполнен узел регистрации изменений.';uk='Не заповнений вузол реєстрації змін.'"), СтатусСообщения.Информация, Истина, Истина);
		Возврат;
	КонецЕсли;
	
	мдУзла = УзелРегистрацииИзменений.Метаданные();
	ЭтоПредопределенныйУзел = ПланыОбмена[мдУзла.Имя].ЭтотУзел() = УзелРегистрацииИзменений;
	Если ЭтоПредопределенныйУзел Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	ВыгруженныеТовары = тзТовары.Скопировать(, "Номенклатура");
	ВыгруженныеТовары.Свернуть("Номенклатура");
	Для Каждого ТекСтрока Из ВыгруженныеТовары Цикл
		ПланыОбмена.УдалитьРегистрациюИзменений(УзелРегистрацииИзменений, ТекСтрока.Номенклатура);
	КонецЦикла;
	
КонецПроцедуры


// ПЕРЕОПРЕДЕЛЯЕМЫЕ: КЭШИРОВАНИЕ ДАННЫХ

Функция прКэшироватьДанные(СтруктураЗаказов, Параметры)
	
	Кэш = Новый Структура;	
	
	Кэш.Вставить("КэшТовары", прКэшироватьДанныеТовары(СтруктураЗаказов, Параметры));
	Кэш.Вставить("КэшЗаказы", прКэшироватьДанныеЗаказы(СтруктураЗаказов, Параметры));
	Кэш.Вставить("КэшКонтрагенты", прКэшироватьДанныеКонтрагенты(СтруктураЗаказов, Параметры));	
	Кэш.Вставить("КэшВалюты", прКэшироватьДанныеВалюты(СтруктураЗаказов, Параметры));
	Кэш.Вставить("КэшЕдИзм", прКэшироватьДанныеЕдИзм(СтруктураЗаказов, Параметры));
	Кэш.Вставить("КэшДоговораКонтрагентов", прКэшироватьДанныеДоговораКонтрагентов(СтруктураЗаказов, Параметры, Кэш.КэшКонтрагенты.Все, Кэш.КэшВалюты.Все));
	
	Возврат Кэш;	
	
КонецФункции

Функция прКэшироватьДанныеТовары(СтруктураЗаказов, Параметры)
	
	Кэш = Новый Структура;
	Кэш.Вставить("ИД", Новый Соответствие);
	Кэш.Вставить("Наименования", Новый Соответствие);
	Кэш.Вставить("Все", Новый Массив);
	
	Если Не Заказ_КэшироватьДанные Тогда
		Возврат Кэш;
	КонецЕсли;
	
	// ИД
	ТоварыЗаказов = СтруктураЗаказов.ТоварыЗаказов.Скопировать(, "ИД");
	ТоварыЗаказов.Свернуть("ИД");	
	ИДТоваров = ТоварыЗаказов.ВыгрузитьКолонку("ИД");
	
	// Наименование
	Наименования = СтруктураЗаказов.ТоварыЗаказов.Скопировать(, "Наименование");
	Наименования.Свернуть("Наименование");
	НаименованияТоваров = Наименования.ВыгрузитьКолонку("Наименование");
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	Номенклатура.Значение КАК Ключ,
	               |	Номенклатура.Ссылка КАК Значение
	               |ИЗ
	               |	Справочник.Номенклатура.ДополнительныеРеквизиты КАК Номенклатура
	               |ГДЕ
	               |	Номенклатура.Свойство = &СвойствоНоменклатурыВнешнийИД
	               |	И Номенклатура.Значение В(&ИДТоваров)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	Номенклатура.Наименование КАК Ключ,
	               |	Номенклатура.Ссылка КАК Значение
	               |ИЗ
	               |	Справочник.Номенклатура КАК Номенклатура
	               |ГДЕ
	               |	НЕ Номенклатура.ЭтоГруппа
	               |	И Номенклатура.Наименование В(&НаименованияТоваров)";
	
	Запрос.УстановитьПараметр("СвойствоНоменклатурыВнешнийИД", Заказ_СвойствоНоменклатурыВнешнийИД);
	Запрос.УстановитьПараметр("ИДТоваров", ИДТоваров);
	Запрос.УстановитьПараметр("НаименованияТоваров", НаименованияТоваров);
	
	РезультатыЗапросов = Запрос.ВыполнитьПакет();
	
	ПорядокЗапросов = Новый Соответствие;
	ПорядокЗапросов.Вставить(0, "ИД");
	ПорядокЗапросов.Вставить(1, "Наименования");
	
	_ЗаполнитьКэшДанными(Кэш, ПорядокЗапросов, РезультатыЗапросов);
	
	Кэш.Вставить("Все", _СгруппироватьЗначенияКэша(Кэш));
	
	Возврат Кэш;
	
КонецФункции

Функция прКэшироватьДанныеЗаказы(СтруктураЗаказов, Параметры)
	
	Кэш = Новый Структура;
	Кэш.Вставить("ИД", Новый Соответствие);
	Кэш.Вставить("Все", Новый Массив);
	
	Если Не Заказ_КэшироватьДанные Тогда
		Возврат Кэш;
	КонецЕсли;
	
	// ИД
	_ИмяДокументаЗаказ = Параметры.КонстантыОбмена.Заказ.ИмяДокумента;	
	_ИмяРеквизитаНомерЗаказаВходящий = Параметры.КонстантыОбмена.Заказ.ИмяРеквизитаНомерЗаказаВходящий;
	
	Заказы = СтруктураЗаказов.Заказы.Скопировать(, "ИД");
	Заказы.Свернуть("ИД");
	
	ИДЗаказов = Заказы.ВыгрузитьКолонку("ИД");
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	Заказы.Номер КАК Ключ,
	               |	Заказы.Ссылка КАК Значение
	               |ИЗ
	               |	Документ."+_ИмяДокументаЗаказ+" КАК Заказы
	               |ГДЕ
	               |	Заказы."+_ИмяРеквизитаНомерЗаказаВходящий+" В(&ИДЗаказов)";
	
	Запрос.УстановитьПараметр("ИДЗаказов", ИДЗаказов);
	
	РезультатыЗапросов = Запрос.ВыполнитьПакет();
	
	ПорядокЗапросов = Новый Соответствие;
	ПорядокЗапросов.Вставить(0, "ИД");
	
	_ЗаполнитьКэшДанными(Кэш, ПорядокЗапросов, РезультатыЗапросов);	
	
	Кэш.Вставить("Все", _СгруппироватьЗначенияКэша(Кэш));
	
	Возврат Кэш;
	
КонецФункции

Функция прКэшироватьДанныеКонтрагенты(СтруктураЗаказов, Параметры)
	
	Кэш = Новый Структура;
	Кэш.Вставить("ИД", Новый Соответствие);
	Кэш.Вставить("ЭлектроннаяПочта", Новый Соответствие);
	Кэш.Вставить("Телефоны", Новый Соответствие);
	Кэш.Вставить("Все", Новый Массив);
	
	Если Не Заказ_КэшироватьДанные Тогда
		Возврат Кэш;
	КонецЕсли;
	
	// ИД
	Контрагенты = СтруктураЗаказов.Контрагенты.Скопировать(, "ИД");
	Контрагенты.Свернуть("ИД");	
	ИДКонтрагентов = Контрагенты.ВыгрузитьКолонку("ИД");
	
	// Электронная почта
	ЭП = СтруктураЗаказов.КонтактнаяИнформация.Скопировать(СтруктураЗаказов.КонтактнаяИнформация.НайтиСтроки(Новый Структура("ТипКонтактнойИнформации", "Почта")), "Значение");
	ЭП.Свернуть("Значение");	
	ЭлектроннаяПочтаКонтрагентов = ЭП.ВыгрузитьКолонку("Значение");
	
	// Телефоны
	Телефоны = СтруктураЗаказов.КонтактнаяИнформация.Скопировать(СтруктураЗаказов.КонтактнаяИнформация.НайтиСтроки(Новый Структура("ТипКонтактнойИнформации", "ТелефонРабочий")), "Значение");
	Телефоны.Свернуть("Значение");	
	ТелефоныКонтрагентов = Телефоны.ВыгрузитьКолонку("Значение");
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	Контрагенты.Значение КАК Ключ,
	               |	Контрагенты.Ссылка КАК Значение
	               |ИЗ
	               |	Справочник.Контрагенты.ДополнительныеРеквизиты КАК Контрагенты
	               |ГДЕ
	               |	Контрагенты.Свойство = &СвойствоКонтрагентаВнешнийИД
	               |	И Контрагенты.Значение В(&ИДКонтрагентов)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	Контрагенты.Представление КАК Ключ,
	               |	Контрагенты.Ссылка КАК Значение
	               |ИЗ
	               |	Справочник.Контрагенты.КонтактнаяИнформация КАК Контрагенты
	               |ГДЕ
	               |	Контрагенты.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.АдресЭлектроннойПочты)
	               |	И Контрагенты.Вид = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.EmailКонтрагента)
	               |	И Контрагенты.Представление В(&ЭлектроннаяПочтаКонтрагентов)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	Контрагенты.Представление КАК Ключ,
	               |	Контрагенты.Ссылка КАК Значение
	               |ИЗ
	               |	Справочник.Контрагенты.КонтактнаяИнформация КАК Контрагенты
	               |ГДЕ
	               |	Контрагенты.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.Телефон)
	               |	И Контрагенты.Вид = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.ТелефонКонтрагента)
	               |	И Контрагенты.Представление В(&ТелефоныКонтрагентов)";
				   
	Запрос.УстановитьПараметр("СвойствоКонтрагентаВнешнийИД", Заказ_СвойствоКонтрагентаВнешнийИД);
	Запрос.УстановитьПараметр("ИДКонтрагентов", ИДКонтрагентов);
	Запрос.УстановитьПараметр("ЭлектроннаяПочтаКонтрагентов", ЭлектроннаяПочтаКонтрагентов);
	Запрос.УстановитьПараметр("ТелефоныКонтрагентов", ТелефоныКонтрагентов);
	
	РезультатыЗапросов = Запрос.ВыполнитьПакет();
	
	ПорядокЗапросов = Новый Соответствие;
	ПорядокЗапросов.Вставить(0, "ИД");
	ПорядокЗапросов.Вставить(1, "ЭлектроннаяПочта");
	ПорядокЗапросов.Вставить(2, "Телефоны");
	
	_ЗаполнитьКэшДанными(Кэш, ПорядокЗапросов, РезультатыЗапросов);
	
	Кэш.Вставить("Все", _СгруппироватьЗначенияКэша(Кэш));
	
	Возврат Кэш;
	
КонецФункции

Функция прКэшироватьДанныеВалюты(СтруктураЗаказов, Параметры)
	
	Кэш = Новый Структура;
	Кэш.Вставить("ИД", Новый Соответствие);
	Кэш.Вставить("Все", Новый Массив);
	
	Если Не Заказ_КэшироватьДанные Тогда
		Возврат Кэш;
	КонецЕсли;
	
	// ИД
	КлассификаторВалют = Параметры.КонстантыОбмена.ДопустимыеВалютыИнвертированные;
	
	Валюты = СтруктураЗаказов.ТоварыЗаказов.Скопировать(, "Валюта");
	Валюты.Свернуть("Валюта");	
	
	ВалютыЗаказов = Новый Массив;	
	Для Каждого ТекВалюта Из Валюты Цикл
		КодВалюты = КлассификаторВалют[ТекВалюта.Валюта];
		Если ЗначениеЗаполнено(КодВалюты) Тогда
			ВалютыЗаказов.Добавить(КодВалюты);
		КонецЕсли;		
	КонецЦикла;	
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	Валюты.Код КАК Ключ,
	               |	Валюты.Ссылка КАК Значение
	               |ИЗ
	               |	Справочник.Валюты КАК Валюты
	               |ГДЕ
	               |	Валюты.Код В(&Валюты)";
	
	Запрос.УстановитьПараметр("Валюты", ВалютыЗаказов);			   
	
	РезультатыЗапросов = Запрос.ВыполнитьПакет();
	
	ПорядокЗапросов = Новый Соответствие;
	ПорядокЗапросов.Вставить(0, "ИД");
	
	_ЗаполнитьКэшДанными(Кэш, ПорядокЗапросов, РезультатыЗапросов);
	
	Кэш.Вставить("Все", _СгруппироватьЗначенияКэша(Кэш));
	
	Возврат Кэш;
	
КонецФункции

Функция прКэшироватьДанныеЕдИзм(СтруктураЗаказов, Параметры)
	
	Кэш = Новый Структура;
	Кэш.Вставить("ИД", Новый Соответствие);
	Кэш.Вставить("Все", Новый Массив);
	
	Если Не Заказ_КэшироватьДанные Тогда
		Возврат Кэш;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ЕдИзм.Наименование КАК Ключ,
	               |	ЕдИзм.Ссылка КАК Значение
	               |ПОМЕСТИТЬ ЕдИзм
	               |ИЗ
	               |	&ЕдИзм КАК ЕдИзм
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ЕдИзм.Ключ,
	               |	ЕдИзм.Значение
	               |ИЗ
	               |	ЕдИзм КАК ЕдИзм";
			 
	Запрос.УстановитьПараметр("ЕдИзм", прПолучитьЕдиницыИзмерения());
	
	РезультатыЗапросов = Запрос.ВыполнитьПакет();
	
	ПорядокЗапросов = Новый Соответствие;
	ПорядокЗапросов.Вставить(1, "ИД");
	
	_ЗаполнитьКэшДанными(Кэш, ПорядокЗапросов, РезультатыЗапросов);
	
	Кэш.Вставить("Все", _СгруппироватьЗначенияКэша(Кэш));
	
	Возврат Кэш;
	
КонецФункции

Функция прКэшироватьДанныеДоговораКонтрагентов(СтруктураЗаказов, Параметры, Контрагенты, Валюты)
	
	Кэш = Новый Структура;
	
	КлассификаторВалют = Параметры.КонстантыОбмена.ДопустимыеВалюты;	
	
	Для Каждого Валюта Из Валюты Цикл
		Кэш.Вставить(КлассификаторВалют[Валюта.Код], Новый Соответствие);
	КонецЦикла;
	
	Кэш.Вставить("Все", Новый Массив);
	
	Если Не Заказ_КэшироватьДанные Тогда
		Возврат Кэш;
	КонецЕсли;	
	
	Запрос = Новый Запрос;
		
	Запрос.Текст = "ВЫБРАТЬ
	               |	ДоговорыКонтрагентов.ВалютаРасчетов КАК Валюта,
	               |	ДоговорыКонтрагентов.ВалютаРасчетов.Код КАК ВалютаКод,
	               |	ДоговорыКонтрагентов.Владелец КАК Контрагент,
	               |	МАКСИМУМ(ДоговорыКонтрагентов.ДатаДоговора) КАК ДатаНачалаДействия
	               |ПОМЕСТИТЬ втДоговорыКонтрагентов
	               |ИЗ
	               |	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	               |ГДЕ
	               |	ДоговорыКонтрагентов.ВидДоговора = ЗНАЧЕНИЕ(Перечисление.ВидыДоговоров.СПокупателем)
	               |	И ДоговорыКонтрагентов.ВалютаРасчетов В(&Валюты)
	               |	И ДоговорыКонтрагентов.Владелец В(&Контрагенты)
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ДоговорыКонтрагентов.ВалютаРасчетов,
	               |	ДоговорыКонтрагентов.ВалютаРасчетов.Код,
	               |	ДоговорыКонтрагентов.Владелец
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	втДоговорыКонтрагентов.Валюта КАК Валюта,
	               |	втДоговорыКонтрагентов.ВалютаКод КАК ВалютаКод,
	               |	втДоговорыКонтрагентов.ДатаНачалаДействия КАК ДатаНачалаДействия,
	               |	втДоговорыКонтрагентов.Контрагент КАК Ключ,
	               |	ДоговорыКонтрагентов.Ссылка КАК Значение
	               |ИЗ
	               |	втДоговорыКонтрагентов КАК втДоговорыКонтрагентов
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	               |		ПО втДоговорыКонтрагентов.Валюта = ДоговорыКонтрагентов.ВалютаРасчетов
	               |			И втДоговорыКонтрагентов.Контрагент = ДоговорыКонтрагентов.Владелец
	               |			И втДоговорыКонтрагентов.ДатаНачалаДействия = ДоговорыКонтрагентов.ДатаДоговора
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	ДатаНачалаДействия";
				   
	Запрос.УстановитьПараметр("Валюты", Валюты);
	Запрос.УстановитьПараметр("Контрагенты", Контрагенты);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		_УстановитьЗначениеВКэш(Кэш, КлассификаторВалют[Выборка.ВалютаКод], Выборка.Ключ, Выборка.Значение);
	КонецЦикла;	
	
	Кэш.Вставить("Все", _СгруппироватьЗначенияКэша(Кэш));
	
	Возврат Кэш;
	
КонецФункции


// ПЕРЕОПРЕДЕЛЯЕМЫЕ: КОНТРОЛЬ И ПРОВЕРКИ

Процедура прПроверитьКорректностьЗаполненияДанныхПередВыгрузкойТоваров(Отказ, Параметры)
		
	прПроверитьКорректностьЗаполненияОсновныхДанных(Отказ, Параметры);
	
	Если (ЗначениеЗаполнено(КатегорияЦеныРозница) и ЗначениеЗаполнено(КатегорияЦеныОпт)) и
			КатегорияЦеныРозница.ВалютаЦены <> КатегорияЦеныОпт.ВалютаЦены Тогда
		Отказ = Истина;
		_ВывестиСообщение(НСтр("ru='Валюта розничной и оптовой цены должны совпадать.';uk='Валюта роздрібної і оптової ціни повинні співпадати.'"), СтатусСообщения.Важное);
	КонецЕсли;
	Если ВыгружатьТолькоИзменения И Не ЗначениеЗаполнено(УзелРегистрацииИзменений) Тогда
		Отказ = Истина;
		_ВывестиСообщение(НСтр("ru='Не заполнен узел регистрации изменений.';uk='Не заповнений вузол реєстрації змін.'"), СтатусСообщения.Важное);
	КонецЕсли;
	
КонецПроцедуры

Процедура прПроверитьКорректностьЗаполненияДанныхПередЗагрузкойЗаказов(Отказ, Параметры)	
	
	прПроверитьКорректностьЗаполненияОсновныхДанных(Отказ, Параметры);
	
	Если ЗначениеЗаполнено(Заказ_ГруппаНовойНоменклатуры) И НЕ Параметры.КонстантыОбмена.СправочникНоменклатураИерархический Тогда
		Заказ_ГруппаНовойНоменклатуры = Неопределено;
	КонецЕсли;	
	Если ЗначениеЗаполнено(Заказ_ГруппаНовыхКонтрагентов) И НЕ Параметры.КонстантыОбмена.СправочникКонтрагентыИерархический Тогда
		Заказ_ГруппаНовыхКонтрагентов = Неопределено;
	КонецЕсли;	
		
КонецПроцедуры

Процедура прПроверитьКорректностьЗаполненияОсновныхДанных(Отказ, Параметры)
	
	Если Не ЗначениеЗаполнено(Организация) Тогда
		Отказ = Истина;
		_ВывестиСообщение(НСтр("ru='Не выбрана организация.';uk='Не обрана організація.'"), СтатусСообщения.Информация);
	КонецЕсли;	
	Если РежимОбмена = Параметры.КонстантыОбмена.РежимыОбмена.Сайт Тогда
		Если Не ЗначениеЗаполнено(Интернет_АдресСайта) или Не ЗначениеЗаполнено(Интернет_КлючПользователя) Тогда
			Отказ = Истина;
			_ВывестиСообщение(НСтр("ru='Для работы с сайтом нужно заполнить поля ""Адрес сайта"" и ""Ключ пользователя"".';uk='Для роботи з сайтом треба заповнити поля ""Адреса сайту"" і ""Ключ користувача"".'"), СтатусСообщения.Важное);
		КонецЕсли;
	КонецЕсли;
	Если РежимОбмена = Параметры.КонстантыОбмена.РежимыОбмена.Файл Тогда
		Если Не ЗначениеЗаполнено(ИмяФайлаОбмена) Тогда 
			Отказ = Истина;
			_ВывестиСообщение(НСтр("ru='Не заполнено имя файла обмена.';uk='Не заповнено ім`я файлу обміну.'"), СтатусСообщения.Важное);
		КонецЕсли;
		Если ИспользоватьПодключениеКСайту Тогда
			Если Не ЗначениеЗаполнено(Интернет_АдресСайта) или Не ЗначениеЗаполнено(Интернет_КлючПользователя) Тогда
				Отказ = Истина;
				_ВывестиСообщение(НСтр("ru='Для работы с сайтом нужно заполнить поля ""Адрес сайта"" и ""Ключ пользователя"".';uk='Для роботи з сайтом треба заповнити поля ""Адреса сайту"" і ""Ключ користувача"".'"), СтатусСообщения.Важное);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	Если РежимОбмена = Параметры.КонстантыОбмена.РежимыОбмена.Сайт ИЛИ ИспользоватьПодключениеКСайту Тогда
		Если Прокси_Использование Тогда
			Если Не ЗначениеЗаполнено(Прокси_Сервер) Тогда
				Отказ = Истина;
				_ВывестиСообщение(НСтр("ru='Не заполнен прокси-сервер.';uk='Не заповнений проксі-сервер.'"), СтатусСообщения.Важное);
			КонецЕсли;
			Если Не ЗначениеЗаполнено(Прокси_Порт) Тогда
				Отказ = Истина;
				_ВывестиСообщение(НСтр("ru='Не заполнен порт прокси-сервера.';uk='Не заповнений порт проксі-сервера.'"), СтатусСообщения.Важное);
			КонецЕсли;
			Если Не ЗначениеЗаполнено(Прокси_ИмяПользователя) Тогда
				Отказ = Истина;
				_ВывестиСообщение(НСтр("ru='Не заполнен пользователь прокси-сервера.';uk='Не заповнений користувач проксі-сервера.'"), СтатусСообщения.Важное);
			КонецЕсли;
			Если Не ЗначениеЗаполнено(Прокси_Пароль) Тогда
				Отказ = Истина;
				_ВывестиСообщение(НСтр("ru='Не заполнен пароль прокси-сервера.';uk='Не заповнений пароль проксі-сервера.'"), СтатусСообщения.Важное);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;	
	
КонецПроцедуры


// ПЕРЕОПРЕДЕЛЯЕМЫЕ: ЭКСПОРТНЫЕ

Функция прПолучитьСхемуКомпоновкиДанныхТоваров() Экспорт
		
	Возврат ПолучитьМакет("ПодборТовара");
	
КонецФункции

Функция прПолучитьТовары(Параметры) Экспорт
		
	КомпоновщикТоваров = Параметры.КомпоновщикТоваров;
	
	СхемаКД = прПолучитьСхемуКомпоновкиДанныхТоваров();
	
	КомпоновщикТоваров.Настройки.ПараметрыДанных.УстановитьЗначениеПараметра("КатегорияЦены1", КатегорияЦеныРозница);
	КомпоновщикТоваров.Настройки.ПараметрыДанных.УстановитьЗначениеПараметра("КатегорияЦены2", КатегорияЦеныОпт);
	ИспользованиеХарактеристик = ИспользоватьХарактеристики И Константы[Параметры.КонстантыОбмена.Характеристика.ИмяКонстантыИспользоватьХарактеристики].Получить();
	КомпоновщикТоваров.Настройки.ПараметрыДанных.УстановитьЗначениеПараметра("ИспользоватьХарактеристики", ИспользованиеХарактеристик);
	КомпоновщикТоваров.Настройки.ПараметрыДанных.УстановитьЗначениеПараметра("ВыгружатьТолькоИзменения", ВыгружатьТолькоИзменения);
	КомпоновщикТоваров.Настройки.ПараметрыДанных.УстановитьЗначениеПараметра("УзелРегистрацииИзменений", УзелРегистрацииИзменений);
		
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКД, КомпоновщикТоваров.ПолучитьНастройки(),,,Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"));
	
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки);
	
	Результат = Новый ТаблицаЗначений;
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;
	ПроцессорВывода.УстановитьОбъект(Результат);
	ПроцессорВывода.Вывести(ПроцессорКомпоновки, Истина);
	
	Результат.Колонки.Добавить("Выгружать");
	Результат.Колонки.Добавить("НоменклатураИД");	
	Для каждого СтрокаТаблицы Из Результат Цикл
		СтрокаТаблицы.Выгружать = Истина;
		СтрокаТаблицы.НоменклатураИД = xmlСтрока(СтрокаТаблицы.Номенклатура);
	КонецЦикла;
	
	Возврат Результат;
		
КонецФункции

Функция прПолучитьДанныеИспользуемыхЕдиницИзмерений(ДопустимыеЕдиницыИзмерения) Экспорт
	
	Результат = Новый Структура;
	
	// 1. предопределенные сайт
	Результат.Вставить("ЕдиницыИспользуемыеНаСайте", Новый Массив);
	Для каждого ЕдиницаИзмерения Из ДопустимыеЕдиницыИзмерения Цикл
		Результат.ЕдиницыИспользуемыеНаСайте.Добавить(ЕдиницаИзмерения);
	КонецЦикла;
	// 2. алиасы сайт
	ДанныеССайта = _ПолучитьДанныеССайта();
	Если ДанныеССайта = Неопределено Тогда
		_ВывестиСообщение(НСтр("ru='Не удалось получить с сайта данные о используемых единицах измерений!';uk='Не вдалося отримати з сайту дані про використовувані одиниці виміру!'"), СтатусСообщения.Важное);
	Иначе
		Для каждого ЕдиницаИзмерения Из ДанныеССайта["measure_units"] Цикл
			Результат.ЕдиницыИспользуемыеНаСайте.Добавить(ЕдиницаИзмерения.Ключ);
		КонецЦикла;
	КонецЕсли;
	
	// 3. существующие в 1с	
	Результат.Вставить("ЕдиницыИспользуемыеВ1С", прПолучитьЕдиницыИзмерения().ВыгрузитьКолонку("Наименование"));
	
	Возврат Результат;
	
КонецФункции

Процедура прЗаполнитьДополнительныеРеквизитыИСведения() Экспорт
	
	Свойства.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	НаборыРеквизитовИСведений.Ссылка КАК НаборРеквизитовИСведений
	               |ПОМЕСТИТЬ НаборыРеквизитовИСведений
	               |ИЗ
	               |	Справочник.НаборыДополнительныхРеквизитовИСведений КАК НаборыРеквизитовИСведений
	               |ГДЕ
	               |	(НаборыРеквизитовИСведений.Ссылка = ЗНАЧЕНИЕ(Справочник.НаборыДополнительныхРеквизитовИСведений.Справочник_Номенклатура)
	               |			ИЛИ НаборыРеквизитовИСведений.Ссылка = ЗНАЧЕНИЕ(Справочник.НаборыДополнительныхРеквизитовИСведений.Справочник_ХарактеристикиНоменклатуры))
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	Свойства.Свойство КАК Свойство,
	               |	Свойства.СвойствоНаименование КАК СвойствоНаименование,
	               |	ИСТИНА КАК Выгружать
	               |ИЗ
	               |	(ВЫБРАТЬ
	               |		Свойства.Ссылка КАК Свойство,
	               |		Свойства.Заголовок КАК СвойствоНаименование
	               |	ИЗ
	               |		ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения КАК Свойства
	               |			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.НаборыДополнительныхРеквизитовИСведений.ДополнительныеРеквизиты КАК НаборыРеквизитов
	               |			ПО Свойства.Ссылка = НаборыРеквизитов.Свойство
	               |	ГДЕ
	               |		НаборыРеквизитов.Ссылка В
	               |				(ВЫБРАТЬ
	               |					НаборыРеквизитовИСведений.НаборРеквизитовИСведений
	               |				ИЗ
	               |					НаборыРеквизитовИСведений КАК НаборыРеквизитовИСведений)
	               |		И НЕ Свойства.ПометкаУдаления
	               |	
	               |	ОБЪЕДИНИТЬ
	               |	
	               |	ВЫБРАТЬ
	               |		Свойства.Ссылка,
	               |		Свойства.Заголовок
	               |	ИЗ
	               |		ПланВидовХарактеристик.ДополнительныеРеквизитыИСведения КАК Свойства
	               |			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.НаборыДополнительныхРеквизитовИСведений.ДополнительныеСведения КАК НаборыСведений
	               |			ПО Свойства.Ссылка = НаборыСведений.Свойство
	               |	ГДЕ
	               |		НаборыСведений.Ссылка В
	               |				(ВЫБРАТЬ
	               |					НаборыРеквизитовИСведений.НаборРеквизитовИСведений
	               |				ИЗ
	               |					НаборыРеквизитовИСведений КАК НаборыРеквизитовИСведений)
	               |		И НЕ Свойства.ПометкаУдаления) КАК Свойства
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	Свойства.СвойствоНаименование";
				   
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		СвойствоНаименование = СокрЛП(Выборка.СвойствоНаименование);
				
		НоваяСтрока = Свойства.Добавить();
		
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка, , "СвойствоНаименование");
		
		НоваяСтрока.СвойствоНаименование = СвойствоНаименование;
		
	КонецЦикла;
	
	СвойстваПредопределенные = _ПолучитьСвойстваПредопределенные();
	
	Для Каждого СвойствоПредопределенное Из СвойстваПредопределенные Цикл
		
		Свойство = СвойствоПредопределенное.Ключ;
		ПараметрыСвойства = СвойствоПредопределенное.Значение;
		
		Если Не ЗначениеЗаполнено(ЭтотОбъект[ПараметрыСвойства.Реквизит]) Тогда
			ЭтотОбъект[ПараметрыСвойства.Реквизит] = ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоРеквизиту("Заголовок", Свойство);
		КонецЕсли;
		
	КонецЦикла;	
	
КонецПроцедуры

Функция прПолучитьРеквизитыЗаказаАвтоЗаполнения(ОбластьРеквизитов) Экспорт
	
	Результат = Новый Массив;
	
	Если ОбластьРеквизитов = "Шапка" Тогда
		
		Результат.Добавить("Организация");
		Результат.Добавить("ВалютаДокумента");
		Результат.Добавить("Валюта");
		Результат.Добавить("Комментарий");
		Результат.Добавить("Контрагент");
		Результат.Добавить("Партнер");
		Результат.Добавить("Соглашение");
		Результат.Добавить("Договор");
		Результат.Добавить("СуммаДокумента");
		Результат.Добавить("НомерВходящегоДокумента");
		Результат.Добавить("ДатаВходящегоДокумента");
		Результат.Добавить("НомерПоДаннымКлиента");
		Результат.Добавить("ДатаПоДаннымКлиента");
		
	ИначеЕсли ОбластьРеквизитов = "ТЧ" Тогда
		
		Результат.Добавить("Номенклатура");
		Результат.Добавить("Характеристика");
		Результат.Добавить("НоменклатураНабора");
		Результат.Добавить("ХарактеристикаНабора");
		Результат.Добавить("Упаковка");
		Результат.Добавить("ЕдиницаИзмерения");
		Результат.Добавить("КоличествоУпаковок");
		Результат.Добавить("Количество");
		Результат.Добавить("Цена");
		Результат.Добавить("Сумма");
		Результат.Добавить("СуммаНДС");
		Результат.Добавить("СуммаСНДС");
		Результат.Добавить("СуммаРучнойСкидки");
		Результат.Добавить("ПроцентАвтоматическойСкидки");
		Результат.Добавить("СуммаАвтоматическойСкидки");
		Результат.Добавить("Всего");
		
		// Служебные реквизиты
		Результат.Добавить("КодСтроки");
		Результат.Добавить("КлючСвязи");
		Результат.Добавить("Серия");
		Результат.Добавить("СтатусУказанияСерий");
		Результат.Добавить("УказыватьСерии");
		Результат.Добавить("Отменено");
		
	КонецЕсли;	
	
	Возврат Результат;
	
КонецФункции

// ЯДРО: ЭКСПОРТНЫЕ

Процедура _ИнициализироватьКонстантыОбмена(Источник, Параметры) Экспорт
	
	Источник = Новый Структура;
	
	// "РежимВыгрузки"
	Источник.Вставить("РежимыОбмена", Новый Структура);
	Источник["РежимыОбмена"].Вставить("Сайт", "Сайт");
	Источник["РежимыОбмена"].Вставить("Файл", "Файл");
	
	// "ВнутренняяОбработка"	
	ВнутренняяОбработка = Истина;
	ИдентификаторКоманды = "";
	Параметры.Свойство("ИдентификаторКоманды", ИдентификаторКоманды);
	Если НЕ ЗначениеЗаполнено(ИдентификаторКоманды) И НЕ Метаданные.Обработки.Содержит(Метаданные()) Тогда
		ВнутренняяОбработка = Ложь;
	КонецЕсли;
	Источник.Вставить("ВнутренняяОбработка", ВнутренняяОбработка);
	
	// "КаталогВременныхФайлов"
	Источник.Вставить("КаталогВременныхФайлов", КаталогВременныхФайлов());
	
	// "ДопустимыеВалюты"
	Источник.Вставить("ДопустимыеВалюты", 
		_ТаблицуВСоответствие(
			_МакетВТаблицуЗначений(ПолучитьМакет("ДопустимыеВалюты"), 2),
			"Код",
			НСтр("ru='Сокращение';uk='Скорочення'")		
		)	
	);
	
	// "ДопустимыеВалютыИнвертированные"
	Источник.Вставить("ДопустимыеВалютыИнвертированные", 
		_ТаблицуВСоответствие(
			_МакетВТаблицуЗначений(ПолучитьМакет("ДопустимыеВалюты"), 2),
			НСтр("ru='Сокращение';uk='Скорочення'"),
			"Код"		
		)	
	);
	
	// "ДопустимыеЕдиницыИзмерения"
	Источник.Вставить("ДопустимыеЕдиницыИзмерения", 
		_МакетВМассив(
			ПолучитьМакет("ДопустимыеЕдиницыИзмерения"),
			1,
			1
		)
	);
	
	// "АдресаСайтов"
	АдресаСайтов = Новый Соответствие;
	АдресаСайтов.Вставить("my.prom.ua", "PROMUA");
	Источник.Вставить("АдресаСайтов", АдресаСайтов);
	
	// "КаталогКэша"
	Файл = Новый Файл(ПолучитьИмяВременногоФайла());
	КаталогКэша = Файл.Путь+"cache\";
	Файл = Новый Файл(КаталогКэша);
	Если Не Файл.Существует() Тогда
		// НЕ обрабатываем исключения если нет прав на запись
		СоздатьКаталог(КаталогКэша);
	КонецЕсли;	
	Источник.Вставить("КаталогКэша", КаталогКэша);
	
	// "ПараметрыБитойСсылки"
	ПараметрыБитойСсылки = Новый Соответствие;
	ПараметрыБитойСсылки.Вставить("en", Новый Структура("Представление, Длина", ВРег("<Object not found>"), СтрДлина(ВРег("<Object not found>")))); //английский
	ПараметрыБитойСсылки.Вставить("ru", Новый Структура("Представление, Длина", ВРег("<Объект не найден>"), СтрДлина(ВРег("<Объект не найден>")))); //русский
	ПараметрыБитойСсылки.Вставить("uk", Новый Структура("Представление, Длина", ВРег("<Об`єкт не знайдено>"), СтрДлина(ВРег("<Об`єкт не знайдено>")))); //украинский
	//ПараметрыБитойСсылки.Вставить("bg", Новый Структура("Представление, Длина", ВРег("<Обектът не е намерен>"), СтрДлина(ВРег("<Обектът не е намерен>")))); //болгарский
	//ПараметрыБитойСсылки.Вставить("vi", Новый Структура("Представление, Длина", ВРег("<Không tìm thấy đối tượng>"), СтрДлина(ВРег("<Không tìm thấy đối tượng>")))); //вьетнамский
	//ПараметрыБитойСсылки.Вставить("kk", Новый Структура("Представление, Длина", ВРег("<Объект табылған жоқ>"), СтрДлина(ВРег("<Объект табылған жоқ>")))); //казахский
	//ПараметрыБитойСсылки.Вставить("lv", Новый Структура("Представление, Длина", ВРег("<Objekts nav atrasts>"), СтрДлина(ВРег("<Objekts nav atrasts>")))); //латышский
	//ПараметрыБитойСсылки.Вставить("lt", Новый Структура("Представление, Длина", ВРег("<Objektas nerastas>"), СтрДлина(ВРег("<Objektas nerastas>")))); //литовский
	//ПараметрыБитойСсылки.Вставить("de", Новый Структура("Представление, Длина", ВРег("<Objekt nicht gefunden>"), СтрДлина(ВРег("<Objekt nicht gefunden>")))); //немецкий
	//ПараметрыБитойСсылки.Вставить("ro", Новый Структура("Представление, Длина", ВРег("<Obiectul nu a fost găsit>"), СтрДлина(ВРег("<Obiectul nu a fost găsit>")))); //румынский	
	Источник.Вставить("ПараметрыБитойСсылки", ПараметрыБитойСсылки[ТекущийЯзыкСистемы()]);
	
	// "СправочникНоменклатураИерархический"
	Источник.Вставить("СправочникНоменклатураИерархический", Метаданные.Справочники.Номенклатура.Иерархический);
	
	// "СправочникКонтрагентыИерархический"
	Источник.Вставить("СправочникКонтрагентыИерархический", Метаданные.Справочники.Контрагенты.Иерархический);
	
	// переопределяемая часть инициализации констант
	прИнициализироватьКонстантыОбмена(Источник);
	
КонецПроцедуры

Процедура _ИнициализироватьРеквизитыОбъекта(КонстантыОбмена) Экспорт
	
	// Режим обмена
	//
	РежимОбмена = КонстантыОбмена.РежимыОбмена.Сайт;
	
	// Дополнительные реквизиты и сведения
	//
	прЗаполнитьДополнительныеРеквизитыИСведения();
	
	// Дата формирования
	//
	Если Не ЗначениеЗаполнено(Заказ_ДатаЗагрузки) Тогда
		
		ДатаПоследнейУдачнойЗагрузкиЗаказов = Неопределено;		
		ЗначениеВнутреннихНастроек = _ПолучитьЗначенияВнутреннихНастроек();		
		ЗначениеВнутреннихНастроек.Свойство("ДатаПоследнейУдачнойЗагрузкиЗаказов", ДатаПоследнейУдачнойЗагрузкиЗаказов);
		
		Если ЗначениеЗаполнено(ДатаПоследнейУдачнойЗагрузкиЗаказов) Тогда
			Заказ_ДатаЗагрузки = ДатаПоследнейУдачнойЗагрузкиЗаказов;
		КонецЕсли;	
		
	КонецЕсли;	
	
КонецПроцедуры

Функция _СериализоватьНастройкиXML(Параметры) Экспорт
	
	СтруктураНастроек = Новый Структура;
	
	// Резвизиты обработки
	//
	Для Каждого мдРеквизита Из Метаданные().Реквизиты Цикл
		Если Параметры.РеквизитыИсключения.Найти(мдРеквизита.Имя) <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		СтруктураНастроек.Вставить(мдРеквизита.Имя, ЭтотОбъект[мдРеквизита.Имя]);
	КонецЦикла;
	// Резвизиты шапки заказа
	//
	СтруктураНастроек.Вставить("РеквизитыШапкиЗаказа", Новый Структура);
	Для Каждого Поле Из Параметры.РеквизитыШапкиЗаказа Цикл
		СтруктураНастроек["РеквизитыШапкиЗаказа"].Вставить(Поле.Ключ, Поле.Значение);
	КонецЦикла;
	// Резвизиты ТЧ заказа
	//
	СтруктураНастроек.Вставить("РеквизитыТЧЗаказа", Параметры.РеквизитыТЧЗаказа);
	// Свойства
	//
	СтруктураНастроек.Вставить("ТаблицаСвойств", Новый Массив);
	СтруктураКолонок = Новый Структура;
	Для Каждого КолонкаСвойств Из Свойства.ВыгрузитьКолонки().Колонки Цикл
		СтруктураКолонок.Вставить(КолонкаСвойств.Имя, "");
	КонецЦикла;
	Для Каждого СтрокаСвойств Из Свойства Цикл
		СтруктураСтроки = _СкопироватьСтруктуру(СтруктураКолонок);
		ЗаполнитьЗначенияСвойств(СтруктураСтроки, СтрокаСвойств);
		СтруктураНастроек["ТаблицаСвойств"].Добавить(СтруктураСтроки);
	КонецЦикла;	
	// Подбор товаров
	//
	СтруктураНастроек.Вставить("НастройкаСКД", Параметры.КомпоновщикТоваров.ПолучитьНастройки());
	
	ЗаписьXML = Новый ЗаписьXML;
	ЗаписьXML.УстановитьСтроку();
		
	СериализаторXDTO.ЗаписатьXML(ЗаписьXML, СтруктураНастроек);
	
	ТекстНастроек = ЗаписьXML.Закрыть();
	
	Возврат ТекстНастроек;
	
КонецФункции

Функция _ДесериализоватьНастройкиXML(ТекстНастроек, Параметры) Экспорт 
	
	Результат = Новый Структура;
	
	Если Не ЗначениеЗаполнено(ТекстНастроек) Тогда
		Возврат Результат;
	КонецЕсли;
	
	мдОбработки = Метаданные();
	
	Попытка
		
		ЧтениеXML = Новый ЧтениеXML;
		ЧтениеXML.УстановитьСтроку(ТекстНастроек);
		
		СтруктураНастроек = СериализаторXDTO.ПрочитатьXML(ЧтениеXML);
		Если ТипЗнч(СтруктураНастроек) <> Тип("Структура") Тогда 
			_ВывестиСообщение("Настройки не соответствуют ожидаемому формату, загрузка настроек остановлена.", СтатусСообщения.Информация);
			Возврат Результат;
		КонецЕсли;
		
		Для Каждого ЭлементНастройки из СтруктураНастроек Цикл
			
			Если ЭлементНастройки.Ключ = "НастройкаСКД" Тогда
				
				Параметры.КомпоновщикТоваров.ЗагрузитьНастройки(ЭлементНастройки.Значение);
				
			ИначеЕсли ЭлементНастройки.Ключ = "РеквизитыШапкиЗаказа" Тогда 

				Результат.Вставить("РеквизитыШапкиЗаказа", Новый Структура);
				Для каждого ЭлементНастройки Из ЭлементНастройки.Значение Цикл
					Результат["РеквизитыШапкиЗаказа"].Вставить(ЭлементНастройки.Ключ, ЭлементНастройки.Значение);
				КонецЦикла;
				
			ИначеЕсли ЭлементНастройки.Ключ = "РеквизитыТЧЗаказа" Тогда 
				
				Результат.Вставить("РеквизитыТЧЗаказа", ЭлементНастройки.Значение);
				
			ИначеЕсли ЭлементНастройки.Ключ = "ТаблицаСвойств" Тогда 
				
				прЗаполнитьДополнительныеРеквизитыИСведения();
				
				Для каждого СтруктураСтроки Из ЭлементНастройки.Значение Цикл
					
					НайденныеСтроки = Свойства.НайтиСтроки(Новый Структура("Свойство", СтруктураСтроки["Свойство"]));
					Если НайденныеСтроки.Количество() = 1 Тогда
						ЗаполнитьЗначенияСвойств(НайденныеСтроки[0], СтруктураСтроки);
					КонецЕсли;
					
				КонецЦикла;
				
			Иначе
				Если мдОбработки.Реквизиты.Найти(ЭлементНастройки.Ключ) <> Неопределено Тогда
					Попытка
						ЭтотОбъект[ЭлементНастройки.Ключ] = ЭлементНастройки.Значение;	
					Исключение
						ИнформацияОбОшибке = ИнформацияОбОшибке();
						_ВывестиСообщение(ПодробноеПредставлениеОшибки(ИнформацияОбОшибке), СтатусСообщения.Информация);
					КонецПопытки;					
				КонецЕсли;				
			КонецЕсли;
			
		КонецЦикла;
				
	Исключение
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		_ВывестиСообщение(ПодробноеПредставлениеОшибки(ИнформацияОбОшибке), СтатусСообщения.Внимание);
	КонецПопытки;	
	
	Возврат Результат;
	
КонецФункции

Функция _ВыгрузитьТовары(Параметры) Экспорт
	
	Отказ = Ложь;

	// Выполнить проверку заполнения данных
	прПроверитьКорректностьЗаполненияДанныхПередВыгрузкойТоваров(Отказ, Параметры);
	Если Отказ Тогда
		_ВывестиСообщение(НСтр("ru='Выгрузка товаров остановлена.';uk='Вивантаження товарів зупинено.'"), СтатусСообщения.Внимание);
		Возврат Ложь;
	КонецЕсли;
	
	_ЛогироватьНастройкиПередОбменом(Параметры);
	
	тзТовары = Новый ТаблицаЗначений;
	Если Товары.Количество() = 0 Тогда
		тзТовары = прПолучитьТовары(Параметры);
	Иначе
		тзТовары = Товары.Выгрузить(Товары.НайтиСтроки(Новый Структура("Выгружать", Истина)));
	КонецЕсли;
	
	Если тзТовары.Количество() = 0 Тогда
		Возврат Ложь;
	КонецЕсли;	
	
	ПакетДанных = _ПодготовитьТоварыДляВыгрузки(Отказ, тзТовары, Параметры);
	Если Отказ Тогда
		_ВывестиСообщение(НСтр("ru='Подготовка к выгрузке данных о товарах завершена с ошибками, выгрузка товаров остановлена.';uk='Підготовка до вивантаження даних про товари завершена з помилками, вивантаження товарів зупинено.'"), СтатусСообщения.Внимание);
		Возврат Ложь;
	КонецЕсли;
		
	Параметры.Вставить("Файлы", Новый Структура);
	Параметры.Файлы.Вставить("Макеты", Новый Массив);
	Параметры.Файлы.Вставить("ФайлCML", ПолучитьИмяВременногоФайла("cml"));
	Параметры.Файлы.Вставить("ФайлыКартинок", Новый Массив);
			
	// Выполнить обмен сформированной информацией
	Если РежимОбмена = Параметры.КонстантыОбмена.РежимыОбмена.Сайт Тогда
		
		ДанныеССайта = _ПолучитьДанныеССайта();
		Если ДанныеССайта = Неопределено Тогда
			_ВывестиСообщение(НСтр("ru='Не удалось получить с сайта служебные данные!';uk='Не вдалося отримати з сайту службові дані!'"), СтатусСообщения.Важное);
			Отказ = Истина;
			Возврат Неопределено;
		КонецЕсли;
		
		Если ВыгружатьКартинки Тогда
			
			// Выгрузить картинки товаров в каталог на диске
			ВыгруженныеКартинки = _ВыгрузитьКартинкиТоваровНаДиск(Отказ, Параметры.КонстантыОбмена.КаталогВременныхФайлов, Параметры, ПакетДанных);	
			Если Отказ Тогда
				_ВывестиСообщение(НСтр("ru='Выгрузка картинок товаров на диск завершена с ошибками, выгрузка товаров остановлена.';uk='Вивантаження картинок товарів на диск завершене з помилками, вивантаження товарів зупинено.'"), СтатусСообщения.Внимание);
				Возврат Ложь;
			КонецЕсли;

		КонецЕсли;
		
		// Сформировать файл cml
		РезультатВыгрузкиCMLНаДиск = _ВыгрузитьТоварыНаДиск(Отказ, Параметры, ПакетДанных);
		Если Отказ Тогда
			_ВывестиСообщение(НСтр("ru='Выгрузка товаров на диск завершена с ошибками, выгрузка товаров остановлена.';uk='Вивантаження товарів на диск завершене з помилками, вивантаження товарів зупинено.'"), СтатусСообщения.Внимание);
			Возврат Ложь;
		КонецЕсли;
		
		Параметры.Вставить("АдресаСкриптов", ДанныеССайта["urls"]);
		
		РезультатВыгрузкиCMLНаСайт = _ВыгрузитьФайлCMLНаСайт(Отказ, Параметры);
		Если Отказ Тогда
			_ВывестиСообщение(НСтр("ru='Выгрузка товаров на сайт завершена с ошибками, выгрузка товаров остановлена.';uk='Вивантаження товарів на сайт завершене з помилками, вивантаження товарів зупинено.'"), СтатусСообщения.Внимание);
			Возврат Ложь;
		Иначе
			
			Если ВыгружатьКартинки Тогда
				Для каждого ПутьКФайлу Из ВыгруженныеКартинки Цикл
					Параметры.Файлы.ФайлыКартинок.Добавить(ПутьКФайлу);	
				КонецЦикла;
				
				// Выгрузить картинки товаров на сайт
				РезультатВыгрузкиКартинокНаСайт = _ВыгрузитьКартинкиТоваровНаСайт(Отказ, Параметры, ПакетДанных);
				Если Отказ Тогда
					_ВывестиСообщение(НСтр("ru='Выгрузка картинок товаров на сайт завершена с ошибками, выгрузка товаров остановлена.';uk='Вивантаження картинок товарів на сайт завершене з помилками, вивантаження товарів зупинено.'"), СтатусСообщения.Внимание);
					Возврат Ложь;
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
	Иначе
		
		ДокументCML = Новый ТекстовыйДокумент;	
		Попытка
			ДокументCML.Записать(ИмяФайлаОбмена, КодировкаТекста.UTF8);
		Исключение
			_ВывестиСообщение(НСтр("ru='Не удалось записать файл CML на диск.';uk='Не вдалося записати файл CML на диск.'"));
			Возврат Ложь;
		КонецПопытки;
		
		Файл = _ПолучитьФайл(ИмяФайлаОбмена);
		
		// Выгрузить картинки товаров в каталог на диске
		Если ВыгружатьКартинки Тогда
			
			ВыгруженныеКартинки = _ВыгрузитьКартинкиТоваровНаДиск(Отказ, Файл.Путь, Параметры, ПакетДанных);
			Если Отказ Тогда
				_ВывестиСообщение(НСтр("ru='Выгрузка картинок товаров на диск завершена с ошибками.';uk='Вивантаження картинок товарів на диск завершене з помилками.'"), СтатусСообщения.Внимание);
				Возврат Ложь;
			КонецЕсли;
			
		КонецЕсли;
		
		// Сформировать файл cml
		РезультатВыгрузкиCMLНаДиск = _ВыгрузитьТоварыНаДиск(Отказ, Параметры, ПакетДанных);
		Если Отказ Тогда
			_ВывестиСообщение(НСтр("ru='Выгрузка товаров на диск завершена с ошибками, выгрузка товаров остановлена.';uk='Вивантаження товарів на диск завершене з помилками, вивантаження товарів зупинено.'"), СтатусСообщения.Внимание);
			Возврат Ложь;
		КонецЕсли;
		
		// Только если пути файлов доступны
		Попытка
			КопироватьФайл(Параметры.Файлы.ФайлCML, ИмяФайлаОбмена);
		Исключение
			Отказ = Истина;
			ИнформацияОбОшибке = ИнформацияОбОшибке();
			_ВывестиСообщение(ПодробноеПредставлениеОшибки(ИнформацияОбОшибке), СтатусСообщения.ОченьВажное);
		КонецПопытки;
		
	КонецЕсли;
	
	// Удаление регистрации с узла
	Если Не Отказ Тогда
		прУдалитьРегистрациюИзменений(Отказ, тзТовары);
	КонецЕсли;
	
	// Удаление временных файлов
	_УдалитьФайлСОбработкойОшибок(Параметры.Файлы.ФайлCML);
	Для каждого ПутьКФайлу Из Параметры.Файлы.ФайлыКартинок Цикл
		_УдалитьФайлСОбработкойОшибок(ПутьКФайлу);
	КонецЦикла;	
	Для каждого ПутьКФайлу Из Параметры.Файлы.Макеты Цикл
		_УдалитьФайлСОбработкойОшибок(ПутьКФайлу);
	КонецЦикла;	
		
КонецФункции

Функция _ЗагрузитьЗаказы(Параметры) Экспорт
	
	Отказ = Ложь;

	// Выполнить проверку заполнения данных
	прПроверитьКорректностьЗаполненияДанныхПередЗагрузкойЗаказов(Отказ, Параметры);
	Если Отказ Тогда
		_ВывестиСообщение(НСтр("ru='Загрузка заказов остановлена.';uk='Завантаження замовлень зупинено.'"), СтатусСообщения.Внимание);
		Возврат Ложь;
	КонецЕсли;
	
	_ЛогироватьНастройкиПередОбменом(Параметры);
	
	_ПутьКЗагружаемомуФайлу = ИмяФайлаОбмена;
	
	Если РежимОбмена = Параметры.КонстантыОбмена.РежимыОбмена.Сайт Тогда
		
		_ПутьКЗагружаемомуФайлу = _ПолучитьЗаказыССайта(Отказ, Параметры);
		Если Отказ Тогда
			_ВывестиСообщение(НСтр("ru='Не удалось получить файл заказов с сайта.';uk='Не вдалося отримати файл замовлень з сайту.'"), СтатусСообщения.Важное);
			Возврат Ложь;
		КонецЕсли;
				
	КонецЕсли;
	
	ПерваяСтрокаОтветаСервера = _ПолучитьСтрокуФайла(_ПутьКЗагружаемомуФайлу, КодировкаТекста.UTF8, 1);
	
	ДанныеCML = "";
	
	Если Лев(ПерваяСтрокаОтветаСервера, 2) = "PK" Тогда
		ДанныеCML = _ПолучитьСодержимоеZIPАрхива(_ПутьКЗагружаемомуФайлу, "*.сml");
	Иначе
		Если Лев(ПерваяСтрокаОтветаСервера, 5) = "<?xml" Тогда
			ДанныеCML = _ПолучитьСодержимоеФайла(_ПутьКЗагружаемомуФайлу, КодировкаТекста.UTF8);
		КонецЕсли;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ДанныеCML) Тогда
		_ВывестиСообщение(НСтр("ru='Не удалось прочитать данные, загруженные с сервера.';uk='Не вдалося прочитати дані, завантажені з сервера.'"), СтатусСообщения.Важное);
		Возврат Ложь;
	КонецЕсли;
	
	ДанныеЗаказов = прСформироватьДанныеЗаказов(ДанныеCML);
	
	Если ДанныеЗаказов = Неопределено Тогда
		_ВывестиСообщение(НСтр("ru='Не удалось разобрать данные о заказах загруженных с сервера.';uk='Не вдалося розібрати дані про замовлення завантажені з сервера.'"), Ложь);
		Возврат Ложь;		
	КонецЕсли;
	
	СтруктураЗаказов = прОбработатьДеревоЗаказов(ДанныеЗаказов.ДеревоЗаказов);
	
	Если СтруктураЗаказов = Неопределено Тогда 
		_ВывестиСообщение(НСтр("ru='Не удалось обработать заказы загруженные с сервера.';uk='Не вдалося обробити замовлення завантажені з сервера.'"), Ложь);
		Возврат Ложь;
	КонецЕсли;
	
	ВсегоЗаказов = СтруктураЗаказов["Заказы"].Количество();	
	ОбработаноЗаказов = прСоздатьЗаказы(Отказ, Параметры, СтруктураЗаказов);
	
	_ВывестиСообщение(НСтр("ru='Обработано ';uk='Оброблено '")+ОбработаноЗаказов+НСтр("ru=' заказов из ';uk=' замовлень з '")+ВсегоЗаказов, СтатусСообщения.Информация);
	
	Если Отказ Тогда
		_ВывестиСообщение(НСтр("ru='Не удалось сохранить все заказы загруженные с сервера.';uk='Не вдалося зберегти усі замовлення завантажені з сервера.'"), Ложь);
		Возврат Ложь;
	КонецЕсли;

	//сохраняем дату формирования файла, если загрузка прошла успешно
	ДатаФормирования = ДанныеЗаказов.ДатаФормирования;
	Если ЗначениеЗаполнено(ДатаФормирования) Тогда
		
		СписокВнутреннихНастроек = Новый Структура;
		СписокВнутреннихНастроек.Вставить("ДатаПоследнейУдачнойЗагрузкиЗаказов", ДатаФормирования);
		_ЗаписатьЗначенияВнутреннихНастроек(СписокВнутреннихНастроек);
		
		//обновим дату загрузки
		Заказ_ДатаЗагрузки = ДатаФормирования;
		
	КонецЕсли;
		
	Возврат Истина;
	
КонецФункции

Функция _ПолучитьИнформациюОКонтекстеВыполнения(КонстантыОбмена=Неопределено) Экспорт
	
	Результат = Новый Структура;
		                                       
	СистемнаяИнформация = Новый СистемнаяИнформация;
	ИнформацияОСоединении = СтрокаСоединенияИнформационнойБазы();
	
	ВариантБазы = ВРег(Лев(ИнформацияОСоединении,4));		

	Манифест = _МакетТекстовыйВСоответствие(ПолучитьМакет("manifest"));
	
	Результат.Вставить("ВерсияОбмена", Манифест["ВерсияОбмена"]);
	Результат.Вставить("ВерсияДрайвера", Манифест["ВерсияДрайвера"]);	
	Результат.Вставить("ВариантБазы", ВариантБазы);
	Результат.Вставить("КонфигурацияИмя", Метаданные.Имя);
	Результат.Вставить("КонфигурацияВерсия", Метаданные.Версия);	
	Результат.Вставить("РежимСовместимости", Строка(Метаданные.РежимСовместимости));
	Результат.Вставить("ИспользоватьОбычныеФормыВУправляемомПриложении", Метаданные.ИспользоватьОбычныеФормыВУправляемомПриложении);
	Результат.Вставить("ИспользоватьУправляемыеФормыВОбычномПриложении", Метаданные.ИспользоватьУправляемыеФормыВОбычномПриложении);
	Результат.Вставить("ОсновнойРежимЗапуска", Строка(Метаданные.ОсновнойРежимЗапуска));
	Результат.Вставить("ТекущийРежимЗапуска", Строка(ТекущийРежимЗапуска()));
	Результат.Вставить("ВерсияПриложения", СистемнаяИнформация.ВерсияПриложения);
	Результат.Вставить("ВерсияОС", СистемнаяИнформация.ВерсияОС);
	Результат.Вставить("ТипПлатформы", Строка(СистемнаяИнформация.ТипПлатформы));
	Результат.Вставить("ОперативнаяПамять", Строка(СистемнаяИнформация.ОперативнаяПамять));	
	
	ВерсияПлатформы = Лев(СистемнаяИнформация.ВерсияПриложения, 3);
	Результат.Вставить("ВерсияПлатформы", ВерсияПлатформы);
	
	Если ВерсияПлатформы = "8.2" Тогда
		Результат.Вставить("РежимИспользованияМодальности", "Использовать");
	ИначеЕсли ВерсияПлатформы = "8.3" Тогда
		Результат.Вставить("РежимИспользованияМодальности", Строка(Метаданные.РежимИспользованияМодальности));
	КонецЕсли;	
	
	Если КонстантыОбмена <> Неопределено Тогда
		Результат.Вставить("ВстроенныйОбмен", КонстантыОбмена.ВнутренняяОбработка);	
	КонецЕсли;
		
	Возврат Результат;
	
КонецФункции

Процедура _ЗаполнитьТаблицыСвойствКатегорий(ТаблицаСвойств, Параметры) Экспорт
		
	ТаблицаСвойств = Новый ТаблицаЗначений; 
	
	ТаблицаСвойств.Колонки.Добавить("ИД_Категории");
	ТаблицаСвойств.Колонки.Добавить("Категория");
	ТаблицаСвойств.Колонки.Добавить("ИД_Категория_Родитель");
	ТаблицаСвойств.Колонки.Добавить("ИД_Атрибута");
	ТаблицаСвойств.Колонки.Добавить("Атрибут");				
	
	Если ТипЗнч(Параметры) = Тип("Структура") Тогда
		
		Если ЗначениеЗаполнено(Параметры.АдресФайлаКатегорийАтрибутов) Тогда
			ФайлКА = ПолучитьИзВременногоХранилища(Параметры.АдресФайлаКатегорийАтрибутов);
		Иначе
			
			ФайлКА = Новый Файл(Параметры.КаталогКэша+"ca.dat");
			Если ФайлКА.Существует() и Не ПустаяСтрока(_ПолучитьСтрокуФайла(ФайлКА.ПолноеИмя, КодировкаТекста.UTF8, 1)) Тогда
				ПутьКФайлуКА = ФайлКА.ПолноеИмя;
			Иначе
								
				СвойстваПолучены = _ПолучитьДанныеСвойств(ФайлКА.ПолноеИмя);
				Если Не ЗначениеЗаполнено(СвойстваПолучены) Тогда
					Возврат;
				КонецЕсли;
				
				ПутьКФайлуКА = ФайлКА.ПолноеИмя;
				
			КонецЕсли;
			
			ФайлКА = Новый ТекстовыйДокумент;
			ФайлКА.Прочитать(ПутьКФайлуКА, КодировкаТекста.UTF8);
			
			Параметры.АдресФайлаКатегорийАтрибутов = ПоместитьВоВременноеХранилище(ФайлКА, Параметры.УИОсновнойФоры);
			
		КонецЕсли;
		
	КонецЕсли;
	
	Для НомерСтроки=1 По ФайлКА.КоличествоСтрок() Цикл		
				
		Слова = _РазложитьСтрокуВМассив(ФайлКА.ПолучитьСтроку(НомерСтроки), ";");
		Если Слова.Количество() <> 5 Тогда
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока = ТаблицаСвойств.Добавить();		
		
		НоваяСтрока.ИД_Категории = Слова[0];
		НоваяСтрока.Категория = Слова[1];
		НоваяСтрока.ИД_Категория_Родитель = Слова[2];
		НоваяСтрока.ИД_Атрибута = Слова[3];
		НоваяСтрока.Атрибут = Слова[4];
				
	КонецЦикла;	
	
	ТаблицаСвойств.Индексы.Добавить("ИД_Категория_Родитель");					
		
КонецПроцедуры

Функция _ПроверитьОбновленияДрайвераОбмена(Параметры) Экспорт
			
	СтруктураПараметровСайта = _ПолучитьСтруктуруПараметровДляСоединения(Интернет_АдресСайта);
	
	ИнформацияОКонтексте = _ПолучитьИнформациюОКонтекстеВыполнения(Параметры.КонстантыОбмена);
	
	ПараметрыЗапроса = "?key="+СтруктураПараметровСайта.Ключ;
	Для каждого ЭлементКонтента Из ИнформацияОКонтексте Цикл
		ПараметрыЗапроса = ПараметрыЗапроса + "&" + ЭлементКонтента.Ключ + "=" +ЭлементКонтента.Значение;
	КонецЦикла;

	_ВременныйФайл = ПолучитьИмяВременногоФайла("tmp");
	
	ОтветСервера = _ВыполнитьЗапросКСайту(
		"[Список свойств сайта]",
		СтруктураПараметровСайта,
		"cabinet/product_import/check_new_version_driver_1c"+ПараметрыЗапроса,
		Новый Структура("ПутьКФайлу", _ВременныйФайл)
	);
	Если ОтветСервера = Неопределено или ОтветСервера = "" Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Контент = _ПолучитьСодержимоеФайла(_ВременныйФайл, КодировкаТекста.UTF8);
	
	_ВывестиСообщение(НСтр("ru='Проверка новой версии драйвера.';uk='Перевірка нової версії драйвера.'") + Символы.ПС + Контент, СтатусСообщения.Информация, Ложь);
	
	Если Не ЗначениеЗаполнено(Контент) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	JSON = _ПолучитьВстроеннуюОбработку("JSON", Неопределено);
	
	КонтентОтветаСервера = JSON._ПрочитатьJSON(Контент);
	ОшибкаПоиска = КонтентОтветаСервера.Получить("error");
	Если ОшибкаПоиска <> Неопределено Тогда
		_ВывестиСообщение(
			ОшибкаПоиска,
			СтатусСообщения.Важное
		);
		Возврат Ложь;
	КонецЕсли;
	
	ПоследняяВерсияДрайвераСтрокой = "";
	Попытка
		ПоследняяВерсияДрайвераСтрокой = КонтентОтветаСервера["version"];
	Исключение
		Возврат Ложь;
	КонецПопытки;
	
	ПоследняяВерсияДрайвера = 0;
	Попытка
		ПоследняяВерсияДрайвера = Число(_ПолучитьТолькоЦифры(ПоследняяВерсияДрайвераСтрокой));
	Исключение
		Возврат Ложь;
	КонецПопытки;
	
	Если ПоследняяВерсияДрайвера > Число(_ПолучитьТолькоЦифры(ИнформацияОКонтексте["ВерсияДрайвера"])) Тогда
		_ВывестиСообщение(
			НСтр("ru='В личном кабинете доступна новая версия драйвера ';uk='У особистому кабінеті доступна нова версія драйвера '")+ПоследняяВерсияДрайвераСтрокой+".",
			СтатусСообщения.Внимание
		);
	Иначе
		_ВывестиСообщение(
			НСтр("ru='У Вас последняя версия драйвера.';uk='У Вас остання версія драйвера.'"),
			СтатусСообщения.Внимание
		);		
	КонецЕсли;
		
	Попытка
		УдалитьФайлы(_ВременныйФайл);
	Исключение
	КонецПопытки;
		
	Возврат Истина;
		
КонецФункции


// ЯДРО: ОБЩЕЕ

Функция _ПолучитьСтрокуБезРазделителей(Источник, Разделители) 
	
	Результа = Источник;
	
	Если ТипЗнч(Разделители) = Тип("Массив") Тогда
		Для каждого Разделитель Из Разделители Цикл
			Результа = СтрЗаменить(Результа, Разделитель, "");			
		КонецЦикла;
	Иначе
		Результа = СтрЗаменить(Результа, Разделители, "");		
	КонецЕсли;
	
	Возврат Результа;
	
КонецФункции

Функция _ПолучитьКоличествоКартинокТоваров(ТоварыКартинки)
	
	Результат = 0;
	
	Для Каждого ТоварКартинки из ТоварыКартинки Цикл
		
		Для каждого ТоварКартинка Из ТоварКартинки.Значение Цикл
			
			Если Не прКартинкаКорректная(ТоварКартинка) Тогда
				Продолжить;	
			КонецЕсли;
			
			Результат = Результат + 1;
			
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Функция _РазложитьСтрокуВМассив(Знач Стр, Разделитель = ",")
	
	МассивСтрок = Новый Массив();
	Если Разделитель = " " Тогда
		Стр = СокрЛП(Стр);
		Пока 1=1 Цикл
			Поз = Найти(Стр,Разделитель);
			Если Поз=0 Тогда
				МассивСтрок.Добавить(Стр);
				Возврат МассивСтрок;
			КонецЕсли;
			МассивСтрок.Добавить(Лев(Стр,Поз-1));
			Стр = СокрЛ(Сред(Стр,Поз));
		КонецЦикла;
	Иначе
		ДлинаРазделителя = СтрДлина(Разделитель);
		Пока 1=1 Цикл
			Поз = Найти(Стр,Разделитель);
			Если Поз=0 Тогда
				МассивСтрок.Добавить(Стр);
				Возврат МассивСтрок;
			КонецЕсли;
			МассивСтрок.Добавить(Лев(Стр,Поз-1));
			Стр = Сред(Стр,Поз+ДлинаРазделителя);
		КонецЦикла;
	КонецЕсли;
	
КонецФункции

Функция _СтрокаТолькоИзЦифр(Источник) 
	
	Результат = Истина;
	
	СтрокаЦифр = "0123456789";
	
	Для Номер=1 По СтрДлина(Источник) Цикл
		Буква = Сред(Источник, Номер, 1);
		Если Найти(СтрокаЦифр, Буква) = 0 Тогда
			Результат = Ложь;
			Прервать;
		КонецЕсли;	
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Функция _ПолучитьТолькоЦифры(Источник) 
	
	Результат = "";
	
	СтрокаЦифр = "0123456789";
	
	Для Номер=1 По СтрДлина(Источник) Цикл
		Буква = Сред(Источник, Номер, 1);
		Если Найти(СтрокаЦифр, Буква) > 0 Тогда
			Результат = Результат + Буква;
		КонецЕсли;	
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Процедура _ПроверитьДрайверПередЗапуском()
	
	мдОбработки = Метаданные();
	
	ТекущийРежимЗапуска = ТекущийРежимЗапуска();
	ОсновнойРежимЗапуска = Метаданные.ОсновнойРежимЗапуска;	
	
	ИспользоватьОбычныеФормыВУправляемомПриложении = Метаданные.ИспользоватьОбычныеФормыВУправляемомПриложении;
	ИспользоватьУправляемыеФормыВОбычномПриложении = Метаданные.ИспользоватьУправляемыеФормыВОбычномПриложении;
	
	ЭтоВстроеннаяОбработка = Метаданные.Обработки.Содержит(мдОбработки);
	
	ТипыФормыДляЗапуска = Новый Массив;
	
	Если ТекущийРежимЗапуска = РежимЗапускаКлиентскогоПриложения.УправляемоеПриложение Тогда
		
		ТипыФормыДляЗапуска.Добавить(Метаданные.СвойстваОбъектов.ТипФормы.Управляемая);
		
		Если ИспользоватьОбычныеФормыВУправляемомПриложении И ЭтоВстроеннаяОбработка Тогда
			ТипыФормыДляЗапуска.Добавить(Метаданные.СвойстваОбъектов.ТипФормы.Обычная);
		КонецЕсли;
		
	ИначеЕсли ТекущийРежимЗапуска = РежимЗапускаКлиентскогоПриложения.ОбычноеПриложение Тогда
		
		ТипыФормыДляЗапуска.Добавить(Метаданные.СвойстваОбъектов.ТипФормы.Обычная);
		
		Если ИспользоватьУправляемыеФормыВОбычномПриложении И ЭтоВстроеннаяОбработка Тогда
			ТипыФормыДляЗапуска.Добавить(Метаданные.СвойстваОбъектов.ТипФормы.Управляемая);
		КонецЕсли;
		
	ИначеЕсли ТекущийРежимЗапуска = РежимЗапускаКлиентскогоПриложения.Авто Тогда
		
		Если ОсновнойРежимЗапуска = РежимЗапускаКлиентскогоПриложения.УправляемоеПриложение Тогда
			
			ТипыФормыДляЗапуска.Добавить(Метаданные.СвойстваОбъектов.ТипФормы.Управляемая);
			
			Если ИспользоватьОбычныеФормыВУправляемомПриложении И ЭтоВстроеннаяОбработка Тогда
				ТипыФормыДляЗапуска.Добавить(Метаданные.СвойстваОбъектов.ТипФормы.Обычная);
			КонецЕсли;
			
		ИначеЕсли ОсновнойРежимЗапуска = РежимЗапускаКлиентскогоПриложения.ОбычноеПриложение Тогда
			
			ТипыФормыДляЗапуска.Добавить(Метаданные.СвойстваОбъектов.ТипФормы.Обычная);
			
			Если ИспользоватьУправляемыеФормыВОбычномПриложении И ЭтоВстроеннаяОбработка Тогда
				ТипыФормыДляЗапуска.Добавить(Метаданные.СвойстваОбъектов.ТипФормы.Управляемая);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если ТипыФормыДляЗапуска.Количество() = 0 Тогда
		_ВывестиСообщение(НСтр("ru='Не определен режим запуска приложения.';uk='Не визначений режим запуску додатка.'"), СтатусСообщения.Информация, Истина);
	КонецЕсли;	
	
	ФормыОбработки = мдОбработки.Формы;
	
	НетПодходящейФормы = Истина;
	
	Для Каждого ТекФорма Из ФормыОбработки Цикл
		
		Если ТекФорма.Имя = "ФормаЗаглушка" Тогда
			Продолжить;
		КонецЕсли;
		
		Если ТипыФормыДляЗапуска.Найти(ТекФорма.ТипФормы) <> Неопределено Тогда
			НетПодходящейФормы = Ложь;
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	Если НетПодходящейФормы Тогда
		Если ЭтоВстроеннаяОбработка Тогда
			_ВывестиСообщение(НСтр("ru='Запуск выбранного драйвера не поддерживается в текущем режиме. Выберите другой драйвер.';uk='Запуск обраного драйвера не підтримується в поточному режимі. Виберіть інший драйвер.'"), СтатусСообщения.Информация, Истина);
		Иначе
			Если ИспользоватьОбычныеФормыВУправляемомПриложении	ИЛИ ИспользоватьУправляемыеФормыВОбычномПриложении Тогда
				_ВывестиСообщение(НСтр("ru='Запуск выбранного драйвера не поддерживается в текущем режиме. Выберите другой драйвер или добавьте драйвер в конфигурацию.';uk='Запуск обраного драйвера не підтримується в поточному режимі. Виберіть інший драйвер або додайте драйвер в конфігурацію.'"), СтатусСообщения.Информация, Истина);
			Иначе
				_ВывестиСообщение(НСтр("ru='Запуск выбранного драйвера не поддерживается в текущем режиме. Выберите другой драйвер.';uk='Запуск обраного драйвера не підтримується в поточному режимі. Виберіть інший драйвер.'"), СтатусСообщения.Информация, Истина);
			КонецЕсли;
		КонецЕсли;		
	КонецЕсли;
	
КонецПроцедуры


// ЯДРО: ЛОГИРОВАНИЕ

Процедура _ВывестиСообщение(Текст, Состояние=Неопределено, ВыводитьСообщение=Истина, ЛогироватьСообщение=Истина)
	
	Если Не ВыводитьСообщение и Не ЛогироватьСообщение Тогда
		Возврат;
	КонецЕсли;
	
	СостояниеСообщения = ?(Состояние=Неопределено, СтатусСообщения.Информация, Состояние);
	
	ВременнаяМетка = ТекущаяДата();
	
	Если ВыводитьСообщение Тогда
		
		ВременнаяМеткаСтрокой = "[ "+Формат(ВременнаяМетка, "ДЛФ=DT")+"]";
		
		Сообщить("С: "+ВременнаяМеткаСтрокой +": "+ Текст, СостояниеСообщения);
		
	КонецЕсли;	

	Если ЛогироватьДействия и ЛогироватьСообщение Тогда
			
		НоваяСтрока = Логи.Добавить();
		
		НоваяСтрока.Контекст = "С";
		
		НоваяСтрока.ДатаВремя = ВременнаяМетка;
		
		НоваяСтрока.Сообщение = Текст;
		
		НоваяСтрока.Статус = Строка(СостояниеСообщения);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура _ЛогироватьНастройкиПередОбменом(Параметры)
	
	_ВывестиСообщение(НСтр("ru='Режим обмена: ';uk='Режим обміну: '") + РежимОбмена, СтатусСообщения.Информация, Ложь, Истина);
	_ВывестиСообщение(НСтр("ru='Адрес сайта: ';uk='Адреса сайту: '") + Интернет_АдресСайта, СтатусСообщения.Информация, Ложь, Истина);
	_ВывестиСообщение(НСтр("ru='Ключ пользователя: ';uk='Ключ користувача: '") + Интернет_КлючПользователя, СтатусСообщения.Информация, Ложь, Истина);
	_ВывестиСообщение(НСтр("ru='Использовать прокси-сервер: ';uk='Використовувати проксі-сервер: '") + Прокси_Использование, СтатусСообщения.Информация, Ложь, Истина);
	_ВывестиСообщение(НСтр("ru='Выгружать только изменения: ';uk='Вивантажувати тільки зміни: '") + ВыгружатьТолькоИзменения, СтатусСообщения.Информация, Ложь, Истина);
	_ВывестиСообщение(НСтр("ru='Выгружать картинки: ';uk='Вивантажувати картинки: '") + ВыгружатьКартинки, СтатусСообщения.Информация, Ложь, Истина);
	_ВывестиСообщение(НСтр("ru='Использовать характеристики: ';uk='Використовувати характеристики: '") + ИспользоватьХарактеристики, СтатусСообщения.Информация, Ложь, Истина);
	_ВывестиСообщение(НСтр("ru='Кэшировать данные: ';uk='Кешувати дані:'") + Заказ_КэшироватьДанные, СтатусСообщения.Информация, Ложь, Истина);
	
КонецПроцедуры


// ЯДРО: МАКЕТЫ

Функция _ПолучитьВстроеннуюОбработку(ИмяМакета, Параметры, БезопасныйРежим=Ложь)
	
	Файл = Новый Файл(ПолучитьИмяВременногоФайла("tmp"));
	
	ПолноеИмяФайла = Файл.Путь+ИмяМакета+".epf";
	
	Попытка
		
		Макет = ПолучитьМакет(ИмяМакета);
		
		Макет.Записать(ПолноеИмяФайла);
		
		Если ТипЗнч(Параметры) = Тип("Структура") Тогда
			Параметры.Файлы.Макеты.Добавить(ПолноеИмяФайла);	
		КонецЕсли;		
		
	Исключение
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		_ВывестиСообщение(ПодробноеПредставлениеОшибки(ИнформацияОбОшибке), СтатусСообщения.ОченьВажное);
		Возврат Неопределено;
	КонецПопытки;
	
	Возврат ВнешниеОбработки.Создать(ПолноеИмяФайла, БезопасныйРежим);
	
КонецФункции 

Функция _МакетВТаблицуЗначений(Макет, ПерваяСтрока=1)
	
	Результат = Новый ТаблицаЗначений();
	
	// Колонки
	Для НомерКолонки=1 По Макет.ШиринаТаблицы Цикл
		Результат.Колонки.Добавить(Макет.Область(1, НомерКолонки).Текст);
	КонецЦикла;
	// Строки
	Для НомерСтроки=ПерваяСтрока По Макет.ВысотаТаблицы Цикл
		
		НоваяСтрока = Результат.Добавить();
		
		Для НомерКолонки=1 По Макет.ШиринаТаблицы Цикл
			
			Ячейка = Макет.Область(НомерСтроки, НомерКолонки);
			Если Ячейка.СодержитЗначение Тогда
				Значение = Ячейка.Значение;
			Иначе
				Значение = Ячейка.Текст;
			КонецЕсли;			
			Если ТипЗнч(Значение) = Тип("Строка") и ПустаяСтрока(Значение) Тогда
				Значение=Неопределено;			
			КонецЕсли;
						
			НоваяСтрока[НомерКолонки-1] = Значение;
			
		КонецЦикла;
					
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции 

Функция _МакетВМассив(Макет, НомерКолонки, ПерваяСтрока)
	
	Результат = Новый Массив;
	
	Для НомерСтроки=ПерваяСтрока По Макет.ВысотаТаблицы Цикл
		
		Ячейка = Макет.Область(НомерСтроки, НомерКолонки);
		Если Ячейка.СодержитЗначение Тогда
			Значение = Ячейка.Значение;
		Иначе
			Значение = Ячейка.Текст;
		КонецЕсли;			
		Если ТипЗнч(Значение) = Тип("Строка") и ПустаяСтрока(Значение) Тогда
			Значение=Неопределено;			
		КонецЕсли;
					
		Результат.Добавить(Значение);
								
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Функция _МакетТекстовыйВСоответствие(Макет) 
	
	Результат = Новый Структура;
	
	Для НомерСтроки = 1 По Макет.КоличествоСтрок() Цикл
		ДанныеСтрока = Макет.ПолучитьСтроку(НомерСтроки);
		Если Не ЗначениеЗаполнено(ДанныеСтрока) Тогда
			Продолжить;
		КонецЕсли;
		Слова = _РазложитьСтрокуВМассив(ДанныеСтрока, "=");
		Результат.Вставить(Слова[0], Слова[1]);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции


// ЯДРО: ГЕНЕРАЦИЯ И ЗАПОЛНЕНИЕ ОБЪЕКТОВ

Функция _НовыйМассив(
	Значение1,
	Значение2=Неопределено,
	Значение3=Неопределено,
	Значение4=Неопределено,
	Значение5=Неопределено
	) 
	
	Результат = Новый Массив;
	
	Результат.Добавить(Значение1);
	Если Значение2 <> Неопределено Тогда Результат.Добавить(Значение2); КонецЕсли;
	Если Значение3 <> Неопределено Тогда Результат.Добавить(Значение3); КонецЕсли;
	Если Значение4 <> Неопределено Тогда Результат.Добавить(Значение4); КонецЕсли;
	Если Значение5 <> Неопределено Тогда Результат.Добавить(Значение5); КонецЕсли;
		
	Возврат Результат;
	
КонецФункции

Функция _ТаблицуВСоответствие(Таблица, КолонкаКлюч, КолонкаЗначение) 
	
	Результат = Новый Соответствие;
	
	Для каждого СтрокаТаблицы Из Таблица Цикл
		Результат.Вставить(СтрокаТаблицы[КолонкаКлюч], СтрокаТаблицы[КолонкаЗначение]);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Функция _СкопироватьСтруктуру(Источник) 
		
	Результат = Новый Структура;
	
	Для каждого Элемент Из Источник Цикл
		Результат.Вставить(Элемент.Ключ, Элемент.Значение);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции


// ЯДРО: КОНВЕРТАЦИЯ

Функция _ПривестиСтрокуКЧислу(ЧислоСтрокой, ВозвращатьНеопределено = Ложь) Экспорт
	
	ОписаниеТипаЧисла = Новый ОписаниеТипов("Число");
	
	ЗначениеЧисла = ОписаниеТипаЧисла.ПривестиЗначение(ЧислоСтрокой);
	
	Если ВозвращатьНеопределено И (ЗначениеЧисла = 0) Тогда
		
		Стр = Строка(ЧислоСтрокой);
		Если Стр = "" Тогда
			Возврат Неопределено;
		КонецЕсли;
		
		Стр = СтрЗаменить(СокрЛП(Стр), "0", "");
		Если (Стр <> "") И (Стр <> ".") И (Стр <> ",") Тогда
			Возврат Неопределено;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ЗначениеЧисла;	
	
КонецФункции

Функция _ПривестиСтрокуКДате(ДатаВремяСтрока, НачальнаяДата = Неопределено)
	
	ДатаВремя = Неопределено;
	
	Если ЗначениеЗаполнено(НачальнаяДата) Тогда
		Время 	  = СтрЗаменить(ДатаВремяСтрока, ":", "");
		ДатаВремя = Дата(Формат(НачальнаяДата, "ДФ=yyyyMMdd") + Время);
	Иначе	
		ДатаВремя = Дата(СтрЗаменить(ДатаВремяСтрока, "-", "") + "000000");	
	КонецЕсли;	
	
	Возврат ДатаВремя;
	
КонецФункции


// ЯДРО: CML

Функция _ДобавитьЭлементКПоследовательности(Знач ПоследовательностьЭлементов, Знач ИмяУзла)
	
	ИсключатьИзПоследовательности = Новый Массив;
	ИсключатьИзПоследовательности.Добавить("КоммерческаяИнформация");
	
	Если ИсключатьИзПоследовательности.Найти(ИмяУзла) = Неопределено Тогда
		
		Если НЕ ПоследовательностьЭлементов = "" Тогда
			ПоследовательностьЭлементов = ПоследовательностьЭлементов + ".";	
		КонецЕсли;	
		ПоследовательностьЭлементов = ПоследовательностьЭлементов + ИмяУзла;
		
	КонецЕсли;
	
	Возврат ПоследовательностьЭлементов;
	
КонецФункции

Функция _УдалитьПоследнийЭлементИзПоследовательности(Знач ПоследовательностьЭлементов);
	
	ПромСтрока = СтрЗаменить(ПоследовательностьЭлементов, ".", Символы.ПС);
	
	КоличествоЭлементов = СтрЧислоСтрок(ПромСтрока);
	
	ПоследовательностьЭлементов	= "";
	Если КоличествоЭлементов > 0 Тогда
		
		КоличествоЭлементов = КоличествоЭлементов - 1;
		
		Для Счетчик = 1 По КоличествоЭлементов Цикл
			ПоследовательностьЭлементов	= ПоследовательностьЭлементов + "." + СтрПолучитьСтроку(ПромСтрока, Счетчик);
		КонецЦикла;	
		
		ПоследовательностьЭлементов = Прав(ПоследовательностьЭлементов, СтрДлина(ПоследовательностьЭлементов) - 1);
		
	КонецЕсли;
	
	Возврат ПоследовательностьЭлементов;
	
КонецФункции

Функция _ПолучитьИмяЭлементаИзПоследовательности(Знач ПоследовательностьЭлементов)
	
	ПромСтрока 			= СтрЗаменить(ПоследовательностьЭлементов, ".", Символы.ПС);
	КоличествоЭлементов = СтрЧислоСтрок(ПромСтрока);
	
	ИмяПоследнегоЭлемента = "";
	Если КоличествоЭлементов > 0 Тогда
		ИмяПоследнегоЭлемента = СтрПолучитьСтроку(ПромСтрока, КоличествоЭлементов);
	КонецЕсли;
	
	Возврат ИмяПоследнегоЭлемента;
	
КонецФункции

Функция _ПолучитьЗначенияКоммерческойИнформации(ДанныеCML, СписокАтрибутов)

	Результат = Новый Структура;

	ТД = Новый ТекстовыйДокумент;
	ТД.УстановитьТекст(ДанныеCML);
	ДанныеСтроки = ТД.ПолучитьСтроку(1);
	ДанныеСтроки = ДанныеСтроки + Символы.ПС + ТД.ПолучитьСтроку(2);
	ДанныеСтроки = ДанныеСтроки + Символы.ПС + ?(Найти(ДанныеСтроки, "</КоммерческаяИнформация>") <> 0, "", "</КоммерческаяИнформация>");

	ОбъектЧтениеXML = Новый ЧтениеXML;
	Попытка
		ОбъектЧтениеXML.УстановитьСтроку(ДанныеСтроки);
	Исключение
		Возврат Неопределено;
	КонецПопытки;

	Если ОбъектЧтениеXML.Прочитать() Тогда
		Для каждого ИмяАтрибута Из СписокАтрибутов Цикл
			Результат.Вставить(ИмяАтрибута, ОбъектЧтениеXML.ПолучитьАтрибут(ИмяАтрибута));
		КонецЦикла;
	КонецЕсли;

	Возврат Результат;

КонецФункции


// ЯДРО: ФАЙЛЫ

Функция _ПолучитьСодержимоеZIPАрхива(ПолноеИмяФайла, МаскаФайла)
	
	Результат = Неопределено;
	
	ИмяКаталога = КаталогВременныхФайлов() + Строка(Новый УникальныйИдентификатор);
	СоздатьКаталог(ИмяКаталога);
		
	ЧтениеZIP = Новый ЧтениеZIPФайла(ПолноеИмяФайла);
	ЧтениеZIP.ИзвлечьВсе(ИмяКаталога);
	ЧтениеZIP.Закрыть();
	
	РаспакованныеФайлы = НайтиФайлы(ИмяКаталога, МаскаФайла);
	
	Если РаспакованныеФайлы.Количество() = 1 Тогда
		Результат = _ПолучитьСодержимоеФайла(РаспакованныеФайлы[0].ПолноеИмя, КодировкаТекста.UTF8);
	КонецЕсли;
	
	Попытка
		УдалитьФайлы(ИмяКаталога);
	Исключение
	КонецПопытки;
	
	Возврат Результат;
	
КонецФункции

Процедура _УбратьИзXMLМеткуBOM(ПолноеИмяФайла)
	
	// Читаем исходные данные
	ИсходныйФайл = Новый ЧтениеТекста(ПолноеИмяФайла, КодировкаТекста.UTF8);
	Данные = ИсходныйФайл.Прочитать();
	ИсходныйФайл.Закрыть();
	ИсходныйФайл = Неопределено;
	
	// Создаем файл без BOM
	ЗаписьТекста = Новый ЗаписьТекста(ПолноеИмяФайла, КодировкаТекста.ANSI);
	ЗаписьТекста.Закрыть();
	ЗаписьТекста = Неопределено;
	
	// Дозаписываем данные в новый файл
	ЗаписьТекста = Новый ЗаписьТекста(ПолноеИмяФайла,,, Истина, Символы.ПС);
	ЗаписьТекста.Записать(Данные);
	ЗаписьТекста.Закрыть();
	ЗаписьТекста = Неопределено;
	
КонецПроцедуры

Процедура _УдалитьФайлСОбработкойОшибок(ПутьКФайлу) 
	
	Попытка
		УдалитьФайлы(ПутьКФайлу);
	Исключение
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		_ВывестиСообщение(ПодробноеПредставлениеОшибки(ИнформацияОбОшибке), СтатусСообщения.Важное);
	КонецПопытки;
	
КонецПроцедуры

Функция _ПолучитьФайл(ПутьКФайлу) 
	
	Файл = Новый Файл(ПутьКФайлу);
	
	Если Не Файл.Существует() Тогда 
		Возврат Неопределено;
	КонецЕсли;	
	
	Возврат Файл;
	
КонецФункции

Функция _ТипДанныхПоФайлу(ИмяФайла)
	
	СоответствиеТиповДанных = Новый Соответствие;
	СоответствиеТиповДанных.Вставить(".GIF" , "image/gif");
	СоответствиеТиповДанных.Вставить(".JPEG", "image/jpeg");
	СоответствиеТиповДанных.Вставить(".PNG" , "image/png");
	СоответствиеТиповДанных.Вставить(".CML" , "application/xml");
	
	Файл = _ПолучитьФайл(ИмяФайла);
	Если Файл = Неопределено Тогда
		Возврат "";
	КонецЕсли;	
	
	Возврат СокрЛП(СоответствиеТиповДанных[СокрЛП(ВРег(Файл.Расширение))]);
	
КонецФункции

Функция _РазделитьФайлыНаФрагменты(СписокФайлов, ОграничениеРазмераФрагмента)
	
	НовыйСписокФайлов = Новый Массив;
	
	Для Каждого ТекущийФайл Из СписокФайлов цикл
		
		ФайлНаДиске = _ПолучитьФайл(ТекущийФайл);
		Если ФайлНаДиске = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		Если ФайлНаДиске.Размер() > ОграничениеРазмераФрагмента Тогда
			
			НовыйСписокФайлов = РазделитьФайл(ФайлНаДиске.ПолноеИмя, ОграничениеРазмераФрагмента);
			
			УдалитьФайлы(ФайлНаДиске.ПолноеИмя);
			
		Иначе
			НовыйСписокФайлов.Добавить(ТекущийФайл);	
		КонецЕсли;	
		
	КонецЦикла;
	
	Возврат НовыйСписокФайлов;
	
КонецФункции

Функция _ПолучитьСодержимоеФайла(ПолноеИмя, Кодировка)
		
	ТекстОтвета = Новый ТекстовыйДокумент();
	ТекстОтвета.Прочитать(ПолноеИмя, Кодировка);	
	Если ТекстОтвета.КоличествоСтрок() = 0 Тогда
		Возврат "";
	КонецЕсли;
	
	Попытка
		Возврат ТекстОтвета.ПолучитьТекст();	
	Исключение
		Возврат Неопределено;
	КонецПопытки;	
	
КонецФункции

Функция _ПолучитьСтрокуФайла(ПолноеИмя, Кодировка, НомерСтроки)
		
	ТекстОтвета = Новый ТекстовыйДокумент();
	ТекстОтвета.Прочитать(ПолноеИмя, Кодировка);	
	
	Если ТекстОтвета.КоличествоСтрок() = 0 Тогда
		Возврат "";
	КонецЕсли;
	
	Попытка
		Возврат ТекстОтвета.ПолучитьСтроку(НомерСтроки);	
	Исключение
		Возврат Неопределено;
	КонецПопытки;	
	
КонецФункции


// ЯДРО: УНИКАЛЬНЫЙ ИДЕНТИФИТАКТОР

Функция _УИД(Ссылка, РезультатСтрокой=Ложь, СлучайныйУИД=Ложь) Экспорт
	
	Результат = Неопределено;
	
	Если ЗначениеЗаполнено(Ссылка) Тогда
		Результат = Ссылка.УникальныйИдентификатор();
	Иначе
		Если СлучайныйУИД Тогда
			Результат = Новый УникальныйИдентификатор;
		Иначе
			Результат = _ПолучитьУИДПоСтроке("00000000-0000-0000-0000-000000000000");
		КонецЕсли;		
	КонецЕсли;	
	
	Возврат ?(РезультатСтрокой, XMLСтрока(Результат), Результат);
	
КонецФункции

Функция _ПолучитьОбъединенныйУИД(Ссылка1, Ссылка2=Неопределено) 
	
	Результат = _УИД(Ссылка1, Истина);
	
	Если ЗначениеЗаполнено(Ссылка2) Тогда
		Результат = Результат+"#"+_УИД(Ссылка2, Истина);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция _СформироватьКлючИзУИД(УИД) 
	Возврат "_"+_ПолучитьСтрокуБезРазделителей(УИД, _НовыйМассив("-", "#"));
КонецФункции

Функция _ПолучитьКлючТипаЦены(ТипЦен)
	Возврат ?(ЗначениеЗаполнено(ТипЦен), _СформироватьКлючИзУИД(_УИД(ТипЦен, Истина)), "без_цены");
КонецФункции

Функция _ПолучитьУИДПоСтроке(УИД)
	
	Результат = Неопределено;
	
	Попытка 
		Результат = Новый УникальныйИдентификатор(УИД); 
	Исключение
	КонецПопытки;	
	
	Возврат Результат;
	
КонецФункции


// ЯДРО: СОЕДИНЕНИЕ

Функция _РазобратьАдресСайта(АдресДляОбработки)
	
	Адрес = СокрЛП(АдресДляОбработки); 
	
	HTTPСервер		 = ""; 
	HTTPПорт		 = 0;
	HTTPАдресСкрипта = "";
	
	Если ЗначениеЗаполнено(Адрес) Тогда
		
		Адрес = СтрЗаменить(Адрес, "\", "/");
		Адрес = СтрЗаменить(Адрес, " ", "");
		Адрес = СтрЗаменить(Адрес, "http://", "");
		ПозицияСлэша = Найти(Адрес, "/");
		Если ПозицияСлэша > 0 Тогда
			HTTPСервер 		 = Лев(Адрес, ПозицияСлэша - 1);	
			HTTPАдресСкрипта = Прав(Адрес, СтрДлина(Адрес) - ПозицияСлэша);
		Иначе	
			HTTPСервер 		 = Адрес;	
			HTTPАдресСкрипта = "";
		КонецЕсли;	
		ПозицияДвоеточия = Найти(HTTPСервер, ":");
		Если ПозицияДвоеточия > 0 Тогда
			HTTPСерверСПортом = HTTPСервер;
			HTTPСервер		  = Лев(HTTPСерверСПортом, ПозицияДвоеточия - 1);
			HTTPПортСтрока 	  = Прав(HTTPСерверСПортом, СтрДлина(HTTPСерверСПортом) - ПозицияДвоеточия);
		Иначе
			HTTPПортСтрока = "0";
		КонецЕсли;
		
		HTTPПорт = _ПривестиСтрокуКЧислу(HTTPПортСтрока);
		
	КонецЕсли;
	
	СтруктураРезультата = Новый Структура;
	СтруктураРезультата.Вставить("HTTPСервер"	  , HTTPСервер); 
	СтруктураРезультата.Вставить("HTTPПорт"		  , HTTPПорт);
	СтруктураРезультата.Вставить("HTTPАдресСкрипта", HTTPАдресСкрипта);
	
	Возврат СтруктураРезультата;
	
КонецФункции

Функция _ПолучитьСтруктуруПараметровДляСоединения(Адрес, Защищенное=Ложь)
	
	СтруктураАдреса = _РазобратьАдресСайта(Адрес);

	Результат = Новый Структура;
	Результат.Вставить("Ключ", Интернет_КлючПользователя);
	// HTTP
	Результат.Вставить("АдресСкрипта", СтруктураАдреса.HTTPАдресСкрипта);
	Результат.Вставить("Сервер", СтруктураАдреса.HTTPСервер);
	Результат.Вставить("Порт", СтруктураАдреса.HTTPПорт);
	// HTTPS
	Результат.Вставить("Защищенное", Защищенное);	
	// Proxy
	Результат.Вставить("ПроксиИспользование", Прокси_Использование);
	Результат.Вставить("ПроксиСервер", Прокси_Сервер);
	Результат.Вставить("ПроксиПорт", Прокси_Порт);
	Результат.Вставить("ПроксиИмяПользователя", Прокси_ИмяПользователя);
	Результат.Вставить("ПроксиПароль", Прокси_Пароль);	
	
	Возврат Результат;
	
КонецФункции

Функция _HTTPУстановитьСоединение(СтруктураПараметровСайта)
	
	Результат = Неопределено;
	
	ИнтернетПрокси = Неопределено;
	
	Если СтруктураПараметровСайта.ПроксиИспользование Тогда
		
		ИнтернетПрокси = Новый ИнтернетПрокси;
		ИнтернетПрокси.Пользователь = СтруктураПараметровСайта.ПроксиИмяПользователя;
		ИнтернетПрокси.Пароль		= СтруктураПараметровСайта.ПроксиПароль;
		
		Если СтруктураПараметровСайта.ПроксиПорт = 0 Тогда
			ИнтернетПрокси.Установить("HTTP", СтруктураПараметровСайта.ПроксиСервер);
		Иначе	
			ИнтернетПрокси.Установить("HTTP", СтруктураПараметровСайта.ПроксиСервер, СтруктураПараметровСайта.ПроксиПорт);
		КонецЕсли;	
		
	КонецЕсли;	
	
	ЗащищенноеСоединение = ?(СтруктураПараметровСайта.Свойство("Защищенное")=Неопределено, Ложь, СтруктураПараметровСайта["Защищенное"]);
	
	Порт = ?(ЗначениеЗаполнено(СтруктураПараметровСайта.Порт), СтруктураПараметровСайта.Порт, 80);
	Если Порт = 80 и ЗащищенноеСоединение Тогда
		Порт = 443;
	КонецЕсли;	
	
	
	Попытка
		
		Если ПустаяСтрока(СтруктураПараметровСайта.Сервер) или ПустаяСтрока(СтруктураПараметровСайта.Порт) Тогда
			ВызватьИсключение "НеУказанСерверПорт";
		КонецЕсли;
		
		Результат = Новый HTTPСоединение(СтруктураПараметровСайта.Сервер, Порт, "", "", ИнтернетПрокси, ЗащищенноеСоединение);
		
	Исключение
		
		_ВывестиСообщение(НСтр("ru='Не удалось установить соединение с сервером ';uk='Не вдалося встановити з`єднання з сервером '")+
					?(ПустаяСтрока(СтруктураПараметровСайта.Сервер), "", СтруктураПараметровСайта.Сервер)+":"+
					?(ЗначениеЗаполнено(СтруктураПараметровСайта.Порт), Строка(СтруктураПараметровСайта.Порт), "")+НСтр("ru='"
"Проверьте правильность адреса сервера, порт, имя пользователя и пароль.';uk='"
"Перевірте правильність адреси сервера, порт, ім`я користувача і пароль.'"));
		
		Результат = Неопределено;
		
	Конецпопытки;	
	
	Возврат Результат;
	
КонецФункции

Функция _HTTPОтправитьФайлНаСервер(ПолноеИмяФайла, Соединение, ПараметрыЗапроса="", Заголовки="", ТипДанных="", СокрИмяФайла="catalog.cml", КлючиФайла="[]")
	
	Отказ = Ложь;
	
	МассивФайлов = Новый Массив;
	
	Boundary = СтрЗаменить(Строка(Новый УникальныйИдентификатор()), "-", "");
	
	// 1 Часть: Параметры
	ИмяЧастиФайла = ПолучитьИмяВременногоФайла("txt");
	ЗаписьТекста = Новый ЗаписьТекста(ИмяЧастиФайла, КодировкаТекста.UTF8);
	ЗаписьТекста.ЗаписатьСтроку("--"+boundary);
	ЗаписьТекста.ЗаписатьСтроку("Content-Disposition: form-data; name=""file""; filename="""+СокрИмяФайла+"""");
	ЗаписьТекста.ЗаписатьСтроку("Content-Type: "+ТипДанных+"");
	ЗаписьТекста.ЗаписатьСтроку("");	
	ЗаписьТекста.Закрыть();	
	_УбратьИзXMLМеткуBOM(ИмяЧастиФайла);	
	МассивФайлов.Добавить(ИмяЧастиФайла);
	
	// Часть 2: Данные
	МассивФайлов.Добавить(ПолноеИмяФайла);
	
	// Часть 3: Конец
	ИмяЧастиФайла = ПолучитьимяВременногоФайла("txt");
	ЗаписьТекста = Новый ЗаписьТекста(ИмяЧастиФайла, КодировкаТекста.UTF8);
	ЗаписьТекста.ЗаписатьСтроку("");
	ЗаписьТекста.ЗаписатьСтроку("--"+boundary+"--");
	ЗаписьТекста.Закрыть();
	_УбратьИзXMLМеткуBOM(ИмяЧастиФайла);	
	МассивФайлов.Добавить(ИмяЧастиФайла);
	
	// Результирующий файл
	ИмяФайлаОтправки = ПолучитьИмяВременногоФайла("out");
	
	ОбъединитьФайлы(массивФайлов, ИмяФайлаОтправки);
	
	// Передадим файл на сервер
	ФайлОтправки = Новый Файл(ИмяФайлаОтправки);	
		
	Заголовки = Новый Соответствие();
	Заголовки.Вставить("Content-Type", "multipart/form-data; boundary="+boundary);
	Заголовки.Вставить("Charset", "utf-8");
	Заголовки.Вставить("Content-Length", XMLСтрока(ФайлОтправки.Размер()));
	Заголовки.Вставить("Accept-Encoding", "identity, deflate, compress, gzip");
	Если КлючиФайла <> "" Тогда 
		Заголовки.Вставить("X-Chunk-Keys", КлючиФайла);
	КонецЕсли;
	
	ИмяФайлаОтвета = ПолучитьИмяВременногоФайла();
	
	Попытка
		
		HTTPОтвет = Соединение.ОтправитьДляОбработки(ИмяФайлаОтправки, СокрЛП(ПараметрыЗапроса), ИмяФайлаОтвета, Заголовки);
		Если HTTPОтвет <> Неопределено и HTTPОтвет.КодСостояния <> 200 Тогда
			Отказ = Истина;
			_ВывестиСообщение(НСтр("ru='Получен код возврата ""';uk='Отриманий код повернення ""'")+HTTPОтвет.КодСостояния+НСтр("ru='"" при отправке файла ""';uk='"" при відправці файлу ""'")+ПолноеИмяФайла+НСтр("ru='"" на сервер.';uk='"" на сервер.'"), СтатусСообщения.Важное);	
		КонецЕсли;
		
	Исключение
		Отказ = Истина;
		ОписаниеОшибки = ОписаниеОшибки();
		_ВывестиСообщение(ОписаниеОшибки, СтатусСообщения.Важное);
	КонецПопытки;	

	ФайлОтвета = _ПолучитьФайл(ИмяФайлаОтвета);
	Если ФайлОтвета = Неопределено Тогда
		_ВывестиСообщение(НСтр("ru='Отправка файла на сервер: Ответ сервера не получен.';uk='Відправка файлу на сервер: Відповідь сервера не отримана.'"), СтатусСообщения.Важное);
	Иначе	
		ТекстОтвета = Новый ТекстовыйДокумент;
		ТекстОтвета.Прочитать(ИмяФайлаОтвета, "windows-1251");
		Если ТекстОтвета.КоличествоСтрок()>0 Тогда
			ОтветСервера = ТекстОтвета.ПолучитьТекст();
			Если Отказ Тогда				
				_ВывестиСообщение(ОтветСервера, СтатусСообщения.Важное);	
			КонецЕсли;
		Иначе
			_ВывестиСообщение(НСтр("ru='Отправка файла на сервер: Получен пустой ответ сервера.';uk='Відправка файлу на сервер: Отримана порожня відповідь сервера.'"), СтатусСообщения.Важное);
		КонецЕсли;	
	КонецЕсли;	
			
	Попытка
		УдалитьФайлы(ИмяФайлаОтвета);
	Исключение
		__Ошибка = Истина;
	КонецПопытки;
	
	Попытка
		УдалитьФайлы(ИмяФайлаОтправки);
	Исключение
		__Ошибка = Истина;
	КонецПопытки;
	
	Возврат ?(Отказ, Неопределено, ОтветСервера);
	
КонецФункции


// ЯДРО: РАБОТА С ИНТЕРНЕТОМ

Функция _ВыполнитьЗапросКСайту(Заголовок, СтруктураПараметровСайта, СтрокаЗапроса, Параметры=Неопределено)
	
	Результат = Неопределено;
		
	Соединение = _HTTPУстановитьСоединение(СтруктураПараметровСайта);
	
	Если Соединение = Неопределено Тогда		
		_ВывестиСообщение(Заголовок+НСтр("ru='. Ошибка при установке соединения с сайтом.';uk='. Помилка при установці з`єднання з сайтом.'"));
		Возврат Неопределено;
	КонецЕсли;
	
	ИмяФайлаОтвета = ПолучитьИмяВременногоФайла("tmp");	
	
	Если ТипЗнч(Параметры) = Тип("Структура") Тогда
		Если Параметры.Свойство("ПутьКФайлу") Тогда
			ИмяФайлаОтвета = Параметры["ПутьКФайлу"];
		КонецЕсли;	
	КонецЕсли; 
	
	Попытка
		HTTPОтвет = Соединение.Получить(СтрокаЗапроса, ИмяФайлаОтвета);
	Исключение
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		_ВывестиСообщение(ПодробноеПредставлениеОшибки(ИнформацияОбОшибке), СтатусСообщения.ОченьВажное);
		Возврат Неопределено;
	КонецПопытки;	
	
	ФайлОтвета = Новый Файл(ИмяФайлаОтвета);
	Если Не ФайлОтвета.Существует() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат ИмяФайлаОтвета;
	
КонецФункции

Функция _ПолучитьДанныеСвойств(ПутьКФайлу)
	
	ДанныеССайта = _ПолучитьДанныеССайта();
	Если ДанныеССайта = Неопределено Тогда
		_ВывестиСообщение(НСтр("ru='Не удалось получить с сайта служебные данные!';uk='Не вдалося отримати з сайту службові дані!'"), СтатусСообщения.Важное);
		Отказ = Истина;
		Возврат Неопределено;
	КонецЕсли;
	
	_Скрипт = ДанныеССайта["urls"]["category_attributes"];
		
	СтруктураПараметровСайта = _ПолучитьСтруктуруПараметровДляСоединения(Интернет_АдресСайта);
	
	ОтветСервера = _ВыполнитьЗапросКСайту(
		"[Список свойств сайта]",
		СтруктураПараметровСайта,
		_Скрипт+"?key="+СтруктураПараметровСайта.Ключ,
		Новый Структура("ПутьКФайлу", ПутьКФайлу)
	);
	Если ОтветСервера = Неопределено или ОтветСервера = "" Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
			
КонецФункции

Функция _КодироватьВHTML(Источник)
	Возврат СтрЗаменить(Источник, "#", "%23");
КонецФункции


// ЯДРО: ВЫГРУЗКА ДАННЫХ

Функция _ПодготовитьТоварыДляВыгрузки(Отказ, тзТовары, Параметры) 
		
	Результат = Новый Структура;
	// Основные данные
	Результат.Вставить("Организация", Новый Структура);
	Результат.Вставить("Товары", Новый Соответствие);
	Результат.Вставить("ТоварыЦены", Новый Соответствие);
	Результат.Вставить("ТоварыОстатки", Новый Соответствие);
	Результат.Вставить("ТоварыИерархия", Новый Соответствие);
	Результат.Вставить("ТоварыСвойства", Новый Соответствие);
	Результат.Вставить("ТоварыСвойстваРазличные", Новый Массив);
	Результат.Вставить("ТоварыРодители", Новый Соответствие);
	Результат.Вставить("ТоварыКартинки", Новый Соответствие);
	Результат.Вставить("ТоварыТипыЦен", Новый Массив);
	
	АлиасыЕдиницИзмеренийСайта = _ПолучитьАлиасыЕдиницИзмеренийССайта(Ложь, Параметры);
	
	СвойстваЗаменяемые = _ПолучитьСвойстваЗаменяемые(Параметры);
	СвойстваСайта = _ПолучитьСвойстваСайта(Параметры);
	СвойстваВыбранные = Свойства.Выгрузить(Свойства.НайтиСтроки(Новый Структура("Выгружать", Истина))).ВыгрузитьКолонку("Свойство");
	
	ДанныеЗапроса = прСформироватьЗапросОсновныеДанные();
	
	прПроверитьКолонкиТЗТовары(тзТовары, Параметры);
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Товары", тзТовары);
	Запрос.УстановитьПараметр("СвойстваВыбранные", СвойстваВыбранные);
	Запрос.УстановитьПараметр("ВыгружатьКартинки", ВыгружатьКартинки);
	Запрос.УстановитьПараметр("ТипХраненияФайлов", ?(ЗначениеЗаполнено(Товары_ТомХраненияФайлов), "ВТомахНаДиске", "ВИнформационнойБазе"));
	Запрос.УстановитьПараметр("ТомХраненияФайлов", Товары_ТомХраненияФайлов);
			
	Запрос.Текст = ДанныеЗапроса.ТекстЗапроса;
	
	РезультатыЗапросов = Запрос.ВыполнитьПакет();
	
	
	// Организация
	Результат.Организация.Вставить("УИД", _УИД(Организация));
	Результат.Организация.Вставить("Наименование", Организация.Наименование);
			
	прЗаполнитьДанныеТоваров(
		РезультатыЗапросов[ДанныеЗапроса.Индексы.Товары].Выбрать(ОбходРезультатаЗапроса.Прямой),
		Результат.Товары,
		Новый Структура("КонстантыОбмена, АлиасыЕдиницИзмеренийСайта", Параметры.КонстантыОбмена, АлиасыЕдиницИзмеренийСайта)
	);
		
	прЗаполнитьДанныеТоварыЦены(
		РезультатыЗапросов[ДанныеЗапроса.Индексы.ТоварыЦены].Выбрать(ОбходРезультатаЗапроса.Прямой),
		Результат.ТоварыЦены,
		Новый Структура("КонстантыОбмена", Параметры.КонстантыОбмена)
	);
	
	прЗаполнитьДанныеТоварыОстатки(
		РезультатыЗапросов[ДанныеЗапроса.Индексы.ТоварыОстатки].Выбрать(ОбходРезультатаЗапроса.Прямой),
		Результат.ТоварыОстатки,
		Новый Структура("КонстантыОбмена", Параметры.КонстантыОбмена)
	);
		
	прЗаполнитьДанныеИерархияТоваров(
		РезультатыЗапросов[ДанныеЗапроса.Индексы.ТоварыИерархия].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам),
		Результат.ТоварыИерархия,
		Новый Структура("КонстантыОбмена", Параметры.КонстантыОбмена)
	);
	
	прЗаполнитьДанныеТоварыРодители(
		РезультатыЗапросов[ДанныеЗапроса.Индексы.ТоварыРодители].Выбрать(ОбходРезультатаЗапроса.Прямой),
		Результат.ТоварыРодители,
		Новый Структура("КонстантыОбмена", Параметры.КонстантыОбмена)
	);
	
	прЗаполнитьДанныеТоварыТипыЦен(
		РезультатыЗапросов[ДанныеЗапроса.Индексы.ТоварыТипыЦен].Выбрать(ОбходРезультатаЗапроса.Прямой),
		Результат.ТоварыТипыЦен,
		Новый Структура("КонстантыОбмена", Параметры.КонстантыОбмена)
	);
	
	прЗаполнитьДанныеТоварыКартинки(
		РезультатыЗапросов[ДанныеЗапроса.Индексы.ТоварыКартинки].Выбрать(ОбходРезультатаЗапроса.Прямой),
		Результат.ТоварыКартинки,
		Новый Структура("КонстантыОбмена", Параметры.КонстантыОбмена)
	);
		
	прЗаполнитьДанныеТоварыСвойства(
		РезультатыЗапросов[ДанныеЗапроса.Индексы.ТоварыСвойства].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам),
		Результат.ТоварыСвойства,
		Новый Структура("КонстантыОбмена, СвойстваЗаменяемые, СвойстваСайта", Параметры.КонстантыОбмена, СвойстваЗаменяемые, СвойстваСайта)
	);
	
	прЗаполнитьДанныеТоварыСвойстваРазличные(
		РезультатыЗапросов[ДанныеЗапроса.Индексы.ТоварыСвойстваРазличные].Выбрать(ОбходРезультатаЗапроса.Прямой),
		Результат.ТоварыСвойстваРазличные,
		Новый Структура("КонстантыОбмена, СвойстваЗаменяемые, СвойстваСайта", Параметры.КонстантыОбмена, СвойстваЗаменяемые, СвойстваСайта)
	);
		
	Возврат Результат;
	
КонецФункции

Функция _СформироватьДанныеCMLФайлаТоваров(Отказ, Параметры, Пакет) 
		
	CML = _ПолучитьВстроеннуюОбработку("CML", Параметры);
	Если CML = Неопределено Тогда
		_ВывестиСообщение("Не удалось получить модуль CML.");
		Отказ = Истина;
		Возврат "";
	КонецЕсли;
	
	ИменаCML = CML.ПолучитьПредопределенныеИмена();
		
	ЗаписьCML = CML.ПолучитьЗаписьCML();
	
	ЗаписьCML.ЗаписатьОбъявлениеXML();
	
	// Корневой элемент	
	ЗаписьCML.ЗаписатьНачалоЭлемента("КоммерческаяИнформация");	
	ЗаписьCML.ЗаписатьАтрибут("ВерсияСхемы", "2.05");
	ЗаписьCML.ЗаписатьАтрибут("ДатаФормирования", CML.ФорматДаты(ТекущаяДата(), Истина,Истина));	
	
	// Классификатор
	ЗаписьCML.ЗаписатьНачалоЭлемента("Классификатор");
	
	CML.ЗаписатьТекстовойУзел(ЗаписьCML, "Ид", Пакет.Организация.УИД);
	CML.ЗаписатьТекстовойУзел(ЗаписьCML, "Наименование", CML.ФорматНаименования(ИменаCML.НаименованиеКлассификатора));	
	
	CML.ВыгрузитьДанныеОрганизации(ЗаписьCML, Пакет.Организация);	
	CML.ВыгрузитьГруппыТоваров(ЗаписьCML, Пакет.ТоварыИерархия);
	CML.ВыгрузитьСвойства(ЗаписьCML, Пакет.ТоварыСвойстваРазличные, ИменаCML);
	
	ЗаписьCML.ЗаписатьКонецЭлемента(); // Классификатор
	
	
	// Пакет предложений
	ЗаписьCML.ЗаписатьНачалоЭлемента("ПакетПредложений");
	ЗаписьCML.ЗаписатьАтрибут("СодержитТолькоИзменения", CML.ПолучитьЗначение(ВыгружатьТолькоИзменения));	
	
	CML.ЗаписатьТекстовойУзел(ЗаписьCML, "Ид", Пакет.Организация.УИД);
	CML.ЗаписатьТекстовойУзел(ЗаписьCML, "ИдКлассификатора", Пакет.Организация.УИД);
	CML.ЗаписатьТекстовойУзел(ЗаписьCML, "Наименование", "Каталог товаров");	
	
	CML.ВыгрузитьДанныеОрганизации(ЗаписьCML, Пакет.Организация);
	
	ЗаписьCML.ЗаписатьНачалоЭлемента("ТипыЦен");
	Для каждого ДанныеТипаЦены Из Пакет.ТоварыТипыЦен Цикл
		
		ЗаписьCML.ЗаписатьНачалоЭлемента("ТипЦены");
		
		CML.ЗаписатьТекстовойУзел(ЗаписьCML, "Ид", ДанныеТипаЦены.УИД);
		CML.ЗаписатьТекстовойУзел(ЗаписьCML, "Наименование", ДанныеТипаЦены.Наименование);
		CML.ЗаписатьТекстовойУзел(ЗаписьCML, "ИДЦенаСайт", ДанныеТипаЦены.ТипЦенСайт);
		CML.ЗаписатьТекстовойУзел(ЗаписьCML, "Валюта", Сред(ДанныеТипаЦены.ВалютаСокращение, 1, 3));
				
		ЗаписьCML.ЗаписатьКонецЭлемента();
		
	КонецЦикла;
	ЗаписьCML.ЗаписатьКонецЭлемента(); // ТипыЦен
	
	// Предложения
	ЗаписьCML.ЗаписатьНачалоЭлемента("Предложения");
	Для каждого ЭлементТовар Из Пакет.Товары Цикл
		
		ДанныеТовара = ЭлементТовар.Значение;		
		ТоварКлюч = ЭлементТовар.Ключ;
		
		ЗаписьCML.ЗаписатьНачалоЭлемента("Предложение");
		
		CML.ЗаписатьТекстовойУзел(ЗаписьCML, "Ид", ДанныеТовара.ТоварУИД);
		
		ЗаписьCML.ЗаписатьНачалоЭлемента("Цены");
		
		Для каждого ЭлементТоварЦена Из Пакет.ТоварыЦены[ТоварКлюч] Цикл
			
			ДанныеЦены = ЭлементТоварЦена.Значение;
			
			Если ЗначениеЗаполнено(ДанныеЦены) и ДанныеЦены.Цена = 0 Тогда
				Продолжить;
			КонецЕсли;						
			
			ЗаписьCML.ЗаписатьНачалоЭлемента("Цена");
					
			CML.ЗаписатьТекстовойУзел(ЗаписьCML, "Представление", ""+ДанныеЦены.Цена+" "+ДанныеЦены.ВалютаСокращение+" за "+ДанныеТовара.ЕдиницаИзмерения);
			CML.ЗаписатьТекстовойУзел(ЗаписьCML, "ИдТипаЦены"	,	ДанныеЦены.ТипЦенУИД);
			CML.ЗаписатьТекстовойУзел(ЗаписьCML, "ИДЦенаСайт"	,	ДанныеЦены.ТипЦенСайт);
			CML.ЗаписатьТекстовойУзел(ЗаписьCML, "ЦенаЗаЕдиницу",	ДанныеЦены.Цена);
			CML.ЗаписатьТекстовойУзел(ЗаписьCML, "Валюта", Сред(ДанныеЦены.ВалютаСокращение, 1, 3));
			CML.ЗаписатьТекстовойУзел(ЗаписьCML, "Единица",	ДанныеТовара.ЕдиницаИзмерения);
			CML.ЗаписатьТекстовойУзел(ЗаписьCML, "Коэффициент",	"1");
			
			ЗаписьCML.ЗаписатьКонецЭлемента(); // Цена		
			
		КонецЦикла;
				
		ЗаписьCML.ЗаписатьКонецЭлемента(); // Цены
		
		ЗаписьCML.ЗаписатьКонецЭлемента(); // Предложение						
		
	КонецЦикла;
	
	ЗаписьCML.ЗаписатьКонецЭлемента(); // Предложения
	
	
	// Товары
	ЗаписьCML.ЗаписатьНачалоЭлемента("Товары");
	
	Для каждого ЭлементТовар Из Пакет.Товары Цикл
		
		ДанныеТовара = ЭлементТовар.Значение;		
		ТоварКлюч = ЭлементТовар.Ключ;
		
		Если Параметры.КонстантыОбмена.ДопустимыеЕдиницыИзмерения.Найти(СокрЛП(ДанныеТовара.ЕдиницаИзмерения)) = Неопределено Тогда 
			_ВывестиСообщение(НСтр("ru='Недопустимая единица измерения ""';uk='Неприпустима одиниця виміру ""'")+СокрЛП(ДанныеТовара.ЕдиницаИзмерения)+НСтр("ru='"" товар ';uk='"" товар '")+СокрЛП(ДанныеТовара.Наименование));
			Продолжить;
		КонецЕсли;
		
		// Товар
		ЗаписьCML.ЗаписатьНачалоЭлемента("Товар");
		
		CML.ЗаписатьТекстовойУзел(ЗаписьCML, "Ид", ДанныеТовара.ТоварУИД);
		
		Если СтрДлина(ДанныеТовара.Артикул) >= 8 И СтрДлина(ДанныеТовара.Артикул) <= 14 Тогда
			CML.ЗаписатьТекстовойУзел(ЗаписьCML, "Штрихкод", ДанныеТовара.Артикул);
		КонецЕсли;
		
		CML.ЗаписатьТекстовойУзел(ЗаписьCML, "Артикул", ДанныеТовара.Артикул);
		CML.ЗаписатьТекстовойУзел(ЗаписьCML, "Наименование", CML.ФорматНаименования(ДанныеТовара.Наименование));
		CML.ЗаписатьТекстовойУзел(ЗаписьCML, "ПолноеНаименование", CML.ФорматНаименования(ДанныеТовара.НаименованиеПолное));		
		CML.ЗаписатьТекстовойУзел(ЗаписьCML, "БазоваяЕдиница", CML.ФорматНаименования(ДанныеТовара.ЕдиницаИзмерения));		
		CML.ЗаписатьТекстовойУзел(ЗаписьCML, "Описание", СокрЛП(ДанныеТовара.Описание));

		// Группы
		ЗаписьCML.ЗаписатьНачалоЭлемента("Группы");			
		
		ДанныеТовараРодитель = Пакет.ТоварыРодители[ТоварКлюч];
		Если ДанныеТовараРодитель = Неопределено Тогда
			CML.ЗаписатьТекстовойУзел(ЗаписьCML, "Ид", "0");
		Иначе
			CML.ЗаписатьТекстовойУзел(ЗаписьCML, "Ид", ДанныеТовараРодитель.РодительУИД);			
		КонецЕсли;
		
		ЗаписьCML.ЗаписатьКонецЭлемента(); // Группы
		
		// Картинки
		КартинкиТовара = Пакет.ТоварыКартинки[_СформироватьКлючИзУИД(ДанныеТовара.ТоварУИД)];
		Если КартинкиТовара <> Неопределено Тогда
			Для каждого КартинкаТовара Из КартинкиТовара Цикл
				
				Если Не прКартинкаКорректная(КартинкаТовара) Тогда
					Продолжить;
				КонецЕсли;
			
				ЗаписьCML.ЗаписатьНачалоЭлемента("Картинка");

				ЗаписьCML.ЗаписатьАтрибут("main_image", CML.ПолучитьЗначение(КартинкаТовара.ЭтоОсновноеИзображение));
				
				ЗаписьCML.ЗаписатьТекст(КартинкаТовара.КартинкаУИД);
				
				ЗаписьCML.ЗаписатьКонецЭлемента(); // Картинка
				
			КонецЦикла;
		КонецЕсли;
		
		// Значения свойств
		ЗаписьCML.ЗаписатьНачалоЭлемента("ЗначенияСвойств");		
		
		ОстаткиТовара = Пакет.ТоварыОстатки[_СформироватьКлючИзУИД(ДанныеТовара.ТоварУИД)];
		
		ЗаписьCML.ЗаписатьНачалоЭлемента("ЗначенияСвойства");		
		CML.ЗаписатьТекстовойУзел(ЗаписьCML, "Ид", ИменаCML.ИдНаличия);
		CML.ЗаписатьТекстовойУзел(ЗаписьCML, "Наименование", "Наличие");
		CML.ЗаписатьТекстовойУзел(ЗаписьCML, "Значение", CML.ПолучитьЗначение(?(ЗначениеЗаполнено(ОстаткиТовара), ОстаткиТовара.Наличие, Ложь)));
		ЗаписьCML.ЗаписатьКонецЭлемента();
		
		ЗаписьCML.ЗаписатьНачалоЭлемента("ЗначенияСвойства");		
		CML.ЗаписатьТекстовойУзел(ЗаписьCML, "Ид", ИменаCML.ИдКоличества);
		CML.ЗаписатьТекстовойУзел(ЗаписьCML, "Наименование", "Количество");
		CML.ЗаписатьТекстовойУзел(ЗаписьCML, "Значение", CML.ПолучитьЗначение(?(ЗначениеЗаполнено(ОстаткиТовара), ОстаткиТовара.Количество, 0)));
		ЗаписьCML.ЗаписатьКонецЭлемента();
		
		
		
		Если ДанныеТовара.НоменклатураУИД = ДанныеТовара.ТоварУИД Тогда
			// доп. свойства только номенклатуры
			СвойстваТовара = Пакет.ТоварыСвойства[ТоварКлюч];			
		Иначе
			// доп. свойства и номенклатуры и характеристик
			СвойстваТовара = Пакет.ТоварыСвойства[ТоварКлюч];
			Если СвойстваТовара = Неопределено Тогда
				СвойстваТовара = Новый Массив;
			КонецЕсли;
			СвойстваНоменклатуры = Пакет.ТоварыСвойства[_СформироватьКлючИзУИД(ДанныеТовара.НоменклатураУИД)];
			
			Если СвойстваНоменклатуры <> Неопределено Тогда				
				
				Для Каждого СвойствоНоменклатуры Из СвойстваНоменклатуры Цикл  
					
					_Свойство = _СкопироватьСтруктуру(СвойствоНоменклатуры);
					// подменяем УИД что бы доп. свойство номенклатуры использовалось во всех его характеристиках
					_Свойство.ТоварУИД = ДанныеТовара.ТоварУИД;
					СвойстваТовара.Добавить(_Свойство);
					
				КонецЦикла;			
				
			КонецЕсли;
			
		КонецЕсли;
		
		// Выгружаем дополнительные свойства номенклатура+характеристика или только номенклатура
		Если СвойстваТовара <> Неопределено Тогда
			
			Для Каждого СвойствоТовара Из СвойстваТовара Цикл  
					
				ЗаписьCML.ЗаписатьНачалоЭлемента("ЗначенияСвойства");
				
				CML.ЗаписатьТекстовойУзел(ЗаписьCML, "Ид", СвойствоТовара.УИД);
				Если СвойствоТовара.СвойствоСайт <> Неопределено Тогда
					CML.ЗаписатьТекстовойУзел(ЗаписьCML, "ИДСвойстваСайт", СвойствоТовара.СвойствоСайт);
				КонецЕсли;
				CML.ЗаписатьТекстовойУзел(ЗаписьCML, "Наименование", СвойствоТовара.Наименование);
				CML.ЗаписатьТекстовойУзел(ЗаписьCML, "Значение", СвойствоТовара.Значение);
														
				ЗаписьCML.ЗаписатьКонецЭлемента();
					
			КонецЦикла;
			
		КонецЕсли;
				
		ЗаписьCML.ЗаписатьКонецЭлемента(); // ЗначенияСвойств
								
		ЗаписьCML.ЗаписатьКонецЭлемента(); // Товар	
				
	КонецЦикла;
	
	ЗаписьCML.ЗаписатьКонецЭлемента(); // Товары
		
	ЗаписьCML.ЗаписатьКонецЭлемента(); // ПакетПредложений
	
	ЗаписьCML.ЗаписатьКонецЭлемента(); // КоммерческаяИнформация
	
	ДанныеCML = ЗаписьCML.Закрыть();
	
	Возврат ДанныеCML;
	
КонецФункции

Функция _ВыгрузитьТоварыНаДиск(Отказ, Параметры, ПакетДанных)
					
	ДанныеCMLТовары = _СформироватьДанныеCMLФайлаТоваров(Отказ, Параметры, ПакетДанных);
	Если Отказ Тогда
		Возврат Ложь;
	КонецЕсли;		
	
	ДокументCML = Новый ТекстовыйДокумент;
	
	ДокументCML.УстановитьТекст(ДанныеCMLТовары);
	
	Попытка
		
		ДокументCML.Записать(Параметры.Файлы.ФайлCML, КодировкаТекста.UTF8);
		
		_УбратьИзXMLМеткуBOM(Параметры.Файлы.ФайлCML);
				
	Исключение
		Отказ = Истина;
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		_ВывестиСообщение(ПодробноеПредставлениеОшибки(ИнформацияОбОшибке), СтатусСообщения.Важное);
		Возврат Ложь;
	КонецПопытки;
				
	Возврат	Истина;
	
КонецФункции

Функция _ВыгрузитьКартинкиТоваровНаДиск(Отказ, Путь, Параметры, Пакет) 
	
	Результат = Новый Массив;
	
	Для каждого КартинкиТовара Из Пакет.ТоварыКартинки Цикл
		
		Для каждого Картинка Из КартинкиТовара.Значение Цикл
						
			СтруктураДанныхКартинки = прВыгрузитьКартинкуНаДиск(Путь, Картинка);
			Если СтруктураДанныхКартинки = Неопределено Тогда
				_ВывестиСообщение(НСтр("ru='Картинка: ';uk='Картинка: '")+Картинка.ХранилищеНаименование+НСтр("ru=', размер: ';uk='розмір: '")+Картинка.Размер+
									НСтр("ru=' не была выгружена на диск. Номенклатура (';uk=' не була вивантажена на диск. Номенклатура ('")+Картинка.ТоварКод+"): "+
									Картинка.ТоварНаименование, СтатусСообщения.Внимание);
				Продолжить;
			КонецЕсли;
			
			Картинка.Вставить("ПолныйПуть", СтруктураДанныхКартинки.ПолныйПуть);
						
			Результат.Добавить(СтруктураДанныхКартинки.ПолныйПуть);
			
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Функция _ВыгрузитьФайлCMLНаСайт(Отказ, Параметры)
									
	МаксимальныйРазмерФайла = 10*1048576; // по умолчанию 10 Мб
	
	СписокФайловДляОтправки = _РазделитьФайлыНаФрагменты(
		_НовыйМассив(Параметры.Файлы.ФайлCML),
		МаксимальныйРазмерФайла
	);
	
	ИнформацияОКонтексте = _ПолучитьИнформациюОКонтекстеВыполнения(Параметры.КонстантыОбмена);
	
	СтруктураПараметровСайта = _ПолучитьСтруктуруПараметровДляСоединения(Интернет_АдресСайта);
	
	_Скрипт = Параметры["АдресаСкриптов"]["upload_import_file"];

	АдресДляРаботы = СтруктураПараметровСайта.АдресСкрипта+
						?(Лев(СокрЛП(_Скрипт),1)="/","","/")+_Скрипт+
						"?key="+СтруктураПараметровСайта.Ключ+
						"&cid="+СокрЛП(Метаданные.Имя)+
						"&pv="+СокрЛП(ИнформацияОКонтексте.ВерсияПриложения)+
						"&cv="+СокрЛП(Метаданные.Версия)+
						"&api="+ИнформацияОКонтексте.ВерсияОбмена+
						"&ba="+?(ИнформацияОКонтексте.ВстроенныйОбмен, "1", "0");
		
	Соединение = _HTTPУстановитьСоединение(СтруктураПараметровСайта);
	Если Соединение = Неопределено Тогда
		Отказ = Истина;
		Возврат Ложь;
	КонецЕсли;
		
	ВсегоФайловДляОтправки = СписокФайловДляОтправки.Количество();
	НомерФайла = 0;
	ОтветыПоФайлам = Новый Соответствие;
	КлючиФайла = "";
	масКлючей = Новый Массив;
	
	Для Каждого ПутьОтправляемогоФайла Из СписокФайловДляОтправки Цикл
		
		НомерФайла = НомерФайла + 1;
		Если ВсегоФайловДляОтправки = 1 Тогда 
			КлючиФайла = "[]";
		ИначеЕсли НомерФайла = ВсегоФайловДляОтправки Тогда 
			КлючиФайла = "[";
			НомерКлюча = 0;
			Для Каждого Ключ из масКлючей Цикл
				НомерКлюча = НомерКлюча + 1;
				КлючиФайла = КлючиФайла + """" + СокрЛП(Ключ) + """" + ?(НомерКлюча<масКлючей.Количество(),",","");
			КонецЦикла;
			КлючиФайла = КлючиФайла + "]";
		КонецЕсли;
		
		ФайлОтправляемый = _ПолучитьФайл(ПутьОтправляемогоФайла);
		Если ФайлОтправляемый = Неопределено Тогда 
			Продолжить;
		КонецЕсли;
		
	    ФайлОбмена = Новый Файл(Параметры.Файлы.ФайлCML);	
		ТипДанныхДляОтправки = _ТипДанныхПоФайлу(Параметры.Файлы.ФайлCML);
			
		//
		ОтветСервера = _HTTPОтправитьФайлНаСервер(
			ФайлОтправляемый.ПолноеИмя,
			Соединение,
			АдресДляРаботы,
			"",
			ТипДанныхДляОтправки,
			?(НомерФайла=ВсегоФайловДляОтправки, ФайлОбмена.Имя, ФайлОтправляемый.Имя),
			КлючиФайла
		);
		Если ОтветСервера = Неопределено Тогда			
			Отказ = Истина;
			Возврат Ложь;
		КонецЕсли;
		
		Попытка
			
			Если Лев(ОтветСервера, 1) = "<" Тогда
				ВызватьИсключение "Ошибка при отправке файла на сервер";
			КонецЕсли;
			
			JSON = _ПолучитьВстроеннуюОбработку("JSON", Параметры);
			
			ДанныеОтветаСервера = JSON._ПрочитатьJSON(ОтветСервера);					
			
		Исключение
			Отказ = Истина;
			_ВывестиСообщение(НСтр("ru='Ошибка при отправке файла ';uk='Помилка при відправці файлу '") + ПутьОтправляемогоФайла + ".", СтатусСообщения.Важное);
			Возврат Ложь;
		КонецПопытки;		
		
		
		ЭтоПоследнийФайл = (НомерФайла = ВсегоФайловДляОтправки);
		
		Если ЭтоПоследнийФайл Тогда
						
			Попытка
				
				ОтветыПоФайлам[ПутьОтправляемогоФайла] = СтрЗаменить(ДанныеОтветаСервера["status_url"], Интернет_АдресСайта, "");
				
				Если ДанныеОтветаСервера["task_id"] = Неопределено Тогда
					ВызватьИсключение "Ожидаются другие даныне в ответе сервера";
				КонецЕсли;
				
				Параметры.Вставить("ОбменИнформация", Новый Структура);
				Параметры.ОбменИнформация.Вставить("task_id", ДанныеОтветаСервера["task_id"]);
				
			Исключение
				Отказ = Истина;
				ОписаниеОшибки = ОписаниеОшибки();				
				_ВывестиСообщение(НСтр("ru='Произошла ошибка на стороне сервера. Не корректный статус завершения операции. Файл не отправлен (';uk='Сталася помилка на стороні сервера. Не коректний статус завершення операції. Файл не відправлений ('") + ПутьОтправляемогоФайла + ").", СтатусСообщения.Важное);
				Возврат Ложь;
			КонецПопытки;
						
		Иначе
			
			Попытка
				масКлючей.Добавить(ДанныеОтветаСервера["key"]);
			Исключение
				Отказ = Истина;
				_ВывестиСообщение(НСтр("ru='Произошла ошибка на стороне сервера. Не корректный статус завершения операции. Файл не отправлен (';uk='Сталася помилка на стороні сервера. Не коректний статус завершення операції. Файл не відправлений ('") + ПутьОтправляемогоФайла + ").", СтатусСообщения.Важное);
				Возврат Ложь;
			КонецПопытки;
						
		КонецЕсли;
		
	КонецЦикла; // Для Каждого ПутьОтправляемогоФайла Из СписокФайловДляОтправки Цикл
	
	
	Возврат	Истина;
	
КонецФункции

Функция _ВыгрузитьКартинкиТоваровНаСайт(Отказ, Параметры, ПакетДанных) 
		
	Успешно = Истина;
	
	//УстановитьПривилегированныйРежим(Истина);
	
	РазмерПорцииКартинок = 15;		
	ОбработаноКартинок = 0;
	СтрокаИДКартинок = "";
	JSON = _ПолучитьВстроеннуюОбработку("JSON", Параметры);
	ВсегоКартинок = _ПолучитьКоличествоКартинокТоваров(ПакетДанных.ТоварыКартинки);
	КаталогНаДиске = Параметры.КонстантыОбмена.КаталогВременныхФайлов;
	
	_Скрипт = Параметры["АдресаСкриптов"]["upload_product_image"];	
	
	СтруктураПараметровСайта = _ПолучитьСтруктуруПараметровДляСоединения(Интернет_АдресСайта);
	Соединение = _HTTPУстановитьСоединение(СтруктураПараметровСайта);
	Если Соединение = Неопределено Тогда
		Отказ = Истина;
		Возврат Ложь;
	КонецЕсли;
	
	Для Каждого ТоварКартинки из ПакетДанных.ТоварыКартинки Цикл
		
		Для каждого ТоварКартинка Из ТоварКартинки.Значение Цикл
			
			Если Не прКартинкаКорректная(ТоварКартинка) Тогда
				Продолжить;
			КонецЕсли;
			
			ОбработаноКартинок = ОбработаноКартинок + 1;
			
			АдресДляРаботы = СтруктураПараметровСайта.АдресСкрипта+
								?(Лев(СокрЛП(_Скрипт),1) = "/", "", "/")+_Скрипт+
								"?key="+СтруктураПараметровСайта.Ключ+
								"&product_id="+_КодироватьВHTML(ТоварКартинка.ТоварУИД)+
								"&task_id="+Параметры.ОбменИнформация.task_id+
								"&main_image="+?(ТоварКартинка.ЭтоОсновноеИзображение, "1", "0")+
								"&final_image="+?(ВсегоКартинок=ОбработаноКартинок, "1", "0");
								
			_Ф = _ПолучитьФайл(ТоварКартинка.ПолныйПуть);
			
			ОтветСервера = _HTTPОтправитьФайлНаСервер(ТоварКартинка.ПолныйПуть, Соединение, АдресДляРаботы, "", _ТипДанныхПоФайлу(ТоварКартинка.ПолныйПуть), _Ф.Имя);
			
			Если ОтветСервера = Неопределено Тогда
				_ВывестиСообщение(НСтр("ru='Не удалось получить ответ сервера. Файл не отправлен (';uk='Не вдалося отримати відповідь сервера. Файл не відправлений ('") + ТоварКартинка.ПолныйПуть + ").", СтатусСообщения.Важное);
				Успешно = Ложь;
			ИначеЕсли ОтветСервера = "{}" Тогда 
				// Выгрузка прошла корректно
				СтрокаИДКартинок = СтрокаИДКартинок+?(ПустаяСтрока(СтрокаИДКартинок),"",",")+_Ф.ИмяБезРасширения;
			Иначе
				Если Лев(ОтветСервера,1) = "{" Тогда
					РезультатОтвета = JSON._ПрочитатьJSON(ОтветСервера, Истина);
					Ошибка = РезультатОтвета["error"];
					Если Ошибка <> Неопределено Тогда 
						ТекстОшибки = "" + СокрЛП(Ошибка);
						_ВывестиСообщение("" + _Ф.ИмяБезРасширения + НСтр("ru=' : Произошла ошибка на стороне сервера. ';uk=' : Сталася помилка на стороні сервера. '")+ТекстОшибки, СтатусСообщения.Важное);
					КонецЕсли;
					Успешно = Ложь;
				КонецЕсли;
			КонецЕсли;
			
			Если ОбработаноКартинок%РазмерПорцииКартинок = 0 или ОбработаноКартинок = ВсегоКартинок Тогда
				_ВывестиСообщение(НСтр("ru='   Обработано ';uk='   Оброблено '")+ОбработаноКартинок+НСтр("ru=' картинок из ';uk=' картинок з '")+ВсегоКартинок+" [ids: "+СтрокаИДКартинок+" ]", СтатусСообщения.Информация);
				СтрокаИДКартинок = "";
			КонецЕсли;
								
		КонецЦикла;
			
	КонецЦикла;
	
	//УстановитьПривилегированныйРежим(Ложь);
	
	Возврат Успешно;
	
КонецФункции

Функция _ПолучитьДанныеССайта() Экспорт
	
	ЗаголовокЗапроса = "[Данные с сайта]";

	СтруктураПараметровСайта = _ПолучитьСтруктуруПараметровДляСоединения(Интернет_АдресСайта);
	
	_ФайлОтвета = _ВыполнитьЗапросКСайту(
		ЗаголовокЗапроса,
		СтруктураПараметровСайта,
		"cabinet/product_import/get_1c_system_info?key="+СтруктураПараметровСайта.Ключ
	);
	Если _ФайлОтвета = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;	
	
	JSON = _ПолучитьВстроеннуюОбработку("JSON", Неопределено);
	
	ДанныеССайта = Неопределено;
	Попытка
		ДанныеССайта = JSON._ПрочитатьJSON(_ПолучитьСодержимоеФайла(_ФайлОтвета, КодировкаТекста.UTF8));
	Исключение
		_ВывестиСообщение(НСтр("ru='Ожидается другой формат ответа с сайта.';uk='Очікується інший формат відповіді з сайту.'"), СтатусСообщения.Важное);
	КонецПопытки;	
	
	Возврат ДанныеССайта;
	
КонецФункции

Функция _ПолучитьЗаказыССайта(Отказ, Параметры) 
	
	ДанныеССайта = _ПолучитьДанныеССайта();
	Если ДанныеССайта = Неопределено Тогда
		_ВывестиСообщение(НСтр("ru='Не удалось получить с сайта служебные данные!';uk='Не вдалося отримати з сайту службові дані!'"), СтатусСообщения.Важное);
		Отказ = Истина;
		Возврат Неопределено;
	КонецЕсли;
	
	_Скрипт = ДанныеССайта["urls"]["export_orders"];

	СтруктураПараметровСайта = _ПолучитьСтруктуруПараметровДляСоединения(_Скрипт);
	
	_Адрес = СтруктураПараметровСайта.АдресСкрипта + "&modified_since=" + 
		Формат(?(ЗначениеЗаполнено(Заказ_ДатаЗагрузки), Заказ_ДатаЗагрузки, ДобавитьМесяц(ТекущаяДата(), -1)), "ДФ=ггггММддЧЧммсс");
		
	ОтветСервера = _ВыполнитьЗапросКСайту(
		"[Получение заказов с сайта]",
		СтруктураПараметровСайта,
		_Адрес
	);
	Если ОтветСервера = Неопределено или ОтветСервера = "" Тогда
		Отказ = Истина;
		Возврат Неопределено;
	КонецЕсли;
		
	Возврат ОтветСервера;
		
КонецФункции

Функция _ПолучитьАлиасыЕдиницИзмеренийССайта(ГруппироватьПоЗначению = Ложь, Параметры)
	
	Результат = Новый Соответствие;	
	
	АлиасыЕдиницИзмеренийСайта = Новый Соответствие;	
	Если РежимОбмена = Параметры.КонстантыОбмена.РежимыОбмена.Сайт ИЛИ ИспользоватьПодключениеКСайту Тогда
		ДанныеССайта = _ПолучитьДанныеССайта();
		Если ДанныеССайта <> Неопределено Тогда
			АлиасыЕдиницИзмеренийСайта = ДанныеССайта["measure_units"];
		КонецЕсли;
	КонецЕсли;
	
	Если ГруппироватьПоЗначению Тогда		
		Для Каждого ЕдиницаИзмерения Из АлиасыЕдиницИзмеренийСайта Цикл
			
			Ключ = ЕдиницаИзмерения.Ключ;
			Значение = ЕдиницаИзмерения.Значение;			
			
			СписокВариантов = Результат[Значение];
			Если СписокВариантов = Неопределено Тогда 
				СписокВариантов = Новый Массив;
				Результат.Вставить(Значение, СписокВариантов);
			КонецЕсли;
			
			СписокВариантов.Добавить(Ключ);
			
		КонецЦикла;
	Иначе
		Результат = АлиасыЕдиницИзмеренийСайта;
	КонецЕсли;	
	
	Возврат Результат;
	
КонецФункции

Функция _ПолучитьСвойстваПредопределенные()
	
	СвойстваПредопределенные = Новый Соответствие;
	СвойстваПредопределенные.Вставить("КлючевыеСлова", Новый Структура("Выгружать, Реквизит", Истина, "Товары_СвойствоНоменклатурыКлючевыеСлова"));
	СвойстваПредопределенные.Вставить("ВнешнийИдентификаторТовара", Новый Структура("Выгружать, Реквизит", Ложь, "Заказ_СвойствоНоменклатурыВнешнийИД"));
	СвойстваПредопределенные.Вставить("ВнешнийИдентификаторКонтрагента", Новый Структура("Выгружать, Реквизит", Ложь, "Заказ_СвойствоКонтрагентаВнешнийИД"));
	
	Возврат СвойстваПредопределенные;
	
КонецФункции

Функция _ПолучитьСвойстваЗаменяемые(Параметры)
	
	СвойстваЗаменяемые = Новый Соответствие;
	
	СвойстваПредопределенные = _ПолучитьСвойстваПредопределенные();
	
	Для Каждого СвойствоПредопределенное Из СвойстваПредопределенные Цикл
		
		Свойство = СвойствоПредопределенное.Ключ;
		ПараметрыСвойства = СвойствоПредопределенное.Значение;
		
		Если ЗначениеЗаполнено(ЭтотОбъект[ПараметрыСвойства.Реквизит]) Тогда
			СвойстваЗаменяемые.Вставить(ЭтотОбъект[ПараметрыСвойства.Реквизит], Новый Структура("Выгружать, Свойство", ПараметрыСвойства.Выгружать, Свойство));
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат СвойстваЗаменяемые;
	
КонецФункции

Функция _ПолучитьСвойстваСайта(Параметры)
	
	СвойстваСайта = Новый Соответствие;
	
	Для каждого Свойство Из Свойства.НайтиСтроки(Новый Структура("Выгружать", Истина)) Цикл
		
		Если Не ЗначениеЗаполнено(Свойство.СвойствоСайт) Тогда
			Продолжить;
		КонецЕсли;
		
		СвойстваСайта.Вставить(Свойство.Свойство, Свойство.СвойствоСайт);
		
	КонецЦикла;
	
	Возврат СвойстваСайта;
	
КонецФункции


// ЯДРО: ИНТЕГРАЦИЯ В КОНФИГУРАЦИЮ

Функция СведенияОВнешнейОбработке() Экспорт
	
	ИнформацияОКонтексте = _ПолучитьИнформациюОКонтекстеВыполнения();
	
	РегистрационныеДанные = Новый Структура;
	РегистрационныеДанные.Вставить("Наименование", "Обмен с личным кабинетом");	
	РегистрационныеДанные.Вставить("Информация", "Обмен с личным кабинетом");
	РегистрационныеДанные.Вставить("Версия", ИнформацияОКонтексте.ВерсияДрайвера);
	РегистрационныеДанные.Вставить("Вид", "ДополнительнаяОбработка");	
	РегистрационныеДанные.Вставить("БезопасныйРежим", Ложь);
	
	тзКоманд = Новый ТаблицаЗначений;
	тзКоманд.Колонки.Добавить("Идентификатор");
	тзКоманд.Колонки.Добавить("Представление");
	тзКоманд.Колонки.Добавить("Модификатор");
	тзКоманд.Колонки.Добавить("ПоказыватьОповещение");
	тзКоманд.Колонки.Добавить("Использование");
	
	строкаКоманды = тзКоманд.Добавить();
	строкаКоманды.Идентификатор = "ОткрытьФорму";
	строкаКоманды.Представление = "Обмен с личным кабинетом";
	строкаКоманды.ПоказыватьОповещение = Истина;
	строкаКоманды.Использование = "ОткрытиеФормы";
	
	строкаКоманды = тзКоманд.Добавить();
	строкаКоманды.Идентификатор = "ЗагрузкаДанных";
	строкаКоманды.Представление = "Обмен с личным кабинетом: загрузка данных";
	строкаКоманды.ПоказыватьОповещение = Истина;
	строкаКоманды.Использование = "ВызовСерверногоМетода";
	
	строкаКоманды = тзКоманд.Добавить();
	строкаКоманды.Идентификатор = "ВыгрузкаДанных";
	строкаКоманды.Представление = "Обмен с личным кабинетом: выгрузка данных";
	строкаКоманды.ПоказыватьОповещение = Истина;
	строкаКоманды.Использование = "ВызовСерверногоМетода";
	
	РегистрационныеДанные.Вставить("Команды", тзКоманд);
	
	Возврат РегистрационныеДанные;
	
КонецФункции

Процедура ВыполнитьКоманду(ИдентификаторКоманды) Экспорт
	
	Если ИдентификаторКоманды = "ВыгрузкаДанных" Тогда
		_ВыполнитьВыгрузку(ИдентификаторКоманды);
	ИначеЕсли ИдентификаторКоманды = "ЗагрузкаДанных" Тогда
		_ВыполнитьЗагрузку(ИдентификаторКоманды);	
	Конецесли;		
	
КонецПроцедуры

Функция _ВыполнитьКоманду(КлючЗапуска) Экспорт
	
	ДоступныеКлючиЗапуска = Новый Массив;
	ДоступныеКлючиЗапуска.Добавить("ОбменСЛичнымКабинетом");
	ДоступныеКлючиЗапуска.Добавить("ОбменСЛичнымКабинетом_Выгрузка");
	ДоступныеКлючиЗапуска.Добавить("ОбменСЛичнымКабинетом_Загрузка");
	
	Если ДоступныеКлючиЗапуска.Найти(КлючЗапуска) = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
		
	Если КлючЗапуска = "ОбменСЛичнымКабинетом" Тогда
		ВыполнитьКоманду("ВыгрузкаДанных");
		ВыполнитьКоманду("ЗагрузкаДанных");		
	ИначеЕсли КлючЗапуска = "ОбменСЛичнымКабинетом_Выгрузка" Тогда
		ВыполнитьКоманду("ВыгрузкаДанных");
	ИначеЕсли КлючЗапуска = "ОбменСЛичнымКабинетом_Загрузка" Тогда
		ВыполнитьКоманду("ЗагрузкаДанных");	
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

Процедура _ВыполнитьВыгрузку(ИдентификаторКоманды)
	
	Параметры = _ЗаполнитьПараметрыДляОбмена(ИдентификаторКоманды);
	
	_ВыгрузитьТовары(Параметры);
	
КонецПроцедуры

Процедура _ВыполнитьЗагрузку(ИдентификаторКоманды)		
	
	Параметры = _ЗаполнитьПараметрыДляОбмена(ИдентификаторКоманды);
	
	_ЗагрузитьЗаказы(Параметры);		
	
КонецПроцедуры

Функция _ЗаполнитьПараметрыДляОбмена(ИдентификаторКоманды)
	
	Результат = Новый Структура;
	
	//определение параметров
	КонстантыОбмена = "";
	АдресЛога = "";
	КомпоновщикТоваров = Новый КомпоновщикНастроекКомпоновкиДанных;
	
	//заполнение параметров
	Параметры = Новый Структура();
	Параметры.Вставить("ИдентификаторКоманды", ИдентификаторКоманды);
	
	_ИнициализироватьКонстантыОбмена(КонстантыОбмена, Параметры);
	
	_ИнициализироватьРеквизитыОбъекта(КонстантыОбмена);	
	
	ТекстНастроек = Неопределено;
	ЗначенияВнутреннихНастроек = _ПолучитьЗначенияВнутреннихНастроек();
	ЗначенияВнутреннихНастроек.Свойство("ТекстНастроек", ТекстНастроек);
	
	ПараметрыКомпоновщика = Новый Структура("КомпоновщикТоваров");
	ПараметрыКомпоновщика.Вставить("КомпоновщикТоваров", КомпоновщикТоваров);
	
	Настройки = _ДесериализоватьНастройкиXML(ТекстНастроек, ПараметрыКомпоновщика);	
	
	//подготавливаем структуру для возврата
	Результат.Вставить("КонстантыОбмена", КонстантыОбмена);
	Результат.Вставить("АдресЛога", АдресЛога);
	Результат.Вставить("КомпоновщикТоваров", КомпоновщикТоваров);
	Если Настройки.Свойство("РеквизитыШапкиЗаказа") Тогда
		Результат.Вставить("ЗначенияРеквизитовШапкиЗаказаПоУмолчанию", Настройки.РеквизитыШапкиЗаказа);
	КонецЕсли;
	Если Настройки.Свойство("РеквизитыТЧЗаказа") Тогда
		Результат.Вставить("ЗначенияРеквизитовТЧЗаказаПоУмолчанию", Настройки.РеквизитыТЧЗаказа);
	КонецЕсли;	
	
	Возврат Результат;
	
КонецФункции


//ЯДРО: ХРАНИЛИЩЕ НАСТРОЕК

Функция _ПолучитьЗначенияВнутреннихНастроек() Экспорт
	
	ПараметрыНастроек = _ПолучитьСтруктуруПараметровДляХранилищаНастроек();

	_ХранилищеЗагрузитьНастройки(ПараметрыНастроек);
	
	Возврат ПараметрыНастроек.Настройки;		
	
КонецФункции

Процедура _ЗаписатьЗначенияВнутреннихНастроек(СписокНастроек) Экспорт
	
	ПараметрыНастроек = _ПолучитьСтруктуруПараметровДляХранилищаНастроек();
	
	//читаем настройки
	_ХранилищеЗагрузитьНастройки(ПараметрыНастроек);
	
	ВнутренниеНастройки = ПараметрыНастроек.Настройки;
	
	//перезаписываем значения
	Для Каждого Настройка Из СписокНастроек Цикл
		Если ВнутренниеНастройки.Свойство(Настройка.Ключ) Тогда
			ВнутренниеНастройки[Настройка.Ключ] = Настройка.Значение;
		КонецЕсли;		
	КонецЦикла;
		
	//сохраняем настройки
	_ХранилищеСохранитьНастройки(ПараметрыНастроек);	
	
КонецПроцедуры

Процедура _ОчиститьЗначенияВнутреннихНастроек() Экспорт
	
	ПараметрыНастроек = _ПолучитьСтруктуруПараметровДляХранилищаНастроек();
	
	_ХранилищеУдалитьНастройки(ПараметрыНастроек);
	
КонецПроцедуры

Процедура _ХранилищеЗагрузитьНастройки(Параметры)
	
	КлючОбъекта = Параметры.КлючОбъекта;
	КлючНастроек = Параметры.КлючНастроек;
	ИмяПользователя	= Параметры.ИмяПользователя;
	ОписаниеНастроек = Параметры.ОписаниеНастроек;
	
	УстановитьПривилегированныйРежим(Истина);
	ВнутренниеНастройки = ХранилищеОбщихНастроек.Загрузить(КлючОбъекта, КлючНастроек, ОписаниеНастроек, ИмяПользователя);	
	УстановитьПривилегированныйРежим(Ложь);
	
	Если ВнутренниеНастройки <> Неопределено Тогда
		Параметры.Настройки = ВнутренниеНастройки;
	КонецЕсли;
	
КонецПроцедуры

Процедура _ХранилищеСохранитьНастройки(Параметры)
	
	КлючОбъекта = Параметры.КлючОбъекта;
	КлючНастроек = Параметры.КлючНастроек;	
	Настройки = Параметры.Настройки;
	ОписаниеНастроек = Параметры.ОписаниеНастроек;
	ИмяПользователя	= Параметры.ИмяПользователя;
	
	УстановитьПривилегированныйРежим(Истина);
	ХранилищеОбщихНастроек.Сохранить(КлючОбъекта, КлючНастроек, Настройки, ОписаниеНастроек, ИмяПользователя);
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

Процедура _ХранилищеУдалитьНастройки(Параметры)
	
	КлючОбъекта = Параметры.КлючОбъекта;
	КлючНастроек = Параметры.КлючНастроек;
	ИмяПользователя = Параметры.ИмяПользователя;
	
	УстановитьПривилегированныйРежим(Истина);
	ХранилищеОбщихНастроек.Удалить(КлючОбъекта, КлючНастроек, ИмяПользователя);
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

Функция _ПолучитьСтруктуруПараметровДляХранилищаНастроек()	
	
	Возврат Новый Структура("КлючОбъекта, КлючНастроек, Настройки, ОписаниеНастроек, ИмяПользователя", 
							"ОбменСЛичнымКабинетом", "ОбменСЛичнымКабинетом", _ПолучитьСписокВнутреннихНастроек(), "ОбменСЛичнымКабинетом", "ОбменСЛичнымКабинетом");
							
КонецФункции
						
Функция _ПолучитьСписокВнутреннихНастроек()
	
	Результат = Новый Структура;
	
	Результат.Вставить("ТекстНастроек", "");
	Результат.Вставить("ДатаПоследнейУдачнойЗагрузкиЗаказов", Дата("00010101000000"));
	
	Возврат Результат;
	
КонецФункции


//ЯДРО: КЭШИРОВАНИЕ ДАННЫХ

Функция _ЗаполнитьКэшДанными(Кэш, ПорядокЗапросов, РезультатыЗапросов)
	
	Для Каждого ОчереднойЗапрос Из ПорядокЗапросов Цикл
		Выборка = РезультатыЗапросов[ОчереднойЗапрос.Ключ].Выбрать();
		Пока Выборка.Следующий() Цикл
			_УстановитьЗначениеВКэш(Кэш, ОчереднойЗапрос.Значение, Выборка.Ключ, Выборка.Значение);
		КонецЦикла;
	КонецЦикла;
	
КонецФункции

Функция _ПолучитьЗначениеИзКэша(Кэш, Имя, Ключ)
	
	Если ТипЗнч(Ключ) = Тип("Строка") Тогда
		Возврат Кэш[Имя].Получить(СокрЛП(Ключ));
	Иначе
		Возврат Кэш[Имя].Получить(Ключ);
	КонецЕсли;
	
КонецФункции

Процедура _УстановитьЗначениеВКэш(Кэш, Имя, Ключ, Значение)
	
	Если ТипЗнч(Ключ) = Тип("Строка") Тогда
		Кэш[Имя].Вставить(СокрЛП(Ключ), Значение);
	Иначе
		Кэш[Имя].Вставить(Ключ, Значение);	
	КонецЕсли;
	
КонецПроцедуры

Процедура _ДобавитьЗначениеВКэш(Кэш, Имя, Значение)
	
	Кэш[Имя].Добавить(Значение);
	
КонецПроцедуры

Функция _СгруппироватьЗначенияКэша(Кэш)
	
	Результат = Новый Массив;
	
	Для Каждого ВидКэша Из Кэш Цикл
		Для Каждого КлючКэша Из ВидКэша.Значение Цикл
			Если Результат.Найти(КлючКэша.Значение) = Неопределено Тогда 
				Результат.Добавить(КлючКэша.Значение);
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции


// ИНИЦИАЛИЗАЦИЯ ОБЪЕКТА

_ПроверитьДрайверПередЗапуском();