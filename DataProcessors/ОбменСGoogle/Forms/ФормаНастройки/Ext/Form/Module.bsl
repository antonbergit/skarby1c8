
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПрочитатьЗначенияНастроек();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ВключитьЗагрузкуКонтактовПриИзменении(Элемент)
	
	ВключитьЗагрузкуКонтактовПриИзмененииНаСервере();
	
	Оповестить("ОчиститьСеансовыеДанные");
	
КонецПроцедуры

&НаКлиенте
Процедура ВключитьСинхронизациюКалендарейСGoogleПриИзменении(Элемент)
	
	ВключитьСинхронизациюКалендарейСGoogleПриИзмененииНаСервере();
	
	Оповестить("ОчиститьСеансовыеДанные");
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияПояснениеОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	Если НавигационнаяСсылкаФорматированнойСтроки <> "ИдентификацияПриложенияGoogle" Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	ОткрытьФорму("Обработка.ОбменСGoogle.Форма.ИдентификацияПриложенияGoogle");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗагрузитьКонтакты(Команда)
	
	НадписьКонтактыЗагружены = НСтр("ru='Выполняется загрузка контактов из Google...';uk='Виконується завантаження контактів з Google...'");
	
	ПодключитьОбработчикОжидания("ПродолжитьЗагрузкуКонтактов", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура СинхронизироватьКалендарь(Команда)
	
	НадписьКалендарьСинхронизирован = НСтр("ru='Выполняется синхронизация календаря с Google...';uk='Виконується синхронізація календаря з Google...'");
	
	ПодключитьОбработчикОжидания("ПродолжитьСинхронизациюКалендаря", 0.1, Истина);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПрочитатьЗначенияНастроек()
	
	Если ОбщегоНазначенияПовтИсп.ДоступноИспользованиеРазделенныхДанных() Тогда
		ОтключенныеОбластиДоступа = РегистрыСведений.СеансовыеДанныеGoogle.ОтключенныеОбластиДоступа(Пользователи.ТекущийПользователь());
		ВключитьЗагрузкуКонтактов = ОтключенныеОбластиДоступа.Найти(Перечисления.ОбластиДоступаGoogle.Контакты) = Неопределено;
		Если ВключитьЗагрузкуКонтактов
			И РегистрыСведений.СчетчикиПодсказок.ПревышеноЗначение(Перечисления.ВидыПодсказок.Google, 5,,, Ложь) Тогда
			ВключитьЗагрузкуКонтактов = Ложь;
		КонецЕсли;
		ВключитьСинхронизациюКалендаря = ОтключенныеОбластиДоступа.Найти(Перечисления.ОбластиДоступаGoogle.Календарь) = Неопределено;
	КонецЕсли;
	
	НастроитьФорму()
	
КонецПроцедуры

&НаСервере
Процедура ВключитьЗагрузкуКонтактовПриИзмененииНаСервере()
	
	РегистрыСведений.СчетчикиПодсказок.Сбросить(Перечисления.ВидыПодсказок.Google);
	
	РегистрыСведений.СеансовыеДанныеGoogle.ОтключитьОбластьДоступа(
	Пользователи.ТекущийПользователь(),
	Перечисления.ОбластиДоступаGoogle.Контакты,
	Не ВключитьЗагрузкуКонтактов);
	
	НастроитьФорму();
	
КонецПроцедуры

&НаСервере
Процедура ВключитьСинхронизациюКалендарейСGoogleПриИзмененииНаСервере()
	
	РегистрыСведений.СеансовыеДанныеGoogle.ОтключитьОбластьДоступа(
	Пользователи.ТекущийПользователь(),
	Перечисления.ОбластиДоступаGoogle.Календарь,
	Не ВключитьСинхронизациюКалендаря);
	
	НастроитьФорму();
	
КонецПроцедуры

&НаСервере
Процедура НастроитьФорму()
	
	Если ОбщегоНазначенияПовтИсп.ДоступноИспользованиеРазделенныхДанных() Тогда
		Элементы.Контакты.Видимость = Истина;
		Элементы.Календарь.Видимость = Истина;
		Элементы.ЗагрузитьКонтакты.Доступность = ВключитьЗагрузкуКонтактов;
		Элементы.СинхронизироватьКалендарь.Доступность = ВключитьСинхронизациюКалендаря;
	Иначе
		Элементы.Контакты.Видимость = Ложь;
		Элементы.Календарь.Видимость = Ложь;
	КонецЕсли;
	
	Если Пользователи.ЭтоПолноправныйПользователь(, Истина)
		И Не ОбщегоНазначения.ИспользованиеРазделителяСеанса() Тогда
		Элементы.НастройкиИдентификации.Видимость = Истина;
	Иначе
		Элементы.НастройкиИдентификации.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПродолжитьЗагрузкуКонтактов()
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить(
	"ОбластьДоступа",
	ПредопределенноеЗначение("Перечисление.ОбластиДоступаGoogle.Контакты"));
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
	"ОбработатьЗакрытиеФормыОбменаСGoogle",
	ЭтотОбъект,
	ПараметрыФормы);
	
	ОткрытьФорму(
	"Обработка.ОбменСGoogle.Форма.ВыполнитьОбменСGoogle",
	ПараметрыФормы,,,,,
	ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте 
Процедура ПродолжитьСинхронизациюКалендаря()
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить(
	"ОбластьДоступа",
	ПредопределенноеЗначение("Перечисление.ОбластиДоступаGoogle.Календарь"));
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
	"ОбработатьЗакрытиеФормыОбменаСGoogle",
	ЭтотОбъект,
	ПараметрыФормы);
	
	ОткрытьФорму(
	"Обработка.ОбменСGoogle.Форма.ВыполнитьОбменСGoogle",
	ПараметрыФормы,,,,,
	ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьЗакрытиеФормыОбменаСGoogle(Результат, Параметры) Экспорт
	
	Если Не Параметры.Свойство("ОбластьДоступа") Тогда
		Возврат;
	КонецЕсли;
	
	Если Параметры.ОбластьДоступа = ПредопределенноеЗначение("Перечисление.ОбластиДоступаGoogle.Календарь") Тогда
		НадписьКалендарьСинхронизирован = НСтр("ru='Календарь синхронизирован';uk='Календар синхронізований'");
	КонецЕсли;
	
	Если Параметры.ОбластьДоступа = ПредопределенноеЗначение("Перечисление.ОбластиДоступаGoogle.Контакты") Тогда
		НадписьКонтактыЗагружены = НСтр("ru='Контакты загружены';uk='Контакти завантажені'");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти