#Область ОбработчикиСобытий

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ИдентификацияПриложенияGoogle = Константы.ИдентификацияПриложенияGoogle.Получить();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	СохранитьДанныеИдентификацииПриложенияGoogleНаСервере();
	
	Если ИдентификацияКорректна Тогда
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура СохранитьДанныеИдентификацииПриложенияGoogleНаСервере()
	
	ИдентификацияКорректна = ИдентификацияКорректна();
	
	Если ИдентификацияКорректна Тогда
		Константы.ИдентификацияПриложенияGoogle.Установить(ИдентификацияПриложенияGoogle);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ИдентификацияКорректна()
	
	Если Не ЗначениеЗаполнено(ИдентификацияПриложенияGoogle) Тогда
		Возврат Истина;
	КонецЕсли;
	
	ЧтениеJSON = Новый ЧтениеJSON;
	ЧтениеJSON.УстановитьСтроку(ИдентификацияПриложенияGoogle);
	Попытка 
		РезультатЧтенияJSON = ПрочитатьJSON(ЧтениеJSON);
	Исключение
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
		НСтр("ru='Требуется указать данные в формате JSON';uk='Потрібно вказати дані у форматі JSON'"),,
		"ИдентификацияПриложенияGoogle");
		Возврат Ложь;
	КонецПопытки;
	
	Для Каждого ВидИдентификации Из ОбменСGoogleКлиентСервер.ВидыИдентификацииПриложения() Цикл
		Если РезультатЧтенияJSON.Свойство(ВидИдентификации) Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЦикла;
	
	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
	СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
	НСтр("ru='Некорректный формат идентификации клиента Google: ожидается один из ключей: %1';uk='Некоректний формат ідентифікації клієнта Google: очікується один із ключів: %1'"),
	СтрСоединить(ОбменСGoogleКлиентСервер.ВидыИдентификацииПриложения(), ", ")),,
	"ИдентификацияПриложенияGoogle");
	
	Возврат Ложь;
	
КонецФункции

#КонецОбласти