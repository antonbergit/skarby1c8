
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ТипДокумента = Параметры.ТипДокумента;
	
	Для Каждого СтрокаРеквизитов Из Метаданные.Документы.Найти(ТипДокумента).Реквизиты Цикл
		
		Если Найти(НРег(СтрокаРеквизитов.Имя), "удалить") <> 0 Тогда
			Продолжить;	
		КонецЕсли;
		
		НоваяСтрокаРеквизитов = ТаблицаРеквизитовДокумента.Добавить();
		НоваяСтрокаРеквизитов.ИмяРеквизита = СтрокаРеквизитов.Имя;
		НоваяСтрокаРеквизитов.ИмяРеквизитаПредставление = СтрокаРеквизитов.Синоним;
		НоваяСтрокаРеквизитов.ТипДанных = Новый ОписаниеТипов(СтрокаРеквизитов.Тип.Типы());
		НоваяСтрокаРеквизитов.Значение = НоваяСтрокаРеквизитов.ТипДанных.ПривестиЗначение();
		
	КонецЦикла;
	
	Для Каждого СтрокаТабличнойЧасти Из Метаданные.Документы.Найти(ТипДокумента).ТабличныеЧасти Цикл
		
		Если Найти(НРег(СтрокаТабличнойЧасти.Имя), "удалить") <> 0 Тогда
			Продолжить;	
		КонецЕсли;
		
		Для Каждого РеквизитТЧ Из СтрокаТабличнойЧасти.Реквизиты Цикл
			
			Если Найти(НРег(РеквизитТЧ.Имя), "удалить") <> 0 Тогда
				Продолжить;	
			КонецЕсли;
			
			НоваяСтрокаРеквизитов = ТаблицаРеквизитовДокумента.Добавить();
			НоваяСтрокаРеквизитов.ИмяРеквизита = СтрокаТабличнойЧасти.Имя + "." + РеквизитТЧ.Имя;
			НоваяСтрокаРеквизитов.ИмяРеквизитаПредставление = СтрокаТабличнойЧасти.Синоним + "." + РеквизитТЧ.Синоним;
			
			НоваяСтрокаРеквизитов.ТипДанных = Новый ОписаниеТипов(РеквизитТЧ.Тип.Типы());
			НоваяСтрокаРеквизитов.Значение = НоваяСтрокаРеквизитов.ТипДанных.ПривестиЗначение();
			
		КонецЦикла;	
		
	КонецЦикла;
	
	АдресНастроек = Параметры.АдресНастроекЗаполняемыхДанных;
	
	Если ЗначениеЗаполнено(АдресНастроек) Тогда
		Попытка
			ТаблицаСохраненныхДанных = ВостановитьИзХранилищаНастроек("МодульОбменаПрива24-1С", АдресНастроек);
			
			Для Каждого СтрокаТаблицы Из ТаблицаСохраненныхДанных Цикл
				ПараметрыОтбора = Новый Структура;
				ПараметрыОтбора.Вставить("ИмяРеквизита", СтрокаТаблицы.ИмяРеквизита);
				НайденныеСтроки = ТаблицаРеквизитовДокумента.НайтиСтроки(ПараметрыОтбора);
				Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл
					НайденнаяСтрока.Значение = СтрокаТаблицы.Значение;
					НайденнаяСтрока.Пометка = Истина;
				КонецЦикла;
				
			КонецЦикла;
		Исключение
		КонецПопытки;	
	Иначе
		АдресНастроек = Строка(Новый УникальныйИдентификатор);
	КонецЕсли;

//	//вариант условия отбора для выбора типов значений субконто//уже реализовано
//	АдресУсловия = Параметры.АдресУсловия;
//	Если ЗначениеЗаполнено(АдресУсловия) Тогда
//		Попытка
//			ТаблицаСохраненныхУсловий = ВостановитьИзХранилищаНастроек("МодульОбменаПрива24-1С", АдресУсловия);
//			Для Каждого СтрокаТаблицы Из ТаблицаСохраненныхУсловий.Отбор.Элементы Цикл
//				Если Найти(СтрокаТаблицы.ИмяРеквизита, "Субконто") > 0 Тогда
//					Если СтрокаТаблицы.Использование Тогда
//						ЛевоеЗначение = Строка(СтрокаТаблицы.ЛевоеЗначение);
//						ВидСравнения = Строка(СтрокаТаблицы.ВидСравнения);
//						ПравоеЗначение = Строка(СтрокаТаблицы.ПравоеЗначение)
//					КонецЕсли
//				КонецЕсли
//			КонецЦикла;
//		Исключение
//		КонецПопытки;	
//	Иначе
//		АдресУсловия = Строка(Новый УникальныйИдентификатор);
//	КонецЕсли;

	Заголовок = СтрЗаменить( НСтр("ru='Настройка значений реквизитов для документа: ""%1""';uk='Налаштування значень реквізитів для документа: ""%1""'"), "%1", Параметры.ПредставлениеДокумента);

КонецПроцедуры

&НаКлиенте
Процедура ТаблицаРеквизитовДокументаЗначениеПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.ТаблицаРеквизитовДокумента.ТекущиеДанные;
	ТекущаяСтрока.Пометка = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура Записать(Команда)
	
	ПредставлениеЗаполняемыхДанных = ПоместитьВХранилищеНастроек("МодульОбменаПрива24-1С", АдресНастроек);
	
	СтруктураВозврата = Новый Структура;
	СтруктураВозврата.Вставить("АдресНастроекЗаполняемыхДанных", АдресНастроек);
	СтруктураВозврата.Вставить("ПредставлениеЗаполняемыхДанных", ПредставлениеЗаполняемыхДанных); 	
	ОповеститьОВыборе(СтруктураВозврата);

КонецПроцедуры

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ПоместитьВХранилищеНастроек(ИмяОбъекта, ИмяНастройки)
	
	ТаблицаСохраненныхДанных = Новый ТаблицаЗначений;
	ТаблицаСохраненныхДанных.Колонки.Добавить("ИмяРеквизита");
	ТаблицаСохраненныхДанных.Колонки.Добавить("Значение");
	
	ПредставлениеЗаполняемыхДанных = "";
	Для Каждого СтрокаТаблицы Из ТаблицаРеквизитовДокумента Цикл
		Если СтрокаТаблицы.Пометка Тогда
			НоваяСтрока = ТаблицаСохраненныхДанных.Добавить();
			НоваяСтрока.ИмяРеквизита = СтрокаТаблицы.ИмяРеквизита;
			НоваяСтрока.Значение = СтрокаТаблицы.Значение;
			ПредставлениеЗаполняемыхДанных = ПредставлениеЗаполняемыхДанных + СтрокаТаблицы.ИмяРеквизитаПредставление + ", ";  
		КонецЕсли;
	КонецЦикла;	
	
	ХранилищеНастроекДанныхФорм.Сохранить(ИмяОбъекта, ИмяНастройки, ТаблицаСохраненныхДанных);
	
	Возврат Лев(ПредставлениеЗаполняемыхДанных, СтрДлина(ПредставлениеЗаполняемыхДанных) - 2);
	
КонецФункции

&НаСервере
Функция ВостановитьИзХранилищаНастроек(ИмяОбъекта, ИмяНастройки)
	
	Возврат ХранилищеНастроекДанныхФорм.Загрузить(ИмяОбъекта, ИмяНастройки);
	
КонецФункции

&НаКлиенте
Процедура ТаблицаРеквизитовДокументаПриАктивизацииСтроки(Элемент)
	
	Если Элемент.ТекущийЭлемент <> Неопределено И Элемент.ТекущийЭлемент.Имя = "ТаблицаРеквизитовДокументаЗначение" Тогда
		Элемент.ТекущийЭлемент.ОграничениеТипа = Элемент.ТекущиеДанные.ТипДанных;
		
		Элемент.ТекущийЭлемент.ВыбиратьТип = Элемент.ТекущиеДанные.ТипДанных.Типы().Количество() > 1 
	КонецЕсли;	

КонецПроцедуры

#КонецОбласти
