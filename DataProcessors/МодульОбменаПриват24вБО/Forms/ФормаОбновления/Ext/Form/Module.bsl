&НаСервере
Функция УстановитьНаСервере()
	
	ОбработкаОбьект = РеквизитФормыВЗначение("Объект");
	ИспользуемоеИмяФайла = ОбработкаОбьект.ИспользуемоеИмяФайла;
	ИнтернетАдресДляСоединения = ОбработкаОбьект.ИнтернетАдресДляСоединения;
	ЭтоВебКлиент = ОбработкаОбьект.ЭтоВебКлиент;
	ЭтоWindows = ОбработкаОбьект.ЭтоWindows;
	БазовоеИмяФайла = ОбработкаОбьект.БазовоеИмяФайла;
	РасширениеФайла = ОбработкаОбьект.РасширениеФайла;
	ТекВерсия = ОбработкаОбьект.ТекВерсия;
	Путьmop24versions = Путьmop24versions;
	Возврат ОбработкаОбьект.ОбновитьОбработку()
	
КонецФункции

&НаКлиенте
Процедура Установить(Команда)

	Если Объект.ЭтоОткат Тогда
		Если ЗначениеЗаполнено(ФайлОтката) Тогда
			КопироватьФайл(ФайлОтката, ЭтотОбъект.ИспользуемоеИмяФайла);
			ОткатУспешен = Истина;
			Закрыть()
		КонецЕсли
	Иначе
		Если УстановитьНаСервере() Тогда
			ОбновлениеУспешно = Истина;
			Закрыть()
		КонецЕсли
	КонецЕсли
	
КонецПроцедуры

&НаКлиенте
Процедура ТекстПодробнееНажатие(Элемент)

	Элементы.ГруппаКартиникиИнструкции.Видимость = НЕ Элементы.ГруппаКартиникиИнструкции.Видимость;

КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)

	Если Не ЗавершениеРаботы = Истина Тогда
		Если ОбновлениеУспешно Или ОткатУспешен Тогда
			ОткрытьОбработку();
		КонецЕсли
	КонецЕсли

КонецПроцедуры

&НаКлиенте
Процедура ОткрытьОбработку()
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПослеПомещенияФайла", ЭтаФорма);
	НачатьПомещениеФайла(ОписаниеОповещения, , ИспользуемоеИмяФайла, Ложь, ЭтаФорма.УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеПомещенияФайла(Результат, АдресИлиРезультатВыбора, ВыбранноеИмяФайла, ДополнительныеПараметры) Экспорт

	//обновление или откат

	Если НЕ Результат Тогда
		Возврат;
	КонецЕсли;

	//запись тек.версии как успешной в файл Путьmop24versions\versions.txt и копирование в файл в ту же директорию с именем тек.версии+номер версии
	КаталогПутьmop24versions = Новый Файл(Путьmop24versions);
	Если Не КаталогПутьmop24versions.Существует() Тогда
		Попытка
			СоздатьКаталог(Путьmop24versions)
		Исключение
			ТекстСообщения = НСтр("ru='Каталог для файлов отката не создан. Откат при необходимости будет невозможен.';uk='Каталог до файлів відкату не створений. Відкат при необхідності буде неможливий.'")
				+ ОписаниеОшибки();
			СообщениеПользователю(ТекстСообщения)
		КонецПопытки
	КонецЕсли;
	Если КаталогПутьmop24versions.Существует() Тогда
		ТекстДокументversions = Новый ТекстовыйДокумент;
		Файлversions = Новый Файл(Путьmop24versions + "versions.txt");
		Если Файлversions.Существует() Тогда
			ТекстДокументversions.Прочитать(Файлversions.ПолноеИмя)
		КонецЕсли;
		АрхивноеИмяФайла = Объект.БазовоеИмяФайла + УдалитьСимволl(Объект.ТекВерсия) + Объект.РасширениеФайла;
		//запись от старшей (последней) версии в начале файла к младшей (более старой) в конце файла
		ТекстДокументversions.ВставитьСтроку(0, АрхивноеИмяФайла);
		ТекстДокументversions.Записать(Путьmop24versions + "versions.txt");
		КопироватьФайл(ЭтотОбъект.ИспользуемоеИмяФайла, Путьmop24versions + АрхивноеИмяФайла)
	КонецЕсли;

	//актуальная версия или версия отката скачана
	ИмяОбработки = ПодключитьВнешнююОбработку(Новый Структура("Адрес", АдресИлиРезультатВыбора));

	ПутьИИмяОбработки = ИспользуемоеИмяФайла;
	//удаление символа "l"
	ПутьИИмяОбработки = УдалитьСимволl(ПутьИИмяОбработки);
	Если Объект.ЭтоОткат Тогда
		ПутьИИмяСкачаннойОбработки = ФайлОтката
	Иначе
		ПутьИИмяСкачаннойОбработки = ПутьИИмяОбработки
	КонецЕсли;

	//получаем номер версии обновления на случай наличия в файле номера версии или версию отката
	ПозицияБазовоеИмяФайла = Найти(ПутьИИмяСкачаннойОбработки, Объект.БазовоеИмяФайла);
	ПозицияРасширениеФайла = Найти(ПутьИИмяСкачаннойОбработки, Объект.РасширениеФайла);
	ПозицияНачалаВерсии = ПозицияБазовоеИмяФайла + СтрДлина(Объект.БазовоеИмяФайла);
	ВерсияОбновленияОтката = Сред(ПутьИИмяСкачаннойОбработки, ПозицияНачалаВерсии, ПозицияРасширениеФайла - ПозицияНачалаВерсии);
	//если в имени отсутствует номер версии (обновление), берем ее из АктуальнаяВерсияОбработки
	Если Не ЗначениеЗаполнено(ВерсияОбновленияОтката) Тогда
		ВерсияОбновленияОтката = АктуальнаяВерсияОбработки
	КонецЕсли;

	//копирование текущей обработки в файл с номером текущей версии в каталог текущей обработки для последующего копирования в mop24\versions
	// необходимо на случай, если обработка обновления/отката НЕуспешно откроется
	ПозицияРасширенияФайлаСТочкой = Найти(ПутьИИмяОбработки, Объект.РасширениеФайла);
	ПутьИИмяТекОбработкиСНомеромВерсии = Лев(ПутьИИмяОбработки, ПозицияРасширенияФайлаСТочкой - 1) + СокрЛП(УдалитьСимволl(Объект.ТекВерсия))
		+ Сред(ПутьИИмяОбработки, ПозицияРасширенияФайлаСТочкой);
	КопироватьФайл(ПутьИИмяОбработки, ПутьИИмяТекОбработкиСНомеромВерсии);
	//перемещение скачанной/откатной обработки на место используемой
	ПереместитьФайл(ПутьИИмяСкачаннойОбработки, ПутьИИмяОбработки);

	ПараметрыОбработки = Новый Структура("ОткрытиеПослеОбновления");
	Форма = ОткрытьФорму("ВнешняяОбработка." + ИмяОбработки + ".Форма.ФормаУправляемая", ПараметрыОбработки, ,Истина);
	Если Форма.Открыта() Тогда

		//запись об удачной прежней версии
		Файл = Новый Файл(ПутьИИмяТекОбработкиСНомеромВерсии);
		Если Файл.Существует() Тогда
			Если ПроверитьСоздатьКаталогmop24versions() Тогда
				//сохраняем файл в mop24\versions
				ПереместитьФайл(ПутьИИмяТекОбработкиСНомеромВерсии, Путьmop24versions + Файл.Имя);
				////записываем номер версии в versions.txt
				//Файлversions = Новый Файл(Путьmop24versions + "versions.txt");
				//ТекстДокументversions = Новый ТекстовыйДокумент;
				//Если Файлversions.Существует() Тогда
				//	ТекстДокументversions.Прочитать(Файлversions.ПолноеИмя)
				//КонецЕсли;
				//ТекстДокументversions.ВставитьСтроку(0, Файл.Имя);
				//ТекстДокументversions.Записать(Файлversions.ПолноеИмя)
			КонецЕсли
		Иначе
			ТекстСообщения = НСтр("ru='Ошибка записи файла об удачной прежней версии.';uk='Помилка запису файла о вдалій попередній версії.'");
			СообщениеПользователю(ТекстСообщения, Ложь)
		КонецЕсли

	Иначе

		//ошибка открытия скачанной версии, возвращаемся к прежней версии
		//копируем файл текущей обработки по єтому же пути, но с номером версии в файле
		ПереместитьФайл(ПутьИИмяТекОбработкиСНомеромВерсии, ПутьИИмяОбработки);
		//переоткрываем В ФОРМЕ восстановленный прежний файл, ибо он будет закрыт
		//НоваяОбработка = ВнешниеОбработки.Создать(ПутьИИмяОбработки);
		//ФормаОбновления = НоваяОбработка.ПолучитьФорму();
		//ФормаОбновления.Открыть();
		//запись о неудачной версии
		Файлbadversions = Новый Файл(Путьmop24versions + "badversions.txt");
		ТекстДокументbadversions = Новый ТекстовыйДокумент;
		Если Файлbadversions.Существует() Тогда
			ТекстДокументbadversions.Прочитать(Файлbadversions.ПолноеИмя)
		КонецЕсли;
		ТекстДокументbadversions.ВставитьСтроку(0, Объект.БазовоеИмяФайла + СокрЛП(ВерсияОбновленияОтката) + Объект.РасширениеФайла);
		ТекстДокументbadversions.Записать(Файлbadversions.ПолноеИмя);
		ТекстСообщения = НСтр("ru='Ошибка открытия обработки предыдущей версии ';uk='Помилка відктиття обробки попередньої версії '")
			+ ВерсияОбновленияОтката;
		Сообщить(ТекстСообщения)

	КонецЕсли

КонецПроцедуры

//удаление символа "l"
&НаКлиенте
Функция УдалитьСимволl(ПутьИИмяОбработки)

	ПозицияСимволаl = Найти(ПутьИИмяОбработки, "l");
	Если ПозицияСимволаl > 0 Тогда
		ПутьИИмяОбработки = СтрЗаменить(ПутьИИмяОбработки, "l", "")
	КонецЕсли;
	Возврат ПутьИИмяОбработки

КонецФункции

&НаКлиенте
Функция ПроверитьСоздатьКаталогmop24versions()

	КаталогПутьmop24versions = Новый Файл(Путьmop24versions);
	Если Не КаталогПутьmop24versions.Существует() Тогда
		СоздатьКаталог(Путьmop24versions)
	КонецЕсли;
	Если Не КаталогПутьmop24versions.Существует() Тогда
		ТекстСообщения = НСтр("ru='Не удается создать каталог ';uk='Не вдається створити каталог '") + Путьmop24versions + ОписаниеОшибки();
		СообщениеПользователю(ТекстСообщения);
		Возврат Ложь
	КонецЕсли;
	Возврат Истина

КонецФункции

&НаСервере
Функция ПодключитьВнешнююОбработку(АдресХранилища)

	Возврат ВнешниеОбработки.Подключить(АдресХранилища.Адрес, , Ложь);

КонецФункции

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Объект.ПутьКурл = Параметры.ПутьКурл;
	Объект.ПоддерживаетсяTLS12 = Параметры.ПоддерживаетсяTLS12;
	Объект.ИнтернетАдресДляСоединения = Параметры.ИнтернетАдресДляСоединения;
	Объект.ЭтоВебКлиент = Параметры.ЭтоВебКлиент;
	Объект.ЭтоWindows = Параметры.ЭтоWindows;
	Объект.БазовоеИмяФайла = Параметры.БазовоеИмяФайла;
	Объект.РасширениеФайла = Параметры.РасширениеФайла;
	Объект.ТекВерсия = Параметры.ТекВерсия;
	АктуальнаяВерсияОбработки = Параметры.АктуальнаяВерсияОбработки;
	Путьmop24versions = Параметры.Путьmop24versions;
	Попытка
		Объект.ЭтоОткат = Параметры.ЭтоОткат;
		ФайлОтката = Параметры.ФайлОтката
	Исключение
		Объект.ЭтоОткат = Ложь;
		ФайлОтката = Неопределено
	КонецПопытки;
	ОбработкаОбьект = РеквизитФормыВЗначение("Объект");
	ИспользуемоеИмяФайла = ОбработкаОбьект.ИспользуемоеИмяФайла

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)

	СкачатьОбновлениеВручнуюТекст = "https://acp.privatbank.ua/api/mop24/download/epf/8283managed";
 
	Если Объект.ЭтоОткат Тогда
		Элементы.ТекстДоступноОбновление.Заголовок = НСтр("ru='Откат до предыдущей версии ';uk='Відкат до попередньої версії '")
			+ АктуальнаяВерсияОбработки + Символы.ПС + НСтр("ru='Текущая версия модуля (обработки) ';uk='Поточна версія модуля (обробки) '")
			+ Объект.ТекВерсия + ".";
		Элементы.ТекстНажмитеУстановить.Заголовок = НСтр("ru='Нажмите кнопку `Установить` для автоматического отката до предыдущей версии.';uk='Натисніть кнопку `Встановити` для початку автоматичного відкату до попередньої версії'");
		Элементы.ТекстПроверкаСертификата.Видимость = Ложь;
		Элементы.тСкачатьОбновлениеВручную.Доступность = Ложь;
		Элементы.тСкачатьОбновлениеВручную.Видимость = Ложь;
		Элементы.СкачатьОбновлениеВручную.Доступность = Ложь;
		Элементы.СкачатьОбновлениеВручную.Видимость = Ложь;
		Элементы.ТекстПояснение.Заголовок = НСтр("ru='Предыдущая версия модуля обмена после отката будет сберегаться в каталоге: %AppData%\mop24\versions. Текущий модуль будет закрыт, файл будет заменен версией отката и будет открыта версия отката.';uk='Попередня версія модуля обміну після відкату буде зберігатися в каталозі: %AppData%\mop24\versions. Поточний модуль буде закритий, файл буде замінений версією відкату та буде відкрита версія відкату.'")
	Иначе
		//обновление
		Элементы.ТекстДоступноОбновление.Заголовок = Элементы.ТекстДоступноОбновление.Заголовок + НСтр("ru=' до версии ';uk=' до версії '")
			+ АктуальнаяВерсияОбработки + Символы.ПС + НСтр("ru='Текущая версия модуля (обработки) ';uk='Поточна версія модуля (обробки) '")
			+ Объект.ТекВерсия + ".";
		Элементы.СкачатьОбновлениеВручную.Заголовок = СкачатьОбновлениеВручнуюТекст;
		Элементы.ТекстПояснение.Заголовок = НСтр("ru='Предыдущая версия модуля обмена после обновления будет сберегаться в каталоге: %AppData%\mop24\versions. Текущий модуль будет закрыт, файл будет заменен версией обновления и будет открыта версия обновления.';uk='Попередня версія модуля обміну після оновлення буде зберігатися в каталозі: %AppData%\mop24\versions. Поточний модуль буде закритий, файл буде замінений версією оновлення та відкрита версія оновлення.'")
	КонецЕсли

КонецПроцедуры

&НаКлиенте
Процедура СкачатьОбновлениеВручнуюОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;
	ЗапуститьПриложение(Элементы.СкачатьОбновлениеВручную.Заголовок)//НавигационнаяСсылкаФорматированнойСтроки);

КонецПроцедуры

&НаСервере
Процедура СообщениеПользователю(ТекстСообщения, ОтправлятьСтатистику = Истина)
	
	ОбработкаОбьект = РеквизитФормыВЗначение("Объект");
	ОбработкаОбьект.СообщениеПользователю(ТекстСообщения, ОтправлятьСтатистику);
	ЗначениеВРеквизитФормы(ОбработкаОбьект, "Объект");

КонецПроцедуры
