
&НаСервере
Функция АдресНастроекКомпоновщикаОтбора()
	
	Возврат ПоместитьВоВременноеХранилище(КомпоновщикПредварительногоОтбора.Настройки, УникальныйИдентификатор)
	
КонецФункции

&НаСервереБезКонтекста
Функция ДоступныеИменаМетаРеквизитовОтбора(Знач МетаКоллекция)

	Результат = "";
	ТипХранилища = Тип("ХранилищеЗначения");
	
	Для Каждого МетаРеквизит Из МетаКоллекция Цикл
		ЭтоХранилище = МетаРеквизит.Тип.СодержитТип(ТипХранилища);
		Если Не ЭтоХранилище Тогда
			Результат = Результат + "," + МетаРеквизит.Имя;
		КонецЕсли
	КонецЦикла;
	
	Возврат Результат;

КонецФункции

&НаСервере
Процедура ИнициализироватьКомпоновщикОтбораИПравила()

	// 1. Очистка всего
	КомпоновщикПредварительногоОтбора = Новый КомпоновщикНастроекКомпоновкиДанных;
	Если ЭтоАдресВременногоХранилища(АдресСхемыКомпоновки) Тогда
		УдалитьИзВременногоХранилища(АдресСхемыКомпоновки);
		АдресСхемыКомпоновки = "";
	КонецЕсли;
	
	ПравилаПоиска.Очистить();
	
	Если ПустаяСтрока(ОбластьПоиска) Тогда
		Возврат;
	КонецЕсли;
	
	МетаОбласть = Метаданные.НайтиПоПолномуИмени("Документ." + ОбластьПоиска);
	
	// 2. Собираем компоновщик для поиска - отбора
	ДоступныеРеквизитыОтбора = ДоступныеИменаМетаРеквизитовОтбора(МетаОбласть.СтандартныеРеквизиты);
	ДоступныеРеквизитыОтбора = ?(ПустаяСтрока(ДоступныеРеквизитыОтбора), ",", ДоступныеРеквизитыОтбора)
		+ ДоступныеИменаМетаРеквизитовОтбора(МетаОбласть.Реквизиты);
		
	СхемаКомпоновки = Новый СхемаКомпоновкиДанных;
	ИсточникДанных = СхемаКомпоновки.ИсточникиДанных.Добавить();
	ИсточникДанных.ТипИсточникаДанных = "Local";
	
	НаборДанных = СхемаКомпоновки.НаборыДанных.Добавить(Тип("НаборДанныхЗапросСхемыКомпоновкиДанных"));
	НаборДанных.Запрос = "ВЫБРАТЬ " + Сред(ДоступныеРеквизитыОтбора, 2) + " ИЗ Документ." + ОбластьПоиска;
	НаборДанных.АвтоЗаполнениеДоступныхПолей = Истина;
	
	// Сохраним схему в разрезе основной формы, чтобы компоновщик не терял актуальность
	АдресСхемыКомпоновки = ПоместитьВоВременноеХранилище(СхемаКомпоновки, УникальныйИдентификатор);
	
	КомпоновщикПредварительногоОтбора.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(СхемаКомпоновки));
	
	Если ОбластиПоиска.НайтиПоЗначению("Документ." + ОбластьПоиска).Пометка Тогда
		// Есть прикладной функционал
		
		// Пустая структура параметров
		ПараметрыПоУмолчанию = ПрикладныеПараметрыПоУмолчанию(
			ПравилаПоиска.Выгрузить(,"Реквизит, Правило"),
			КомпоновщикПредварительногоОтбора
		);
		
	КонецЕсли;
	
КонецПроцедуры

// Параметры по умолчанию для передачи в прикладной код
// 
// Параметры:
//     ПравилаПоиска - ТаблицаЗначений - Предлагаемые правила поиска, содержит колонки
//         Реквизит - Строка - Имя реквизита области поиска
//         Правило  - Строка - Идентификатор правила сравнения, "Равно" или "Подобно"
//
// Возвращаемое значение:
//     Структура
//
&НаСервере
Функция ПрикладныеПараметрыПоУмолчанию(Знач ПравилаПоиска, Знач КомпоновщикОтбора) Экспорт

	ПараметрыПоУмолчанию = Новый Структура;
	ПараметрыПоУмолчанию.Вставить("ПравилаПоиска",        ПравилаПоиска);
	ПараметрыПоУмолчанию.Вставить("ОграниченияСравнения", Новый Массив);
	ПараметрыПоУмолчанию.Вставить("КомпоновщикОтбора",    КомпоновщикОтбора);
	ПараметрыПоУмолчанию.Вставить("КоличествоЭлементовДляСравнения", 1000);
	
	Возврат ПараметрыПоУмолчанию;
	
КонецФункции

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Объект.ТаблицаПравил.Загрузить(Параметры.ТаблицаПравил.Выгрузить());
	ИдКонф = Параметры.ИдКонФ;
	ИнициализироватьОсновныеПараметры();	
	
	ЗаполнитьСписокВыбораТиповДокументов(Элементы.ТаблицаПравилТипДокумента.СписокВыбора, ИдКонф);
	
КонецПроцедуры

&НаСервере
Процедура ИнициализироватьОсновныеПараметры()
	
	ОбъектОбработки = РеквизитФормыВЗначение("Объект");
	МетаОбъектОбработки = ОбъектОбработки.Метаданные();
	
	ЭтоВнешняяОбработка = Истина;//ОбъектОбработки.ОбработкаВнешняя;
	БазовоеИмяФормы     = МетаОбъектОбработки.ПолноеИмя() + ".Форма.";
	
	ОбластиПоиска(ОбластиПоиска);
	
КонецПроцедуры

&НаСервере
// Заполняет список всех возможных областей для поиска 
// 
// Параметры:
//     Список - СписокЗначений - Заполняемый список, в котором устанавливаются:
//               Значение      - Строка   - Полное имя метаданных объекта-таблицы
//               Представление - Строка   - Представление для пользователя
//               Картинка      - Картинка - Соответствующая картинка из платформенной библиотеки
//
//
Процедура ОбластиПоиска(Список) Экспорт
	
	Список.Очистить();
	
	ГруппаОбластейПоиска(Список,  Документы, "Документ");
	
КонецПроцедуры
	
&НаСервере
Процедура ГруппаОбластейПоиска(Результат, Знач МенеджерГруппы, Знач Пиктограмма)
	
	Для Каждого Элемент Из МенеджерГруппы Цикл
		Мета = Метаданные.НайтиПоТипу(ТипЗнч(Элемент));
		Если Не ПравоДоступа("Чтение", Мета) Тогда
			// Нет доступа, не отражаем в списке
			Продолжить;
		КонецЕсли;
		
		Результат.Добавить(Мета.ПолноеИмя(), Строка(Мета), Ложь, БиблиотекаКартинок[Пиктограмма]);
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаПравилПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	ТекущаяСтрока = Элементы.ТаблицаПравил.ТекущиеДанные;

	Если НоваяСтрока Тогда
		
		ТекущаяСтрока.ИмяНастройки = НСтр("ru='Новая настройка';uk='Нове налаштування'");
		
	КонецЕсли;
		
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокВыбораТиповДокументов(СписокВыбора, ИдКонф)
	
	Если ИдКонф = "UNF" ИЛИ ИдКонф = "BSB" Тогда//(УправлениеНебольшойФирмойДляУкраины УНФ УправлениеШвейнымПроизводствомДляУкраины)
		//BSB(BASSmallBusiness BASSmallBusinessFlyDoc)
		СписокВыбора.Добавить(НСтр("ru = 'Расход со счета'; uk='Витрата з рахунку'"));//РасходСоСчета
		СписокВыбора.Добавить(НСтр("ru = 'Поступление на счет'; uk='Надходження на рахунок'"));//ПоступлениеНаСчет
		СписокВыбора.Добавить(НСтр("ru = 'Перемещение денег'; uk='Переміщення грошей'"));//ПеремещениеДС
		СписокВыбора.Добавить(НСтр("ru = 'Оприходование запасов'; uk='Оприбуткування запасів'"));//ОприходованиеЗапасов
		СписокВыбора.Добавить(НСтр("ru = 'Счет на оплату (полученный)'; uk='Рахунок на оплату (отриманий)'"));//СчетНаОплатуПоставщика
		СписокВыбора.Добавить(НСтр("ru = 'Приходная накладная'; uk='Прибуткова накладна'"));//ПриходнаяНакладная
		СписокВыбора.Добавить(НСтр("ru = 'Заказ покупателя'; uk='Замовлення покупця'"));//ЗаказПокупателя
		
	ИначеЕсли ИдКонф = "BP" ИЛИ ИдКонф = "BASBBO" ИЛИ ИдКонф = "BASC" ИЛИ ИдКонф = "UPP" Тогда
		//(БухгалтерияДляУкраины БухгалтерияСельскохозяйственногоПредприятия БухгалтерияСтроительнойОрганизации
		//BASБух ОбщепитУкр1С КомплексноеРешениеСельскохозяйственногоПредприятия BASБухгалтерия
		//BASBBO BASАгроБухгалтерия BASPublicCatering)
		//BASC(BASБухгалтерияКОРП)
		СписокВыбора.Добавить(НСтр("ru = 'Списание с банковского счета'; uk='Списання з банківського рахунку'"));//СписаниеСРасчетногоСчета
		СписокВыбора.Добавить(НСтр("ru = 'Поступление на банковский счет'; uk='Надходження на банківський рахунок'"));//ПоступлениеНаРасчетныйСчет
		СписокВыбора.Добавить(НСтр("ru = 'Установка цен номенклатуры'; uk='Встановлення цін номенклатури'"));//УстановкаЦенНоменклатуры//!!!Нет в BASАгроБухгалтерия
		СписокВыбора.Добавить(НСтр("ru = 'Оприходование товаров'; uk='Оприбуткування товарів'"));//ОприходованиеТоваров
		СписокВыбора.Добавить(НСтр("ru = 'Счет на оплату поставщика'; uk='Рахунок на оплату постачальника'"));//СчетНаОплатуПоставщика
		СписокВыбора.Добавить(НСтр("ru = 'Поступление товаров и услуг'; uk='Надходження товарів і послуг'"));//ПоступлениеТоваровУслуг
		
	ИначеЕсли ИдКонф = "UT_3" ИЛИ ИдКонф = "ERP" Тогда
		//UT_3(УправлениеТорговлейДляУкраины BASУправлениеТорговлей)
		//ERP(ERPforUkraine BASERP BASКомплексноеУправлениеПредприятием BASIEM_ЗаполнитьДанныеРЛ)
		СписокВыбора.Добавить(НСтр("ru = 'Списание безналичных денежных средств'; uk='Списання безготівкових коштів'"));//СписаниеБезналичныхДенежныхСредств
		СписокВыбора.Добавить(НСтр("ru = 'Поступление безналичных денежных средств'; uk='Надходження безготівкових грошових коштів'"));//ПоступлениеБезналичныхДенежныхСредств
		СписокВыбора.Добавить(НСтр("ru = 'Выписка по банковскому счету'; uk='Виписка з банківського рахунку'"));//ВыпискаПоРасчетномуСчету//!!!Нет в BASКомплексноеУправлениеПредприятием и BASERP
		СписокВыбора.Добавить(НСтр("ru = 'Поступление товаров и услуг'; uk='Надходження товарів і послуг'")); //ПоступлениеТоваровУслуг
		СписокВыбора.Добавить(НСтр("ru = 'Заказ клиента'; uk='Замовлення клієнта'")); //ЗаказКлиента

	ИначеЕсли ИдКонф = "BASR" Тогда
		СписокВыбора.Добавить(НСтр("ru = 'Установка цен номенклатуры'; uk='Встановлення цін номенклатури'"));//УстановкаЦенНоменклатуры//!!!Нет в BASАгроБухгалтерия
		СписокВыбора.Добавить(НСтр("ru = 'Оприходование товаров'; uk='Оприбуткування товарів'"));//ОприходованиеТоваров
		СписокВыбора.Добавить(НСтр("ru = 'Поступление товаров'; uk='Надходження товарів'"));//ПоступлениеТоваров

	ИначеЕсли ИдКонф = "UABY" Тогда
		СписокВыбора.Добавить(НСтр("ru = 'Банковская выписка'; uk='Банківська виписка'"));//БанковскаяВыписка
		СписокВыбора.Добавить(НСтр("ru = 'Платежное поручение'; uk='Платіжне доручення'")); //ПлатежноеПоручение
		СписокВыбора.Добавить(НСтр("ru = 'Получение услуг'; uk='Отримання послуг'")); //ПолучениеУслугОтПоставщика
		СписокВыбора.Добавить(НСтр("ru = 'Поступление ТМЦ'; uk='Надходження ТМЦ'")); //ПоступлениеТМЦ
		СписокВыбора.Добавить(НСтр("ru = 'Счет входящий'; uk='Вхідний рахунок'")); //СчетВходящий

	ИначеЕсли ИдКонф = "OSMD" Тогда
		//OSMD(УчетВОСМДдляУкраины)
		СписокВыбора.Добавить(НСтр("ru = 'Расход со счета'; uk='Витрата з рахунку'"));//РасходСоСчета
		СписокВыбора.Добавить(НСтр("ru = 'Поступление на счет'; uk='Надходження на рахунок'"));//ПоступлениеНаСчет
		СписокВыбора.Добавить(НСтр("ru = 'Перемещение денежных средств'; uk='Переміщення грошових коштів'"));//ПеремещениеДенежныхСредств
		СписокВыбора.Добавить(НСтр("ru = 'Оприходование запасов'; uk='Оприбуткування запасів'"));//ОприходованиеЗапасов
		СписокВыбора.Добавить(НСтр("ru = 'Установка цен номенклатуры'; uk='Встановлення цін номенклатури'"));//УстановкаЦенНоменклатуры
		СписокВыбора.Добавить(НСтр("ru = 'Поступление запасов и услуг'; uk='Надходження запасів і послуг'"));//ПоступлениеЗапасовУслуг

	//ZUP УП неизвестна конфигурация
	//UTP УП неизвестна конфигурация
	//KUBU(КомплексныйУчетДляБюджетныхУчрежденийУкраины) УП неизвестна конфигурация
	КонецЕсли;	
	
	Если ИдКонф <> "BASR" Тогда
		СписокВыбора.Добавить(НСтр("ru = 'Регистрация входящего налогового документа'; uk='Реєстрація вхідного податкового документа'"));//РегистрацияВходящегоНалоговогоДокумента
	КонецЕсли
	
КонецПроцедуры

&НаКлиенте
Функция ПолучитьТипДокументаПоПредставлению(Представление)
	
	СписокВыбораТиповДок = Новый СписокЗначений;
	
	//Расход со счета
	СписокВыбораТиповДок.Добавить(НСтр("ru = 'Расход со счета'; uk='Витрата з рахунку'"), "РасходСоСчета");
	СписокВыбораТиповДок.Добавить(НСтр("ru = 'Перемещение денег'; uk='Переміщення грошей'"), "ПеремещениеДС");
	СписокВыбораТиповДок.Добавить(НСтр("ru = 'Списание с банковского счета'; uk='Списання з банківського рахунку'"), "СписаниеСРасчетногоСчета");
	СписокВыбораТиповДок.Добавить(НСтр("ru = 'Списание безналичных денежных средств'; uk='Списання безготівкових коштів'"), "СписаниеБезналичныхДенежныхСредств");
	//Поступление на счет
	СписокВыбораТиповДок.Добавить(НСтр("ru = 'Поступление на счет'; uk='Надходження на рахунок'"), "ПоступлениеНаСчет");
	СписокВыбораТиповДок.Добавить(НСтр("ru = 'Поступление на банковский счет'; uk='Надходження на банківський рахунок'"), "ПоступлениеНаРасчетныйСчет");
	СписокВыбораТиповДок.Добавить(НСтр("ru = 'Поступление безналичных денежных средств'; uk='Надходження безготівкових грошових коштів'"), "ПоступлениеБезналичныхДенежныхСредств");
	
	//Оприходование запасов
	СписокВыбораТиповДок.Добавить(НСтр("ru = 'Оприходование запасов'; uk='Оприбуткування запасів'"), "ОприходованиеЗапасов");
	СписокВыбораТиповДок.Добавить(НСтр("ru = 'Оприходование товаров'; uk='Оприбуткування товарів'"), "ОприходованиеТоваров");
	
	СписокВыбораТиповДок.Добавить(НСтр("ru = 'Счет на оплату (полученный)'; uk='Рахунок на оплату (отриманий)'"), "СчетНаОплатуПоставщика");
	СписокВыбораТиповДок.Добавить(НСтр("ru = 'Приходная накладная'; uk='Прибуткова накладна'"), "ПриходнаяНакладная");
	СписокВыбораТиповДок.Добавить(НСтр("ru = 'Заказ покупателя'; uk='Замовлення покупця'"), "ЗаказПокупателя");
	
	СписокВыбораТиповДок.Добавить(НСтр("ru = 'Установка цен номенклатуры'; uk='Встановлення цін номенклатури'"), "УстановкаЦенНоменклатуры");
	СписокВыбораТиповДок.Добавить(НСтр("ru = 'Счет на оплату поставщика'; uk='Рахунок на оплату постачальника'"), "СчетНаОплатуПоставщика");
	СписокВыбораТиповДок.Добавить(НСтр("ru = 'Поступление товаров и услуг'; uk='Надходження товарів і послуг'"), "ПоступлениеТоваровУслуг");
	
	СписокВыбораТиповДок.Добавить(НСтр("ru = 'Выписка по банковскому счету'; uk='Виписка з банківського рахунку'"), "ВыпискаПоРасчетномуСчету");
	СписокВыбораТиповДок.Добавить(НСтр("ru = 'Заказ клиента'; uk='Замовлення клієнта'"), "ЗаказКлиента");
	
	СписокВыбораТиповДок.Добавить(НСтр("ru = 'Регистрация входящего налогового документа'; uk='Реєстрація вхідного податкового документа'"), "РегистрацияВходящегоНалоговогоДокумента");

	СписокВыбораТиповДок.Добавить(НСтр("ru = 'Банковская выписка'; uk='Банківська виписка'"), "БанковскаяВыписка");
	СписокВыбораТиповДок.Добавить(НСтр("ru = 'Платежное поручение'; uk='Платіжне доручення'"), "ПлатежноеПоручение");

	СписокВыбораТиповДок.Добавить(НСтр("ru = 'Получение услуг'; uk='Отримання послуг'"), "ПолучениеУслугОтПоставщика");
	СписокВыбораТиповДок.Добавить(НСтр("ru = 'Поступление ТМЦ'; uk='Надходження ТМЦ'"), "ПоступлениеТМЦ");
	СписокВыбораТиповДок.Добавить(НСтр("ru = 'Счет входящий'; uk='Вхідний рахунок'"), "СчетВходящий");

	СписокВыбораТиповДок.Добавить(НСтр("ru = 'Перемещение денежных средств'; uk='Переміщення грошових коштів'"), "ПеремещениеДенежныхСредств");
	СписокВыбораТиповДок.Добавить(НСтр("ru = 'Поступление запасов и услуг'; uk='Надходження запасів і послуг'"), "ПоступлениеЗапасовУслуг");

	НайденноеЗначение = СписокВыбораТиповДок.НайтиПоЗначению(Представление);
	Если НайденноеЗначение = Неопределено Тогда
		Сообщить("Не найден тип документа");
		Возврат "";	
	Иначе
		Возврат НайденноеЗначение.Представление;
	КонецЕсли;	
	
КонецФункции

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	Если ВыбранноеЗначение = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Найти(ИсточникВыбора.ИмяФормы, "ПравилаОтбора") Тогда
		ТекущаяСтрока = Элементы.ТаблицаПравил.ТекущиеДанные;
		ТекущаяСтрока.Условие = ВыбранноеЗначение.ОписаниеОтбора;
		ТекущаяСтрока.АдресУсловия = ВыбранноеЗначение.АдресКомпоновщика;
	ИначеЕсли Найти(ИсточникВыбора.ИмяФормы, "ЗаполняемыеДанные") Тогда
		ТекущаяСтрока = Элементы.ТаблицаПравил.ТекущиеДанные;
		ТекущаяСтрока.ЗаполняемыеПоля = ВыбранноеЗначение.ПредставлениеЗаполняемыхДанных;
		ТекущаяСтрока.АдресЗаполняемыхПолей = ВыбранноеЗначение.АдресНастроекЗаполняемыхДанных; 	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросИзменениеТипаДокумента(Ответ, Параметры) Экспорт
	
	Если Ответ <> КодВозвратаДиалога.Да Тогда
		Возврат;	
	КонецЕсли;	
	
	ТекущаяСтрока = Элементы.ТаблицаПравил.ТекущиеДанные;
	ТекущаяСтрока.Условие = "";
	УдалитьНастройкуИзХранилища("МодульОбменаПрива24-1С", ТекущаяСтрока.АдресУсловия);
	ТекущаяСтрока.АдресУсловия = "";
	
	УдалитьНастройкуИзХранилища("МодульОбменаПрива24-1С", ТекущаяСтрока.АдресЗаполняемыхПолей);
	ТекущаяСтрока.АдресЗаполняемыхПолей = "";
	ТекущаяСтрока.ЗаполняемыеПоля = "";
	
	ТекущаяСтрока.ТипДокумента = Параметры.ТипДокумента;
	ТекущаяСтрока.ТипДокументаПредставление = Параметры.ТипДокументаПредставление;
	
КонецПроцедуры

&НаСервере
Процедура УдалитьНастройкуИзХранилища(ИмяОбъекта, ИмяНастройки)
	
	ХранилищеВариантовОтчетов.Удалить(ИмяОбъекта, ИмяНастройки, Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаПравилВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекущаяСтрока = Элементы.ТаблицаПравил.ТекущиеДанные;
	
	Если Элемент.ТекущийЭлемент.Имя = "ТаблицаПравилУсловие" Тогда
		
		ОбластьПоиска = ТекущаяСтрока.ТипДокумента;
		Если НЕ ЗначениеЗаполнено(ОбластьПоиска) Тогда
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = НСтр("ru = 'Не выбран тип документа'; uk='Не вибрано тип документа'");
			Сообщение.Поле = "ТаблицаПравил[" + (Элемент.ТекущаяСтрока) + "].ТипДокументаПредставление";
			Сообщение.Сообщить();
			Возврат;	
		КонецЕсли;
		
		ИнициализироватьКомпоновщикОтбораИПравила();
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("АдресСхемыКомпоновки",            АдресСхемыКомпоновки);
		ПараметрыФормы.Вставить("АдресНастроекКомпоновщикаОтбора", ?(ЗначениеЗаполнено(ТекущаяСтрока.АдресУсловия), ТекущаяСтрока.АдресУсловия, АдресНастроекКомпоновщикаОтбора()));
		ПараметрыФормы.Вставить("ИдентификаторОсновнойФормы",      УникальныйИдентификатор);
		ПараметрыФормы.Вставить("ПредставлениеОбластиОтбора",      ТекущаяСтрока.ТипДокументаПредставление);
		
		ОткрытьФорму(БазовоеИмяФормы + "ПравилаОтбора", ПараметрыФормы, ЭтотОбъект);
		
	ИначеЕсли Элемент.ТекущийЭлемент.Имя = "ТаблицаПравилЗаполняемыеПоля" Тогда
		
		ОбластьПоиска = ТекущаяСтрока.ТипДокумента;
		Если НЕ ЗначениеЗаполнено(ОбластьПоиска) Тогда
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = НСтр("ru = 'Не выбран тип документа'; uk='Не вибрано тип документа'");
			Сообщение.Поле = "ТаблицаПравил[" + (Элемент.ТекущаяСтрока) + "].ТипДокументаПредставление";
			Сообщение.Сообщить();
			Возврат;	
		КонецЕсли;
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("ПредставлениеДокумента",  		  ТекущаяСтрока.ТипДокументаПредставление);
		ПараметрыФормы.Вставить("ТипДокумента", 		  		  ТекущаяСтрока.ТипДокумента);
		ПараметрыФормы.Вставить("АдресНастроекЗаполняемыхДанных", ТекущаяСтрока.АдресЗаполняемыхПолей);

		////ПараметрыФормы.Вставить("АдресУсловия", ТекущаяСтрока.АдресУсловия);//для выбора типа значений Субконто

		ОткрытьФорму(БазовоеИмяФормы + "ЗаполняемыеДанные", ПараметрыФормы, ЭтотОбъект);
		
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаПравилТипДокументаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
		
	ИзменениеОчисткаТипаДокумента(ВыбранноеЗначение, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаПравилТипДокументаОчистка(Элемент, СтандартнаяОбработка)
	
	ИзменениеОчисткаТипаДокумента("", СтандартнаяОбработка);	
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменениеОчисткаТипаДокумента(ВыбранноеЗначение, СтандартнаяОбработка)
	
	ТекущаяСтрока = Элементы.ТаблицаПравил.ТекущиеДанные;
	
	ТекущаяСтрока.ТипДокумента = ПолучитьТипДокументаПоПредставлению(ВыбранноеЗначение);
	
	Если ЗначениеЗаполнено(ТекущаяСтрока.АдресУсловия) И ТекущаяСтрока.ТипДокументаПредставление <> ВыбранноеЗначение Тогда
		
		СтандартнаяОбработка = Ложь;
		ТекстВопроса = НСтр("ru = 'После изменения вида документа условие отбора будет очищено. Продолжить?'; uk='Після зміни виду документа умова відбору буде очищено. Продовжити?'");
		СтруктураПараметров = Новый Структура;
		СтруктураПараметров.Вставить("ТипДокументаПредставление", ВыбранноеЗначение);
		СтруктураПараметров.Вставить("ТипДокумента", ТекущаяСтрока.ТипДокумента);
		ОписаниеОповещения = Новый ОписаниеОповещения("ВопросИзменениеТипаДокумента", ЭтаФорма, СтруктураПараметров);
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет,, КодВозвратаДиалога.Да);
		
	КонецЕсли;
	
КонецПроцедуры	

&НаКлиенте
Процедура ТаблицаПравилПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)

	Отказ = Копирование;

КонецПроцедуры

&НаКлиенте
Процедура Записать(Команда)
	
	СтруктураВозврата = Новый Структура;
	СтруктураВозврата.Вставить("ТаблицаПравил", Объект.ТаблицаПравил);
	ОповеститьОВыборе(СтруктураВозврата);
	
КонецПроцедуры

&НаКлиенте
Функция ДобавитьКонечныйРазделительПути(Знач ПутьКаталога, Знач Удалить_Платформа = Неопределено)

	Если ПустаяСтрока(ПутьКаталога) Тогда
		Возврат ПутьКаталога;
	КонецЕсли;
	
	ДобавляемыйСимвол = ПолучитьРазделительПути();
	
	Если Прав(ПутьКаталога, 1) = ДобавляемыйСимвол Тогда
		Возврат ПутьКаталога;
	Иначе 
		Возврат ПутьКаталога + ДобавляемыйСимвол;
	КонецЕсли;

КонецФункции

&НаКлиенте
Процедура СохранитьНастройки(Команда)

	ВыбратьКаталог();

КонецПроцедуры

&НаСервере
Процедура ВосстановитьНастройкиНаСервере(ПутьКНастройками)
	
	ДвоичныеДанные = ПолучитьИзВременногоХранилища(ПутьКНастройками);
	// получаем имя временного файла в локальной ФС на сервере
	ИмяВременногоФайла = ПолучитьИмяВременногоФайла();
	// получаем файл правил для зачитки
	ДвоичныеДанные.Записать(ИмяВременногоФайла);
	Объект.ТаблицаПравил.Загрузить(ЗначениеИзФайла(ИмяВременногоФайла));
	
КонецПроцедуры

&НаКлиенте
Процедура ВосстановитьНастройки(Команда)

	ВыбратьФайл();

КонецПроцедуры

// Вызывает диалог выбора файла
//
// Параметры:
//  ПараметрыВыбора - Структура - путь к каталогу.
//		Описание струткуры:
//			ИмяРеквизита 		- Строка - имя реквизита в который происходит выбор
//          ИмяТЧ 		        - Строка - имя табличной части в которой происходит выбор
//			ТекстРедактирования - Строка - текуший адрес файла
//			РасширениеФайла     - Строка - расширение файла
//  ВыборКаталогаВТабличнойЧасти - Булево - признак что выбор проиходит из табличной части формы
//
&НаКлиенте
Процедура ВыбратьФайл()
	
	Попытка
		// Вызываем диалог выбора 
		ВыборФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
		ВыборФайла.Фильтр = " (*. pb1c)|*.pb1c";
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ВыбратьФайлЗавершение", ЭтотОбъект);
		НачатьПомещениеФайлов(ОписаниеОповещения, , ВыборФайла , Истина, Новый УникальныйИдентификатор);

	Исключение
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru='Ошибка при загрузке файла';uk='Помилка при завантаженні файлу'") + Символы.ПС + ОписаниеОшибки();
		Сообщение.Сообщить();
	КонецПопытки;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьФайлЗавершение(МассивФайлов, ДопПараметры) Экспорт
	
	Попытка
		Для Каждого Файл Из МассивФайлов Цикл
			
			ВосстановитьНастройкиНаСервере(Файл.Хранение);
			
		КонецЦикла;
	Исключение
	КонецПопытки;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьКаталог()
	
	Если Не ПодключитьРасширениеРаботыСФайлами() Тогда
		НачатьУстановкуРасширенияРаботыСФайлами();
		Возврат;
	КонецЕсли;
	
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога);
	
	Диалог.Заголовок = НСтр("ru = 'Укажите каталог для сохранения файла настроек';uk='Вкажіть каталог для збереження файлу налаштувань'");
	
	СтруктураПараметров = Новый Структура;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ДиалогВыбораКаталога", ЭтаФорма, СтруктураПараметров);
	
	Диалог.Показать(ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ДиалогВыбораКаталога(Ответ, Параметры) Экспорт
	
	Если ТипЗнч(Ответ) = Тип("Массив") И Ответ.Количество() <> 0 Тогда
		
		КаталогВыгрузки = Ответ[0];
		ИмяФайла = "params.pb1c";
		АдресФайла = ЭкспортНастроекНаСервере(ИмяФайла);
		
		СохранитьФайлВыгрузкиНаКлиенте(КаталогВыгрузки, ИмяФайла, АдресФайла);
		
	КонецЕсли;	
	
КонецПроцедуры


// Сохраняет все данные формы в строку и возвращает её
&НаСервере
Функция ЭкспортНастроекНаСервере(ИмяКонечногоФайла)
	
	ПолноеИмяКонечногоФайла = КаталогВременныхФайлов() + ИмяКонечногоФайла;
	ЗначениеВФайл(ПолноеИмяКонечногоФайла, Объект.ТаблицаПравил.Выгрузить());
	Возврат ПоместитьВоВременноеХранилище(Новый ДвоичныеДанные(ПолноеИмяКонечногоФайла), Новый УникальныйИдентификатор);
    
КонецФункции

&НаКлиенте
Процедура СохранитьФайлВыгрузкиНаКлиенте(КаталогВыгрузки, ИмяФайла, АдресФайла)
	
	Попытка
		
		ПолучаемыеФайлы = Новый Массив;
		
		ОписаниеФайла = Новый ОписаниеПередаваемогоФайла(ДобавитьКонечныйРазделительПути(КаталогВыгрузки) + ИмяФайла, АдресФайла);
		ПолучаемыеФайлы.Добавить(ОписаниеФайла);
		
		ОписаниеОповещения = Новый ОписаниеОповещения("СохранитьФайлыССервера", ЭтаФорма);
		НачатьПолучениеФайлов(ОписаниеОповещения, ПолучаемыеФайлы, ДобавитьКонечныйРазделительПути(КаталогВыгрузки), Ложь);
		
	Исключение
		
		ТекстСообщения = НСтр("ru='Не удалось записать файлы! Возможно, недостаточно места на диске или диск защищен от записи.';
		|;uk='Не вдалося записати файли! Можливо, недостатньо місця на диску або диск захищений від запису'");
		ТекстСообщения = ТекстСообщения + НСтр("ru=' Также, возможно, не подключено расширение для работы с файлами.';uk='Також, можливо, не підключено розширення для роботи з файлами.'");
		Сообщить(ТекстСообщения);
		Сообщить(НСтр("ru = 'Ошибка при сохранении файла вігрузки на клиенте '; uk='Помилка при збереженні файла вивантаження на клієнті '") + ОписаниеОшибки());

		Сообщить(НСтр("ru='Не удалось выполнить сохранение документов!';uk='Не вдалося виконати збереження документів!'"));	
		
	КонецПопытки;
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьФайлыССервера(Ответ, Параметры) Экспорт
	
	Если Ответ = Неопределено Тогда
		ТекстСообщения = НСтр("ru='Не удалось записать файлы! Возможно, недостаточно места на диске или диск защищен от записи.';
		|;uk='Не вдалося записати файли! Можливо, недостатньо місця на диску або диск захищений від запису'");
		ТекстСообщения = ТекстСообщения + НСтр("ru=' Также, возможно, не подключено расширение для работы с файлами.';uk='Також, можливо, не підключено розширення для роботи з файлами.'");
		Сообщить(ТекстСообщения);
		Сообщить(НСтр("ru='Не удалось выполнить сохранение документов!';uk='Не вдалося виконати збереження документів!'"));		
	Иначе
	    Сообщить(НСтр("ru='Сохранение завершено.';uk='Збереження завершено.'"));
	КонецЕсли;
		
КонецПроцедуры

