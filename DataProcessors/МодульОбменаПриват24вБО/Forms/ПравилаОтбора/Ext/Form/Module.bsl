// Ожидаются параметры:
//
//     ИдентификаторОсновнойФормы      - УникальныйИдентификатор - Идентификатор формы, через хранилище которой происходит обмен
//     АдресСхемыКомпоновки            - Строка - Адрес временного хранилища схемы компоновки, для которой редактируются настройки
//     АдресНастроекКомпоновщикаОтбора - Строка - Адрес временного хранилища редактируемых настроек компоновщика
//     ПредставлениеОбластиОтбора      - Строка - Представление для формирования заголовка
//
// Возвращается результатом выбора:
//
//     Неопределено - Отказ от редактирования
//     Строка       - Адрес временного хранилища новых настроек компоновщика
//

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ИдентификаторОсновнойФормы = Параметры.ИдентификаторОсновнойФормы;
	
	КомпоновщикПредварительногоОтбора = Новый КомпоновщикНастроекКомпоновкиДанных;
	Попытка
		КомпоновщикПредварительногоОтбора.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(Параметры.АдресСхемыКомпоновки));
	
		Если ЭтоАдресВременногоХранилища(Параметры.АдресНастроекКомпоновщикаОтбора) Тогда
			
			АдресНастроекКомпоновщикаОтбора = Параметры.АдресНастроекКомпоновщикаОтбора;
			КомпоновщикПредварительногоОтбора.ЗагрузитьНастройки(ПолучитьИзВременногоХранилища(АдресНастроекКомпоновщикаОтбора));
			УдалитьИзВременногоХранилища(АдресНастроекКомпоновщикаОтбора);
			
		Иначе
			
			КомпоновщикПредварительногоОтбора.ЗагрузитьНастройки(ВостановитьИзХранилищаНастроек("МодульОбменаПрива24-1С", Параметры.АдресНастроекКомпоновщикаОтбора)); 
			
		КонецЕсли;

	Исключение
		Отказ = Истина;
		Сообщить(НСтр("ru='Невозможно восстановить настройки отбора. Удалите текущую строку и заполните настройку заново';uk='Неможливо відновити налаштування відбору. Видаліть поточний рядок і заповніть налаштування знову'"));
		Возврат;	
	КонецПопытки;	

	Заголовок = СтрЗаменить(НСтр("ru='Правила отбора ""%1""';uk='Правила відбору ""%1""'"), "%1", Параметры.ПредставлениеОбластиОтбора);
	
	АдресНастроек = ?(ЭтоАдресВременногоХранилища(Параметры.АдресНастроекКомпоновщикаОтбора), Строка(Новый УникальныйИдентификатор), Параметры.АдресНастроекКомпоновщикаОтбора);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Выбрать(Команда)
	
	Если Модифицированность Тогда
		
		ПоместитьВХранилищеНастроек("МодульОбменаПрива24-1С", АдресНастроек, КомпоновщикПредварительногоОтбора); 
		
		ОписаниеОтбора = ПредставлениеОписанияОтбора();
		
		СтруктураВозврата = Новый Структура;
		СтруктураВозврата.Вставить("АдресКомпоновщика", АдресНастроек);
		СтруктураВозврата.Вставить("ОписаниеОтбора", ОписаниеОтбора);
		
		ОповеститьОВыборе(СтруктураВозврата);

	Иначе
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПоместитьВХранилищеНастроек(ИмяОбъекта, ИмяНастройки, ЗначениеНастройки)
	
	ХранилищеНастроекДанныхФорм.Сохранить(ИмяОбъекта, ИмяНастройки, ЗначениеНастройки.ПолучитьНастройки());
	
КонецПроцедуры

&НаСервере
Функция ВостановитьИзХранилищаНастроек(ИмяОбъекта, ИмяНастройки)
	
	Возврат ХранилищеНастроекДанныхФорм.Загрузить(ИмяОбъекта, ИмяНастройки);
	
КонецФункции

#КонецОбласти

&НаКлиенте
Функция ПредставлениеОписанияОтбора()
	
	ОписаниеОтбора = Строка(КомпоновщикПредварительногоОтбора.Настройки.Отбор);
	Если ПустаяСтрока(ОписаниеОтбора) Тогда
		ОписаниеОтбора = НСтр("ru='Все элементы';uk='Всі елементи'");
	КонецЕсли;
	
	Возврат ОписаниеОтбора;
	
КонецФункции

