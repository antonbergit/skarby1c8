////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

// Сохраняет настройки формы.
&НаСервере
Процедура СохранитьНастройкиФормы()
	
	Настройки = Новый Соответствие;
	Настройки.Вставить("ФайлВыгрузки",				Объект.ФайлВыгрузки);
	Настройки.Вставить("ФайлЗагрузки",				Объект.ФайлЗагрузки);
	Настройки.Вставить("Программа",					Объект.Программа);
	Настройки.Вставить("СтатьяДДСИсходяший",		Объект.СтатьяДДСИсходяший);
	Настройки.Вставить("СтатьяДДСВходящий",			Объект.СтатьяДДСВходящий);
	Настройки.Вставить("ПроводитьЗагружаемые",		Объект.ПроводитьЗагружаемые);
	Настройки.Вставить("Кодировка", 				Объект.Кодировка);
	Настройки.Вставить("ТипФайла",                  Объект.ТипФайла);
	
	ХранилищеСистемныхНастроек.Сохранить("Обработка.КлиентБанк.Форма.ОсновнаяФорма/" + ПолучитьНавигационнуюСсылку(Объект.БанковскийСчет), "ВыгрузкаВСбербанк", Настройки);
	
КонецПроцедуры // СохранитьНастройкиФормы()

// Загружает настройки формы.
// Если загрузка настроек осуществляется при изменении реквизита формы,
// например, для новой организации, то обязательно проверить подлючено ли 
// расширение для работы с файлами.
//
// Признаком отсутствия подключения будит информация в реквизитах объекта обработки:
// ФайлВыгрузки, ФайлЗагрузки
//
&НаСервере
Процедура ЗагрузитьНастройкиФормы()
	
	Настройки = ХранилищеСистемныхНастроек.Загрузить("Обработка.КлиентБанк.Форма.ОсновнаяФорма/" + ПолучитьНавигационнуюСсылку(Объект.БанковскийСчет), "ВыгрузкаВСбербанк");
	
	Если Настройки <> Неопределено Тогда
		
		//  См. описание процедуры
		ЗначениеНастройки = Настройки.Получить("ФайлВыгрузки");
		Если РасширениеРаботыСФайламиПодключено Тогда
			
			Объект.ФайлВыгрузки 			= ЗначениеНастройки;
			
		КонецЕсли;
		
		//  См. описание процедуры
		ЗначениеНастройки = Настройки.Получить("ФайлЗагрузки");
		Если РасширениеРаботыСФайламиПодключено Тогда
			
			Объект.ФайлЗагрузки 			= ЗначениеНастройки;
			
		КонецЕсли;
		
		Объект.Программа 				= Настройки.Получить("Программа");
		Объект.СтатьяДДСИсходяший 		= Настройки.Получить("СтатьяДДСИсходяший");
		Объект.СтатьяДДСВходящий 		= Настройки.Получить("СтатьяДДСВходящий");
		Объект.ПроводитьЗагружаемые 	= Настройки.Получить("ПроводитьЗагружаемые");
		Объект.Кодировка 				= Настройки.Получить("Кодировка");
		Объект.ТипФайла                 = Настройки.Получить("ТипФайла");
		
	КонецЕсли;
		
КонецПроцедуры // ЗагрузитьНастройкиФормы()

// Получает набор данных с сервера для процедуры ДоговорПриИзменении.
//
&НаСервереБезКонтекста
Функция ПолучитьДанныеОрганизацияПриИзменении(Организация, БанковскийСчет)
	
	СтруктураДанные = Новый Структура;
	
	СтруктураДанные.Вставить(
		"БанковскийСчет",
		?(БанковскийСчет.Владелец = Организация, БанковскийСчет, Организация.БанковскийСчетПоУмолчанию)
	);
	
	Возврат СтруктураДанные;
	
КонецФункции // ПолучитьДанныеОрганизацияПриИзменении()

// Процедура открывает файл для просмотра.
//
&НаКлиенте
Процедура ОткрытьФайлДляПросмотра(ФайлВыгрузки, Кодировка, Заголовок)
	
	Если НЕ ПодключитьРасширениеРаботыСФайлами() Тогда
		ТекстСообщения = НСтр("ru='Для данной операции необходимо"
"установить расширение работы с файлами!"
"Установка выполняется в разделе"
"""Сервис и администрирование""/"
"""Настройка сервисных функций"".';uk='Для даної операції необхідно"
"встановити розширення роботи з файлами!"
"Установка виконується в розділі"
"""Сервіс і адміністрування""/"
"""Налаштування сервісних функцій"".'"
		);
		ПоказатьПредупреждение(,ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	ФайлНаДиске = Новый Файл(ФайлВыгрузки);
	Если НЕ ФайлНаДиске.Существует() Тогда
		ПоказатьПредупреждение(, НСтр("ru='Не найден файл!';uk='Не знайдений файл!'"));
		Возврат;
	КонецЕсли;
	Текст = Новый ТекстовыйДокумент();
	Если Кодировка = "DOS" Тогда
		Кодир = КодировкаТекста.OEM;
	ИначеЕсли Кодировка = "Windows" Тогда
		Кодир = КодировкаТекста.ANSI;
	Иначе
		Кодир = КодировкаТекста.UTF8;
	КонецЕсли;
	Текст.Прочитать(ФайлВыгрузки, Кодир);
	Текст.Показать(Заголовок, ФайлВыгрузки);
	
КонецПроцедуры // ОткрытьФайлДляПросмотра()

// Функция проверяет правильность заполнения реквизитов формы.
//
&НаКлиенте
Функция ПроверитьЗаполнениеРеквизитовФормы(ЭтоВыгрузка)
	
	РезультатПроверкиОк = Истина;
	
	// Проверка заполненности реквизитов.
	Если НЕ ЗначениеЗаполнено(Объект.Кодировка) Тогда
		ТекстСообщения = НСтр("ru='Не заполнена кодировка!';uk='Не заповнене кодування!'");
		УправлениеНебольшойФирмойКлиент.СообщитьОбОшибке(, ТекстСообщения, , , "Кодировка", РезультатПроверкиОк);
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(Объект.ТипФайла) Тогда
		ТекстСообщения = НСтр("ru='Не заполнен тип файла!';uk='Не заповнений тип файлу!'");
		УправлениеНебольшойФирмойКлиент.СообщитьОбОшибке(, ТекстСообщения, , , "ТипФайла", РезультатПроверкиОк);
	КонецЕсли;
	Если ЭтоВыгрузка И НЕ ЗначениеЗаполнено(Объект.НачПериода) Тогда
		ТекстСообщения = НСтр("ru='Не заполнен начальный период!';uk='Не заповнений початковий період!'");
		УправлениеНебольшойФирмойКлиент.СообщитьОбОшибке(, ТекстСообщения, , , "НачПериода", РезультатПроверкиОк);
	КонецЕсли;
	Если ЭтоВыгрузка И НЕ ЗначениеЗаполнено(Объект.КонПериода) Тогда
		ТекстСообщения = НСтр("ru='Не заполнен конечный период!';uk='Не заповнений кінцевий період!'");
		УправлениеНебольшойФирмойКлиент.СообщитьОбОшибке(, ТекстСообщения, , , "КонПериода", РезультатПроверкиОк);
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(Объект.Организация) Тогда
		ТекстСообщения = НСтр("ru='Не заполнена организация!';uk='Не заповнена організація!'");
		УправлениеНебольшойФирмойКлиент.СообщитьОбОшибке(, ТекстСообщения, , , "Организация", РезультатПроверкиОк);
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(Объект.БанковскийСчет) Тогда
		ТекстСообщения = НСтр("ru='Не заполнен банковский счет!';uk='Не заповнений банківський рахунок!'");
		УправлениеНебольшойФирмойКлиент.СообщитьОбОшибке(, ТекстСообщения, , , "БанковскийСчет", РезультатПроверкиОк);
	КонецЕсли;
	Если ЭтоВыгрузка И НЕ ЗначениеЗаполнено(Объект.Программа) Тогда
		ТекстСообщения = НСтр("ru='Не заполнена программа %Тип%!';uk='Не заповнена програма %Тип%!'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Тип%", ?(ЭтоВыгрузка, "приемник'", "источник'"));
		УправлениеНебольшойФирмойКлиент.СообщитьОбОшибке(, ТекстСообщения, , , "Программа", РезультатПроверкиОк);		
	КонецЕсли;
	Если ЭтоВыгрузка И НЕ ЗначениеЗаполнено(Объект.ФайлВыгрузки) И РасширениеРаботыСФайламиПодключено Тогда
		ТекстСообщения = НСтр("ru='Не заполнено имя файла выгрузки!';uk='Не заповнене ім`я файлу вивантаження!'");
		УправлениеНебольшойФирмойКлиент.СообщитьОбОшибке(, ТекстСообщения, , , "ФайлВыгрузки", РезультатПроверкиОк);
	КонецЕсли;
	Если НЕ ЭтоВыгрузка И НЕ ЗначениеЗаполнено(Объект.ФайлЗагрузки) И РасширениеРаботыСФайламиПодключено Тогда
		ТекстСообщения = НСтр("ru='Не заполнено имя файла загрузки!';uk='Не заповнене ім`я файлу завантаження!'");
		УправлениеНебольшойФирмойКлиент.СообщитьОбОшибке(, ТекстСообщения, , , "ФайлЗагрузки", РезультатПроверкиОк);
	КонецЕсли;
	
	Возврат РезультатПроверкиОк;
	
КонецФункции // ПроверитьЗаполнениеРеквизитовФормы()

// Функция проверяет корректность данных для выгрузки.
//
&НаСервере
Функция ПроверитьНаКорректностьИПустоеЗначениеЭкспорта(СтрокаДокумента)
	
	ПеречислениеНалога          = Ложь;
	НепрямыеРасчетыУПлательщика = Ложь;
	НепрямыеРасчетыУПолучателя  = Ложь;
	ПеречислениеНалога          = (СтрокаДокумента.ВидОперации = Перечисления.ВидыОперацийПлатежноеПоручение.ПеречислениеНалога);
	НепрямыеРасчетыУПлательщика = ЗначениеЗаполнено(СтрокаДокумента.ОрганизацияБанкРасчетов);
	НепрямыеРасчетыУПолучателя  = ЗначениеЗаполнено(СтрокаДокумента.КонтрагентБанкРасчетов);
	Плательщик = "Организация";
	Получатель = "Контрагент";
	РеквизитыПлДокЭксОсновные = "Номер,Дата,СуммаДокумента";
	РеквизитыПлДокЭксПлательщик = Плательщик + "Счет," + Плательщик + "," + Плательщик + "ИНН";
	РеквизитыПлДокЭксПлательщикНПР = Плательщик + "РасчСчетОрг," + Плательщик + "БанкРасчетов," + Плательщик + "ГородБанка," + Плательщик + "МФОРЦБанка";
	РеквизитыПлДокЭксПолучатель = Получатель + "Счет," + Получатель + "," + Получатель + "ИНН";
	РеквизитыПлДокЭксПолучательНПР = Получатель + "РасчСчетОрг," + Получатель + "БанкРасчетов," + Получатель + "ГородБанка," + Получатель + "МФОРЦБанка";
	РеквизитыПлДокЭксБюджетПлатеж = "ЕДРПОУПлательщика,ЕДРПОУПолучателя,КодБК,КодКОАТУУ,ПоказательОснования,ПоказательПериода,ПоказательНомера,ПоказательДаты,ПоказательТипа";
	
	СтрокаРеквизиты = "%РеквизитыПлДокЭксОсновные%,%РеквизитыПлДокЭксПлательщик%,%РеквизитыПлДокЭксПлательщикНПР%%РеквизитыПлДокЭксПолучатель%,%РеквизитыПлДокЭксПолучательНПР%";
	СтрокаРеквизиты = СтрЗаменить(СтрокаРеквизиты, "%РеквизитыПлДокЭксОсновные%", РеквизитыПлДокЭксОсновные);
	СтрокаРеквизиты = СтрЗаменить(СтрокаРеквизиты, "%РеквизитыПлДокЭксПлательщик%", РеквизитыПлДокЭксПлательщик);
	СтрокаРеквизиты = СтрЗаменить(СтрокаРеквизиты, "%РеквизитыПлДокЭксПлательщикНПР%", ?(НепрямыеРасчетыУПлательщика, РеквизитыПлДокЭксПлательщикНПР + ",", ""));
	СтрокаРеквизиты = СтрЗаменить(СтрокаРеквизиты, "%РеквизитыПлДокЭксПолучатель%", РеквизитыПлДокЭксПолучатель);
	СтрокаРеквизиты = СтрЗаменить(СтрокаРеквизиты, "%РеквизитыПлДокЭксПолучательНПР%", ?(НепрямыеРасчетыУПолучателя, РеквизитыПлДокЭксПолучательНПР + ",", ""));
	
	ЭкспортНеПустые = СоздатьСоответствиеИзСтроки(СтрокаРеквизиты);
	
	Для каждого Свойство Из ЭкспортНеПустые Цикл
		ПроверитьНаПустоеЗначениеЭкспорта(СтрокаДокумента, Свойство.Ключ);
	КонецЦикла;
	Если ПеречислениеНалога Тогда
		ПроверитьЗаполнениеНалоговыхРеквизитов(СтрокаДокумента);
	КонецЕсли;
	ПроверитьНаКорректностьНомераПриВыгрузке(СтрокаДокумента);
	
КонецФункции // ПроверитьНаКорректностьИПустоеЗначениеЭкспорта()

// Процедура проверяет пустое значение данных для выгрузки.
//
&НаСервере
Процедура ПроверитьНаПустоеЗначениеЭкспорта(СтрокаЭкспорта, ИмяСвойства)
	
	Если НЕ ЗначениеЗаполнено(СтрокаЭкспорта[СокрЛП(ИмяСвойства)]) Тогда
		СтрокаЗамечание = НСтр("ru='Не заполнено ""%ИмяСвойства%""!';uk='Не заповнене ""%ИмяСвойства%""!'");
		СтрокаЗамечание = СтрЗаменить(СтрокаЗамечание, "%ИмяСвойства%", ИмяСвойства);
		ДобавитьЗамечание(СтрокаЭкспорта, 3, СтрокаЗамечание);
		УстановитьГотовность(СтрокаЭкспорта.Готовность, 4);
	КонецЕсли;
	
КонецПроцедуры // ПроверитьНаПустоеЗначениеЭкспорта()

// Процедура проверяет корректность номера для выгрузки.
//
&НаСервере
Процедура ПроверитьНаКорректностьНомераПриВыгрузке(СтрокаЭкспорта)
	
	Значение = СокрЛП(СтрокаЭкспорта.Номер);
	Попытка
		Если Число(Строка(Число(Прав(Значение, 3)))) = 0 Тогда
			ДобавитьЗамечание(СтрокаЭкспорта, 4, НСтр("ru='Номер должен оканчиваться на три цифры и не на ""000""!';uk='Номер повинен закінчуватися на три цифри і не на ""000""!'"));
		КонецЕсли;
	Исключение
		ДобавитьЗамечание(СтрокаЭкспорта, 4, НСтр("ru='Номер должен оканчиваться на три цифры и не на ""000""!';uk='Номер повинен закінчуватися на три цифри і не на ""000""!'"));
	КонецПопытки;
	
КонецПроцедуры // ПроверитьНаКорректностьНомераПриВыгрузке()

// Функция проверяет правильность заполнения налоговых реквизитов.
//
&НаСервере
Функция ПроверитьЗаполнениеНалоговыхРеквизитов(СтрокаЭкспорта)
	
	Ошибка = Новый СписокЗначений();
	П104 = СокрЛП(СтрокаЭкспорта.КодБК);
	П105 = СокрЛП(СтрокаЭкспорта.КодКОАТУУ);
	П106 = СокрЛП(СтрокаЭкспорта.ПоказательОснования);
	П107 = ?(
		ПустаяСтрока(СокрЛП(СтрЗаменить(СтрокаЭкспорта.ПоказательПериода , ".", ""))) = 1,
		"",
		СтрокаЭкспорта.ПоказательПериода
	);
	П107 = ?(
		СокрЛП(СтрЗаменить(СтрокаЭкспорта.ПоказательПериода, ".", "")) = "0",
		"",
		СтрокаЭкспорта.ПоказательПериода
	);
	П108 = СокрЛП(СтрокаЭкспорта.ПоказательНомера);
	П109 = ?(
		НЕ ЗначениеЗаполнено(СтрокаЭкспорта.ПоказательДаты),
		"0",
		Строка(СтрокаЭкспорта.ПоказательДаты)
	);
	П110 = СокрЛП(СтрокаЭкспорта.ПоказательТипа);
	Если СтрЗаменить(П104, "0", "") = "" Тогда
		ДобавитьЗамечание(
			СтрокаЭкспорта,
			3,
			НСтр("ru='Не заполнено поле ""КБК"" на закладке ""Реквизиты для перечисления налогов"".';uk='Не заповнене поле ""КБК"" на закладці ""Реквізити для перерахування податків"".'")
		);
		УстановитьГотовность(СтрокаЭкспорта.Готовность, 4);
	КонецЕсли;
	Если ПустаяСтрока(П105) Тогда
		ДобавитьЗамечание(
			СтрокаЭкспорта,
			3,
			НСтр("ru='Не заполнено поле ""Код ОКАТУУ"" на закладке ""Реквизиты для перечисления налогов"".';uk='Не заповнене поле ""Код КОАТУУ"" на закладці ""Реквізити для перерахування податків"".'")
		);
		УстановитьГотовность(СтрокаЭкспорта.Готовность, 4);
	КонецЕсли;
	
	// Проверяем в зависимости от основания платежа.
	Если СтрЗаменить(СокрЛП(П106), "0", "") = "" Тогда
		Если СтрЗаменить(П107, "0", "") <> "" Тогда
			Если НЕ ЗначениеЗаполнено(П107) Тогда
				ДобавитьЗамечание(
				СтрокаЭкспорта,
				3,
				НСтр("ru='Возможно, неверно указано значение в поле ""Налоговый период"" на закладке ""Реквизиты для перечисления налогов"".';uk='Можливо, невірно зазначене значення в полі ""Податковий період"" на закладці ""Реквізити для перерахування податків"".'")
				);
				УстановитьГотовность(СтрокаЭкспорта.Готовность, 4);
			КонецЕсли;
		КонецЕсли;
	ИначеЕсли СтрДлина(П106) <> 2 Тогда
		Если СтрЗаменить(П107, "0", "") <> "" Тогда
			Если НЕ ЗначениеЗаполнено(П107) Тогда
				ДобавитьЗамечание(
				СтрокаЭкспорта,
				3,
				НСтр("ru='Возможно, неверно указано значение в поле ""Налоговый период"" на закладке ""Реквизиты для перечисления налогов"".';uk='Можливо, невірно зазначене значення в полі ""Податковий період"" на закладці ""Реквізити для перерахування податків"".'")
				);
				УстановитьГотовность(СтрокаЭкспорта.Готовность, 4);
			КонецЕсли;
		КонецЕсли;
	ИначеЕсли Найти("АП, АР", П106) > 0 Тогда
		Если СтрЗаменить(П107, "0", "") <> "" Тогда
			ДобавитьЗамечание(
			СтрокаЭкспорта,
			3,
			НСтр("ru='При основании платежа ""АП"" или ""АР"" следует указать ""0"" в поле ""Налоговый период"" на закладке ""Реквизиты для перечисления налогов"".';uk='При підставі платежу ""АП"" або ""АР"" слід указати ""0"" у поле ""Податковий період"" на закладці ""Реквізити для перерахування податків"".'")
			);
			УстановитьГотовность(СтрокаЭкспорта.Готовность, 4);
		КонецЕсли;
	ИначеЕсли Найти("ТР, РС, ОТ, РТ, ВУ, ПР", П106) > 0 Тогда
		Если СтрЗаменить(П107, "0", "") <> "" Тогда
			Если НЕ ЗначениеЗаполнено(П107) Тогда
				ДобавитьЗамечание(
				СтрокаЭкспорта,
				3,
				НСтр("ru='Возможно, неверно указано значение в поле ""Налоговый период"" на закладке ""Реквизиты для перечисления налогов"".';uk='Можливо, невірно зазначене значення в полі ""Податковий період"" на закладці ""Реквізити для перерахування податків"".'")
				);
				УстановитьГотовность(СтрокаЭкспорта.Готовность, 4);
			КонецЕсли;
		КонецЕсли;
	ИначеЕсли Найти("ТП ,ЗД ", П106) > 0 Тогда
		Если СтрЗаменить(П107, "0", "") <> "" Тогда
			ДД = Сред((П107), 1, 2);
			ММ = Сред((П107), 4, 2);
			ГГ = Сред((П107), 7, 4);
			Если НЕ ММ = "" Тогда
				ММ = Число(Сред((П107), 4, 2));
			Иначе
				ММ = 0;
			КонецЕсли;
			Если НЕ ГГ = "" Тогда
				ГГ = Число(Сред((П107), 7, 4));
			Иначе
				ГГ = 0;
			КонецЕсли;
			Если (Найти("Д1, Д2, Д3, МС", ДД) > 0) Тогда
				Если (ММ < 1)
					ИЛИ (ММ > 12)
					ИЛИ (ГГ < 2000)
					ИЛИ (СтрДлина(П107) - СтрДлина(СтрЗаменить(П107, ".", "")) <> 2) Тогда
					ДобавитьЗамечание(
					СтрокаЭкспорта,
					3,
					НСтр("ru='Возможно, неверно указано значение в поле ""Налоговый период"" на закладке ""Реквизиты для перечисления налогов"".';uk='Можливо, невірно зазначене значення в полі ""Податковий період"" на закладці ""Реквізити для перерахування податків"".'")
					);
					УстановитьГотовность(СтрокаЭкспорта.Готовность, 4);
				КонецЕсли;
			ИначеЕсли (Найти("КВ", ДД) > 0) Тогда
				Если (ММ < 1)
					ИЛИ (ММ > 4)
					ИЛИ (ГГ < 2000)
					ИЛИ (СтрДлина(П107) - СтрДлина(СтрЗаменить(П107, ".", "")) <> 2) Тогда
					ДобавитьЗамечание(
					СтрокаЭкспорта,
					3,
					НСтр("ru='Неверно указано значение в поле ""Налоговый период"" на закладке ""Реквизиты для перечисления налогов"".';uk='Невірно зазначене значення в полі ""Податковий період"" на закладці ""Реквізити для перерахування податків"".'")
					);
					УстановитьГотовность(СтрокаЭкспорта.Готовность, 4);
				КонецЕсли;
			ИначеЕсли (Найти("ПЛ", ДД) > 0) Тогда
				Если (ММ < 1)
					ИЛИ (ММ > 2)
					ИЛИ (ГГ < 2000)
					ИЛИ (СтрДлина(П107) - СтрДлина(СтрЗаменить(П107, ".", "")) <> 2) Тогда
					ДобавитьЗамечание(
					СтрокаЭкспорта,
					3,
					НСтр("ru='Неверно указано значение в поле ""Налоговый период"" на закладке ""Реквизиты для перечисления налогов"".';uk='Невірно зазначене значення в полі ""Податковий період"" на закладці ""Реквізити для перерахування податків"".'")
					);
					УстановитьГотовность(СтрокаЭкспорта.Готовность, 4);
				КонецЕсли;
			ИначеЕсли (Найти("ГД", ДД) > 0) Тогда
				Если (ММ <> 0)
					ИЛИ (ГГ < 2000)
					ИЛИ (СтрДлина(П107) - СтрДлина(СтрЗаменить(П107, ".", "")) <> 2) Тогда
					ДобавитьЗамечание(
					СтрокаЭкспорта,
					3,
					НСтр("ru='Неверно указано значение в поле ""Налоговый период"" на закладке ""Реквизиты для перечисления налогов"".';uk='Невірно зазначене значення в полі ""Податковий період"" на закладці ""Реквізити для перерахування податків"".'")
					);
					УстановитьГотовность(СтрокаЭкспорта.Готовность, 4);
				КонецЕсли;
			Иначе
				Если НЕ ЗначениеЗаполнено(П107) Тогда
					ДобавитьЗамечание(
					СтрокаЭкспорта,
					3,
					НСтр("ru='Возможно, неверно указано значение в поле ""Налоговый период"" на закладке ""Реквизиты для перечисления налогов"".';uk='Можливо, невірно зазначене значення в полі ""Податковий період"" на закладці ""Реквізити для перерахування податків"".'")
					); 
					УстановитьГотовность(СтрокаЭкспорта.Готовность, 4);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		Если СтрЗаменить(П108, "0", "") <> "" Тогда
			ДобавитьЗамечание(
			СтрокаЭкспорта,
			3,
			НСтр("ru='При основании платежа ""ТП"" или ""ЗД"" необходимо указывать ""0"" в поле ""Номер документа"" на закладке ""Реквизиты для перечисления налогов"".';uk='При підставі платежу ""ТП"" або ""ЗД"" необхідно вказувати ""0"" у поле ""Номер документа"" на закладці ""Реквізити для перерахування податків"".'")
			);
			УстановитьГотовность(СтрокаЭкспорта.Готовность, 4);
		КонецЕсли;
		Если Найти("ЗД ", П106) > 0 Тогда
			Если СтрЗаменить(П109, "0", "") <> "" Тогда
				ДобавитьЗамечание(
				СтрокаЭкспорта,
				3,
				НСтр("ru='При основании платежа ""ЗД"" не должно заполняться поле ""Дата документа"" на закладке ""Реквизиты для перечисления налогов"".';uk='При підставі платежу ""ЗД"" не повинне заповнюватися поле ""Дата документа"" на закладці ""Реквізити для перерахування податків"".'")
				);
				УстановитьГотовность(СтрокаЭкспорта.Готовность, 4);
			КонецЕсли;
		КонецЕсли;
	ИначеЕсли Найти("БФ", П106) > 0 Тогда
	Иначе
		ДобавитьЗамечание(
		СтрокаЭкспорта,
		3,
		НСтр("ru='Неверно указано значение в поле ""Основание платежа"" на закладке ""Реквизиты для перечисления налогов"".';uk='Невірно зазначене значення в полі ""Підстава платежу"" на закладці ""Реквізити для перерахування податків"".'")
		);
		УстановитьГотовность(СтрокаЭкспорта.Готовность, 4);
	КонецЕсли;
	Если СтрЗаменить(П110, "0", "") = "" Тогда
	ИначеЕсли Найти("НС, АВ, ПЕ, ПЦ, СА, АШ, ИШ, ПЛ, ГП, ВЗ", П110) > 0 Тогда
	Иначе
		ДобавитьЗамечание(
		СтрокаЭкспорта,
		3,
		НСтр("ru='Неверно указано значение в поле ""Тип платежа"" на закладке ""Реквизиты для перечисления налогов"".';uk='Невірно зазначене значення в полі ""Тип платежу"" на закладці ""Реквізити для перерахування податків"".'")
		);
		УстановитьГотовность(СтрокаЭкспорта.Готовность, 4);
	КонецЕсли;
	
	// Выводим список найденых ошибок.
	Для Ном = 0 По Ошибка.Количество() - 1 Цикл
		ТекстСообщения = Ошибка.Получить(Ном);
		УправлениеНебольшойФирмойСервер.СообщитьОбОшибке(ЭтотОбъект, ТекстСообщения);
	КонецЦикла;
	
	Возврат Ошибка;
	
КонецФункции // ПроверитьЗаполнениеНалоговыхРеквизитов()

// Процедура проверяет устанавливает готовность.
//
&НаСервере
Процедура УстановитьГотовность(ТекущаяГотовность, НоваяГотовность)
	
	Если ЗначениеЗаполнено(ТекущаяГотовность)
	   И ТекущаяГотовность < НоваяГотовность Тогда
		ТекущаяГотовность = НоваяГотовность;
	ИначеЕсли НЕ ЗначениеЗаполнено(ТекущаяГотовность) Тогда
		ТекущаяГотовность = НоваяГотовность;
	КонецЕсли;
	
КонецПроцедуры // УстановитьГотовность()

// Процедура добавляет замечание.
//
&НаСервере
Процедура ДобавитьЗамечание(СтруктураДокумента, НоваяГотовность, ТекстЗамечания)
	
	УстановитьГотовность(СтруктураДокумента.Готовность, НоваяГотовность);
	ДобавитьВСтроку(СтруктураДокумента.ОписаниеОшибок, ТекстЗамечания);
	
КонецПроцедуры // ДобавитьЗамечание()

// Процедура добавляет строку.
//
&НаСервере
Процедура ДобавитьВСтроку(Буфер, НоваяСтрока)
	
	Если ПустаяСтрока(Буфер) Тогда
		Буфер = НоваяСтрока;
	Иначе
		Буфер = Буфер + Символы.ПС + НоваяСтрока;
	КонецЕсли;
	
КонецПроцедуры // ДобавитьВСтроку()

// Функция создает соответствие из строки.
//
&НаСервере
Функция СоздатьСоответствиеИзСтроки(Знач СтрокаЧерезЗапятую)
	
	НовоеСоответствие = Новый Соответствие;
	ПозицияРазделителя = Найти(СтрокаЧерезЗапятую, ",");
	Пока ПозицияРазделителя > 0 Цикл
		ИмяИтема = Лев(СтрокаЧерезЗапятую, ПозицияРазделителя - 1);
		НовоеСоответствие.Вставить(ИмяИтема, Истина);
		СтрокаЧерезЗапятую = Сред(СтрокаЧерезЗапятую, ПозицияРазделителя + 1);
		ПозицияРазделителя = Найти(СтрокаЧерезЗапятую, ",");
	КонецЦикла;
	Если СтрДлина(СтрокаЧерезЗапятую) > 0 Тогда
		НовоеСоответствие.Вставить(СтрокаЧерезЗапятую, Истина);
	КонецЕсли;
	
	Возврат НовоеСоответствие;
	
КонецФункции // СоздатьСоответствиеИзСтроки()

// Процедура заполняет таблицу для выгрузки.
//
&НаСервере
Процедура ЗаполнитьВыгрузка()
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ПлатежноеПоручение.Дата,
	|	ПлатежноеПоручение.Номер,
	|	ПлатежноеПоручение.НазначениеПлатежа,
	|	ПлатежноеПоручение.ВидПлатежа,
	|	ПлатежноеПоручение.Ссылка КАК Документ,
	|	ПлатежноеПоручение.ПоказательДаты,
	|	ПлатежноеПоручение.ПоказательНомера,
	|	ПлатежноеПоручение.ПоказательОснования,
	|	ПлатежноеПоручение.ПоказательТипа,
	|	ПлатежноеПоручение.ПоказательПериода,
	|	ПлатежноеПоручение.СуммаДокумента,
	|	ПлатежноеПоручение.Контрагент,
	|	ПлатежноеПоручение.ВидОперации,
	|	ПлатежноеПоручение.ОчередностьПлатежа,
	|	ПлатежноеПоручение.ТекстПлательщика,
	|	ПлатежноеПоручение.ТекстПолучателя,
	|	ПлатежноеПоручение.ИННПлательщика КАК ИННПлательщика,
	|	ПлатежноеПоручение.ЕДРПОУПлательщика КАК ЕДРПОУПлательщика,
	|	ПлатежноеПоручение.ИННПолучателя КАК ИННПолучателя,
	|	ПлатежноеПоручение.ЕДРПОУПолучателя КАК ЕДРПОУПолучателя,
	|	ПлатежноеПоручение.КодБК,
	|	ПлатежноеПоручение.КодКОАТУУ,
	|	ПлатежноеПоручение.Организация.НаименованиеПолное КАК Организация,
	|	ПлатежноеПоручение.Организация.НаименованиеПлательщикаПриПеречисленииНалогов КАК ОрганизацияПеречислениеНалога,
	|	ПлатежноеПоручение.Организация.ИНН КАК ОрганизацияИНН,
	|	ПлатежноеПоручение.Организация.КодПоЕДРПОУ КАК ОрганизацияЕДРПОУ,
	|	ПлатежноеПоручение.БанковскийСчет КАК ОрганизацияСчет,
	|	ПлатежноеПоручение.БанковскийСчет.НомерСчета КАК ОрганизацияНомерСчета,
	|	ПлатежноеПоручение.БанковскийСчет.Банк.Код КАК ОрганизацияМФОБанка,
	|	ПлатежноеПоручение.БанковскийСчет.Банк КАК ОрганизацияБанк,
	|	ПлатежноеПоручение.БанковскийСчет.Банк.КоррСчет КАК ОрганизацияРасчСчет,
	|	ПлатежноеПоручение.БанковскийСчет.Банк.Город КАК ОрганизацияГородБанка,
	|	ПлатежноеПоручение.БанковскийСчет.БанкРасчетов КАК ОрганизацияБанкРасчетов,
	|	ПлатежноеПоручение.БанковскийСчет.БанкРасчетов.Город КАК ОрганизацияГородРЦБанка,
	|	ПлатежноеПоручение.БанковскийСчет.БанкРасчетов.Код КАК ОрганизацияМФОРЦБанка,
	|	ПлатежноеПоручение.БанковскийСчет.БанкРасчетов.КоррСчет КАК ОрганизацияКоррСчетРЦБанка,
	|	ПлатежноеПоручение.Контрагент.ИНН КАК КонтрагентИНН,
	|	ПлатежноеПоручение.Контрагент.КодПоЕДРПОУ КАК КонтрагентЕДРПОУ,
	|	ПлатежноеПоручение.СчетКонтрагента КАК КонтрагентСчет,
	|	ПлатежноеПоручение.СчетКонтрагента.НомерСчета КАК КонтрагентНомерСчета,
	|	ПлатежноеПоручение.СчетКонтрагента.Банк КАК КонтрагентБанк,
	|	ПлатежноеПоручение.СчетКонтрагента.Банк.КоррСчет КАК КонтрагентРасчСчет,
	|	ПлатежноеПоручение.СчетКонтрагента.Банк.Город КАК КонтрагентГородБанка,
	|	ПлатежноеПоручение.СчетКонтрагента.БанкРасчетов КАК КонтрагентБанкРасчетов,
	|	ПлатежноеПоручение.СчетКонтрагента.БанкРасчетов.Город КАК КонтрагентГородРЦБанка,
	|	ПлатежноеПоручение.СчетКонтрагента.Банк.Код КАК КонтрагентМФОБанка,
	|	ПлатежноеПоручение.СчетКонтрагента.БанкРасчетов.Код КАК КонтрагентМФОРЦБанка,
	|	ПлатежноеПоручение.СчетКонтрагента.БанкРасчетов.КоррСчет КАК КонтрагентКоррСчетРЦБанка,
	|	""Платежное поручение"" КАК ВидДокумента,
	|	ВЫРАЗИТЬ("""" КАК СТРОКА(255)) КАК ОписаниеОшибок,
	|	0 КАК Готовность,
	|	0 КАК НомерКартинки,
	|	ИСТИНА КАК Выгружать
	|ИЗ
	|	Документ.ПлатежноеПоручение КАК ПлатежноеПоручение
	|ГДЕ
	|	ПлатежноеПоручение.Организация = &Организация
	|	И ПлатежноеПоручение.БанковскийСчет = &БанковскийСчет
	|	И ПлатежноеПоручение.Дата МЕЖДУ &НачПериода И &КонПериода
	|	И ПлатежноеПоручение.ПометкаУдаления = ЛОЖЬ
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПлатежноеПоручение.Дата,
	|	Документ");
	
	Запрос.УстановитьПараметр("Организация", Объект.Организация);
	Запрос.УстановитьПараметр("БанковскийСчет", Объект.БанковскийСчет);
	Запрос.УстановитьПараметр("НачПериода", НачалоДня(Объект.НачПериода));
	Запрос.УстановитьПараметр("КонПериода", КонецДня(Объект.КонПериода));
	
	Выгрузка = Запрос.Выполнить().Выгрузить();
	
	Для каждого СтрокаДокумента Из Выгрузка Цикл
		ПроверитьНаКорректностьИПустоеЗначениеЭкспорта(СтрокаДокумента);
		СтрокаДокумента.Выгружать = ПустаяСтрока(СтрокаДокумента.ОписаниеОшибок);
		СтрокаДокумента.НазначениеПлатежа = СокрЛП(СтрокаДокумента.НазначениеПлатежа);
		СтрокаДокумента.НомерКартинки = ?(ПустаяСтрока(СтрокаДокумента.ОписаниеОшибок), 0, 1);
	КонецЦикла;
	
	Объект.Выгрузка.Загрузить(Выгрузка);
	
КонецПроцедуры // ЗаполнитьВыгрузка()

// Процедура выгружает данные в файл.
//
&НаСервере
Функция ВыгрузитьДанныеВФайл()
	
	ПотокВыгрузки = РеквизитФормыВЗначение("Объект").Выгрузить();
	Если РасширениеРаботыСФайламиПодключено Тогда
		
		Возврат ПотокВыгрузки;
		
	Иначе
		
		Возврат ПоместитьВоВременноеХранилище(ПотокВыгрузки.ПолучитьТекст());
		
	КонецЕсли;
	
КонецФункции // ВыгрузитьДанныеВФайл()

// Функция сохраняет файл выгрузки.
//
&НаКлиенте
Процедура СохранитьФайлВыгрузки(ПотокВыгрузки)
	
	Попытка
		
		Если НЕ РасширениеРаботыСФайламиПодключено Тогда
		
			ПолучитьФайл(ПотокВыгрузки, "1c_to_kb.txt", Истина);
			
		Иначе
		
			Если Объект.Кодировка = "DOS" Тогда
				ПотокВыгрузки.Записать(Объект.ФайлВыгрузки, КодировкаТекста.OEM);
			ИначеЕсли Объект.Кодировка = "Windows" Тогда
				ПотокВыгрузки.Записать(Объект.ФайлВыгрузки, КодировкаТекста.ANSI);
			Иначе
				ПотокВыгрузки.Записать(Объект.ФайлВыгрузки, КодировкаТекста.UTF8);
			КонецЕсли;
			
			// Отметим те документы которые успешно выгрузились.
			Для каждого СтрокаСекции Из Объект.Выгрузка Цикл
				Если СтрокаСекции.Готовность = - 2 Тогда
					СтрокаСекции.Готовность = - 1;
				КонецЕсли;
			КонецЦикла;
			
			ТекстСообщения = НСтр("ru='Данные успешно выгружены в файл %ФайлВыгрузки%';uk='Дані успішно вивантажені у файл %ФайлВыгрузки%'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ФайлВыгрузки%", Объект.ФайлВыгрузки);
			УправлениеНебольшойФирмойКлиент.СообщитьОбОшибке(, ТекстСообщения);
			
		КонецЕсли;
		
	Исключение
		
		ТекстСообщения = НСтр("ru='Не удалось записать данные в файл. Возможно, отсутствует каталог. %ФайлВыгрузки%';uk='Не вдалося записати дані у файл. Можливо, відсутній каталог. %ФайлВыгрузки%'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ФайлВыгрузки%", Объект.ФайлВыгрузки);
		УправлениеНебольшойФирмойКлиент.СообщитьОбОшибке(, ТекстСообщения);
		
	КонецПопытки
	
КонецПроцедуры // СохранитьФайлВыгрузки()

// Процедура загружает данные из файла.
//
&НаСервере
Процедура ЗагрузитьДанныеИзФайла()
	
	РеквизитФормыВЗначение("Объект").Загрузить(ИмпортЗаголовок);
	
КонецПРоцедуры // ЗагрузитьДанныеИзФайла()

// Процедура устанавливает флаги.
//
&НаКлиенте
Процедура УстановитьФлаги(Таблица, Поле, ЗначениеФлага)
	
	Для каждого Строка Из Таблица Цикл
		Строка[Поле] = ЗначениеФлага;
	КонецЦикла;
	
КонецПроцедуры // УстановитьФлаги()

// Функция получает дату из строки.
//
&НаСервере
Функция ПолучитьДатуИзСтроки(Приемник, Источник)
	
	Буфер = Источник;
	
	ПозицияТочки = Найти(Буфер, "-");
	
	Если ПозицияТочки = 0 Тогда
		Возврат НСтр("ru='Неверный формат строки с датой';uk='Невірний формат рядку з датою'");
	КонецЕсли;
	
	ГодДаты = Лев(Буфер, ПозицияТочки - 1);
	Буфер = Сред(Буфер, ПозицияТочки + 1);
	
	ПозицияТочки = Найти(Буфер, "-");
	
	Если ПозицияТочки = 0 Тогда
		Возврат НСтр("ru='Неверный формат строки с датой';uk='Невірний формат рядку з датою'");
	КонецЕсли;
	
	МесяцДаты = Лев(Буфер, ПозицияТочки - 1);
	ЧислоДаты = Сред(Буфер, ПозицияТочки + 1);
	Если СтрДлина(ГодДаты) = 2 Тогда
		Если Число(ГодДаты) < 50 Тогда
			ГодДаты = "20" + ГодДаты;
		Иначе
			ГодДаты = "19" + ГодДаты ;
		КонецЕсли;
	КонецЕсли;
	
	Попытка
		Приемник = Дата(Число(ГодДаты), Число(МесяцДаты), Число(ЧислоДаты));
		
	Исключение
		Возврат НСтр("ru='Не удалось преобразовать строку в дату';uk='Не вдалося перетворити строку в дату'");
		
	КонецПопытки;
	
	Возврат Приемник;
	
КонецФункции // ПолучитьДатуИзСтроки()

// Функция определяет является ли организация плательщиком.
//
&НаСервере
Функция ОрганизацияПлательщик(ВидДокумента)
	
	Если ВидДокумента = "ПоступлениеНаСчет" Тогда
		Возврат Ложь;
	Иначе
		Возврат Истина;
	КонецЕсли;
	
КонецФункции // ОрганизацияПлательщик()

// Функция находит договор контрагента.
//
&НаСервере
Функция НайтиДоговор(ВладелецДоговора, ОрганизацияДоговора = Неопределено, СписокВидовДоговора = Неопределено)
	
	НовыйДоговор = Справочники.ДоговорыКонтрагентов.ПустаяСсылка();
	Запрос = Новый Запрос;
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 2
	|	ДоговорыКонтрагентов.Ссылка,
	|	ВЫБОР
	|		КОГДА СправочникВладелец.Ссылка ЕСТЬ НЕ NULL 
	|			ТОГДА 1
	|		ИНАЧЕ 2
	|	КОНЕЦ КАК Приоритет
	|ИЗ
	|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Контрагенты КАК СправочникВладелец
	|		ПО ДоговорыКонтрагентов.Владелец = СправочникВладелец.Ссылка
	|			И ДоговорыКонтрагентов.Ссылка = СправочникВладелец.ДоговорПоУмолчанию
	|ГДЕ
	|	&ТекстФильтра
	|
	|УПОРЯДОЧИТЬ ПО
	|	Приоритет";
	
	Запрос.УстановитьПараметр("ВладелецДоговора", ВладелецДоговора);
	Запрос.УстановитьПараметр("ОрганизацияДоговора", ОрганизацияДоговора);
	Запрос.УстановитьПараметр("СписокВидовДоговора", СписокВидовДоговора);
	
	ТекстФильтра =
	"	ДоговорыКонтрагентов.Владелец = &ВладелецДоговора"
  + ?(ОрганизацияДоговора <> Неопределено, "
	|	И ДоговорыКонтрагентов.Организация = &ОрганизацияДоговора", "") 
  +	"	И ДоговорыКонтрагентов.ПометкаУдаления = ЛОЖЬ"
  + ?(СписокВидовДоговора <> Неопределено, "
	|	И ДоговорыКонтрагентов.ВидДоговора В (&СписокВидовДоговора)", "");
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстФильтра", ТекстФильтра);
	Запрос.Текст = ТекстЗапроса;
	Результат = Запрос.Выполнить();
	Если НЕ Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		Возврат Выборка.Ссылка;
	Иначе
		Возврат НСтр("ru='Не найден';uk='Не знайдений'");
	КонецЕсли;
	
КонецФункции // НайтиДоговор()

// Функция формирует соответствие загружаемых.
//
&НаСервере
Функция СформироватьСоответствиеЗагружаемых()
	
	ИмпортЗагружаемые = СоздатьСоответствиеИзСтроки(
		ВРег("ВидДокумента,Номер,Дата,ДокументИД,СтатусДокумента,Сумма,КодВалюты,ВидОплаты,"
		   + "ПлательщикСчет,Плательщик,ПлательщикБанк,ПлательщикМФО,"
		   + "ПолучательСчет,Получатель,ПолучательБанк,ПолучательМФО,"
		   + "ПлательщикОКПО,ПолучательОКПО,"
		   + "НазначениеПлатежа,"
		   + "НомерСчетаПоставщика,ДатаОтсылкиДок"
		)
	);
	
	Возврат ИмпортЗагружаемые;
	
КонецФункции // СформироватьСоответствиеЗагружаемых()

// Процедура формирует соответствие непустых при загрузке.
//
&НаСервере
Процедура СформироватьСоответствияНеПустыхПриИмпорте(ИмпортНеПустые, ИмпортНеПустыеПлатежноеПоручение, ИмпортНеПустыеПлатежноеПоручениеБюджет)
	
	ИмпортНеПустыеПлатежноеПоручение = СоздатьСоответствиеИзСтроки(
		"Номер,Дата,Сумма,ПлательщикСчет,ПолучательСчет"
	);
	
	// По наличию статуса составителя определяется что платеж - налоговый.
	ИмпортНеПустыеПлатежноеПоручениеБюджет = СоздатьСоответствиеИзСтроки(
		"Номер,Дата,Сумма,ПлательщикСчет,ПолучательСчет,"
	  + "ПлательщикОКПО,ПолучательОКПО,"
	  + ""
	);
	
	ИмпортНеПустые = Новый Массив;
	ИмпортНеПустые.Добавить(ИмпортНеПустыеПлатежноеПоручение);
	ИмпортНеПустые.Добавить(ИмпортНеПустыеПлатежноеПоручениеБюджет);
	
КонецПроцедуры // СформироватьСоответствияНеПустыхПриИмпорте()

// Функция получает строку импорта.
//
&НаСервере
Функция ПолучитьСтрокуИмпорта(ИмпортТекущаяСтрока, ИмпортКоличествоСтрок, ИмпортТекстДляРазбора)
	
	Буфер = "";
	Пока ПустаяСтрока(Буфер)
	 ИЛИ Лев(Буфер, 2) = "//" Цикл
		Если ИмпортТекущаяСтрока > ИмпортКоличествоСтрок Тогда
			Возврат "";
		КонецЕсли;
		Буфер = СокрЛП(СтрПолучитьСтроку(ИмпортТекстДляРазбора, ИмпортТекущаяСтрока));
		ИмпортТекущаяСтрока = ИмпортТекущаяСтрока + 1;
	КонецЦикла;
	
	Возврат Буфер;
	
КонецФункции // ПолучитьСтрокуИмпорта()

// Функция разбирает теговую строку.
//
&НаСервере
Функция РазобратьТеговуюСтроку(СтрокаРазбора, Тег, Значение)
	
	ПозицияПрисваивания = Найти(СтрокаРазбора, "=");
	Если ПозицияПрисваивания = 0 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Тег = ВРег(СокрЛП(Лев(СтрокаРазбора, ПозицияПрисваивания - 1)));
	Значение = СокрЛП(Сред(СтрокаРазбора, ПозицияПрисваивания + 1));
	
	Возврат НЕ ПустаяСтрока(Тег);
	
КонецФункции // РазобратьТеговуюСтроку()

// Функция загружает секцию документа
//
&НаСервере
Функция ЗагрузитьСекциюДокумента(СтрокаДокумента, ИмпортТекущаяСтрока, ИмпортКоличествоСтрок, ИмпортТекстДляРазбора, ИмпортЗагружаемые)
	
	СтрокаРазбора = ПолучитьСтрокуИмпорта(ИмпортТекущаяСтрока, ИмпортКоличествоСтрок, ИмпортТекстДляРазбора);
	Пока Лев(Врег(СокрЛП(СтрокаРазбора)), 14) <> "КОНЕЦДОКУМЕНТА" Цикл
		Значение = "";
		Тег = "";
		Если РазобратьТеговуюСтроку(СтрокаРазбора, Тег, Значение) Тогда
			Если ИмпортЗагружаемые[Тег] = Истина Тогда
				СтрокаДокумента[Тег] = Значение;
			Иначе
				
				// Неправиьный реквизит заголовка.
				ТекстСообщения = НСтр("ru='Неверный реквизит платежного документа, строка %Импорт%: %СтрокаРазбора%';uk='Невірний реквізит платіжного документа, рядок %Импорт%: %СтрокаРазбора%'"
				);
				ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Импорт%", (ИмпортТекущаяСтрока - 1));
				ТекстСообщения = СтрЗаменить(ТекстСообщения, "%СтрокаРазбора%", СтрокаРазбора);
				УправлениеНебольшойФирмойСервер.СообщитьОбОшибке(ЭтотОбъект, ТекстСообщения);
				Возврат Ложь;
				
			КонецЕсли;
		Иначе
			
			// Неправиьный реквизит заголовка.
			ТекстСообщения = НСтр("ru='Нарушена структура платежного документа, строка %Импорт%: %СтрокаРазбора%';uk='Порушена структура платіжного документа, рядок %Импорт%: %СтрокаРазбора%'"
			);
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Импорт%", (ИмпортТекущаяСтрока - 1));
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%СтрокаРазбора%", СтрокаРазбора);
			УправлениеНебольшойФирмойСервер.СообщитьОбОшибке(ЭтотОбъект, ТекстСообщения);
			Возврат Ложь;
			
		КонецЕсли;
		СтрокаРазбора = ПолучитьСтрокуИмпорта(ИмпортТекущаяСтрока, ИмпортКоличествоСтрок, ИмпортТекстДляРазбора);
	КонецЦикла;
	
	Возврат Истина;
	
КонецФункции // ЗагрузитьСекциюДокумента()

// Функция загружает секции расчетного счета.
//
&НаСервере
Функция ЗагрузитьСекциюРасчСчета(СтрокаРССчета, ИмпортТекущаяСтрока, ИмпортКоличествоСтрок, ИмпортТекстДляРазбора, ТегиРасчетногоСчета)
	
	СтрокаРазбора = ПолучитьСтрокуИмпорта(ИмпортТекущаяСтрока, ИмпортКоличествоСтрок, ИмпортТекстДляРазбора);
	Значение = "";
	Тег = "";
	Пока РазобратьТеговуюСтроку(СтрокаРазбора, Тег, Значение) Цикл
		Если ТегиРасчетногоСчета[Тег] = Истина Тогда
			СтрокаРССчета[Тег] = Значение;
		Иначе
			// Неправиьный реквизит заголовка.
			ТекстСообщения = НСтр("ru='Неверный реквизит в секции описания расчетного счета, строка %Импорт%: %СтрокаРазбора%';uk='Невірний реквізит у секції опису розрахункового рахунку, рядок %Импорт%: %СтрокаРазбора%'"
			);
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Импорт%", (ИмпортТекущаяСтрока - 1));
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%СтрокаРазбора%", СтрокаРазбора);
			УправлениеНебольшойФирмойСервер.СообщитьОбОшибке(ЭтотОбъект, ТекстСообщения);
			Возврат Ложь;
		КонецЕсли;
		СтрокаРазбора = ПолучитьСтрокуИмпорта(ИмпортТекущаяСтрока, ИмпортКоличествоСтрок, ИмпортТекстДляРазбора);
		Значение = "";
		Тег = "";
	КонецЦикла;
	
	Если ВРЕГ(Лев(СокрЛП(СтрокаРазбора), 13)) = "КОНЕЦРАСЧСЧЕТ" Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции // ЗагрузитьСекциюРасчСчета()

&НаСервере
// Функция определяет принадлежность счета организации.
//
Функция СчетПринадлежитОрганизации(НомерСчета)
	
	ЗапросПоСчету = Новый Запрос(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	БанковскиеСчета.Владелец,
	|	БанковскиеСчета.НомерСчета,
	|	БанковскиеСчета.Ссылка
	|ИЗ
	|	Справочник.БанковскиеСчета КАК БанковскиеСчета
	|ГДЕ
	|	БанковскиеСчета.Владелец = &Организация
	|	И БанковскиеСчета.НомерСчета = &НомерСчета");
	
	ЗапросПоСчету.УстановитьПараметр("НомерСчета", НомерСчета);
	ЗапросПоСчету.УстановитьПараметр("Организация", Объект.Организация);
	ВыборкаЗапроса = ЗапросПоСчету.Выполнить().Выбрать();
	
	Возврат ВыборкаЗапроса.Следующий();
	
КонецФункции // СчетПринадлежитОрганизации()

// Процедура проверяет на пустое значение при импорте.
//
&НаСервере
Процедура ПроверитьНаПустоеЗначениеИмпорта(СтрокаИмпорта, ИмяСвойства, ПредставлениеСвойства, ИмпортНеПустые)
	
	Если ИмпортНеПустые[0][ИмяСвойства] = Истина Тогда
		Если НЕ ЗначениеЗаполнено(СтрокаИмпорта[ИмяСвойства]) Тогда
			СтрокаЗамечание = НСтр("ru='Не заполнено ""%ИмяСвойства%""!';uk='Не заповнене ""%ИмяСвойства%""!'");
			СтрокаЗамечание = СтрЗаменить(СтрокаЗамечание, "%ИмяСвойства%", ИмяСвойства);
			ДобавитьЗамечание(СтрокаИмпорта, 3, СтрокаЗамечание);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // ПроверитьНаПустоеЗначениеИмпорта()

// Функция загружает заголовок файла обмена.
//
&НаСервере
Функция ЗагрузитьСтрокуЗаголовка(ТекстСтрокиЗаголовка, ТегиЗаголовка, ИмпортЗаголовок, ИмпортТекущаяСтрока)
	
	Значение = "";
	Тег = "";
	РазобратьТеговуюСтроку(ТекстСтрокиЗаголовка, Тег, Значение);
	Если ТегиЗаголовка[Тег] = Истина Тогда
		ИмпортЗаголовок[Тег] = Значение;
	Иначе
		
		// Неправиьный реквизит заголовка.
		ТекстСообщения = НСтр("ru='Неверный реквизит заголовка, строка %Импорт%: %ТекстСтрокиЗаголовка%';uk='Невірний реквізит заголовка, рядок %Импорт%: %ТекстСтрокиЗаголовка%'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Импорт%", (ИмпортТекущаяСтрока - 1));
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ТекстСтрокиЗаголовка%", ТекстСтрокиЗаголовка);
		УправлениеНебольшойФирмойСервер.СообщитьОбОшибке(ЭтотОбъект, ТекстСообщения);
		
	КонецЕсли;
	
КонецФункции // ЗагрузитьСтрокуЗаголовка()

// Функция проверяет наличие в строке только цифр.
//
// Параметры
//  СтрокаПроверки - Строка для проверки только цифр
//
// Возвращаемое значение:
//   Булево
//
&НаСервере
Функция ЕстьНеЦифры(Знач СтрокаПроверки)
	
	Если ТипЗнч(СтрокаПроверки) <> Тип("Строка") Тогда
		Возврат Истина;
	КонецЕсли;
	СтрокаПроверки = СокрЛП(СтрокаПроверки);
	Длина = СтрДлина(СтрокаПроверки);
	Для Сч = 1 По Длина Цикл
		Если Найти("0123456789", Сред(СтрокаПроверки, Сч, 1)) = 0 Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции // ЕстьНеЦифры()

// Процедура распознает данные в строке документа.
//
&НаСервере
Процедура РаспознатьДанныеВСтрокеДокумента(СтрокаДокумента)
	
	ПустаяДата = Дата("00010101");
	
	// 1) Определим вид платежа – входящий или исходящий.
	ПлатежноеПоручение = ВРег(СтрЗаменить(СокрЛП(СтрокаДокумента.Операция), " ", "")) = "ПЛАТЕЖНОЕПОРУЧЕНИЕ";
	Исходяший = (СтрокаДокумента.ПлательщикСчет = Объект.БанковскийСчет.НомерСчета);
	
	// 2) Определим вид документа в программе.
	ВидДокумента = ?(Исходяший, "РасходСоСчета", "ПоступлениеНаСчет");
	ИмяДокумента = ?(Исходяший, "Расход со счета", "Поступление на счет");
	
	СтрокаДокумента.ИмяДокумента = ИмяДокумента;
	СтрокаДокумента.ВидДокумента = ВидДокумента;
	РеквизитСчета = ?(Исходяший, "БанковскийСчет", "СчетКонтрагента");
	
	// 3) Найдем ранее загруженный (введенный вручную) документ.
	// Реквизиты для поиска: Вид документа, Дата, Номер, Номер счета.
	
	// Распознаем дату документа.
	ДатаДок = ПустаяДата;
	
	Если НЕ ПустаяСТрока(СтрокаДокумента.ДатаСписано) Тогда
		Результат = ПолучитьДатуИзСтроки(ДатаДок, СтрокаДокумента.ДатаСписано);
	ИначеЕсли НЕ ПустаяСТрока(СтрокаДокумента.ДатаПоступило) Тогда
		Результат = ПолучитьДатуИзСтроки(ДатаДок, СтрокаДокумента.ДатаПоступило);
	Иначе
		Результат = ПолучитьДатуИзСтроки(ДатаДок, СтрокаДокумента.Дата);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Результат) Тогда
		СтрокаДокумента.ДатаДок = Результат;
		НомерДляПоискаДок = СтрокаДокумента.Номер;
		РеквизитДаты = "ДатаВходящегоДокумента";
		РеквизитНомера = "НомерВходящегоДокумента";
		ВсеРеквизитыПоискаЕсть = Истина;
	КонецЕсли;
	
	СтрокаДокумента.НомерДок = СтрокаДокумента.Номер;
	
	Если ВсеРеквизитыПоискаЕсть Тогда
		
		// При наличии нескольких предпочтение отдается первому, с совпадающим
		// номером счета.
		ЗапросПоискаДокумента = Новый Запрос;
		ЗапросПоискаДокумента.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ПлатежныеДокументы.Ссылка,
		|	ПлатежныеДокументы.Проведен,
		|	ПлатежныеДокументы." + РеквизитНомера + " КАК Номер,
		|	ПлатежныеДокументы." + РеквизитДаты + " КАК Дата,
		|	ПлатежныеДокументы.Организация
		|ИЗ
		|	Документ." + СтрокаДокумента.ВидДокумента + " КАК ПлатежныеДокументы
		|ГДЕ
		|	НАЧАЛОПЕРИОДА(ПлатежныеДокументы." + РеквизитДаты + ", ДЕНЬ)= &ДатаДок
		|	И ПлатежныеДокументы.БанковскийСчет = &БанковскийСчет
		|	И ПлатежныеДокументы.Организация = &Организация";
		
		ЗапросПоискаДокумента.УстановитьПараметр("ДатаДок", ДатаДок);
		ЗапросПоискаДокумента.УстановитьПараметр("Организация", Объект.Организация);
		ЗапросПоискаДокумента.УстановитьПараметр("БанковскийСчет", Объект.БанковскийСчет);
		Результат = ЗапросПоискаДокумента.Выполнить().Выбрать();
		СчетДляПоискаДок = ?(Исходяший, СтрокаДокумента.ПолучательСчет, СтрокаДокумента.ПлательщикСчет);
		ДлинаНомера = СтрДлина(НомерДляПоискаДок);
		КоличествоДок = 0;
		
		Пока Результат.Следующий() Цикл
			ВыборкаНомер = Прав(СокрЛП(Результат.Номер), ДлинаНомера);
			Если ВыборкаНомер = НомерДляПоискаДок Тогда
				Если КоличествоДок = 0 Тогда
					СтрокаДокумента.Документ = Результат.Ссылка;
					СтрокаДокумента.Проведен = Результат.Проведен;
					СтрокаДокумента.НомерДок = Результат.Номер;
					СтрокаДокумента.ДатаДок = Результат.Дата;
				КонецЕсли;
				КоличествоДок = КоличествоДок + 1;
			КонецЕсли;
		КонецЦикла;
		
		Если КоличествоДок > 1 Тогда
			СтрокаЗамечание = НСтр("ru='В информационной базе найдено несколько(%КоличествоДок%) соответствующих документов!';uk='В інформаційній базі знайдено декілька(%КоличествоДок%) відповідних документів!'");
			СтрокаЗамечание = СтрЗаменить(СтрокаЗамечание, "%КоличествоДок%", КоличествоДок);
			ДобавитьЗамечание(СтрокаДокумента, 1, СтрокаЗамечание);
		КонецЕсли;
		
		// Если документ уже есть в ИБ, то берем все данные из него.
		ДокументНайден = ЗначениеЗаполнено(СтрокаДокумента.Документ);
		Если ДокументНайден Тогда
			Документ = СтрокаДокумента.Документ; 
			СтрокаДокумента.ВидОперации = Документ.ВидОперации; 
			СтрокаДокумента.СтатьяДДС = Документ.Статья; 
			СтрокаДокумента.БанковскийСчет = Объект.БанковскийСчет;
			СтрокаДокумента.Контрагент = Документ.Контрагент;
			Если Документ.РасшифровкаПлатежа.Количество() <> 0 Тогда
				СтрокаДокумента.Договор = Документ.РасшифровкаПлатежа[0].Договор;
				СтрокаДокумента.ПризнакАванса = Документ.РасшифровкаПлатежа[0].ПризнакАванса;
				СтрокаДокумента.Заказ = Документ.РасшифровкаПлатежа[0].Заказ;
			КонецЕсли;	
		КонецЕсли;
		
	КонецЕсли;
	
	// 4) Определяем вид операции документа.
	Если НЕ ЗначениеЗаполнено(СтрокаДокумента.ВидОперации) Тогда
		Если Исходяший Тогда
			Если Справочники.БанковскиеСчета.НайтиПоРеквизиту("НомерСчета", СтрокаДокумента.ПолучательСчет).Владелец = Объект.БанковскийСчет.Владелец Тогда // перевод на другой счет
				ВидОперацииДокумента = Перечисления.ВидыОперацийРасходСоСчета.Прочее;
			Иначе // оплата поставщику
				ВидОперацииДокумента = Перечисления.ВидыОперацийРасходСоСчета.Поставщику;
			КонецЕсли; 
		Иначе
			ВидОперацииДокумента = Перечисления.ВидыОперацийПоступлениеНаСчет.ОтПокупателя;
		КонецЕсли;
		СтрокаДокумента.ВидОперации = ВидОперацииДокумента;
	Иначе
		ВидОперацииДокумента = СтрокаДокумента.ВидОперации;
	КонецЕсли;
	
	// 5) Определяем банковский счет организации
	Если НЕ ЗначениеЗаполнено(СтрокаДокумента.БанковскийСчет) Тогда
		СтрокаДокумента.БанковскийСчет = Объект.БанковскийСчет;
	КонецЕсли;
	
	// 6) Определяем банковский счет контрагента
	Если НЕ ЗначениеЗаполнено(СтрокаДокумента.СчетКонтрагента) Тогда
		ЗапросПоискаСчета = Новый Запрос;
		Если ОрганизацияПлательщик(ВидДокумента) Тогда
			СчетКонтрагента = СтрокаДокумента.ПолучательСчет;
			ИННКонтрагента = СтрокаДокумента.ПолучательИНН;
			ЕДРПОУКонтрагента = СтрокаДокумента.ПолучательОКПО;
			//Если ЗначениеЗаполнено(СтрокаДокумента.Получатель1) Тогда
			//	ИмяКонтрагента = СтрокаДокумента.Получатель1;
			//Иначе
				ИмяКонтрагента = СтрокаДокумента.Получатель;
			//КонецЕсли;
			ЗапросПоискаСчета.УстановитьПараметр("НомерСчета", СтрокаДокумента.ПолучательСчет);
		Иначе
			ЗапросПоискаСчета.УстановитьПараметр("НомерСчета", СтрокаДокумента.ПлательщикСчет);
			СчетКонтрагента = СтрокаДокумента.ПлательщикСчет;
			ИННКонтрагента = СтрокаДокумента.ПлательщикИНН;
			ЕДРПОУКонтрагента = СтрокаДокумента.ПлательщикОКПО;
			//Если СтрокаДокумента.Плательщик1 <> "" Тогда
			//	ИмяКонтрагента = СтрокаДокумента.Плательщик1;
			//Иначе
				ИмяКонтрагента = СтрокаДокумента.Плательщик;
			//КонецЕсли;
		КонецЕсли;
		ЗапросПоискаСчета.УстановитьПараметр("ИННКонтрагента", ИННКонтрагента);
		ЗапросПоискаСчета.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	БанковскиеСчета.Владелец,
		|	БанковскиеСчета.Ссылка,
		|	БанковскиеСчета.НомерСчета
		|ИЗ
		|	Справочник.БанковскиеСчета КАК БанковскиеСчета
		|ГДЕ
		|	БанковскиеСчета.Владелец ССЫЛКА Справочник.Контрагенты 
		|	" + ?(НЕ ПустаяСтрока(ИННКонтрагента), "И БанковскиеСчета.Владелец.ИНН = &ИННКонтрагента", "") + "
		|	И БанковскиеСчета.НомерСчета = &НомерСчета";
		
		ВыборкаПоиска = ЗапросПоискаСчета.Выполнить().Выбрать();
		Контрагент = Справочники.Контрагенты.ПустаяСсылка();
		
		Если ВыборкаПоиска.Следующий() Тогда
			СтрокаДокумента.СчетКонтрагента = ВыборкаПоиска.Ссылка;
			Контрагент = ВыборкаПоиска.Владелец;
		Иначе  
			СтрокаЗамечание = НСтр("ru='Не найден счет контрагента (%СчетКонтрагента%).';uk='Не знайдений рахунок контрагента (%СчетКонтрагента%).'");
			СтрокаЗамечание = СтрЗаменить(СтрокаЗамечание, "%СчетКонтрагента%", СчетКонтрагента);
			ДобавитьЗамечание(СтрокаДокумента, 2, СтрокаЗамечание);
			СтрокаСчетКонтрагента = НСтр("ru=' Не найден (%СчетКонтрагента%).';uk=' Не знайдений (%СчетКонтрагента%).'");
			СтрокаСчетКонтрагента = СтрЗаменить(СтрокаЗамечание, "%СчетКонтрагента%", СчетКонтрагента);
			СтрокаДокумента.СчетКонтрагента = СтрокаСчетКонтрагента;
		КонецЕсли;
		
		Если ВыборкаПоиска.Количество() > 1 Тогда
			СтрокаЗамечание = НСтр("ru='В информационной базе найдено несколько(%Количество%) одинаковых банковских счетов!';uk='В інформаційній базі знайдено декілька(%Количество%) однакових банківських рахунків!'");
			СтрокаЗамечание = СтрЗаменить(СтрокаЗамечание, "%Количество%", ВыборкаПоиска.Количество());
			ДобавитьЗамечание(СтрокаДокумента, 1, СтрокаЗамечание);
		КонецЕсли;
	КонецЕсли;
	
	// 7) Определим контрагента.
	Если НЕ ЗначениеЗаполнено(СтрокаДокумента.Контрагент) Тогда
		Если ЗначениеЗаполнено(Контрагент) Тогда
			СтрокаДокумента.Контрагент = Контрагент;
		ИначеЕсли НЕ ПустаяСтрока(ИННКонтрагента) Тогда
			
			СтрокаДокумента.Контрагент = Контрагент;
			ЗапросПоискаКонтрагента = Новый Запрос(
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	Контрагенты.Ссылка,
			|	Контрагенты.ИНН,
			|	Контрагенты.Наименование,
			|	Контрагенты.КодПоЕДРПОУ
			|ИЗ
			|	Справочник.Контрагенты КАК Контрагенты
			|ГДЕ
			|	Контрагенты.ИНН = &КонтрагентИНН");
			
			ЗапросПоискаКонтрагента.УстановитьПараметр("КонтрагентИНН", ИННКонтрагента);
			ВыборкаПоиска = ЗапросПоискаКонтрагента.Выполнить().Выгрузить();
			
			// Ищем контрагента по ИНН, если указан ЕДРПОУ то и по нему.
			ПараметрыОтбора = Новый Структура;
			ПараметрыОтбора.Вставить("ИНН", ИННКонтрагента);
			Если НЕ ПустаяСтрока(ЕДРПОУКонтрагента) Тогда
				ПараметрыОтбора.Вставить("ЕДРПОУ", ЕДРПОУКонтрагента);
			КонецЕсли;
			НайденныеКонтрагенты = ВыборкаПоиска.НайтиСтроки(ПараметрыОтбора);
			
			// Если не нашли и по ИНН и по ЕДРПОУ то попробуем поискать только по ИНН.
			Если НайденныеКонтрагенты.Количество() = 0
			И НЕ ПустаяСтрока(ЕДРПОУКонтрагента) Тогда
				ПараметрыОтбора = Новый Структура;
				ПараметрыОтбора.Вставить("ИНН", ИННКонтрагента);
				НайденныеКонтрагенты = ВыборкаПоиска.НайтиСтроки(ПараметрыОтбора);
			КонецЕсли;
			
			Если НайденныеКонтрагенты.Количество() > 0 Тогда
				СтрокаДокумента.Контрагент = НайденныеКонтрагенты[0].Ссылка;
			КонецЕсли;
			
			Если НайденныеКонтрагенты.Количество() > 1 Тогда
				СтрокаЗамечание = НСтр("ru='В информационной базе найдено несколько(%Количество%) контрагентов с одинаковым ИНН!';uk='В інформаційній базі знайдено декілька(%Количество%) контрагентів з однаковим ІПН!'");
				СтрокаЗамечание = СтрЗаменить(СтрокаЗамечание, "%Количество%", НайденныеКонтрагенты.Количество());
				ДобавитьЗамечание(СтрокаДокумента, 2, СтрокаЗамечание);
			ИначеЕсли НайденныеКонтрагенты.Количество() = 0 Тогда
				СтрокаЗамечание = НСтр("ru='Не найден контрагент (%ИмяКонтрагента%, ИНН %ИННКонтрагента%).';uk='Не знайдений контрагент (%ИмяКонтрагента%, ІПН %ИННКонтрагента%).'");
				СтрокаЗамечание = СтрЗаменить(СтрокаЗамечание, "%ИмяКонтрагента%", ИмяКонтрагента);
				СтрокаЗамечание = СтрЗаменить(СтрокаЗамечание, "%ИННКонтрагента%", ИННКонтрагента);
				ДобавитьЗамечание(СтрокаДокумента, 2, СтрокаЗамечание);
				СтрокаКонтрагент = НСтр("ru=' Не найден (%ИмяКонтрагента%, ИНН %ИННКонтрагента%).';uk=' Не знайдений (%ИмяКонтрагента%, ІПН %ИННКонтрагента%).'");
				СтрокаКонтрагент = СтрЗаменить(СтрокаКонтрагент, "%ИмяКонтрагента%", ИмяКонтрагента);
				СтрокаКонтрагент = СтрЗаменить(СтрокаКонтрагент, "%ИННКонтрагента%", ИННКонтрагента);
				СтрокаДокумента.Контрагент = СтрокаСчетКонтрагента;
			КонецЕсли;
			
		Иначе
			ДобавитьЗамечание(СтрокаДокумента, 2, НСтр("ru='Не указан ИНН контрагента. ';uk='Не зазначений ІПН контрагента.'"));
			СтрокаКонтрагент = НСтр("ru=' Не найден (%ИмяКонтрагента%, не указан ИНН).';uk=' Не знайдений (%ИмяКонтрагента%, не вказан ІПН).'");
			СтрокаКонтрагент = СтрЗаменить(СтрокаКонтрагент, "%ИмяКонтрагента%", ИмяКонтрагента);
			СтрокаДокумента.Контрагент = СтрокаКонтрагент;
		КонецЕсли;
	КонецЕсли;
		
	// 8) Определим договор контрагента
	Если СтрокаДокумента.ВидОперации <> Перечисления.ВидыОперацийРасходСоСчета.Налоги
	И НЕ ЗначениеЗаполнено(СтрокаДокумента.Договор) Тогда
		СтрокаДокумента.Договор = НайтиДоговор(СтрокаДокумента.Контрагент, Объект.Организация);
		Если СтрокаДокумента.Договор = НСтр("ru='Не найден';uk='Не знайдений'") Тогда
			СтрокаДокумента.Договор = НайтиДоговор(СтрокаДокумента.Контрагент);
		КонецЕсли;
		Если СтрокаДокумента.Договор = НСтр("ru='Не найден';uk='Не знайдений'") Тогда
			ДобавитьЗамечание(СтрокаДокумента, 2, НСтр("ru='Не найден договор контрагента. ';uk='Не знайдений договір контрагента.'"));
		КонецЕсли;
	КонецЕсли;
	
	// 9) Определим Статью ДДС по умолчанию.
	Если НЕ ЗначениеЗаполнено(СтрокаДокумента.СтатьяДДС) Тогда
		Если Исходяший Тогда
			СтрокаДокумента.СтатьяДДС = Объект.СтатьяДДСИсходяший;
		Иначе
			СтрокаДокумента.СтатьяДДС = Объект.СтатьяДДСВходящий;
		КонецЕсли;
	КонецЕсли;
	
	// 10) Определим сумму.
	
	// Преобразуем из строки в число.
	Буфер = СокрЛП(СтрЗаменить(СтрокаДокумента.Сумма, " ", ""));
	
	Если Не ЕстьНеЦифры(СтрЗаменить(СтрЗаменить(СтрЗаменить(Буфер, ".", ""), "-", ""), ",", "")) Тогда
		Сумма = Число(Буфер);
		Если Сумма < 0 Тогда
			Сумма = - Сумма;
		КонецЕсли;
		СтрокаДокумента.СуммаДокумента = Сумма;
		Если Исходяший Тогда
			СтрокаДокумента.СуммаСписано = Сумма;
		Иначе
			СтрокаДокумента.СуммаПоступило = Сумма;
		КонецЕсли;
	Иначе
		СтрокаЗамечание = НСтр("ru='Указана неверная сумма документа(%Буфер%)!';uk='Зазначена невірна сума документа(%Буфер%)!'");
		СтрокаЗамечание = СтрЗаменить(СтрокаЗамечание, "%Буфер%", Буфер);
		ДобавитьЗамечание(СтрокаДокумента, 4, СтрокаЗамечание);
	КонецЕсли;
	
	// 11) Определим очередность платежа.
	
	// Преобразуем из строки в число
	Буфер = СокрЛП(СтрокаДокумента.Очередность);
	Если Буфер <> "" И НЕ ЕстьНеЦифры(Буфер) Тогда
		СтрокаДокумента.ОчередностьПлатежа = Число(Буфер);
	Иначе
		СтрокаДокумента.ОчередностьПлатежа = 0;
	КонецЕсли;
	
	// 12) Определим ПоказательДатыДок (для Платежного поручения исхожящего при
	// перечислении налогов).
	
	// Преобразуем в дату из строки, если не пустая
	Если НЕ ПустаяСТрока(СтрокаДокумента.ПоказательДаты) Тогда
		Результат = ПолучитьДатуИзСтроки(СтрокаДокумента.ПоказательДатыДок, СтрокаДокумента.ПоказательДаты);
		Если НЕ ЗначениеЗаполнено(Результат) Тогда
			СтрокаДокумента.ПоказательДатыДок = Неопределено;
		КонецЕсли;
	КонецЕсли;
	
	// 13) ДатаСписано и ДатаПоступило, ДатаПроведения.
	
	// Преобразуем в дату из строки, если не пустая
	Если НЕ ПустаяСТрока(СтрокаДокумента.ДатаСписано) Тогда
		Результат = ПолучитьДатуИзСтроки(СтрокаДокумента.Списано, СтрокаДокумента.ДатаСписано);
		Если НЕ ЗначениеЗаполнено(Результат) Тогда
			СтрокаДокумента.Списано = ПустаяДата;
		Иначе
			СтрокаДокумента.ДатаПроведения = СтрокаДокумента.Списано;
		КонецЕсли;
	Иначе
		СтрокаДокумента.Списано = ПустаяДата;
	КонецЕсли;
	
	// Преобразуем в дату из строки, если не пустая.
	Если НЕ ПустаяСТрока(СтрокаДокумента.ДатаПоступило) Тогда
		Результат = ПолучитьДатуИзСтроки(СтрокаДокумента.Поступило, СтрокаДокумента.ДатаПоступило);
		Если НЕ ЗначениеЗаполнено(Результат) Тогда
			СтрокаДокумента.Поступило = ПустаяДата;
		Иначе
			СтрокаДокумента.ДатаПроведения = СтрокаДокумента.Поступило;
		КонецЕсли;
	Иначе
		СтрокаДокумента.Поступило = ПустаяДата;
	КонецЕсли;
	
	// Если НазначениеПлатежа пустое, формируем его из НазначениеПлатежа1...НазначениеПлатежа6.
	Если ПустаяСтрока(СтрокаДокумента.НазначениеПлатежа) Тогда
		СтрокаДокумента.НазначениеПлатежа = СтрокаДокумента.НазначениеПлатежа1;
		Для Сч = 2 По 6 Цикл
			Если НЕ ЗначениеЗаполнено(СтрокаДокумента["НазначениеПлатежа" + Сч]) Тогда
				Прервать;
			КонецЕсли;
			СтрокаДокумента.НазначениеПлатежа = СтрокаДокумента.НазначениеПлатежа + Символы.ПС + СтрокаДокумента["НазначениеПлатежа" + Сч];
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры // РаспознатьДанныеВСтрокеДокумента()

// Функция возвращает найденный элемент дерева.
//
&НаСервере
Функция НайтиЭлементДерева(ЭлементыДерева, ИмяКолонки, ИскомоеЗначение)
	
	Для Ном = 0 По ЭлементыДерева.Количество() - 1 Цикл
		
		ЭлементДерева = ЭлементыДерева.Получить(Ном);
		
		Если ЭлементДерева[ИмяКолонки] = ИскомоеЗначение Тогда
			Возврат ЭлементДерева;
		КонецЕсли;
		
		Если ЭлементДерева.ПолучитьЭлементы().Количество() > 0 Тогда
			
			РезультатПоиска = НайтиЭлементДерева(ЭлементДерева.ПолучитьЭлементы(), ИмяКолонки, ИскомоеЗначение);
			
			Если НЕ РезультатПоиска = Неопределено Тогда
				Возврат РезультатПоиска;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Неопределено;
	
КонецФункции // НайтиЭлементДерева()

// Функция добавляет и возвращает описание нового реквизита.
//
&НаСервере
Функция ДобавитьОписаниеНовогоРеквизита(Представление, Реквизит, ТипКонтрагента, НовыйКонтрагент, СтрокаДокумента)
	
	РеквизитыНовогоКонтрагента = НовыйКонтрагент.Добавить();
	
	РеквизитыНовогоКонтрагента.Представление = Представление;
	РеквизитыНовогоКонтрагента.Значение      = СтрокаДокумента[ТипКонтрагента + Реквизит];
	РеквизитыНовогоКонтрагента.Реквизит      = ТипКонтрагента + Реквизит;
	
	Возврат РеквизитыНовогоКонтрагента;
	
КонецФункции // ДобавитьОписаниеНовогоРеквизита()

// Процедура создает список не найденных контрагентов.
//
&НаСервере
Процедура СписокНеНайденных(СтрокаДокумента, БанковскийСчет, ТаблицаКонтрагентов)
	
	НайденКонтрагент = ТипЗнч(СтрокаДокумента.Контрагент) <> Тип("Строка");
	НайденСчет       = ТипЗнч(СтрокаДокумента.СчетКонтрагента) <> Тип("Строка");
	
	ТипКонтрагента = ?(СтрокаДокумента.ПлательщикСчет = БанковскийСчет.НомерСчета, "ПОЛУЧАТЕЛЬ", "ПЛАТЕЛЬЩИК");
	
	НайденнаяЗаписьОКонтрагенте = НайтиЭлементДерева(ТаблицаКонтрагентов.ПолучитьЭлементы(), "Значение", СтрокаДокумента[ТипКонтрагента + "ИНН"]);
	
	// Контрагент
	Если НайденнаяЗаписьОКонтрагенте = Неопределено Тогда
		
		НовыйКонтрагент = ТаблицаКонтрагентов.ПолучитьЭлементы().Добавить();
		
		НовыйКонтрагент.Представление = СтрокаДокумента[ТипКонтрагента];
		НовыйКонтрагент.НомСтроки     = СтрокаДокумента.НомерСтроки;
	
		ДобавитьОписаниеНовогоРеквизита("Наименование", "", ТипКонтрагента, НовыйКонтрагент.ПолучитьЭлементы(), СтрокаДокумента);
		ДобавитьОписаниеНовогоРеквизита("ИНН"		  , "ИНН"	 , ТипКонтрагента, НовыйКонтрагент.ПолучитьЭлементы(), СтрокаДокумента);
		ДобавитьОписаниеНовогоРеквизита("ЕДРПОУ"      , "ОКПО"	 , ТипКонтрагента, НовыйКонтрагент.ПолучитьЭлементы(), СтрокаДокумента);
		
		Если НайденКонтрагент Тогда
			НовыйКонтрагент.Реквизит = СтрокаДокумента.Контрагент;
		КонецЕсли;

	Иначе
		
		НовыйКонтрагент = НайденнаяЗаписьОКонтрагенте.ПолучитьРодителя();
		
		Если НовыйКонтрагент = Неопределено Тогда
			НовыйКонтрагент = НайденнаяЗаписьОКонтрагенте;
		КонецЕсли;
			
	КонецЕсли;
	
	НайденныеСтроки = НайтиЭлементДерева(НовыйКонтрагент.ПолучитьЭлементы(), "Значение", СтрокаДокумента[ТипКонтрагента + "СЧЕТ"]);
	
	Если НЕ НайденСчет И НайденныеСтроки = Неопределено Тогда
			
		РеквизитыНовогоКонтрагента = ДобавитьОписаниеНовогоРеквизита("Р/счет", "СЧЕТ", ТипКонтрагента, НовыйКонтрагент.ПолучитьЭлементы(), СтрокаДокумента);
				
		ПрямыеРасчеты = ПустаяСтрока(СтрокаДокумента[ТипКонтрагента]);
		
		Если ПрямыеРасчеты Тогда
			
			ДобавитьОписаниеНовогоРеквизита("Банк",            "БАНК",   ТипКонтрагента, РеквизитыНовогоКонтрагента.ПолучитьЭлементы(), СтрокаДокумента);
			ДобавитьОписаниеНовогоРеквизита("Город банка",     "БАНК",   ТипКонтрагента, РеквизитыНовогоКонтрагента.ПолучитьЭлементы(), СтрокаДокумента);
			ДобавитьОписаниеНовогоРеквизита("Код банка",       "МФО",     ТипКонтрагента, РеквизитыНовогоКонтрагента.ПолучитьЭлементы(), СтрокаДокумента);
			ДобавитьОписаниеНовогоРеквизита("Кор. счет банка", "КОРСЧЕТ", ТипКонтрагента, РеквизитыНовогоКонтрагента.ПолучитьЭлементы(), СтрокаДокумента);
			
		Иначе
			
			ДобавитьОписаниеНовогоРеквизита("Банк",                     "",        ТипКонтрагента, РеквизитыНовогоКонтрагента.ПолучитьЭлементы(), СтрокаДокумента);
			ДобавитьОписаниеНовогоРеквизита("Город банка",              "",        ТипКонтрагента, РеквизитыНовогоКонтрагента.ПолучитьЭлементы(), СтрокаДокумента);
			ДобавитьОписаниеНовогоРеквизита("Кор. счет банка",          "РАСЧСЧЕТ", ТипКонтрагента, РеквизитыНовогоКонтрагента.ПолучитьЭлементы(), СтрокаДокумента);
			ДобавитьОписаниеНовогоРеквизита("РЦ банка",                 "БАНК",    ТипКонтрагента, РеквизитыНовогоКонтрагента.ПолучитьЭлементы(), СтрокаДокумента);
			ДобавитьОписаниеНовогоРеквизита("Местонахождение РЦ банка", "БАНК",    ТипКонтрагента, РеквизитыНовогоКонтрагента.ПолучитьЭлементы(), СтрокаДокумента);
			ДобавитьОписаниеНовогоРеквизита("Код РЦ банка",             "МФО",      ТипКонтрагента, РеквизитыНовогоКонтрагента.ПолучитьЭлементы(), СтрокаДокумента);
			ДобавитьОписаниеНовогоРеквизита("Кор. счет РЦ банка",       "КОРСЧЕТ",  ТипКонтрагента, РеквизитыНовогоКонтрагента.ПолучитьЭлементы(), СтрокаДокумента);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры // СписокНеНайденных()

// Процедура заполняет документы на импорт.
//
&НаСервере
Процедура ЗаполнитьДокументыНаИмпорт(ИмпортТекстДляРазбора)
	
	ТаблицаКонтрагентов.ПолучитьЭлементы().Очистить();
	
	// Подготавливаем структуры обработки данных.
	ДокументыКИмпорту = Объект.Загрузка.Выгрузить();
	ИмпортЗагружаемые = СформироватьСоответствиеЗагружаемых();
	ИмпортНеПустые = Неопределено;
	ИмпортНеПустыеПлатежноеПоручение = Неопределено;
	ИмпортНеПустыеПлатежноеПоручениеБюджет = Неопределено;
	
	РасчетныеСчетаКИмпорту = Объект.ИмпортРасчетныеСчета.Выгрузить();
	
	СформироватьСоответствияНеПустыхПриИмпорте(
		ИмпортНеПустые,
		ИмпортНеПустыеПлатежноеПоручение,
		ИмпортНеПустыеПлатежноеПоручениеБюджет
	);
	ТегиРасчетногоСчета = СоздатьСоответствиеИзСтроки(
		ВРег("ДатаНачала,ДатаКонца,РасчСчетОрг,НачальныйОстаток,ВсегоПоступило,ВсегоСписано,КонечныйОстаток,КонецРасчСчет")
	);
	ТегиЗаголовка = СоздатьСоответствиеИзСтроки(
		ВРег("ВерсияФормата,Кодировка,Отправитель,Получатель,ДатаСоздания,ВремяСоздания,ДатаНачала,ДатаКонца")
	);
	СтруктураЗаголовок = Новый Структура(
		ВРег("ВерсияФормата,Кодировка,Отправитель,Получатель,ДатаСоздания,ВремяСоздания,ДатаНачала,ДатаКонца")
	);
	ИмпортЗаголовок = СтруктураЗаголовок;
	ИмпортПризнакОбмена = Ложь;
	НайденКонецФайла = Ложь;
	ИмпортВидыДокументов = Новый Массив;
	РасчетныеСчетаКИмпорту.Очистить();
	ДокументыКИмпорту.Очистить();
	
	// Заполняем первичные структуры данных.
	ИмпортКоличествоСтрок = СтрЧислоСтрок(ИмпортТекстДляРазбора);
	ИмпортТекущаяСтрока = 1;
	Пока ИмпортТекущаяСтрока <= ИмпортКоличествоСтрок Цикл
		Стр = ПолучитьСтрокуИмпорта(ИмпортТекущаяСтрока, ИмпортКоличествоСтрок, ИмпортТекстДляРазбора);
		
		// СЕКЦИЯДОКУМЕНТ.
		Если Лев(ВРег(СокрЛП(Стр)), 14) = "СЕКЦИЯДОКУМЕНТ" Тогда
			Значение = "";
			Тег = "";
			РазобратьТеговуюСтроку(Стр, Тег, Значение);
			Если Тег = "СЕКЦИЯДОКУМЕНТ" Тогда
				НоваяСтрокаДокументов = ДокументыКИмпорту.Добавить();
				НоваяСтрокаДокументов.Операция = Значение;
				Если НЕ ЗагрузитьСекциюДокумента(НоваяСтрокаДокументов, ИмпортТекущаяСтрока, ИмпортКоличествоСтрок, ИмпортТекстДляРазбора, ИмпортЗагружаемые) Тогда
					Возврат;
				КонецЕсли;
			Иначе
				ТекстСообщения = НСтр("ru='Нарушена структура файла импорта, строка %Импорт%: %Стр%';uk='Порушена структура файлу імпорту, рядок %Импорт%: %Стр%'");
				ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Импорт%", (ИмпортТекущаяСтрока - 1));
				ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Стр%", Стр);
				УправлениеНебольшойФирмойСервер.СообщитьОбОшибке(ЭтотОбъект, ТекстСообщения);
				Возврат;
			КонецЕсли;
		
		// СЕКЦИЯРАСЧСЧЕТ.
		ИначеЕсли Лев(Врег(СокрЛП(Стр)), 14) = "СЕКЦИЯРАСЧСЧЕТ" Тогда
			
			СтрокаРССчетов = РасчетныеСчетаКИмпорту.Добавить();
			Если НЕ ЗагрузитьСекциюРасчСчета(СтрокаРССчетов, ИмпортТекущаяСтрока, ИмпортКоличествоСтрок, ИмпортТекстДляРазбора, ТегиРасчетногоСчета) Тогда
				ТекстСообщения = НСтр("ru='Нарушена структура файла импорта в секции описания расчетного счета! Строка: %Импорт%';uk='Порушена структура файлу імпорту в секції опису розрахункового рахунку! Рядок: %Импорт%'");
				ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Импорт%", (ИмпортТекущаяСтрока - 1));
				УправлениеНебольшойФирмойСервер.СообщитьОбОшибке(ЭтотОбъект, ТекстСообщения);
				Возврат;
			КонецЕсли;
			Если Объект.БанковскийСчет.НомерСчета <> СтрокаРССчетов.РасчСчетОрг Тогда
				РасчетныеСчетаКИмпорту.Удалить(СтрокаРССчетов);
			КонецЕсли;
			
		// РАСЧСЧЕТ.
		ИначеЕсли Лев(ВРег(СокрЛП(Стр)), 8) = "РАСЧСЧЕТ" Тогда
			
			Значение = "";
			Тег = "";
			РазобратьТеговуюСтроку(Стр, Тег, Значение);
			
			Если Тег = "РАСЧСЧЕТОРГ" Тогда
				Если СчетПринадлежитОрганизации(Значение) Тогда
					Если Объект.БанковскийСчет.НомерСчета = Значение Тогда
						СтрокаРССчетов = РасчетныеСчетаКИмпорту.Найти(Значение, "РасчСчетОрг");
						Если СтрокаРССчетов = Неопределено Тогда
							СтрокаРССчетов = РасчетныеСчетаКИмпорту.Добавить();
							СтрокаРССчетов.РасчСчетОрг = Значение;
						КонецЕсли;
					Иначе
						ТекстСообщения = НСтр("ru='В заголовке файла указан счет (%Значение%) отличный от указанного, расчеты по которому загружаться не будут!';uk='У заголовку файлу зазначений рахунок (%Значение%) відмінний від зазначеного, розрахунки по якому завантажуватися не будуть!'");
						ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Значение%", Значение);
						УправлениеНебольшойФирмойСервер.СообщитьОбОшибке(ЭтотОбъект, ТекстСообщения);
					КонецЕсли;
				Иначе
					ТекстСообщения = НСтр("ru='В заголовке файла указан счет, не принадлежащий организации: %Значение%!';uk='У заголовку файлу зазначений рахунок, що не належить організації: %Значение%!'");
					ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Значение%", Значение);
					УправлениеНебольшойФирмойСервер.СообщитьОбОшибке(ЭтотОбъект, ТекстСообщения);
					Возврат;
				КонецЕсли;
			КонецЕсли;
		
		// ДОКУМЕНТ.
		ИначеЕсли Лев(Врег(СокрЛП(Стр)), 8) = "ДОКУМЕНТ" Тогда
			ИмпортВидыДокументов.Добавить(Значение);
		
		// КОНЕЦФАЙЛА.
		ИначеЕсли Лев(Врег(СокрЛП(Стр)), 10) = "КОНЕЦФАЙЛА" Тогда
			Если НЕ ИмпортПризнакОбмена Тогда
				ТекстСообщения = НСтр("ru='В файле импорта отсутствует признак обмена ""_1CClientBankExchange""!';uk='У файлі імпорту відсутній признак обміну ""_1CClientBankExchange""!'");
				УправлениеНебольшойФирмойСервер.СообщитьОбОшибке(ЭтотОбъект, ТекстСообщения);
				Возврат;
			КонецЕсли;
			
			НайденКонецФайла = Истина;
			НомерСтроки = 0;
			
			// Последовательно обрабатываем каждую загруженную строку.
			Для каждого СтрокаДокумента Из ДокументыКИмпорту Цикл
				
				// Распознаем реквизиты.
				// Если в файле находятся выписка пл. документов по нескольким счетам, то
				// распознаем и отображаем только те которые выгружены по указанному
				// банковскому счету.
				Если СтрокаДокумента.ПлательщикСчет = Объект.БанковскийСчет.НомерСчета
				 ИЛИ СтрокаДокумента.ПолучательСчет = Объект.БанковскийСчет.НомерСчета Тогда
					РаспознатьДанныеВСтрокеДокумента(СтрокаДокумента);
					НомерСтроки = НомерСтроки + 1;
					СтрокаДокумента.LineNumber = НомерСтроки;
					
					// Для каждого реквизита (= колонка) надо проверить на пустое значение.
					Для каждого КолонкаИмпорта Из ДокументыКИмпорту.Колонки Цикл
						ПроверитьНаПустоеЗначениеИмпорта(
							СтрокаДокумента,
							КолонкаИмпорта.Имя,
							КолонкаИмпорта.Заголовок,
							ИмпортНеПустые
						);
					КонецЦикла;
					
					Если ТипЗнч(СтрокаДокумента.Контрагент) = Тип("Строка")
					 ИЛИ ТипЗнч(СтрокаДокумента.СчетКонтрагента) = Тип("Строка") Тогда
						
						// Добавляем реквизиты в табличную часть для дальнейшего использования.
						СписокНенайденных(СтрокаДокумента, Объект.БанковскийСчет, ТаблицаКонтрагентов);
						
					КонецЕсли;
					
				Иначе
					
					// Остальные помечаем для последующего удаления.
					СтрокаДокумента.LineNumber = 0;
					
				КонецЕсли;
			КонецЦикла;
			
			// Удалим ненужные строки из таблицы.
			Количество = ДокументыКИмпорту.Количество() - 1;
			Для Сч = 0 по Количество Цикл
				Если ДокументыКИмпорту[Количество - Сч].LineNumber = 0 Тогда
					ДокументыКИмпорту.Удалить(Количество - Сч);
				КонецЕсли;
			КонецЦикла;
		
		// 1CCLIENTBANKEXCHANGE.
		ИначеЕсли Лев(Врег(СокрЛП(Стр)), 21) = "_1CCLIENTBANKEXCHANGE" Тогда
			ИмпортПризнакОбмена = Истина;
		Иначе
			ЗагрузитьСтрокуЗаголовка(
				Стр,
				ТегиЗаголовка,
				ИмпортЗаголовок,
				ИмпортТекущаяСтрока
			);
		КонецЕсли;
		
	КонецЦикла;
	
	Если НЕ НайденКонецФайла Тогда
		РасчетныеСчетаКИмпорту.Очистить();
		ДокументыКИмпорту.Очистить();
		ТекстСообщения = НСтр("ru='Файл загрузки не соответствует стандарту (не найдена секция КонецФайла)!';uk='Файл завантаження не відповідає стандарту (не знайдена секція КонецФайла)!'");
		УправлениеНебольшойФирмойСервер.СообщитьОбОшибке(ЭтотОбъект, ТекстСообщения);
	КонецЕсли;
	
	Для каждого СтрокаДокумента Из ДокументыКИмпорту Цикл
		СтрокаДокумента.Загружать = ПустаяСтрока(СтрокаДокумента.ОписаниеОшибок);
		СтрокаДокумента.НазначениеПлатежа = СокрЛП(СтрокаДокумента.НазначениеПлатежа);
		СтрокаДокумента.НомерКартинки = ?(ПустаяСтрока(СтрокаДокумента.ОписаниеОшибок), 0, 1);
	КонецЦикла;
	
	Объект.Загрузка.Очистить();
	Объект.Загрузка.Загрузить(ДокументыКИмпорту);
	
	Объект.ИмпортРасчетныеСчета.Очистить();
	Объект.ИмпортРасчетныеСчета.Загрузить(РасчетныеСчетаКИмпорту);
	
КонецПроцедуры // ЗаполнитьДокументыНаИмпорт()

// Функция читает файл.
//
&НаКлиенте
Функция ПрочитатьФайл(ПутьКФайлуИзНастройки)
	
	Файл = СокрЛП(ПутьКФайлуИзНастройки);
	
	Если НЕ РасширениеРаботыСФайламиПодключено Тогда
		Файл = "";
	Иначе
		ФайлЗагр = Новый Файл(Файл);
		Если ФайлЗагр.Существует() = Ложь Тогда
			ТекстСообщения = НСтр("ru='Файла %Файл% не существует!';uk='Файлу %Файл% не існує!'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Файл%", Файл);
			УправлениеНебольшойФирмойСервер.СообщитьОбОшибке(ЭтотОбъект, ТекстСообщения);
			Возврат Неопределено;
		КонецЕсли;
		Попытка
			ПотокЧтения.Прочитать(Файл);
		Исключение
			ТекстСообщения = НСтр("ru='Файл не прочитан.';uk='Файл не прочитаний.'");
			УправлениеНебольшойФирмойСервер.СообщитьОбОшибке(ЭтотОбъект, ТекстСообщения);
			Возврат Неопределено;
		КонецПопытки;
	КонецЕсли;
	
	Если Объект.Кодировка = "DOS" Тогда
		Кодир = КодировкаТекста.OEM;
	ИначеЕсли Объект.Кодировка = "Windows" Тогда
		Кодир = КодировкаТекста.ANSI;
	Иначе
		Кодир = КодировкаТекста.UTF8;
	КонецЕсли;
	
	ПотокЧтения.Прочитать(Файл, Кодир);
	
	Если ПотокЧтения.КоличествоСтрок() < 1 Тогда
		ТекстСообщения = НСтр("ru='В файле нет данных!';uk='У файлі немає даних!'");
		УправлениеНебольшойФирмойСервер.СообщитьОбОшибке(ЭтотОбъект, ТекстСообщения);
		Возврат Неопределено;
	КонецЕсли;
	
	Если СокрЛП(ПотокЧтения.ПолучитьСтроку(1)) <> "_1CClientBankExchange" Тогда
		ТекстСообщения = НСтр("ru='Указанный файл не является файлом обмена или неверно указана кодировка!';uk='Зазначений файл не є файлом обміну чи невірно вказана кодировка!'");
		УправлениеНебольшойФирмойСервер.СообщитьОбОшибке(ЭтотОбъект, ТекстСообщения);
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат ПотокЧтения.ПолучитьТекст();
	
КонецФункции // ПрочитатьФайл()

// Функция читает данные из файла.
//
&НаКлиенте
Процедура ПрочитатьДанныеИзФайла()
	
	ТекФайл = Новый Файл(Объект.ФайлЗагрузки);
	Расширение = ТекФайл.Расширение;
	
	Если НРег(СокрЛП(Расширение)) = ".xml" Или Объект.ТипФайла = "XML" Тогда
		
		ЗаполнитьДокументыНаИмпортXML();
		
	Иначе
		
		// Получаем исходные данные.
		ИмпортТекстДляРазбора = ПрочитатьФайл(Объект.ФайлЗагрузки);
		Если ИмпортТекстДляРазбора = Неопределено Тогда
			ТекстСообщения = НСтр("ru='Файл загрузки не содержит данных!';uk='Файл завантаження не містить даних!'");
			УправлениеНебольшойФирмойКлиент.СообщитьОбОшибке(, ТекстСообщения);
			Возврат;
		КонецЕсли;
		
		ЗаполнитьДокументыНаИмпорт(ИмпортТекстДляРазбора);
		
	КонецЕсли;
	
КонецПроцедуры // ПрочитатьДанныеИзФайла()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

// Процедура - обработчик события ПриСозданииНаСервере.
//
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если НЕ ЗначениеЗаполнено(Объект.Организация) Тогда
		ЗначениеНастройки = УправлениеНебольшойФирмойПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(Пользователи.ТекущийПользователь(), "ОсновнаяОрганизация");
		Если ЗначениеЗаполнено(ЗначениеНастройки) Тогда
			Объект.Организация = ЗначениеНастройки;
		Иначе
			Объект.Организация = Справочники.Организации.ОсновнаяОрганизация;
		КонецЕсли;
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(Объект.БанковскийСчет) Тогда
		Объект.БанковскийСчет = Объект.Организация.БанковскийСчетПоУмолчанию;
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(Объект.НачПериода) Тогда
		Объект.НачПериода = ТекущаяДата();
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(Объект.КонПериода) Тогда
		Объект.КонПериода = ТекущаяДата();
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(Объект.Кодировка) Тогда
		Объект.Кодировка = "Windows";
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(Объект.ТипФайла) Тогда
		Объект.ТипФайла = "Текст";
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(Объект.СтатьяДДСВходящий) Тогда
		Объект.СтатьяДДСВходящий = Справочники.СтатьиДвиженияДенежныхСредств.ОплатаОтПокупателей;
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(Объект.СтатьяДДСИсходяший) Тогда
		Объект.СтатьяДДСИсходяший = Справочники.СтатьиДвиженияДенежныхСредств.ОплатаПоставщикам;
	КонецЕсли;
	
	Если Параметры.Свойство("РежимПоУмолчанию") Тогда
		Элементы.Страницы.ТекущаяСтраница = Элементы[Параметры.РежимПоУмолчанию];
	КонецЕсли;
	
	ЗаполнитьВыгрузка();
	
	ЗагрузитьНастройкиФормы();
	
КонецПроцедуры // ПриСозданииНаСервере()

// Процедура - обработчик события ПриОткрытии.
//
&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ДопустимыеТипы = Новый ОписаниеТипов("СправочникСсылка.Контрагенты", ,);
	Элементы.ЗагрузкаКонтрагент.ОграничениеТипа = ДопустимыеТипы;
	
	ДопустимыеТипы = Новый ОписаниеТипов("СправочникСсылка.ДоговорыКонтрагентов", ,);
	Элементы.ЗагрузкаДоговор.ОграничениеТипа = ДопустимыеТипы;
	
	ЭтаФорма.РасширениеРаботыСФайламиПодключено = ПодключитьРасширениеРаботыСФайлами();
	
	Если ЭтаФорма.РасширениеРаботыСФайламиПодключено Тогда
		
		Если НЕ ЗначениеЗаполнено(Объект.ФайлВыгрузки) Тогда
			
			Если Объект.ТипФайла = "XML" Тогда
				Объект.ФайлВыгрузки = "c:\1c_to_kb.xml";
			Иначе	
				Объект.ФайлВыгрузки = "c:\1c_to_kb.txt";
			КонецЕсли; 
			
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(Объект.ФайлЗагрузки) Тогда
			
			Если Объект.ТипФайла = "XML" Тогда
				Объект.ФайлЗагрузки = "c:\kb_to_1c.xml";
			Иначе
				Объект.ФайлЗагрузки = "c:\kb_to_1c.txt";
			КонецЕсли;
			
		КонецЕсли;
		
		Элементы.ГруппаСтраницыФайлаВыгрузки.ТекущаяСтраница = Элементы.ГруппаСтраницыФайлаВыгрузки.ПодчиненныеЭлементы.ГруппаВыборФайлаВыгрузкиДоступен;
		Элементы.ГруппаСтраницыФайлаЗагрузки.ТекущаяСтраница = Элементы.ГруппаСтраницыФайлаЗагрузки.ПодчиненныеЭлементы.ГруппаВыборФайлаЗагрузкиДоступен;
		
	Иначе
		
		Элементы.ГруппаСтраницыФайлаВыгрузки.ТекущаяСтраница = Элементы.ГруппаСтраницыФайлаВыгрузки.ПодчиненныеЭлементы.ГруппаВыборФайлаВыгрузкиНеДоступен;
		Элементы.ГруппаСтраницыФайлаЗагрузки.ТекущаяСтраница = Элементы.ГруппаСтраницыФайлаЗагрузки.ПодчиненныеЭлементы.ГруппаВыборФайлаЗагрузкиНеДоступен;
		
		Объект.ФайлВыгрузки = "";
		Объект.ФайлЗагрузки = "";
		
	КонецЕсли;
	
КонецПроцедуры // ПриОткрытии()

// Процедура - обработчик события ПриЗакрытии.
//
&НаКлиенте
Процедура ПриЗакрытии()
	
	СохранитьНастройкиФормы();
	
КонецПроцедуры // ПриЗакрытии()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ДЕЙСТВИЯ КОМАНДНЫХ ПАНЕЛЕЙ ФОРМЫ

// Процедура - обработчик команды Выгрузить.
//
&НаКлиенте
Процедура ВыгрузитьВыполнить(Команда)
	
	Если НЕ ПроверитьЗаполнениеРеквизитовФормы(Истина) Тогда
		Возврат;
	КонецЕсли;
	
	Если Объект.Выгрузка.Количество() > 0 Тогда
		
		ТекФайл = Новый Файл(Объект.ФайлВыгрузки);
		Расширение = ТекФайл.Расширение;
		
		Если НРег(СокрЛП(Расширение)) = ".xml" Или Объект.ТипФайла = "XML" Тогда
			ПотокВыгрузки = ВыгрузитьДанныеВФайлXML();
			СохранитьФайлВыгрузкиXML(ПотокВыгрузки);
			ПоказатьПредупреждение(, НСтр("ru='Выгрузка платежных документов завершена.';uk='Вивантаження платіжних документів завершено.'"));
		Иначе	
			ПотокВыгрузки = ВыгрузитьДанныеВФайл();
			СохранитьФайлВыгрузки(ПотокВыгрузки);
			ПоказатьПредупреждение(, НСтр("ru='Выгрузка платежных документов завершена.';uk='Вивантаження платіжних документів завершено.'"));
		КонецЕсли;	
		
	Иначе
		ПоказатьПредупреждение(, 
			НСтр("ru='Список документов для выгрузки пуст."
"Проверьте правильность указанного банковского счета и периода выгрузки.';uk='Список документів для вивантаження порожній."
"Перевірте правильність зазначеного банківського рахунку й періоду вивантаження.'")
		);
	КонецЕсли;
	
КонецПроцедуры // ВыгрузитьВыполнить()

// Процедура - обработчик команды ВыгрузкаОбновить.
//
&НаКлиенте
Процедура ВыгрузкаОбновитьВыполнить(Команда)
	
	ЗаполнитьВыгрузка();
	
КонецПроцедуры // ВыгрузкаОбновитьВыполнить()

// Процедура - обработчик команды ВыгрузкаОтметитьВсе.
//
&НаКлиенте
Процедура ВыгрузкаОтметитьВсеВыполнить(Команда)
	
	УстановитьФлаги(Объект.Выгрузка, "Выгружать", Истина);
	
КонецПроцедуры // ВыгрузкаОтметитьВсеВыполнить()

// Процедура - обработчик команды ВыгрузкаСнятьОтметкуУВсех.
//
&НаКлиенте
Процедура ВыгрузкаСнятьОтметкуУВсехВыполнить(Команда)
	
	УстановитьФлаги(Объект.Выгрузка, "Выгружать", Ложь);
	
КонецПроцедуры // ВыгрузкаСнятьОтметкуУВсехВыполнить()

// Процедура - обработчик команды ЗагрузкаОтметитьВсе.
//
&НаКлиенте
Процедура ЗагрузкаОтметитьВсеВыполнить(Команда)
	
	УстановитьФлаги(Объект.Загрузка, "Загружать", Истина);
	
КонецПроцедуры // ЗагрузкаОтметитьВсеВыполнить()

// Процедура - обработчик команды ЗагрузкаСнятьОтметкуУВсех.
//
&НаКлиенте
Процедура ЗагрузкаСнятьОтметкуУВсехВыполнить(Команда)
	
	УстановитьФлаги(Объект.Загрузка, "Загружать", Ложь);
	
КонецПроцедуры // ЗагрузкаСнятьОтметкуУВсехВыполнить()

// Процедура - обработчик команды ПрочитатьДанныеИзФайла.
//
&НаКлиенте
Процедура ПрочитатьДанныеИзФайлаВыполнить(Команда)
	
	Если НЕ ПроверитьЗаполнениеРеквизитовФормы(Ложь) Тогда
		Возврат;
	КонецЕсли;
	
	ПрочитатьДанныеИзФайла();
	
	Элементы.НеНайденныеРеквизиты.Видимость = (ТаблицаКонтрагентов.ПолучитьЭлементы().Количество() > 0);
	
КонецПроцедуры // ПрочитатьДанныеИзФайлаВыполнить()

// Процедура - обработчик команды ЗагрузкаОбновить.
//
&НаКлиенте
Процедура ЗагрузкаОбновитьВыполнить(Команда)
	
	Если НЕ ПроверитьЗаполнениеРеквизитовФормы(Ложь) Тогда
		
		Возврат;
		
	КонецЕсли;
	
	ПрочитатьДанныеИзФайла();
	
КонецПроцедуры // ЗагрузкаОбновитьВыполнить()

// Процедура - обработчик команды Загрузить.
//
&НаКлиенте
Процедура ЗагрузитьВыполнить(Команда)
	
	Если НЕ ПроверитьЗаполнениеРеквизитовФормы(Ложь) Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Если Объект.Загрузка.Количество() > 0 Тогда
		
		ЗагрузитьДанныеИзФайла();
		
		ПоказатьПредупреждение(, НСтр("ru='Загрузка платежных документов завершена.';uk='Завантаження платіжних документів завершене.'"));
		
	Иначе
		
		ПоказатьПредупреждение(, НСтр("ru='Список документов для загрузки пуст.';uk='Список документів для завантаження порожній.'"));
		
	КонецЕсли;
	
КонецПроцедуры // ЗагрузитьВыполнить()

// Процедура - обработчик команды СоздатьКонтрагентов.
//
&НаКлиенте
Процедура СоздатьКонтрагентов(Команда)
	
	СоздатьНовогоКонтрагента();
	
	ПрочитатьДанныеИзФайла();
	
	Элементы.НеНайденныеРеквизиты.Видимость = (ТаблицаКонтрагентов.ПолучитьЭлементы().Количество() > 0);
	
КонецПроцедуры // СоздатьКонтрагентов()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ РЕКВИЗИТОВ ШАПКИ

// Процедура - обработчик события Открытие поля ввода Организация.
//
&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	СтруктураДанные = ПолучитьДанныеОрганизацияПриИзменении(Объект.Организация, Объект.БанковскийСчет);
	Объект.БанковскийСчет = СтруктураДанные.БанковскийСчет;
	ЗаполнитьВыгрузка();
	ЗагрузитьНастройкиФормы();
	
КонецПроцедуры // ОрганизацияПриИзменении()

// Процедура - обработчик события Открытие поля ввода БанковскийСчет.
//
&НаКлиенте
Процедура БанковскийСчетПриИзменении(Элемент)
	
	ЗаполнитьВыгрузка();
	ЗагрузитьНастройкиФормы();
	
КонецПроцедуры // БанковскийСчетПриИзменении()

// Процедура - обработчик события Открытие поля ввода НачПериода.
//
&НаКлиенте
Процедура НачПериодаПриИзменении(Элемент)
	
	ЗаполнитьВыгрузка();
	
КонецПроцедуры // НачПериодаПриИзменении()

// Процедура - обработчик события Открытие поля ввода КонПериода.
//
&НаКлиенте
Процедура КонПериодаПриИзменении(Элемент)
	
	ЗаполнитьВыгрузка();
	
КонецПроцедуры // КонПериодаПриИзменении()

// Процедура - обработчик события Открытие поля ввода ФайлВыгрузки.
//
&НаКлиенте
Процедура ФайлВыгрузкиОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОткрытьФайлДляПросмотра(Объект.ФайлВыгрузки, Объект.Кодировка, НСтр("ru='Файл выгрузки';uk='Файл вивантаження'"));
	
КонецПроцедуры // ФайлВыгрузкиОткрытие()

&НаКлиенте
Процедура ТипФайлаПриИзменении(Элемент)
	
	Если Объект.ТипФайла = "XML" Тогда
		Если Найти(НРег(Объект.ФайлВыгрузки),".txt") > 0 Тогда
			Объект.ФайлВыгрузки = СтрЗаменить(Объект.ФайлВыгрузки, ".txt", ".xml");
		КонецЕсли;	
		Если Найти(НРег(Объект.ФайлЗагрузки),".txt") > 0 Тогда
			Объект.ФайлЗагрузки = СтрЗаменить(Объект.ФайлЗагрузки, ".txt", ".xml");
		КонецЕсли;	
	Иначе	
		Если Найти(НРег(Объект.ФайлВыгрузки),".xml") > 0 Тогда
			Объект.ФайлВыгрузки = СтрЗаменить(Объект.ФайлВыгрузки, ".xml", ".txt");
		КонецЕсли;	
		Если Найти(НРег(Объект.ФайлЗагрузки),".xml") > 0 Тогда
			Объект.ФайлЗагрузки = СтрЗаменить(Объект.ФайлЗагрузки, ".xml", ".txt");
		КонецЕсли;	
	КонецЕсли; 
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЧНЫХ ЧАСТЕЙ

// Процедура - обработчик события Выбор табличной части Выгрузка.
//
&НаКлиенте
Процедура ВыгрузкаВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если Поле.Имя = "ВыгрузкаВыгружать" Тогда
		Элементы.Выгрузка.ТекущиеДанные.Выгружать = НЕ (Элементы.Выгрузка.ТекущиеДанные.Выгружать);		
	ИначеЕсли Поле.Имя = "ВыгрузкаНомерКартинки" Тогда 
		Если ЗначениеЗаполнено(Элементы.Выгрузка.ТекущиеДанные.ОписаниеОшибок) Тогда
			ПоказатьПредупреждение(, Элементы.Выгрузка.ТекущиеДанные.ОписаниеОшибок);
		Иначе
			ПоказатьПредупреждение(, НСтр("ru='Документ готов к выгрузке!';uk='Документ готовий до вивантаження!'"));
		КонецЕсли;
	ИначеЕсли Поле.Имя = "ВыгрузкаНазначениеПлатежа" Тогда
		ПоказатьПредупреждение(, Элементы.Выгрузка.ТекущиеДанные.НазначениеПлатежа);
	Иначе
		ОткрытьФорму("Документ.ПлатежноеПоручение.ФормаОбъекта",
			Новый Структура("Ключ", Элементы.Выгрузка.ТекущиеДанные.Документ),
			Элементы.Выгрузка.ТекущиеДанные.Документ
		);
	КонецЕсли;
	
КонецПроцедуры // ВыгрузкаВыбор()

// Процедура - обработчик события НачалоВыбора  поля ввода ФайлВыгрузки.
//
&НаКлиенте
Процедура ФайлВыгрузкиНачалоВыбора(Элемент, СтандартнаяОбработка)
	
	Если НЕ ПодключитьРасширениеРаботыСФайлами() Тогда
		ТекстСообщения = НСтр("ru='Для данной операции необходимо"
"установить расширение работы с файлами!"
"Установка выполняется в разделе"
"""Сервис и администрирование""/"
"""Настройка сервисных функций"".';uk='Для даної операції необхідно"
"встановити розширення роботи з файлами!"
"Установка виконується в розділі"
"""Сервіс і адміністрування""/"
"""Налаштування сервісних функцій"".'"
		);
		ПоказатьПредупреждение(, ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	Режим = РежимДиалогаВыбораФайла.Открытие;
	ДиалогОткрытияФайла = Новый ДиалогВыбораФайла(Режим);
	ДиалогОткрытияФайла.ПолноеИмяФайла = Объект.ФайлВыгрузки;
	Если Объект.ТипФайла = "XML" Тогда
		Фильтр = "XML файл (*.xml)|*.xml";
		ДиалогОткрытияФайла.Фильтр = Фильтр;
		ДиалогОткрытияФайла.Расширение = "xml";
	Иначе	
		Фильтр = "Текстовый файл(*.txt)|*.txt";
		ДиалогОткрытияФайла.Фильтр = Фильтр;
		ДиалогОткрытияФайла.Расширение = "txt";
	КонецЕсли; 
	ДиалогОткрытияФайла.МножественныйВыбор = Ложь;
	ДиалогОткрытияФайла.Заголовок = НСтр("ru='Выберите файл';uk='Виберіть файл'");
	Если ДиалогОткрытияФайла.Выбрать() Тогда
		Объект.ФайлВыгрузки = ДиалогОткрытияФайла.ПолноеИмяФайла;
	КонецЕсли;
	
КонецПроцедуры // ФайлВыгрузкиНачалоВыбора()

// Процедура - обработчик события НачалоВыбора  поля ввода ФайлЗагрузки
// табличной части Загрузка.
//
&НаКлиенте
Процедура ФайлЗагрузкиНачалоВыбора(Элемент, СтандартнаяОбработка)
	
	Если НЕ ПодключитьРасширениеРаботыСФайлами() Тогда
		ТекстСообщения = НСтр("ru='Для данной операции необходимо"
"установить расширение работы с файлами!"
"Установка выполняется в разделе"
"""Сервис и администрирование""/"
"""Настройка сервисных функций"".';uk='Для даної операції необхідно"
"встановити розширення роботи з файлами!"
"Установка виконується в розділі"
"""Сервіс і адміністрування""/"
"""Налаштування сервісних функцій"".'"
		);
		ПоказатьПредупреждение(, ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	Режим = РежимДиалогаВыбораФайла.Открытие;
	ДиалогОткрытияФайла = Новый ДиалогВыбораФайла(Режим);
	ДиалогОткрытияФайла.ПолноеИмяФайла = Объект.ФайлВыгрузки;
	Если Объект.ТипФайла = "XML" Тогда
		Фильтр = "XML файл (*.xml)|*.xml";
		ДиалогОткрытияФайла.Фильтр = Фильтр;
		ДиалогОткрытияФайла.Расширение = "xml";
	Иначе	
		Фильтр = "Текстовый файл(*.txt)|*.txt";
		ДиалогОткрытияФайла.Фильтр = Фильтр;
		ДиалогОткрытияФайла.Расширение = "txt";
	КонецЕсли; 
	ДиалогОткрытияФайла.МножественныйВыбор = Ложь;
	ДиалогОткрытияФайла.Заголовок = НСтр("ru='Выберите файл';uk='Виберіть файл'");
	Если ДиалогОткрытияФайла.Выбрать() Тогда
		Объект.ФайлЗагрузки = ДиалогОткрытияФайла.ПолноеИмяФайла; 
	КонецЕсли;
	
КонецПроцедуры // ФайлЗагрузкиНачалоВыбора()

// Процедура - обработчик события Открытие поля ввода ФайлЗагрузки
// табличной части Загрузка.
//
&НаКлиенте
Процедура ФайлЗагрузкиОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = ложь;
	ОткрытьФайлДляПросмотра(Объект.ФайлЗагрузки, Объект.Кодировка, НСтр("ru='Файл загрузки';uk='Файл завантаження'"));
	
КонецПроцедуры // ФайлЗагрузкиОткрытие()

// Процедура - обработчик события Выбор табличной части Загрузка.
//
&НаКлиенте
Процедура ЗагрузкаВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле.Имя = "ЗагрузкаЗагружать" Тогда
		СтандартнаяОбработка = Ложь;
		Элементы.Загрузка.ТекущиеДанные.Загружать = НЕ (Элементы.Загрузка.ТекущиеДанные.Загружать);
	ИначеЕсли Поле.Имя = "ЗагрузкаНомерКартинки" Тогда 
		СтандартнаяОбработка = Ложь;
		Если ЗначениеЗаполнено(Элементы.Загрузка.ТекущиеДанные.ОписаниеОшибок) Тогда
			ПоказатьПредупреждение(, Элементы.Загрузка.ТекущиеДанные.ОписаниеОшибок);
		Иначе
			ПоказатьПредупреждение(, НСтр("ru='Документ готов к загрузке!';uk='Документ готовий до завантаження!'"));
		КонецЕсли;
	ИначеЕсли Поле.Имя = "ЗагрузкаНазначениеПлатежа" Тогда
		СтандартнаяОбработка = Ложь;
		ПоказатьПредупреждение(, Элементы.Загрузка.ТекущиеДанные.НазначениеПлатежа);
	ИначеЕсли ЗначениеЗаполнено(Элементы.Загрузка.ТекущиеДанные.Документ) Тогда
		ОткрытьФорму("Документ." + Элементы.Загрузка.ТекущиеДанные.ВидДокумента + ".ФормаОбъекта",
			Новый Структура("Ключ", Элементы.Загрузка.ТекущиеДанные.Документ),
			Элементы.Загрузка.ТекущиеДанные.Документ
		);
	КонецЕсли;
	
КонецПроцедуры // ЗагрузкаВыбор()

// Процедура создает нового контрагента.
//
&НаСервере
Процедура СоздатьНовогоКонтрагента()
	
	Для каждого Элемент Из ТаблицаКонтрагентов.ПолучитьЭлементы() Цикл
		
		РеквизитФормыВЗначение("Объект").СоздатьКонтрагента(Элемент).Пустая();
		
	КонецЦикла;
	
КонецПроцедуры // СоздатьНовогоКонтрагента()

&НаКлиенте
// Процедура - обработчик события "НачалоВыбора" для поля "Заказ" табличного поля "Загрузка"
//
Процедура ЗагрузкаЗаказНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ТекущаяСтрокаТабличнойЧасти = Элементы.Загрузка.ТекущиеДанные;
	Если ТипЗнч(ТекущаяСтрокаТабличнойЧасти.Контрагент) = Тип("Строка") Тогда
		
		СтандартнаяОбработка	= Ложь;
		
		ТекстСообщения			= НСтр("ru='Контрагент не идентифицирован, выбор заказа не возможен.';uk='Контрагент не ідентифікований, вибір замовлення не можливий.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		
	КонецЕсли;
	
КонецПроцедуры //ЗагрузкаЗаказНачалоВыбора()

// Процедура выгружает данные в файл XML.
//
&НаСервере
Функция ВыгрузитьДанныеВФайлXML()
	
	ПотокВыгрузки = РеквизитФормыВЗначение("Объект").ВыгрузитьXML();
	Возврат ПотокВыгрузки;
	
КонецФункции // ВыгрузитьДанныеВФайл()

// Функция сохраняет файл выгрузки XML.
//
&НаКлиенте
Процедура СохранитьФайлВыгрузкиXML(ПотокВыгрузки)
	
	Если Объект.Кодировка = "DOS" Тогда
		Кодир = КодировкаТекста.OEM;
	ИначеЕсли Объект.Кодировка = "Windows" Тогда
		Кодир = КодировкаТекста.ANSI;
	Иначе
		Кодир = КодировкаТекста.UTF8;
	КонецЕсли;
	
	Попытка 
		ФайлОбмена = Новый ЗаписьТекста;
		
		ФайлОбмена.Открыть(Объект.ФайлВыгрузки, Кодир);	
		ФайлОбмена.ЗаписатьСтроку(ПотокВыгрузки[0]);
		ФайлОбмена.ЗаписатьСтроку(ПотокВыгрузки[1]);
		
		// Отметим те документы которые успешно загрузились
		Для каждого СтрокаСекции из Объект.Выгрузка Цикл
			Если СтрокаСекции.Готовность = -2 Тогда
				СтрокаСекции.Готовность = -1;
			КонецЕсли;
		КонецЦикла;
		
		ТекстСообщения = НСтр("ru='Данные успешно выгружены в файл %ФайлВыгрузки%';uk='Дані успішно вивантажені у файл %ФайлВыгрузки%'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ФайлВыгрузки%", Объект.ФайлВыгрузки);
		УправлениеНебольшойФирмойКлиент.СообщитьОбОшибке(, ТекстСообщения);
	Исключение
		ТекстСообщения = НСтр("ru='Не удалось записать данные в файл. Возможно, отсутствует каталог. %ФайлВыгрузки%';uk='Не вдалося записати дані у файл. Можливо, відсутній каталог. %ФайлВыгрузки%'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ФайлВыгрузки%", Объект.ФайлВыгрузки);
		УправлениеНебольшойФирмойКлиент.СообщитьОбОшибке(, ТекстСообщения);
	КонецПопытки
	
КонецПроцедуры // СохранитьФайлВыгрузки()

// Загружает секцию документа из формата XML
&НаСервере
Функция ЗагрузитьСекциюДокументаXML(ДокументыДляИмпорта, ФайлXML, ИмпортЗагружаемые)
	
	ВидДокумента = ФайлXML.ПолучитьАтрибут("Вид");
	
	Если ВидДокумента <> Неопределено Тогда
		НоваяСтрокаДокументов = ДокументыДляИмпорта.Добавить();
		НоваяСтрокаДокументов.Операция = ВидДокумента;
		
	Иначе // по умолчанию: "Платежное поручение"	
		
		ВидДокумента = "Платежное поручение";
		
		НоваяСтрокаДокументов = ДокументыДляИмпорта.Добавить();
		НоваяСтрокаДокументов.Операция = ВидДокумента;
		
	КонецЕсли;	
	
	
	Пока ФайлXML.Прочитать() Цикл
		
		ИмяТега = ВРег(СокрЛП(ФайлXML.Имя)); 
		
		Если ФайлXML.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
			ФайлXML.Прочитать();	
			Значение = ФайлXML.Значение;
		КонецЕсли;	
		
		Если ФайлXML.ТипУзла = ТипУзлаXML.Текст Тогда
			ФайлXML.Прочитать();	
		КонецЕсли;	
		
		Если ИмяТега = "ВИДДОКУМЕНТА" Тогда
			Если НоваяСтрокаДокументов.Операция <> Значение Тогда
				НоваяСтрокаДокументов.Операция = Значение;
				
			КонецЕсли;	
			
			Продолжить;
		КонецЕсли;
		
		Если ИмяТега="СЕКЦИЯДОКУМЕНТ" Тогда
			Возврат Истина;
		КонецЕсли;	
			
		Если ИмпортЗагружаемые[ИмяТега] = Истина Тогда
			НоваяСтрокаДокументов[ИмяТега] = Значение;
			
		Иначе
			// Неправиьный реквизит заголовка.
			ТекстСообщения = НСтр("ru='Неверный реквизит платежного документа, %Тег%';uk='Невірний реквізит платіжного документа, %Тег%'"
			);
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Тег%", ИмяТега);
			УправлениеНебольшойФирмойСервер.СообщитьОбОшибке(ЭтотОбъект, ТекстСообщения);
			Возврат Ложь;
			
		КонецЕсли;
			
	КонецЦикла;
	
	Возврат Истина;
	
КонецФункции

// Загрузка секции расчетного счета из XML
&НаСервере
Функция ЗагрузитьСекциюРасчСчетаXML(СтрокаРССчета, ФайлXML, ТегиРасчетногоСчета)
	
	Пока ФайлXML.Прочитать() Цикл
		
		ИмяТега = ВРег(СокрЛП(ФайлXML.Имя)); 
		
		Если ФайлXML.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
			ФайлXML.Прочитать();	
			Значение = ФайлXML.Значение;
		КонецЕсли;	
		
		Если ФайлXML.ТипУзла = ТипУзлаXML.Текст Тогда
			ФайлXML.Прочитать();	
		КонецЕсли;	
		
		Если ИмяТега="СЕКЦИЯРАСЧСЧЕТ" Тогда
			Возврат Истина;
		КонецЕсли;	
			
		Если ТегиРасчетногоСчета[ИмяТега] = Истина Тогда
			СтрокаРССчета[ИмяТега] = Значение;
		Иначе
			ТекстСообщения = НСтр("ru='Неверный реквизит в секции описания расчетного счета %Тег%';uk='Невірний реквізит у секції опису розрахункового рахунку %Тег%'"
			);
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Тег%", ИмяТега);
			УправлениеНебольшойФирмойСервер.СообщитьОбОшибке(ЭтотОбъект, ТекстСообщения);
			Возврат Ложь;
		КонецЕсли;
			
	КонецЦикла;
	
КонецФункции

// Загрузка заголовка файла обмена из XML
&НаСервере
Функция ЗагрузитьСтрокуЗаголовкаXML(Тег,Значение,ТегиЗаголовка,ИмпортЗаголовок)
	
	Если ПустаяСтрока(Тег) Тогда
		Возврат Ложь;
	КонецЕсли;	
	
	Если ТегиЗаголовка[Тег] = Истина Тогда
		ИмпортЗаголовок[Тег] = Значение;
		
	Иначе
		
		// Неправиьный реквизит заголовка.
		ТекстСообщения = НСтр("ru='Неверный реквизит заголовка %Тег%';uk='Невірний реквізит заголовка %Тег%'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Тег%", Тег);
		УправлениеНебольшойФирмойСервер.СообщитьОбОшибке(ЭтотОбъект, ТекстСообщения);
		
	КонецЕсли;
		
КонецФункции

// Процедура заполняет документы на импорт из формата XML.
//
&НаСервере
Процедура ЗаполнитьДокументыНаИмпортXML()
	
	ТаблицаКонтрагентов.ПолучитьЭлементы().Очистить();
	
	// Подготавливаем структуры обработки данных.
	ДокументыКИмпорту = Объект.Загрузка.Выгрузить();
	ИмпортЗагружаемые = СформироватьСоответствиеЗагружаемых();
	ИмпортНеПустые = Неопределено;
	ИмпортНеПустыеПлатежноеПоручение = Неопределено;
	ИмпортНеПустыеПлатежноеПоручениеБюджет = Неопределено;
	
	РасчетныеСчетаКИмпорту = Объект.ИмпортРасчетныеСчета.Выгрузить();
	
	СформироватьСоответствияНеПустыхПриИмпорте(
		ИмпортНеПустые,
		ИмпортНеПустыеПлатежноеПоручение,
		ИмпортНеПустыеПлатежноеПоручениеБюджет
	);
	ТегиРасчетногоСчета = СоздатьСоответствиеИзСтроки(
		ВРег("ДатаНачала,ДатаКонца,РасчСчетОрг,НачальныйОстаток,ВсегоПоступило,ВсегоСписано,КонечныйОстаток,КонецРасчСчет")
	);
	ТегиЗаголовка = СоздатьСоответствиеИзСтроки(
		ВРег("ВерсияФормата,Кодировка,Отправитель,Получатель,ДатаСоздания,ВремяСоздания,ДатаНачала,ДатаКонца")
	);
	СтруктураЗаголовок = Новый Структура(
		ВРег("ВерсияФормата,Кодировка,Отправитель,Получатель,ДатаСоздания,ВремяСоздания,ДатаНачала,ДатаКонца")
	);
	ИмпортЗаголовок = СтруктураЗаголовок;
	ИмпортПризнакОбмена = Ложь;
	НайденКонецФайла = Ложь;
	ИмпортВидыДокументов = Новый Массив;
	РасчетныеСчетаКИмпорту.Очистить();
	ДокументыКИмпорту.Очистить();
	
	ФайлXML = Новый ЧтениеXML;
	
	Попытка
		ФайлXML.ОткрытьФайл(Объект.ФайлЗагрузки);
	Исключение
		ТекстСообщения = НСтр("ru='Ошибка открытия файла ';uk='Помилка відкриття файлу '") + Объект.ФайлЗагрузки;
		УправлениеНебольшойФирмойСервер.СообщитьОбОшибке(ЭтотОбъект, ТекстСообщения);
		Возврат;
	КонецПопытки;	
	
	Пока ФайлXML.Прочитать() Цикл
		ИмяТега = ВРег(СокрЛП(ФайлXML.Имя)); 
		
		Если ИмяТега="СЕКЦИЯДОКУМЕНТ" И ФайлXML.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
			
			Если НЕ ЗагрузитьСекциюДокументаXML(ДокументыКИмпорту, ФайлXML, ИмпортЗагружаемые) Тогда
				Возврат;
			КонецЕсли;
				
		ИначеЕсли ИмяТега="СЕКЦИЯРАСЧСЧЕТ" Тогда
			
			СтрокаРССчетов = РасчетныеСчетаКИмпорту.Добавить();
			
			Если НЕ ЗагрузитьСекциюРасчСчетаXML(СтрокаРССчетов, ФайлXML, ТегиРасчетногоСчета) Тогда
				Возврат;
			КонецЕсли;
			
			Если Объект.БанковскийСчет.НомерСчета <> СтрокаРССчетов.РасчСчетОрг Тогда
				РасчетныеСчетаКИмпорту.Удалить(СтрокаРССчетов);
			КонецЕсли;
			
		ИначеЕсли ИмяТега="РАСЧСЧЕТОРГ" Тогда
			
			Если ФайлXML.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
				ФайлXML.Прочитать();	
				Значение = ФайлXML.Значение;
			КонецЕсли;	
			
			Если ФайлXML.ТипУзла = ТипУзлаXML.Текст Тогда
				ФайлXML.Прочитать();	
			КонецЕсли;	
			
			Если СчетПринадлежитОрганизации(Значение) Тогда
				Если Объект.БанковскийСчет.НомерСчета = Значение Тогда
					СтрокаРССчетов = РасчетныеСчетаКИмпорту.Найти(Значение, "РасчСчетОрг");
					Если СтрокаРССчетов = Неопределено Тогда
						СтрокаРССчетов = РасчетныеСчетаКИмпорту.Добавить();
						СтрокаРССчетов.РасчСчетОрг = Значение;
					КонецЕсли;
				Иначе	
					ТекстСообщения = НСтр("ru='В заголовке файла указан счет (%Значение%) отличный от указанного, расчеты по которому загружаться не будут!';uk='У заголовку файлу зазначений рахунок (%Значение%) відмінний від зазначеного, розрахунки по якому завантажуватися не будуть!'");
					ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Значение%", Значение);
					УправлениеНебольшойФирмойСервер.СообщитьОбОшибке(ЭтотОбъект, ТекстСообщения);
				КонецЕсли;
			Иначе
				ТекстСообщения = НСтр("ru='В заголовке файла указан счет, не принадлежащий организации: %Значение%!';uk='У заголовку файлу зазначений рахунок, що не належить організації: %Значение%!'");
				ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Значение%", Значение);
				УправлениеНебольшойФирмойСервер.СообщитьОбОшибке(ЭтотОбъект, ТекстСообщения);
				Возврат;
			КонецЕсли;
			
		ИначеЕсли ИмяТега="_1CCLIENTBANKEXCHANGE" И ФайлXML.ТипУзла = ТипУзлаXML.КонецЭлемента  Тогда
			Если НЕ ИмпортПризнакОбмена Тогда
				ТекстСообщения = НСтр("ru='В файле импорта отсутствует признак обмена ""_1CClientBankExchange""!';uk='У файлі імпорту відсутній признак обміну ""_1CClientBankExchange""!'");
				УправлениеНебольшойФирмойСервер.СообщитьОбОшибке(ЭтотОбъект, ТекстСообщения);
				Возврат;
			КонецЕсли;
			
			НайденКонецФайла = Истина;
			НомерСтроки = 0;
			
			// Формируем объектные структуры данных
			Для каждого СтрокаДокумента Из ДокументыКИмпорту Цикл
				// Последовательно обрабатываем каждую загруженную строку
				
				//Распознаем реквизиты
				//Если в файле находятся выписка пл. документов по нескольким счетам, то распознаем и отображаем
				//только те которые выгружены по указанному банковскому счету
				Если СтрокаДокумента.ПлательщикСчет = Объект.БанковскийСчет.НомерСчета
					ИЛИ СтрокаДокумента.ПолучательСчет  = Объект.БанковскийСчет.НомерСчета Тогда
					РаспознатьДанныеВСтрокеДокумента(СтрокаДокумента);
					НомерСтроки = НомерСтроки + 1;
					СтрокаДокумента.НомерСтроки = НомерСтроки;
					// Для каждого реквизита (= колонка) надо проверить на пустое значение
					Для каждого КолонкаИмпорта из ДокументыКИмпорту.Колонки Цикл
						ПроверитьНаПустоеЗначениеИмпорта(СтрокаДокумента, КолонкаИмпорта.Имя, КолонкаИмпорта.Заголовок, ИмпортНеПустые);
					КонецЦикла;
					
					Если ТипЗнч(СтрокаДокумента.Контрагент)=Тип("Строка") ИЛИ
						ТипЗнч(СтрокаДокумента.СчетКонтрагента)=Тип("Строка") ИЛИ
						ТипЗнч(СтрокаДокумента.Договор)=Тип("Строка") Тогда
						
						//добавляем реквизиты в табличну часть для дальнейнего использования
						СписокНенайденных(СтрокаДокумента, Объект.БанковскийСчет, ТаблицаКонтрагентов);
					КонецЕсли;
				Иначе
					//остальные помечаем для последующего удаления
					СтрокаДокумента.НомерСтроки = 0;					
				КонецЕсли;
			КонецЦикла;
			
			//Удалим не нужные строки из таблицы
			Количество = ДокументыКИмпорту.количество()-1;
			Для й=0 по Количество Цикл
				Если ДокументыКИмпорту[Количество-й].НомерСтроки = 0 Тогда
					ДокументыКИмпорту.Удалить(Количество-й);
				КонецЕсли;
			КонецЦикла;
			
		ИначеЕсли ИмяТега="_1CCLIENTBANKEXCHANGE" И ФайлXML.ТипУзла = ТипУзлаXML.НачалоЭлемента  Тогда
			ИмпортПризнакОбмена = Истина;
			
		Иначе
			
			Если ФайлXML.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
				ФайлXML.Прочитать();	
				Значение = ФайлXML.Значение;
			КонецЕсли;	
			
			ЗагрузитьСтрокуЗаголовкаXML(ИмяТега, Значение, ТегиЗаголовка, ИмпортЗаголовок);
			
			Если ФайлXML.ТипУзла = ТипУзлаXML.Текст Тогда
				ФайлXML.Прочитать();	
			КонецЕсли;	
			
		КонецЕсли;
	КонецЦикла;
	
	Если НЕ НайденКонецФайла Тогда
		РасчетныеСчетаКИмпорту.Очистить();
		ДокументыКИмпорту.Очистить();
		ТекстСообщения = НСтр("ru='Файл загрузки не соответствует стандарту (не найдена секция КонецФайла)!';uk='Файл завантаження не відповідає стандарту (не знайдена секція КонецФайла)!'");
		УправлениеНебольшойФирмойСервер.СообщитьОбОшибке(ЭтотОбъект, ТекстСообщения);
	КонецЕсли;
	
	Для каждого СтрокаДокумента Из ДокументыКИмпорту Цикл
		СтрокаДокумента.Загружать = ПустаяСтрока(СтрокаДокумента.ОписаниеОшибок);
		СтрокаДокумента.НазначениеПлатежа = СокрЛП(СтрокаДокумента.НазначениеПлатежа);
		СтрокаДокумента.НомерКартинки = ?(ПустаяСтрока(СтрокаДокумента.ОписаниеОшибок), 0, 1);
	КонецЦикла;
	
	Объект.Загрузка.Очистить();
	Объект.Загрузка.Загрузить(ДокументыКИмпорту);
	
	Объект.ИмпортРасчетныеСчета.Очистить();
	Объект.ИмпортРасчетныеСчета.Загрузить(РасчетныеСчетаКИмпорту);
	
КонецПроцедуры // ЗаполнитьДокументыНаИмпортXML()
