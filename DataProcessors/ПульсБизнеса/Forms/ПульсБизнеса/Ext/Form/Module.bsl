
#Область ПеременныеФормы

&НаКлиенте
Перем ПараметрыОбработчикаОжидания;	// Параметры опроса завершения фонового задания. См. общий модуль ДлительныеОперацииКлиент

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Инициализация();
	ЗагрузитьНастройки();
	СоздатьЭлементыПоказатели();
	СоздатьЭлементыДиаграммы();
	
	// Признак размещения формы. Если Ложь (значение по умолчанию) то форма открыта средствами платформы на начальной странице.
	Параметры.Свойство("ОткрытаНеНаНачальнойСтранице", ОткрытаНеНаНачальнойСтранице);
	
	Если ОткрытаНеНаНачальнойСтранице Тогда
		Заголовок = НСтр("ru='Пульс бизнеса';uk='Пульс бізнесу'");
	КонецЕсли; 
	
	АдресНастроекПоказателей = ПоместитьВоВременноеХранилище(НастройкиПоказателей.Выгрузить(), УникальныйИдентификатор);
	АдресНастроекДиаграмм = ПоместитьВоВременноеХранилище(НастройкиДиаграмм.Выгрузить(), УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// При старте системы устанавливаем задержку
	Если ОткрытаНеНаНачальнойСтранице Тогда
		ИнтервалЗапускаФоновогоЗадания = 0.1;
	Иначе
		ИнтервалЗапускаФоновогоЗадания = 3;
	КонецЕсли;
	
	ПодключитьОбработчикОжидания("Подключаемый_ЗапуститьФоновоеЗаданиеПриОткрытии", ИнтервалЗапускаФоновогоЗадания, Истина);
		
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	Если НЕ ТипЗнч(ВыбранноеЗначение)=Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	Если ВыбранноеЗначение.Событие="НастройкаПоказателя" Тогда
		
		Если ВыбранноеЗначение.Свойство("ИдентификаторСтроки") Тогда
			Стр = ДобавленныеПоказатели.НайтиПоИдентификатору(ВыбранноеЗначение.ИдентификаторСтроки);
		Иначе
			Стр = ДобавленныеПоказатели.Добавить(); 
		КонецЕсли; 
		ЗаполнитьЗначенияСвойств(Стр, ВыбранноеЗначение);
		
		// Связь по идентификатору с таблицей вариантов показателей
		СтруктураОтбора = Новый Структура;
		СтруктураОтбора.Вставить("Показатель", ВыбранноеЗначение.Показатель);
		СтруктураОтбора.Вставить("Ресурс", ВыбранноеЗначение.Ресурс);
		Строки = НастройкиПоказателей.НайтиСтроки(СтруктураОтбора);
		Если Строки.Количество()>0 Тогда
			Стр.ИдентификаторСтрокиНастроек = Строки[0].ПолучитьИдентификатор();
		КонецЕсли;
	
		Элементы.СтраницаОжидание.Видимость = Истина;
		Элементы.СтраницаДанные.Видимость = Ложь;
		СохранитьНастройкуПоказателяСервер();
		НачатьОжиданиеЗавершенияФоновогоЗаданияНаКлиенте();
		
	ИначеЕсли ВыбранноеЗначение.Событие="НастройкаРазделителя" Тогда
		
		Если ВыбранноеЗначение.Свойство("ИдентификаторСтроки") Тогда
			Стр = ДобавленныеПоказатели.НайтиПоИдентификатору(ВыбранноеЗначение.ИдентификаторСтроки);
		Иначе
			Стр = ДобавленныеПоказатели.Добавить(); 
		КонецЕсли; 
		ЗаполнитьЗначенияСвойств(Стр, ВыбранноеЗначение);
		
		Если ВыбранноеЗначение.Свойство("ИдентификаторСтроки") Тогда
			Для каждого Элемент Из Элементы[Стр.ИмяГруппы].ПодчиненныеЭлементы Цикл
				Элемент.Заголовок = ВыбранноеЗначение.Представление;
			КонецЦикла;
			СохранитьНастройки();
		Иначе
			Элементы.СтраницаОжидание.Видимость = Истина;
			Элементы.СтраницаДанные.Видимость = Ложь;
			СохранитьНастройкуПоказателяСервер();
			НачатьОжиданиеЗавершенияФоновогоЗаданияНаКлиенте();
		КонецЕсли; 
		
	ИначеЕсли ВыбранноеЗначение.Событие="НастройкаДиаграммы" Тогда
		
		// Связь по идентификатору с таблицей вариантов показателей
		СтруктураОтбора = Новый Структура;
		СтруктураОтбора.Вставить("Диаграмма", ВыбранноеЗначение.Диаграмма);
		Строки = НастройкиДиаграмм.НайтиСтроки(СтруктураОтбора);
		Если Строки.Количество()=0 Тогда
			Возврат;
		КонецЕсли;
		СтрНастроек = Строки[0];
		ОписаниеСерий = СтрНастроек.Серии[ВыбранноеЗначение.Серия];
		ОписаниеТочек = СтрНастроек.Точки[ВыбранноеЗначение.Точка];
		
		Если ВыбранноеЗначение.Свойство("ИдентификаторСтроки") Тогда
			Стр = ДобавленныеДиаграммы.НайтиПоИдентификатору(ВыбранноеЗначение.ИдентификаторСтроки);
		Иначе
			Стр = ДобавленныеДиаграммы.Добавить(); 
		КонецЕсли; 
		ЗаполнитьЗначенияСвойств(Стр, ВыбранноеЗначение);
		
		Стр.ИдентификаторСтрокиНастроек = СтрНастроек.ПолучитьИдентификатор();
		Стр.ПредставленияТочек = ОписаниеТочек.Представления;
		
		Если ЗначениеЗаполнено(ВыбранноеЗначение.ПериодСравнения) И НЕ ВыбранноеЗначение.РежимОстатка Тогда
			МассивПредставлений = Новый Массив;
			МассивПредставлений.Добавить(ПредставлениеСтандартногоПериода(ВыбранноеЗначение.Период));
			МассивПредставлений.Добавить(ПредставлениеСтандартногоПериода(ВыбранноеЗначение.ПериодСравнения));
			Стр.ПредставленияСерий = Новый ФиксированныйМассив(МассивПредставлений);
		ИначеЕсли ЗначениеЗаполнено(ВыбранноеЗначение.ПериодСравнения) И ВыбранноеЗначение.РежимОстатка Тогда
			МассивПредставлений = Новый Массив;
			МассивПредставлений.Добавить(ПредставлениеСтандартнойДатыНачала(ВыбранноеЗначение.Период));
			МассивПредставлений.Добавить(ПредставлениеСтандартнойДатыНачала(ВыбранноеЗначение.ПериодСравнения));
			Стр.ПредставленияСерий = Новый ФиксированныйМассив(МассивПредставлений);
		Иначе
			Стр.ПредставленияСерий = ОписаниеСерий.Представления;
		КонецЕсли; 
		
		Элементы.СтраницаОжидание.Видимость = Истина;
		Элементы.СтраницаДанные.Видимость = Ложь;
		СохранитьНастройкуДиаграммыСервер();
		НачатьОжиданиеЗавершенияФоновогоЗаданияНаКлиенте();
		
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия="ВводОстатков" Тогда
		ОбновитьФорму();
		НачатьОжиданиеЗавершенияФоновогоЗаданияНаКлиенте();
	КонецЕсли; 	
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура Подключаемый_ПоказательЗначениеНажатие(Элемент)
	
	КонтекстРасшифроватьПоказатель(Элемент);
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияДобавитьОбъектНажатие(Элемент)
	
	Оповещение = Новый ОписаниеОповещения("ДекорацияДобавитьОбъектНажатиеЗавершение", ЭтотОбъект);
	Меню = Новый СписокЗначений;
	ЕстьВводыОстатков = Ложь;
	Для каждого Режим Из РежимыВводаОстатков Цикл
		Если НЕ Режим.Значение Тогда
			ЕстьВводыОстатков = Истина;
		КонецЕсли; 
	КонецЦикла;
	Если НЕ ЕстьВводыОстатков Тогда
		Меню.Добавить("ВводНачальныхОстатков", НСтр("ru='Ввод начальных остатков';uk='Введення початкових залишків'"));
	КонецЕсли; 
	Меню.Добавить("ЗаказПокупателя", НСтр("ru='Заказ покупателя';uk='Замовлення покупця'"));
	Меню.Добавить("СчетНаОплату", НСтр("ru='Счет на оплату';uk='Рахунок на оплату'"));
	Меню.Добавить("РасходнаяНакладная", НСтр("ru='Расходная накладная';uk='Видаткова накладна'"));
	Меню.Добавить("ПриходнаяНакладная", НСтр("ru='Приходная накладная';uk='Прибуткова накладна'"));
	Меню.Добавить("ПоступлениеВКассу", НСтр("ru='Поступление денег';uk='Надходження грошей'"));
	Меню.Добавить("РасходИзКассы", НСтр("ru='Списание денег';uk='Списання грошей'"));
	ПоказатьВыборИзМеню(Оповещение, Меню, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияДобавитьОбъектНажатиеЗавершение(ВыбранныйЭлемент, ДополнительныеДанные) Экспорт
	
	Если ВыбранныйЭлемент=Неопределено Тогда
		Возврат;
	КонецЕсли;
	ИмяДокумента = ВыбранныйЭлемент.Значение;
	Если ИмяДокумента="ВводНачальныхОстатков" Тогда
		ОткрытьФорму("Обработка.ПомощникВводаНачальныхОстатков.Форма");
	Иначе
		ОткрытьФорму("Документ."+ВыбранныйЭлемент.Значение+".ФормаОбъекта");
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияОбновитьНажатие(Элемент)
	
	ОбновитьФорму();
	НачатьОжиданиеЗавершенияФоновогоЗаданияНаКлиенте();
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ДиаграммаВыбор(Элемент, ЗначениеДиаграммы, СтандартнаяОбработка)
	
	ИмяГруппы = Лев(Элемент.Имя, Найти(Элемент.Имя, "_")-1);
	СтруктураОтбора = Новый Структура;
	СтруктураОтбора.Вставить("ИмяГруппы", ИмяГруппы);
	Строки = ДобавленныеДиаграммы.НайтиСтроки(СтруктураОтбора);
	Если Строки.Количество()=0 Тогда
		Возврат;
	КонецЕсли;
	РасшифроватьДиаграмму(Строки[0].ПолучитьИдентификатор());
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ДобавитьПоказательКоманда(Команда)
	
	СписокКоманд = Новый СписокЗначений;
	СписокКоманд.Добавить("ДобавитьПоказатель", НСтр("ru='Показатель';uk='Показник'"),, БиблиотекаКартинок.ДобавитьЭлементСписка);
	СписокКоманд.Добавить("ДобавитьРазделитель", НСтр("ru='Заголовок';uk='Заголовок'"),, БиблиотекаКартинок.СоздатьГруппу);
	Оповещение = Новый ОписаниеОповещения("ДобавитьПоказательЗавершение", ЭтотОбъект);
	ПоказатьВыборИзМеню(Оповещение, СписокКоманд, Элементы.ДобавитьПоказатель);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьПоказательЗавершение(ВыбранныйЭлемент, ДополнительныеДанные) Экспорт
	
	Если ВыбранныйЭлемент=Неопределено Тогда
		Возврат;
	КонецЕсли; 
	Если ВыбранныйЭлемент.Значение="ДобавитьПоказатель"  Тогда
		ОткрытьФормуДобавленияПоказателя();
	ИначеЕсли ВыбранныйЭлемент.Значение="ДобавитьРазделитель"  Тогда
		ОткрытьФормуДобавленияРазделителя();
	КонецЕсли; 
	
КонецПроцедуры
 
#Область Показатели

&НаКлиенте
Процедура КонтекстНастроитьПоказатель(Команда)
	
	ИмяГруппы = Лев(Команда.Имя, Найти(Команда.Имя, "_")-1);
	СтруктураОтбора = Новый Структура;
	СтруктураОтбора.Вставить("ИмяГруппы", ИмяГруппы);
	Строки = ДобавленныеПоказатели.НайтиСтроки(СтруктураОтбора);
	Если Строки.Количество()=0 Тогда
		Возврат;
	КонецЕсли; 
	ОткрытьФормуНастройкиПоказателя(Строки[0].ПолучитьИдентификатор());
	
КонецПроцедуры

&НаКлиенте
Процедура КонтекстНастроитьРазделитель(Команда)
	
	ИмяГруппы = Лев(Команда.Имя, Найти(Команда.Имя, "_")-1);
	СтруктураОтбора = Новый Структура;
	СтруктураОтбора.Вставить("ИмяГруппы", ИмяГруппы);
	Строки = ДобавленныеПоказатели.НайтиСтроки(СтруктураОтбора);
	Если Строки.Количество()=0 Тогда
		Возврат;
	КонецЕсли; 
	ОткрытьФормуНастройкиРазделителя(Строки[0].ПолучитьИдентификатор());
	
КонецПроцедуры

&НаКлиенте
Процедура КонтекстУдалитьПоказатель(Команда)
	
	ИмяГруппы = Лев(Команда.Имя, Найти(Команда.Имя, "_")-1);
	СтруктураОтбора = Новый Структура;
	СтруктураОтбора.Вставить("ИмяГруппы", ИмяГруппы);
	Строки = ДобавленныеПоказатели.НайтиСтроки(СтруктураОтбора);
	Если Строки.Количество()=0 Тогда
		Возврат;
	КонецЕсли;
	Индекс = ДобавленныеПоказатели.Индекс(Строки[0]);
	Индекс = ?(Индекс=0, 0, Индекс-1);
	КонтекстУдалитьПоказательСервер(Строки[0].ПолучитьИдентификатор());
	Если ДобавленныеПоказатели.Количество()>0 Тогда
		ТекущийЭлемент = Элементы[ДобавленныеПоказатели[Индекс].ИмяГруппы+"_Заголовок"];
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура КонтекстУдалитьПоказательСервер(Идентификатор)
	
	Стр = ДобавленныеПоказатели.НайтиПоИдентификатору(Идентификатор);
	УдалитьЭлементыРекурсивно(Элементы[Стр.ИмяГруппы]);
	Элементы.Удалить(Элементы[Стр.ИмяГруппы]);
	ДобавленныеПоказатели.Удалить(Стр);
	СохранитьНастройки();
	
КонецПроцедуры

&НаСервере
Процедура УдалитьЭлементыРекурсивно(Группа)
	
	Пока Группа.ПодчиненныеЭлементы.Количество()>0 Цикл
		Элемент = Группа.ПодчиненныеЭлементы[0];
		Если ТипЗнч(Элемент)=Тип("КнопкаФормы") Тогда
			Команды.Удалить(Элемент.ИмяКоманды);
		ИначеЕсли ТипЗнч(Элемент)=Тип("ГруппаФормы") Тогда
			УдалитьЭлементыРекурсивно(Элемент);
		КонецЕсли; 
		Элементы.Удалить(Элемент);
	КонецЦикла; 
	
КонецПроцедуры

&НаКлиенте
Процедура КонтекстСместитьВверхПоказатель(Команда)
	
	ИмяГруппы = Лев(Команда.Имя, Найти(Команда.Имя, "_")-1);
	СтруктураОтбора = Новый Структура;
	СтруктураОтбора.Вставить("ИмяГруппы", ИмяГруппы);
	Строки = ДобавленныеПоказатели.НайтиСтроки(СтруктураОтбора);
	Если Строки.Количество()=0 Тогда
		Возврат;
	КонецЕсли;
	КонтекстСместитьВверхПоказательСервер(Строки[0].ПолучитьИдентификатор());
	
КонецПроцедуры

&НаСервере
Процедура КонтекстСместитьВверхПоказательСервер(Идентификатор)
	
	Стр = ДобавленныеПоказатели.НайтиПоИдентификатору(Идентификатор);
	Индекс = ДобавленныеПоказатели.Индекс(Стр);
	Если Индекс=0 Тогда
		Возврат;
	КонецЕсли;
	СтрПеред = ДобавленныеПоказатели[Индекс-1];
	Элементы.Переместить(Элементы[Стр.ИмяГруппы], Элементы.ГруппаДобавленныеПоказатели, Элементы[СтрПеред.ИмяГруппы]);
	ДобавленныеПоказатели.Сдвинуть(Индекс, -1);
	СохранитьНастройки();
	
КонецПроцедуры

&НаКлиенте
Процедура КонтекстСместитьВнизПоказатель(Команда)
	
	ИмяГруппы = Лев(Команда.Имя, Найти(Команда.Имя, "_")-1);
	СтруктураОтбора = Новый Структура;
	СтруктураОтбора.Вставить("ИмяГруппы", ИмяГруппы);
	Строки = ДобавленныеПоказатели.НайтиСтроки(СтруктураОтбора);
	Если Строки.Количество()=0 Тогда
		Возврат;
	КонецЕсли;
	КонтекстСместитьВнизПоказательСервер(Строки[0].ПолучитьИдентификатор());
	
КонецПроцедуры

&НаСервере
Процедура КонтекстСместитьВнизПоказательСервер(Идентификатор)
	
	Стр = ДобавленныеПоказатели.НайтиПоИдентификатору(Идентификатор);
	Индекс = ДобавленныеПоказатели.Индекс(Стр);
	Если Индекс=ДобавленныеПоказатели.Количество()-1 Тогда
		Возврат;
	КонецЕсли;
	Если Индекс=ДобавленныеПоказатели.Количество()-2 Тогда
		Элементы.Переместить(Элементы[Стр.ИмяГруппы], Элементы.ГруппаДобавленныеПоказатели);
	Иначе
		СтрПосле = ДобавленныеПоказатели[Индекс+2];
		Элементы.Переместить(Элементы[Стр.ИмяГруппы], Элементы.ГруппаДобавленныеПоказатели, Элементы[СтрПосле.ИмяГруппы]);
	КонецЕсли; 
	ДобавленныеПоказатели.Сдвинуть(Индекс, 1);
	СохранитьНастройки();
	
КонецПроцедуры

&НаКлиенте
Процедура КонтекстРасшифроватьПоказатель(Команда)
	
	ИмяГруппы = Лев(Команда.Имя, Найти(Команда.Имя, "_")-1);
	СтруктураОтбора = Новый Структура;
	СтруктураОтбора.Вставить("ИмяГруппы", ИмяГруппы);
	Строки = ДобавленныеПоказатели.НайтиСтроки(СтруктураОтбора);
	Если Строки.Количество()=0 Тогда
		Возврат;
	КонецЕсли;
	РасшифроватьПоказатель(Строки[0].ПолучитьИдентификатор());
	
КонецПроцедуры

#КонецОбласти 

#Область Диаграммы

&НаКлиенте
Процедура ДобавитьДиаграммуКоманда(Команда)
	
	ОткрытьФормуДобавленияДиаграммы();
	
КонецПроцедуры

&НаКлиенте
Процедура КонтекстНастроитьДиаграмму(Команда)
	
	ИмяГруппы = Лев(Команда.Имя, Найти(Команда.Имя, "_")-1);
	СтруктураОтбора = Новый Структура;
	СтруктураОтбора.Вставить("ИмяГруппы", ИмяГруппы);
	Строки = ДобавленныеДиаграммы.НайтиСтроки(СтруктураОтбора);
	Если Строки.Количество()=0 Тогда
		Возврат;
	КонецЕсли; 
	ОткрытьФормуНастройкиДиаграммы(Строки[0].ПолучитьИдентификатор());
	
КонецПроцедуры

&НаКлиенте
Процедура КонтекстУдалитьДиаграмму(Команда)
	
	ИмяГруппы = Лев(Команда.Имя, Найти(Команда.Имя, "_")-1);
	СтруктураОтбора = Новый Структура;
	СтруктураОтбора.Вставить("ИмяГруппы", ИмяГруппы);
	Строки = ДобавленныеДиаграммы.НайтиСтроки(СтруктураОтбора);
	Если Строки.Количество()=0 Тогда
		Возврат;
	КонецЕсли;
	КонтекстУдалитьДиаграммуСервер(Строки[0].ПолучитьИдентификатор());
	
КонецПроцедуры

&НаСервере
Процедура КонтекстУдалитьДиаграммуСервер(Идентификатор)
	
	Стр = ДобавленныеДиаграммы.НайтиПоИдентификатору(Идентификатор);
	Элементы.Удалить(Элементы[Стр.ИмяГруппы+"_Настроить"]);
	Команды.Удалить(Команды[Стр.ИмяГруппы+"_Настроить"]);
	Элементы.Удалить(Элементы[Стр.ИмяГруппы+"_Удалить"]);
	Команды.Удалить(Команды[Стр.ИмяГруппы+"_Удалить"]);
	Элементы.Удалить(Элементы[Стр.ИмяГруппы+"_Вверх"]);
	Команды.Удалить(Команды[Стр.ИмяГруппы+"_Вверх"]);
	Элементы.Удалить(Элементы[Стр.ИмяГруппы+"_Вниз"]);
	Команды.Удалить(Команды[Стр.ИмяГруппы+"_Вниз"]);
	Элементы.Удалить(Элементы[Стр.ИмяГруппы+"_Диаграмма"]);
	Элементы.Удалить(Элементы[Стр.ИмяГруппы]);
	ДобавленныеДиаграммы.Удалить(Стр);
	СохранитьНастройки();
	
КонецПроцедуры

&НаКлиенте
Процедура КонтекстСместитьВверхДиаграмму(Команда)
	
	ИмяГруппы = Лев(Команда.Имя, Найти(Команда.Имя, "_")-1);
	СтруктураОтбора = Новый Структура;
	СтруктураОтбора.Вставить("ИмяГруппы", ИмяГруппы);
	Строки = ДобавленныеДиаграммы.НайтиСтроки(СтруктураОтбора);
	Если Строки.Количество()=0 Тогда
		Возврат;
	КонецЕсли;
	КонтекстСместитьВверхДиаграммуСервер(Строки[0].ПолучитьИдентификатор());
	
КонецПроцедуры

&НаСервере
Процедура КонтекстСместитьВверхДиаграммуСервер(Идентификатор)
	
	Стр = ДобавленныеДиаграммы.НайтиПоИдентификатору(Идентификатор);
	Индекс = ДобавленныеДиаграммы.Индекс(Стр);
	Если Индекс=0 Тогда
		Возврат;
	КонецЕсли;
	СтрПеред = ДобавленныеДиаграммы[Индекс-1];
	Элементы.Переместить(Элементы[Стр.ИмяГруппы], Элементы.ГруппаДобавленныеДиаграммы, Элементы[СтрПеред.ИмяГруппы]);
	ДобавленныеДиаграммы.Сдвинуть(Индекс, -1);
	СохранитьНастройки();
	
КонецПроцедуры

&НаКлиенте
Процедура КонтекстСместитьВнизДиаграмму(Команда)
	
	ИмяГруппы = Лев(Команда.Имя, Найти(Команда.Имя, "_")-1);
	СтруктураОтбора = Новый Структура;
	СтруктураОтбора.Вставить("ИмяГруппы", ИмяГруппы);
	Строки = ДобавленныеДиаграммы.НайтиСтроки(СтруктураОтбора);
	Если Строки.Количество()=0 Тогда
		Возврат;
	КонецЕсли;
	КонтекстСместитьВнизДиаграммуСервер(Строки[0].ПолучитьИдентификатор());
	
КонецПроцедуры

&НаСервере
Процедура КонтекстСместитьВнизДиаграммуСервер(Идентификатор)
	
	Стр = ДобавленныеДиаграммы.НайтиПоИдентификатору(Идентификатор);
	Индекс = ДобавленныеДиаграммы.Индекс(Стр);
	Если Индекс=ДобавленныеДиаграммы.Количество()-1 Тогда
		Возврат;
	КонецЕсли;
	Если Индекс=ДобавленныеДиаграммы.Количество()-2 Тогда
		Элементы.Переместить(Элементы[Стр.ИмяГруппы], Элементы.ГруппаДобавленныеДиаграммы);
	Иначе
		СтрПосле = ДобавленныеДиаграммы[Индекс+2];
		Элементы.Переместить(Элементы[Стр.ИмяГруппы], Элементы.ГруппаДобавленныеДиаграммы, Элементы[СтрПосле.ИмяГруппы]);
	КонецЕсли; 
	ДобавленныеДиаграммы.Сдвинуть(Индекс, 1);
	СохранитьНастройки();
	
КонецПроцедуры

&НаКлиенте
Процедура КонтекстРасшифроватьДиаграмму(Команда)
	
	ИмяГруппы = Лев(Команда.Имя, Найти(Команда.Имя, "_")-1);
	СтруктураОтбора = Новый Структура;
	СтруктураОтбора.Вставить("ИмяГруппы", ИмяГруппы);
	Строки = ДобавленныеДиаграммы.НайтиСтроки(СтруктураОтбора);
	Если Строки.Количество()=0 Тогда
		Возврат;
	КонецЕсли;
	РасшифроватьДиаграмму(Строки[0].ПолучитьИдентификатор());
	
КонецПроцедуры

#КонецОбласти 

#КонецОбласти 

#Область ФоновоеЗаданиеПолучениеДанных

&НаКлиенте
Процедура Подключаемый_ЗапуститьФоновоеЗаданиеПриОткрытии()
	
	ЗапуститьФоновоеЗаданиеНаСервере();
	НачатьОжиданиеЗавершенияФоновогоЗаданияНаКлиенте();
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПроверитьЗавершениеДлительнойОперации()
	
	Если ФоновоеЗаданиеЗапущено Тогда
		
		Если ЗаданиеВыполнено(ФоновоеЗаданиеИдентификатор) Тогда
			// Данные расчитаны, обновим данные формы
			ФоновоеЗаданиеЗапущено = Ложь;
			ФоновоеЗаданиеВыполнено = Истина;
			ОбновитьДанные();
			Элементы.СтраницаОжидание.Видимость = Ложь;
			Элементы.СтраницаДанные.Видимость = Истина;
		Иначе
			// Продолжим ожидание
			ДлительныеОперацииКлиент.ОбновитьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
			ПодключитьОбработчикОжидания(
				"Подключаемый_ПроверитьЗавершениеДлительнойОперации",
				ПараметрыОбработчикаОжидания.ТекущийИнтервал,
				Истина);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗапуститьФоновоеЗаданиеНаСервере()
	
	Если МонопольныйРежим() Тогда
		Возврат;
	КонецЕсли;
	
	Если ФоновоеЗаданиеЗапущено И НЕ ЗаданиеВыполнено(ФоновоеЗаданиеИдентификатор) Тогда
		// Фоновое задание было запущено раньше, необходимо дождаться завершения.
		Возврат;
	КонецЕсли;
	
	ТекущаяДатаСеанса = ТекущаяДатаСеанса();
	
	// Параметры обернем в структуру для их передачи через механизм ДлительныеОперации.
	ПараметрыПроцедуры = Новый Структура;
	ПараметрыПроцедуры.Вставить("Показатели", ДобавленныеПоказатели.Выгрузить());
	ПараметрыПроцедуры.Вставить("Диаграммы", ДобавленныеДиаграммы.Выгрузить());
	
	НаименованиеЗадания = НСтр("ru='Обновление виджетов пульса бизнеса';uk='Оновлення виджетів пульсу бізнесу'");
	
	Результат = УправлениеНебольшойФирмойСервер.ЗапуститьВыполнениеВФоне(
		УникальныйИдентификатор,
		"Обработки.ПульсБизнеса.ПолучитьДанные",
		ПараметрыПроцедуры,
		НаименованиеЗадания,
		,
		Ложь);
	
	ФоновоеЗаданиеАдресРезультата = Результат.АдресХранилища;
	ФоновоеЗаданиеИдентификатор   = Результат.ИдентификаторЗадания;
	
	// Если фоновое задание завершилось за время вызова, то данные уже получены
	Если Результат.ЗаданиеВыполнено Тогда
		ОбновитьДанные();
		ФоновоеЗаданиеВыполнено = Истина;
	Иначе
		// иначе начнем ожидания завершения фонового задания
		ФоновоеЗаданиеЗапущено = Истина;
		Элементы.СтраницаОжидание.Видимость = Истина;
		Элементы.СтраницаДанные.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НачатьОжиданиеЗавершенияФоновогоЗаданияНаКлиенте()
	
	Если ФоновоеЗаданиеЗапущено Тогда
		// Начнем опрос окончания фонового задания
		ДлительныеОперацииКлиент.ИнициализироватьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
		ПодключитьОбработчикОжидания("Подключаемый_ПроверитьЗавершениеДлительнойОперации",
			ПараметрыОбработчикаОжидания.ТекущийИнтервал,
			Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЗаданиеВыполнено(ИдентификаторЗадания)
	
	Возврат ДлительныеОперации.ЗаданиеВыполнено(ИдентификаторЗадания);
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Показатели

&НаСервере
Процедура ДобавитьПоказатель(
	Показатель, 
	Ресурс = "", 
	Представление = "", 
	ПредставлениеРесурса = "", 
	Остаток = Ложь, 
	Валютный = Ложь, 
	Формат = "", 
	ИмяМакета = "", 
	ИмяОтчета = "", 
	КлючВарианта = "", 
	КолонкиОтчета = "", 
	РазделУчета = "")
	
	СтруктураПоиска = Новый Структура;
	СтруктураПоиска.Вставить("Показатель", Показатель);
	СтруктураПоиска.Вставить("Ресурс", Ресурс);
	Если НастройкиПоказателей.НайтиСтроки(СтруктураПоиска).Количество()>0 Тогда
		Возврат;
	КонецЕсли; 
	
	Стр = НастройкиПоказателей.Добавить();
	Стр.Показатель = Показатель;
	Стр.Ресурс = Ресурс;
	Стр.Представление = ?(ПустаяСтрока(Представление), Показатель, Представление);
	Стр.ПредставлениеРесурса = ?(ПустаяСтрока(ПредставлениеРесурса), Ресурс, ПредставлениеРесурса);
	Стр.Остаток = Остаток;
	Стр.Валютный = Валютный;
	Стр.Формат = Формат;
	Стр.ИмяМакета = ИмяМакета;
	Стр.ИмяОтчета = ИмяОтчета;
	Стр.КлючВарианта = КлючВарианта;
	Если ЗначениеЗаполнено(ИмяОтчета) И ЗначениеЗаполнено(КлючВарианта) Тогда
		Стр.Вариант = ВариантыОтчетов.ПолучитьСсылку(ОбщегоНазначения.ИдентификаторОбъектаМетаданных(Метаданные.НайтиПоПолномуИмени(ИмяОтчета)), КлючВарианта);
	КонецЕсли; 
	Стр.КолонкиОтчета = КолонкиОтчета;
	Стр.РазделУчета = РазделУчета;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуДобавленияПоказателя()
	
	Если ФоновоеЗаданиеЗапущено Тогда
		Возврат;
	КонецЕсли; 
	
	СтруктураОткрытия = Новый Структура;
	СтруктураОткрытия.Вставить("Показатель", Неопределено);
	СтруктураОткрытия.Вставить("Ресурс", Неопределено);
	СтруктураОткрытия.Вставить("Представление", "");
	СтруктураОткрытия.Вставить("Период", Новый СтандартныйПериод);
	СтруктураОткрытия.Вставить("ПериодСравнения", Новый СтандартныйПериод);
	СтруктураОткрытия.Вставить("Фильтры", Новый ФиксированныйМассив(Новый Массив));
	СтруктураОткрытия.Вставить("Настройки", Новый ФиксированныйМассив(Новый Массив));
	
	СтруктураОткрытия.Вставить("АдресНастроекПоказателей", АдресНастроекПоказателей);
	
	ОткрытьФорму("Обработка.ПульсБизнеса.Форма.ФормаНастройкиПоказателя", СтруктураОткрытия, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуДобавленияРазделителя()
	
	Если ФоновоеЗаданиеЗапущено Тогда
		Возврат;
	КонецЕсли; 
	
	СтруктураОткрытия = Новый Структура;
	СтруктураОткрытия.Вставить("Представление", "");
	
	ОткрытьФорму("Обработка.ПульсБизнеса.Форма.ФормаНастройкиРазделителя", СтруктураОткрытия, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуНастройкиПоказателя(Идентификатор)
	
	Если ФоновоеЗаданиеЗапущено Тогда
		Возврат;
	КонецЕсли; 
	
	Стр = ДобавленныеПоказатели.НайтиПоИдентификатору(Идентификатор);
	
	СтруктураОткрытия = Новый Структура;
	СтруктураОткрытия.Вставить("Показатель", Стр.Показатель);
	СтруктураОткрытия.Вставить("Ресурс", Стр.Ресурс);
	СтруктураОткрытия.Вставить("Представление", Стр.Представление);
	СтруктураОткрытия.Вставить("Период", Стр.Период);
	СтруктураОткрытия.Вставить("ПериодСравнения", Стр.ПериодСравнения);
	СтруктураОткрытия.Вставить("Фильтры", Стр.Фильтры);
	СтруктураОткрытия.Вставить("Настройки", Стр.Настройки);
	СтруктураОткрытия.Вставить("ИдентификаторСтроки", Идентификатор);
	
	СтруктураОткрытия.Вставить("АдресНастроекПоказателей", АдресНастроекПоказателей);
	
	ОткрытьФорму("Обработка.ПульсБизнеса.Форма.ФормаНастройкиПоказателя", СтруктураОткрытия, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуНастройкиРазделителя(Идентификатор)
	
	Если ФоновоеЗаданиеЗапущено Тогда
		Возврат;
	КонецЕсли;
	
	Стр = ДобавленныеПоказатели.НайтиПоИдентификатору(Идентификатор);
	
	СтруктураОткрытия = Новый Структура;
	СтруктураОткрытия.Вставить("Представление", Стр.Представление);
	СтруктураОткрытия.Вставить("ИдентификаторСтроки", Идентификатор);
	
	ОткрытьФорму("Обработка.ПульсБизнеса.Форма.ФормаНастройкиРазделителя", СтруктураОткрытия, ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьЗначенияПоказателей()
	
	ПараметрыПроцедуры = Новый Структура;
	ПараметрыПроцедуры.Вставить("Показатели", ДобавленныеПоказатели.Выгрузить());
	ПараметрыПроцедуры.Вставить("Диаграммы", ДобавленныеДиаграммы.Выгрузить());
	Обработки.ПульсБизнеса.ПолучитьДанные(ПараметрыПроцедуры, ФоновоеЗаданиеАдресРезультата);
	
	Данные = ПолучитьИзВременногоХранилища(ФоновоеЗаданиеАдресРезультата);
	
	Для каждого Стр Из ДобавленныеПоказатели Цикл
		Стр.Значение = 0;
		Стр.Изменение = 0;
		Стр.Подсказка = "";
	КонецЦикла; 
	
	Для каждого Элемент Из Данные.Показатели Цикл
		СтруктураЗначения = Элемент.Значение;
		Если НЕ ЗначениеЗаполнено(СтруктураЗначения.Значение) И НЕ ЗначениеЗаполнено(СтруктураЗначения.ЗначениеСравнения) Тогда
			Продолжить;
		КонецЕсли; 
		Стр = ДобавленныеПоказатели[Элемент.Ключ];
		СтрокаНастроек = НастройкиПоказателей.НайтиПоИдентификатору(Стр.ИдентификаторСтрокиНастроек);
		Если ЗначениеЗаполнено(СтруктураЗначения.Значение) Тогда
			Стр.Значение = ОтформатироватьЗначение(СтруктураЗначения.Значение, Стр);
		КонецЕсли; 
		Если ЗначениеЗаполнено(Стр.ПериодСравнения) Тогда
			Если ТипЗнч(СтруктураЗначения.Значение)=Тип("Соответствие") Тогда
				Продолжить;
			КонецЕсли; 
			Если СтруктураЗначения.ЗначениеСравнения<СтруктураЗначения.Значение Тогда
				Стр.Изменение = 1;
			ИначеЕсли СтруктураЗначения.ЗначениеСравнения>СтруктураЗначения.Значение Тогда
				Стр.Изменение = 2;
			Иначе
				Стр.Изменение = 0;
			КонецЕсли;
			Если СтрокаНастроек.Остаток Тогда
				Стр.Подсказка = НСтр("ru='На ';uk='На '")+НРег(ПредставлениеСтандартнойДатыНачала(Стр.ПериодСравнения))+": "+ОтформатироватьЗначение(СтруктураЗначения.ЗначениеСравнения, Стр);
			Иначе
				Стр.Подсказка = НСтр("ru='За ';uk='За '")+НРег(ПредставлениеСтандартногоПериода(Стр.ПериодСравнения))+": "+ОтформатироватьЗначение(СтруктураЗначения.ЗначениеСравнения, Стр);
			КонецЕсли; 
		КонецЕсли; 
	КонецЦикла;
	
	ЕстьСравнение = Ложь;
	Для каждого Стр Из ДобавленныеПоказатели Цикл
		Если Стр.Изменение>0 Тогда
			ЕстьСравнение = Истина;
			Прервать;
		КонецЕсли; 
	КонецЦикла;
	
	Для каждого Стр Из ДобавленныеПоказатели Цикл
		Если ПустаяСтрока(Стр.Ресурс) Тогда
			Продолжить;
		КонецЕсли;
		Если НЕ ЗначениеЗаполнено(Стр.Значение) Тогда
			Стр.Значение = ?(Стр.ВводОстатков, Новый ФорматированнаяСтрока(БиблиотекаКартинок.СерыйПлюс), "-");
		КонецЕсли; 
		ЭлементЗначение = Элементы[Стр.ИмяГруппы+"_Значение"];
		ЭлементЗначение.Заголовок = Стр.Значение;
		ЭлементЗначение.Гиперссылка = НЕ (Стр.Значение="-");
		ЭлементСравнение = Элементы[Стр.ИмяГруппы+"_Сравнение"];
		Если Стр.Изменение=0 Тогда
			ЭлементСравнение.Картинка = БиблиотекаКартинок.Пустая;
		ИначеЕсли Стр.Изменение=1 Тогда
			ЭлементСравнение.Картинка = БиблиотекаКартинок.ЗначениеУвеличилось;
		ИначеЕсли Стр.Изменение=2 Тогда
			ЭлементСравнение.Картинка = БиблиотекаКартинок.ЗначениеУменьшилось;
		КонецЕсли;
		ЭлементСравнение.Подсказка = Стр.Подсказка;
	КонецЦикла; 
	
КонецПроцедуры

&НаСервере
Процедура СоздатьЭлементыПоказатели()
	
	ЕстьСравнение = Ложь;
	Для каждого Стр Из ДобавленныеПоказатели Цикл
		Если Стр.Изменение>0 Тогда
			ЕстьСравнение = Истина;
			Прервать;
		КонецЕсли; 
	КонецЦикла;
	
	Для каждого Стр Из ДобавленныеПоказатели Цикл
		Если ПустаяСтрока(Стр.ИмяГруппы) Тогда
			Стр.ИмяГруппы = "Показатель"+СтрЗаменить(Строка(Новый УникальныйИдентификатор), "-", "");
			Группа = Элементы.Добавить(Стр.ИмяГруппы, Тип("ГруппаФормы"), Элементы.ГруппаДобавленныеПоказатели);
			Группа.Вид = ВидГруппыФормы.ОбычнаяГруппа;
			Группа.ОтображатьЗаголовок = Ложь;
			Группа.Отображение = ОтображениеОбычнойГруппы.Нет;
			Группа.РастягиватьПоВертикали = Ложь;
			Группа.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Горизонтальная;
			ЭлементЗаголовок = Элементы.Добавить(Стр.ИмяГруппы+"_Заголовок", Тип("ДекорацияФормы"), Группа);
			ЭлементЗаголовок.Вид = ВидДекорацииФормы.Надпись;
			ЭлементЗаголовок.ВертикальноеПоложение = ВертикальноеПоложениеЭлемента.Верх;
			ЭлементЗаголовок.РастягиватьПоГоризонтали = Истина;
			ЭлементЗаголовок.ПропускатьПриВводе = Ложь;
			Если НЕ ПустаяСтрока(Стр.Ресурс) Тогда
				ЭлементЗаголовок.ЦветТекста = ЦветаСтиля.ТекстВторостепеннойНадписи;
				ЭлементЗаголовок.Ширина = 25;
				ЭлементЗначение = Элементы.Добавить(Стр.ИмяГруппы+"_Значение", Тип("ДекорацияФормы"), Группа);
				ЭлементЗначение.Вид = ВидДекорацииФормы.Надпись;
				ЭлементЗначение.Гиперссылка = Истина;
				ЭлементЗначение.Ширина = 11;
				ЭлементЗначение.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Право;
				ЭлементЗначение.ВертикальноеПоложение = ВертикальноеПоложениеЭлемента.Верх;
				ЭлементЗначение.РастягиватьПоГоризонтали = Ложь;
				ЭлементЗначение.Высота = 1;
				ЭлементЗначение.ЦветТекста = ЦветаСтиля.ЦветТекстаФормы;
				ЭлементЗначение.УстановитьДействие("Нажатие", "Подключаемый_ПоказательЗначениеНажатие");
				ЭлементСравнение = Элементы.Добавить(Стр.ИмяГруппы+"_Сравнение", Тип("ДекорацияФормы"), Группа);
				ЭлементСравнение.Вид = ВидДекорацииФормы.Картинка;
				ЭлементСравнение.РастягиватьПоГоризонтали = Ложь;
				ЭлементСравнение.РастягиватьПоВертикали = Ложь;
				ЭлементСравнение.Высота = 1;
				ЭлементСравнение.Ширина = 1;
				ЭлементСравнение.РазмерКартинки = РазмерКартинки.РеальныйРазмер;
			Иначе
				ЭлементЗаголовок.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Центр;
				ЭлементЗаголовок.ВертикальноеПоложение = ВертикальноеПоложениеЭлемента.Низ;
				ЭлементЗаголовок.Высота = ?(ДобавленныеПоказатели.Индекс(Стр)=0, 1, 2);
			КонецЕсли; 
			// Контекстное меню
			Если ПустаяСтрока(Стр.Ресурс) Тогда
				ДобавитьКоманду(Стр.ИмяГруппы, "_Настроить", ЭлементЗаголовок, "КонтекстНастроитьРазделитель", НСтр("ru='Настроить заголовок';uk='Налаштувати заголовок'"), БиблиотекаКартинок.ХранилищеНастроек);
			Иначе
				ДобавитьКоманду(Стр.ИмяГруппы, "_Настроить", ЭлементЗаголовок, "КонтекстНастроитьПоказатель", НСтр("ru='Настроить показатель';uk='Налаштувати показник'"), БиблиотекаКартинок.ХранилищеНастроек);
			КонецЕсли; 
			Кнопка = ДобавитьКоманду(Стр.ИмяГруппы, "_Удалить", ЭлементЗаголовок, "КонтекстУдалитьПоказатель", НСтр("ru='Удалить показатель';uk='Видалити показник'"), БиблиотекаКартинок.Удалить);
			Если ПустаяСтрока(Стр.Ресурс) Тогда
				Кнопка.Заголовок = НСтр("ru='Удалить заголовок';uk='Видалити заголовок'");
			КонецЕсли; 
			ДобавитьКоманду(Стр.ИмяГруппы, "_Вверх", ЭлементЗаголовок, "КонтекстСместитьВверхПоказатель", НСтр("ru='Сместить вверх';uk='Змістити вгору'"), БиблиотекаКартинок.ПереместитьВверх);
			ДобавитьКоманду(Стр.ИмяГруппы, "_Вниз", ЭлементЗаголовок, "КонтекстСместитьВнизПоказатель", НСтр("ru='Сместить вниз';uk='Змістити вниз'"), БиблиотекаКартинок.ПереместитьВниз);
		Иначе
			ЭлементЗаголовок = Элементы[Стр.ИмяГруппы+"_Заголовок"];
		КонецЕсли;
		Если ПустаяСтрока(Стр.Ресурс) Тогда
			ЭлементЗаголовок.Заголовок = Новый ФорматированнаяСтрока(ВРег(Стр.Представление), Новый Шрифт(Новый Шрифт(),,, Истина));
		Иначе
			ЭлементЗаголовок.Заголовок = Стр.Представление;
		КонецЕсли; 
	КонецЦикла; 
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифроватьПоказатель(Идентификатор)
	
	ТекСтр = ДобавленныеПоказатели.НайтиПоИдентификатору(Идентификатор);
	СтрНастроек = НастройкиПоказателей.НайтиПоИдентификатору(ТекСтр.ИдентификаторСтрокиНастроек);
	Если НЕ ЗначениеЗаполнено(СтрНастроек.Вариант) Тогда
		Возврат;
	КонецЕсли;
	
	Если ТекСтр.ВводОстатков Тогда
		// Вместо расшифровки открываем помощник ввода начальных остатков
		ПараметрыОткрытия = Новый Структура;
		ПараметрыОткрытия.Вставить("РазделУчета", СтрНастроек.РазделУчета);
		ОткрытьФорму("Обработка.ПомощникВводаНачальныхОстатков.Форма", ПараметрыОткрытия, ЭтотОбъект);
		Возврат;
	КонецЕсли; 
	
	ОтборРасшифровки = Новый Соответствие;
	Если СтрНастроек.Остаток Тогда
		Если ТипЗнч(ТекСтр.Период)=Тип("СтандартнаяДатаНачала") Тогда
			ПериодРасшифровки = НачалоДня(ТекСтр.Период.Дата)-1;
		Иначе
			ПериодРасшифровки = '0001-01-01';
		КонецЕсли; 
		ОтборРасшифровки.Вставить("НачалоПериода", НачалоДня(ПериодРасшифровки));
		ОтборРасшифровки.Вставить("Период", ПериодРасшифровки);
		ОтборРасшифровки.Вставить("КонецПериода", ПериодРасшифровки);
	Иначе
		Если ТипЗнч(ТекСтр.Период)=Тип("СтандартныйПериод") Тогда
			ДатаНачалаРасшифровки = НачалоДня(ТекСтр.Период.ДатаНачала);
			ДатаКонцаРасшифровки = ?(ЗначениеЗаполнено(ТекСтр.Период.ДатаОкончания), КонецДня(ТекСтр.Период.ДатаОкончания), '0001-01-01');
		Иначе
			ДатаНачалаРасшифровки = '0001-01-01';
			ДатаКонцаРасшифровки = '0001-01-01';
		КонецЕсли; 
		ОтборРасшифровки.Вставить("НачалоПериода", ДатаНачалаРасшифровки);
		ОтборРасшифровки.Вставить("КонецПериода", ДатаКонцаРасшифровки);
	КонецЕсли;
	Если ТипЗнч(ТекСтр.Фильтры)=Тип("ФиксированныйМассив") Тогда
		Для каждого Фильтр Из ТекСтр.Фильтры Цикл
			СтруктураЗначения = Новый Структура;
			СтруктураЗначения.Вставить("ВидСравнения", Фильтр.ВидСравнения);
			СтруктураЗначения.Вставить("Значение", Фильтр.Значение);
			ОтборРасшифровки.Вставить(Фильтр.Поле, СтруктураЗначения);
		КонецЦикла; 
	КонецЕсли; 
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("СформироватьПриОткрытии", Истина);
	Если НЕ ПустаяСтрока(СтрНастроек.КолонкиОтчета) Тогда
		ПараметрыФормы.Вставить("Колонки", СтрНастроек.КолонкиОтчета);
	КонецЕсли; 
	ПараметрыФормы.Вставить("ОтборРасшифровки", ОтборРасшифровки);
	ВариантыОтчетовКлиент.ОткрытьФормуОтчета(ЭтаФорма, СтрНастроек.Вариант, ПараметрыФормы);
	
КонецПроцедуры

&НаСервере
Процедура СохранитьНастройкуПоказателяСервер()
	
	СохранитьНастройки();
	СоздатьЭлементыПоказатели();
	ЗапуститьФоновоеЗаданиеНаСервере();
	
КонецПроцедуры

#КонецОбласти 

#Область Диаграммы

&НаСервере
Процедура ДобавитьОписаниеСерииТочки(СтруктураСерийТочек, Имя, Представления, Заголовок = "", ТипДиаграммы = Неопределено, Валютная = Ложь, Формат = "", ДоступныеТочкиСерии = "", ОбязательныеФильтры = Неопределено)
	
	ОписаниеСерииТочки = Новый Структура;
	Если ТипЗнч(Представления)=Тип("Строка") Тогда
		МассивПредставлений = Новый Массив;
		МассивПредставлений.Добавить(Представления);
	Иначе
		МассивПредставлений = Представления;
	КонецЕсли; 
	ОписаниеСерииТочки.Вставить("Представления", Новый ФиксированныйМассив(МассивПредставлений));
	ОписаниеСерииТочки.Вставить("Заголовок", Заголовок);
	ОписаниеСерииТочки.Вставить("ТипДиаграммы", ТипДиаграммы);
	ОписаниеСерииТочки.Вставить("Валютная", Валютная);
	ОписаниеСерииТочки.Вставить("Формат", Формат);
	ОписаниеСерииТочки.Вставить("ДоступныеТочкиСерии", ДоступныеТочкиСерии);
	ОписаниеСерииТочки.Вставить("ОбязательныеФильтры", ОбязательныеФильтры);
	СтруктураСерийТочек.Вставить(Имя, ОписаниеСерииТочки);
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьДиаграмму(Диаграмма, Представление = "", Серии, Точки, Остаток, ИмяМакета = "", ИмяОтчета = "", КлючВарианта = "", ЗапретитьСравнение = Ложь)
	
	СтруктураПоиска = Новый Структура;
	СтруктураПоиска.Вставить("Диаграмма", Диаграмма);
	Если НастройкиДиаграмм.НайтиСтроки(СтруктураПоиска).Количество()>0 Тогда
		Возврат;
	КонецЕсли; 
	
	Стр = НастройкиДиаграмм.Добавить();
	Стр.Диаграмма = Диаграмма;
	Стр.Представление = ?(ПустаяСтрока(Представление), Диаграмма, Представление);
	Стр.Серии = Серии;
	Стр.Точки = Точки;
	Стр.Остаток = Остаток;
	Стр.ИмяМакета = ИмяМакета;
	Стр.ИмяОтчета = ИмяОтчета;
	Стр.КлючВарианта = КлючВарианта;
	Стр.ЗапретитьСравнение = ЗапретитьСравнение;
	Если ЗначениеЗаполнено(ИмяОтчета) И ЗначениеЗаполнено(КлючВарианта) Тогда
		Стр.Вариант = ВариантыОтчетов.ПолучитьСсылку(ОбщегоНазначения.ИдентификаторОбъектаМетаданных(Метаданные.НайтиПоПолномуИмени(ИмяОтчета)), КлючВарианта);
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуДобавленияДиаграммы()
	
	Если ФоновоеЗаданиеЗапущено Тогда
		Возврат;
	КонецЕсли; 
	
	СтруктураОткрытия = Новый Структура;
	СтруктураОткрытия.Вставить("Диаграмма", Неопределено);
	СтруктураОткрытия.Вставить("Серия", Неопределено);
	СтруктураОткрытия.Вставить("Точка", Неопределено);
	СтруктураОткрытия.Вставить("Представление", "");
	СтруктураОткрытия.Вставить("Период", Новый СтандартныйПериод);
	СтруктураОткрытия.Вставить("ПериодСравнения", Новый СтандартныйПериод);
	СтруктураОткрытия.Вставить("Фильтры", Новый ФиксированныйМассив(Новый Массив));
	СтруктураОткрытия.Вставить("Настройки", Новый ФиксированныйМассив(Новый Массив));
	
	СтруктураОткрытия.Вставить("АдресНастроекДиаграмм", АдресНастроекДиаграмм);
	
	ОткрытьФорму("Обработка.ПульсБизнеса.Форма.ФормаНастройкиДиаграммы", СтруктураОткрытия, ЭтотОбъект);
	
КонецПроцедуры
 
&НаКлиенте
Процедура ОткрытьФормуНастройкиДиаграммы(Идентификатор)
	
	Если ФоновоеЗаданиеЗапущено Тогда
		Возврат;
	КонецЕсли; 
	
	Стр = ДобавленныеДиаграммы.НайтиПоИдентификатору(Идентификатор);
	СтруктураОткрытия = Новый Структура;
	СтруктураОткрытия.Вставить("Диаграмма", Стр.Диаграмма);
	СтруктураОткрытия.Вставить("Серия", Стр.Серия);
	СтруктураОткрытия.Вставить("Точка", Стр.Точка);
	СтруктураОткрытия.Вставить("Представление", Стр.Представление);
	СтруктураОткрытия.Вставить("Период", Стр.Период);
	СтруктураОткрытия.Вставить("ПериодСравнения", Стр.ПериодСравнения);
	Если ТипЗнч(Стр.Фильтры)=Тип("ФиксированныйМассив") Тогда
		СтруктураОткрытия.Вставить("Фильтры", Стр.Фильтры);
	Иначе
		СтруктураОткрытия.Вставить("Фильтры", Новый ФиксированныйМассив(Новый Массив));
	КонецЕсли;
	Если ТипЗнч(Стр.Настройки)=Тип("ФиксированныйМассив") Тогда
		СтруктураОткрытия.Вставить("Настройки", Стр.Настройки);
	Иначе
		СтруктураОткрытия.Вставить("Настройки", Новый ФиксированныйМассив(Новый Массив));
	КонецЕсли;
	СтруктураОткрытия.Вставить("ИдентификаторСтроки", Идентификатор);
	
	СтруктураОткрытия.Вставить("АдресНастроекДиаграмм", АдресНастроекДиаграмм);
	
	ОткрытьФорму("Обработка.ПульсБизнеса.Форма.ФормаНастройкиДиаграммы", СтруктураОткрытия, ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьЗначенияДиаграмм()
	
	Для каждого Стр Из ДобавленныеДиаграммы Цикл
		
		ОбъектДиаграмма = ЭтаФорма[Стр.ИмяРеквизита];
		ОбъектДиаграмма.Обновление = Ложь;
		ОбъектДиаграмма.Очистить();
		
	КонецЦикла; 
	
	Данные = ПолучитьИзВременногоХранилища(ФоновоеЗаданиеАдресРезультата);
	
	Для каждого Элемент Из Данные.Диаграммы Цикл
		
		МассивДанных = Элемент.Значение;
		Стр = ДобавленныеДиаграммы[Элемент.Ключ];
		СтрокаНастройки = НастройкиДиаграмм.НайтиПоИдентификатору(Стр.ИдентификаторСтрокиНастроек);
		ОписаниеТочки = СтрокаНастройки.Точки[Стр.Точка];
		ОписаниеСерии = СтрокаНастройки.Серии[Стр.Серия];
		ОбъектДиаграмма = ЭтаФорма[Стр.ИмяРеквизита];
		ОбъектДиаграмма.Обновление = Истина;
		
		Попытка
			
			ТаблицаЗначений = Новый ТаблицаЗначений;
			ТаблицаЗначений.Колонки.Добавить("Точка");
			ТаблицаЗначений.Колонки.Добавить("ДляСравнения");
			ТаблицаЗначений.Колонки.Добавить("Порядок");
			ТаблицаЗначений.Колонки.Добавить("КоличествоСерий");
			ТаблицаЗначений.Колонки.Добавить("БазовоеЗначение");
			ТекущееКоличествоСерий = 0;
			Для каждого СтруктураЗначения Из МассивДанных Цикл
				Если СтруктураЗначения.КоличествоСерий>ТекущееКоличествоСерий Тогда
					Для ии = ТекущееКоличествоСерий По СтруктураЗначения.КоличествоСерий Цикл
						ТаблицаЗначений.Колонки.Добавить("Серия"+(ии+1));
					КонецЦикла;
					ТекущееКоличествоСерий = СтруктураЗначения.КоличествоСерий;
				КонецЕсли; 
				ЗаполнитьЗначенияСвойств(ТаблицаЗначений.Добавить(), СтруктураЗначения);
			КонецЦикла; 
			ТаблицаЗначений.Сортировать("Порядок, ДляСравнения");
			
			ЕстьДанныеДляОтображения = НЕ ПустаяДиаграмма(ТаблицаЗначений);
			Если НЕ ЕстьДанныеДляОтображения Тогда
				НаполнитьСлучайнымиДанными(ОбъектДиаграмма, МассивДанных, ОписаниеТочки, ОписаниеСерии);
			Иначе
				ОбъектДиаграмма.ОтображатьЗаголовок = Ложь;
				Для каждого СтруктураЗначения Из ТаблицаЗначений Цикл
					Если ОписаниеСерии.ТипДиаграммы=ТипДиаграммы.Круговая ИЛИ 
						ОписаниеСерии.ТипДиаграммы=ТипДиаграммы.КруговаяОбъемная Тогда
						Если НЕ ЗначениеЗаполнено(СтруктураЗначения.Точка) Тогда
							Продолжить;
						КонецЕсли; 
						Точка = ОбъектДиаграмма.УстановитьТочку(Стр.ПредставленияСерий[0]);
						Серия = ОбъектДиаграмма.УстановитьСерию(?(НЕ ЗначениеЗаполнено(СтруктураЗначения.Серия1), 
						НСтр("ru='<не заполнено>';uk='<не заповнене>'"), 
						СтруктураЗначения.Серия1));
						ОбъектДиаграмма.УстановитьЗначение(Точка, Серия, СтруктураЗначения.Точка);
					ИначеЕсли ОписаниеСерии.ТипДиаграммы=ТипДиаграммы.ГрафикСОбластямиИНакоплением Тогда
						Если Не ПустаяСтрока(ОписаниеТочки.Формат) Тогда
							Точка = ОбъектДиаграмма.УстановитьТочку(Формат(СтруктураЗначения.Точка, ОписаниеТочки.Формат));
						Иначе
							Точка = ОбъектДиаграмма.УстановитьТочку(
							?(НЕ ЗначениеЗаполнено(СтруктураЗначения.Точка), 
							НСтр("ru='<не заполнено>';uk='<не заповнене>'"), 
							Строка(СтруктураЗначения.Точка)));
						КонецЕсли;
						Если ЗначениеЗаполнено(СтруктураЗначения.БазовоеЗначение) Тогда
							Серия = ОбъектДиаграмма.УстановитьСерию("БазовоеЗначение");
							Серия.Текст = "-";
							Серия.Цвет = ЦветаСтиля.ЦветДиаграммыОтсутствующиеДанные;
							ОбъектДиаграмма.УстановитьЗначение(Точка, Серия, СтруктураЗначения.БазовоеЗначение); 
						КонецЕсли; 
						Для ии = 1 По СтруктураЗначения.КоличествоСерий Цикл
							Если НЕ ЗначениеЗаполнено(СтруктураЗначения["Серия"+ии]) Тогда
								Продолжить;
							КонецЕсли; 
							Серия = ОбъектДиаграмма.УстановитьСерию(Стр.ПредставленияСерий[ии-1]);
							ОбъектДиаграмма.УстановитьЗначение(Точка, Серия, ?(СтруктураЗначения["Серия"+ии]<0, -СтруктураЗначения["Серия"+ии], СтруктураЗначения["Серия"+ии]));
						КонецЦикла;  
					Иначе
						Если Не ПустаяСтрока(ОписаниеТочки.Формат) Тогда
							Точка = ОбъектДиаграмма.УстановитьТочку(Формат(СтруктураЗначения.Точка, ОписаниеТочки.Формат));
						Иначе
							Точка = ОбъектДиаграмма.УстановитьТочку(
							?(НЕ ЗначениеЗаполнено(СтруктураЗначения.Точка), 
							НСтр("ru='<не заполнено>';uk='<не заповнене>'"), 
							Строка(СтруктураЗначения.Точка)));
						КонецЕсли;
						Если ЗначениеЗаполнено(СтруктураЗначения.Серия1) И СтруктураЗначения.ДляСравнения Тогда
							Серия = ОбъектДиаграмма.УстановитьСерию(Стр.ПредставленияСерий[1]);
							ОбъектДиаграмма.УстановитьЗначение(Точка, Серия, СтруктураЗначения.Серия1);
						Иначе
							Для ии = 1 По СтруктураЗначения.КоличествоСерий Цикл
								Если НЕ ЗначениеЗаполнено(СтруктураЗначения["Серия"+ии]) Тогда
									Продолжить;
								КонецЕсли; 
								Серия = ОбъектДиаграмма.УстановитьСерию(Стр.ПредставленияСерий[ии-1]);
								ОбъектДиаграмма.УстановитьЗначение(Точка, Серия, СтруктураЗначения["Серия"+ии]);
							КонецЦикла;  
						КонецЕсли; 
					КонецЕсли; 
				КонецЦикла;
			КонецЕсли; 
			
			Если НЕ ОписаниеСерии.ТипДиаграммы=ТипДиаграммы.Круговая  
				И НЕ ОписаниеСерии.ТипДиаграммы=ТипДиаграммы.КруговаяОбъемная
				И ОбъектДиаграмма.Серии.Количество()<=1 Тогда
				ОбъектДиаграмма.ОтображатьЛегенду = Ложь;
				ОбъектДиаграмма.ОбластьПостроения.Право = 0.99;
			КонецЕсли; 
			
		Исключение
			
			ОбъектДиаграмма.Очистить();
			ИнформацияОбОшибке = ИнформацияОбОшибке();
			ВывестиОшибкуДиаграммы(ОбъектДиаграмма, ИнформацияОбОшибке);
		    
		КонецПопытки;
		
	КонецЦикла;
	
	Для каждого Стр Из ДобавленныеДиаграммы Цикл
		
		СтрокаНастройки = НастройкиДиаграмм.НайтиПоИдентификатору(Стр.ИдентификаторСтрокиНастроек);
		ОписаниеТочки = СтрокаНастройки.Точки[Стр.Точка];
		ОписаниеСерии = СтрокаНастройки.Серии[Стр.Серия];
		ОбъектДиаграмма = ЭтаФорма[Стр.ИмяРеквизита];
		Если НЕ ОбъектДиаграмма.Обновление Тогда
			НаполнитьСлучайнымиДанными(ОбъектДиаграмма, Новый Массив, ОписаниеТочки, ОписаниеСерии);
		КонецЕсли; 
		УстановитьОтображениеДиаграммы(ОбъектДиаграмма, ЕстьДанныеДляОтображения);
		ОбъектДиаграмма.Обновление = Истина;
		
	КонецЦикла;
		
КонецПроцедуры

&НаСервере
Процедура ВывестиОшибкуДиаграммы(ОбъектДиаграмма, ИнформацияОбОшибке)
	
	ОписаниеОшибки = СтандартныеПодсистемыКлиентСервер.ИсходнаяПричинаОшибки(ИнформацияОбОшибке);
	ПодробноеПредставлениеОшибки = НСтр("ru='Ошибка при формировании:';uk='Помилка при формуванні:'") + Символы.ПС + ПодробноеПредставлениеОшибки(ИнформацияОбОшибке);
	Если ПустаяСтрока(ОписаниеОшибки) Тогда
		ОписаниеОшибки = ПодробноеПредставлениеОшибки;
	КонецЕсли;
	
	ОбъектДиаграмма.ОтображатьЗаголовок = Истина;
	ОбъектДиаграмма.ОбластьЗаголовка.Текст = ОписаниеОшибки;
	ОбъектДиаграмма.ОбластьЗаголовка.ЦветТекста = ЦветаСтиля.ЦветИнформацияОшибочна;
	
КонецПроцедуры
 
&НаСервере
Процедура СоздатьЭлементыДиаграммы()
	
	// Создание реквизитов формы
	МассивРеквизитов = Новый Массив;
	Для каждого Стр Из ДобавленныеДиаграммы Цикл
		Если НЕ ПустаяСтрока(Стр.ИмяРеквизита) Тогда
			Продолжить;
		КонецЕсли;
		Стр.ИмяРеквизита = "Диаграмма"+СтрЗаменить(Строка(Новый УникальныйИдентификатор), "-", "");
		МассивРеквизитов.Добавить(Новый РеквизитФормы(Стр.ИмяРеквизита, Новый ОписаниеТипов("Диаграмма")));
	КонецЦикла;
	ИзменитьРеквизиты(МассивРеквизитов);
	
	Для каждого Стр Из ДобавленныеДиаграммы Цикл
		СтрокаНастройки = НастройкиДиаграмм.НайтиПоИдентификатору(Стр.ИдентификаторСтрокиНастроек);
		ОписаниеТочки = СтрокаНастройки.Точки[Стр.Точка];
		ОписаниеСерии = СтрокаНастройки.Серии[Стр.Серия];
		Если ПустаяСтрока(Стр.ИмяГруппы) Тогда
			Стр.ИмяГруппы = "Диаграмма"+СтрЗаменить(Строка(Новый УникальныйИдентификатор), "-", "");
			Группа = Элементы.Добавить(Стр.ИмяГруппы, Тип("ГруппаФормы"), Элементы.ГруппаДобавленныеДиаграммы);
			Группа.Вид = ВидГруппыФормы.ОбычнаяГруппа;
			Группа.ОтображатьЗаголовок = Ложь;
			Группа.Отображение = ОтображениеОбычнойГруппы.Нет;
			Группа.РастягиватьПоВертикали = Ложь;
			Группа.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
			ЭлементЗаголовок = Элементы.Добавить(Стр.ИмяГруппы+"_Заголовок", Тип("ДекорацияФормы"), Группа);
			ЭлементЗаголовок.Вид = ВидДекорацииФормы.Надпись;
			ЭлементЗаголовок.РастягиватьПоГоризонтали = Истина;
			ЭлементЗаголовок.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Центр;
			ЭлементЗаголовок.Шрифт = Новый Шрифт("Arial", 10, Истина);
			ЭлементЗаголовок.ВертикальноеПоложение = ВертикальноеПоложениеЭлемента.Низ;
			ЭлементЗаголовок.Высота = ?(ДобавленныеДиаграммы.Индекс(Стр)=0, 1, 2);
			ЭлементДиаграмма = Элементы.Добавить(Стр.ИмяГруппы+"_Диаграмма", Тип("ПолеФормы"), Группа);
			ЭлементДиаграмма.Вид = ВидПоляФормы.ПолеДиаграммы;
			ЭлементДиаграмма.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
			ЭлементДиаграмма.ПутьКДанным = Стр.ИмяРеквизита;
			ЭлементДиаграмма.Ширина = 30;
			ЭлементДиаграмма.Высота = 7;
			ЭлементДиаграмма.УстановитьДействие("Выбор", "Подключаемый_ДиаграммаВыбор");
			// Контекстное меню
			ДобавитьКоманду(Стр.ИмяГруппы, "_Настроить", ЭлементДиаграмма, "КонтекстНастроитьДиаграмму", НСтр("ru='Настроить график';uk='Налаштувати графік'"), БиблиотекаКартинок.ХранилищеНастроек);
			ДобавитьКоманду(Стр.ИмяГруппы, "_Удалить", ЭлементДиаграмма, "КонтекстУдалитьДиаграмму", НСтр("ru='Удалить график';uk='Видалити графік'"), БиблиотекаКартинок.Удалить);
			ДобавитьКоманду(Стр.ИмяГруппы, "_Вверх", ЭлементДиаграмма, "КонтекстСместитьВверхДиаграмму", НСтр("ru='Сместить вверх';uk='Змістити вгору'"), БиблиотекаКартинок.ПереместитьВверх);
			ДобавитьКоманду(Стр.ИмяГруппы, "_Вниз", ЭлементДиаграмма, "КонтекстСместитьВнизДиаграмму", НСтр("ru='Сместить вниз';uk='Змістити вниз'"), БиблиотекаКартинок.ПереместитьВниз);
			ДобавитьКоманду(Стр.ИмяГруппы, "_Расшифровать", ЭлементДиаграмма, "КонтекстРасшифроватьДиаграмму", НСтр("ru='Расшифровать';uk='Розшифрувати'"), БиблиотекаКартинок.Найти);
		Иначе
			Группа = Элементы[Стр.ИмяГруппы];
			ЭлементДиаграмма = Элементы[Стр.ИмяГруппы+"_Диаграмма"];
			ЭлементЗаголовок = Элементы[Стр.ИмяГруппы+"_Заголовок"];
		КонецЕсли;
		ЭлементЗаголовок.Заголовок = ВРег(Стр.Представление);
		ОбъектДиаграмма = ЭтаФорма[Стр.ИмяРеквизита];
		ОбъектДиаграмма.Обновление = Ложь;
		ОбъектДиаграмма.ТипДиаграммы = ОписаниеСерии.ТипДиаграммы;
		ОбъектДиаграмма.ОтображатьЗаголовок = Ложь;
		ОбъектДиаграмма.ОбластьЛегенды.Шрифт = Новый Шрифт(ОбъектДиаграмма.ОбластьЛегенды.Шрифт, "Arial", 7);
		ОбъектДиаграмма.ОбластьПостроения.Шрифт = Новый Шрифт(ОбъектДиаграмма.ОбластьПостроения.Шрифт, "Arial", 7);
		ОбъектДиаграмма.ОбластьПостроения.Право = 0.75;
		ОбъектДиаграмма.ОбластьЛегенды.Лево = 0.78;
		ОбъектДиаграмма.ОбластьПостроения.Низ = 0.99;
		Если ОписаниеСерии.ТипДиаграммы=ТипДиаграммы.Круговая ИЛИ 
			ОписаниеСерии.ТипДиаграммы=ТипДиаграммы.КруговаяОбъемная Тогда
			ОбъектДиаграмма.МаксимумСерий = МаксимумСерий.Ограничено;
			ОбъектДиаграмма.МаксимумСерийКоличество = 10;
		ИначеЕсли ОписаниеСерии.ТипДиаграммы=ТипДиаграммы.ГрафикСОбластямиИНакоплением Тогда
			ОбъектДиаграмма.АвтоУстановкаТекстаСерий = Ложь;
			ОбъектДиаграмма.ОбластьПостроения.ОтображатьПодписиШкалыТочек = Ложь;
			ОбъектДиаграмма.ОбластьПостроения.ОтображатьЛинииЗначенийШкалы = Ложь;
			ОбъектДиаграмма.РежимСглаживания = РежимСглаживанияДиаграммы.ГладкаяКривая;
		Иначе
			ОбъектДиаграмма.ОбластьПостроения.ОриентацияМеток = ОриентацияМетокДиаграммы.Вертикально;
			Если НЕ ОписаниеСерии.ТипДиаграммы=ТипДиаграммы.ГистограммаСНакоплением 
				И НЕ ОписаниеСерии.ТипДиаграммы=ТипДиаграммы.ГистограммаСНакоплениемОбъемная Тогда
				ОбъектДиаграмма.РежимПробелов = РежимПробеловДиаграммы.Нет;
			КонецЕсли; 
			ОбъектДиаграмма.ОбластьПостроения.ОтображатьЛинииЗначенийШкалы = Ложь;
		КонецЕсли;
		
		ОбъектДиаграмма.Обновление = Истина;
	КонецЦикла; 
	
КонецПроцедуры

&НаСервере
Процедура НаполнитьСлучайнымиДанными(ОбъектДиаграмма, ТаблицаЗначений, ОписаниеТочки, ОписаниеСерии)
	
	Генератор = Новый ГенераторСлучайныхЧисел;
	СуммаБазовая = 100;
	Если ТаблицаЗначений.Количество()>0 Тогда
		Для каждого СтруктураЗначения Из ТаблицаЗначений Цикл
			СуфиксСравнения = ?(СтруктураЗначения.ДляСравнения, НСтр("ru=' (сравн)';uk=' (сравн)'"), "");
			Если Не ПустаяСтрока(ОписаниеТочки.Формат) Тогда
				Точка = ОбъектДиаграмма.УстановитьТочку(Формат(СтруктураЗначения.Точка, ОписаниеТочки.Формат)+СуфиксСравнения);
			Иначе
				Точка = ОбъектДиаграмма.УстановитьТочку(
				?(НЕ ЗначениеЗаполнено(СтруктураЗначения.Точка), 
				НСтр("ru='<не заполнено>';uk='<не заповнене>'")+СуфиксСравнения, 
				Строка(СтруктураЗначения.Точка)+СуфиксСравнения));
			КонецЕсли; 
			Серия = ОбъектДиаграмма.УстановитьСерию(НСтр("ru='Сумма';uk='Сума'"));
			ОбъектДиаграмма.УстановитьЗначение(Точка, Серия, Генератор.СлучайноеЧисло(0, 100));
			Если ОписаниеСерии.ТипДиаграммы=ТипДиаграммы.ГистограммаСНакоплением 
				ИЛИ ОписаниеСерии.ТипДиаграммы=ТипДиаграммы.ГистограммаСНакоплениемОбъемная Тогда
				Серия = ОбъектДиаграмма.УстановитьСерию(НСтр("ru='Сумма 2';uk='Сума 2'"));
				ОбъектДиаграмма.УстановитьЗначение(Точка, Серия, Генератор.СлучайноеЧисло(0, 30));
			КонецЕсли; 
		КонецЦикла; 
	Иначе
		Для ии = 1 По 8 Цикл
			Если ОписаниеСерии.ТипДиаграммы=ТипДиаграммы.Круговая ИЛИ 
				ОписаниеСерии.ТипДиаграммы=ТипДиаграммы.КруговаяОбъемная Тогда
				СуммаБазовая = Окр(СуммаБазовая*(100-Генератор.СлучайноеЧисло(0, 30))/100, 2);
				Точка = ОбъектДиаграмма.УстановитьТочку(НСтр("ru='Сумма';uk='Сума'"));
				Серия = ОбъектДиаграмма.УстановитьСерию(НСтр("ru='Пример ';uk='Приклад'")+ии);
				ОбъектДиаграмма.УстановитьЗначение(Точка, Серия, СуммаБазовая);
			Иначе
				Точка = ОбъектДиаграмма.УстановитьТочку(НСтр("ru='Пример ';uk='Приклад'")+ии);
				Серия = ОбъектДиаграмма.УстановитьСерию(НСтр("ru='Сумма';uk='Сума'"));
				ОбъектДиаграмма.УстановитьЗначение(Точка, Серия, Генератор.СлучайноеЧисло(0, 100));
				Если ОписаниеСерии.ТипДиаграммы=ТипДиаграммы.ГистограммаСНакоплением 
					ИЛИ ОписаниеСерии.ТипДиаграммы=ТипДиаграммы.ГистограммаСНакоплениемОбъемная Тогда
					Серия = ОбъектДиаграмма.УстановитьСерию(НСтр("ru='Сумма 2';uk='Сума 2'"));
					ОбъектДиаграмма.УстановитьЗначение(Точка, Серия, Генератор.СлучайноеЧисло(0, 30));
				КонецЕсли; 
			КонецЕсли;
		КонецЦикла;
	КонецЕсли; 
	ОбъектДиаграмма.ОтображатьЗаголовок = Истина;
	ОбъектДиаграмма.ОбластьЗаголовка.Текст = НСтр("ru='ПРИМЕР';uk='ПРИКЛАД'");
	ОбъектДиаграмма.ОбластьЗаголовка.ЦветТекста = ЦветаСтиля.ЦветРамки;
	ОбъектДиаграмма.ОбластьЗаголовка.Шрифт = Новый Шрифт("Arial", 18, Истина);
			
КонецПроцедуры

&НаСервере
Функция ПустаяДиаграмма(Данные)
	
	Для каждого Стр Из Данные Цикл
		Если ТипЗнч(Стр.Точка)=Тип("Число") И ЗначениеЗаполнено(Стр.Точка) Тогда
			Возврат Ложь;
		КонецЕсли; 
		Для ии = 1 По Стр.КоличествоСерий Цикл
			Если ТипЗнч(Стр["Серия"+ии])=Тип("Число") И ЗначениеЗаполнено(Стр["Серия"+ии]) Тогда
				Возврат Ложь;
			КонецЕсли; 
		КонецЦикла; 
	КонецЦикла; 
	Возврат Истина;
	
КонецФункции

// Процедура постобработки диаграмм. Устанавливает цвета серий и толщину линий
//
// Параметры:
//  Диаграмма				 - диаграмма - обрабатываемая диаграмма
//  ЕстьДанныеДляОтображения - булево	 - признак наличия/отсутствия данных
//  ЦветаСерий				 - массив	 - массив цветов в соответствии с которым назначаются цвета. Если не задан, то назначаются по умолчанию
&НаСервереБезКонтекста
Процедура УстановитьОтображениеДиаграммы(Диаграмма, ЕстьДанныеДляОтображения, ЦветаСерий = Неопределено)
	
	Диаграмма.Рамка = Новый Рамка(ТипРамкиЭлементаУправления.БезРамки);
	Диаграмма.ЦветРамки = ЦветаСтиля.ЦветФонаФормы;
	Диаграмма.ОбластьЗаголовка.ПрозрачныйФон = Истина;
	Диаграмма.ОбластьПостроения.ПрозрачныйФон = Истина;
	Диаграмма.ОбластьЛегенды.ПрозрачныйФон = Истина;
		
	Если ЦветаСерий = Неопределено Тогда
		ЦветаСерий = УправлениеНебольшойФирмойКлиентСервер.ЦветаСерийДиаграмм();
	КонецЕсли;
	Диаграмма.СводнаяСерия.Цвет = ЦветаСтиля.ЦветДиаграммыОтсутствующиеДанные;
	Диаграмма.СводнаяСерия.Текст = НСтр("ru='Прочее';uk='Інше'");
	
	// Если точек на диаграмме меньше, то серии рисуем толстой линией, если больше - то тонкой
	МаксТочекДиаграммыСТолстойЛинией = 10;
	
	ТонкаяЛиния = Новый Линия(ТипЛинииДиаграммы.Сплошная, 1);
	ТолстаяЛиния = Новый Линия(ТипЛинииДиаграммы.Сплошная, 2);
	
	Для ИндексСерии = 0 По Диаграмма.Серии.Количество() - 1 Цикл
		
		Серия = Диаграмма.Серии[ИндексСерии];
		
		Если Серия.Цвет=ЦветаСтиля.ЦветДиаграммыОтсутствующиеДанные Тогда
			Продолжить;
		КонецЕсли; 
		
		Если ЕстьДанныеДляОтображения И ИндексСерии<ЦветаСерий.Количество() Тогда
			Серия.Цвет = ЦветаСерий[ИндексСерии];
		ИначеЕсли НЕ ЕстьДанныеДляОтображения И ИндексСерии<ЦветаСерий.Количество() Тогда
			ЦветБазовый = ЦветаСерий[ИндексСерии];
			ЦветСерии = Новый Цвет(
			ЦветБазовый.Красный+(255-ЦветБазовый.Красный)/2,
			ЦветБазовый.Зеленый+(255-ЦветБазовый.Зеленый)/2,
			ЦветБазовый.Синий+(255-ЦветБазовый.Синий)/2);
			Серия.Цвет = ЦветСерии;
		Иначе
			Серия.Цвет = ЦветаСтиля.ЦветДиаграммыОтсутствующиеДанные;
		КонецЕсли;
		
		Если Диаграмма.Точки.Количество() > МаксТочекДиаграммыСТолстойЛинией Тогда
			Серия.Линия = ТонкаяЛиния;
		Иначе
			Серия.Линия = ТолстаяЛиния;
		КонецЕсли;
		
	КонецЦикла;
	
	Диаграмма.ОбластьЗаголовка.Лево = 0;
	Диаграмма.ОбластьЗаголовка.Право = 1;
	Диаграмма.ОбластьЗаголовка.Верх = 0;
	Диаграмма.ОбластьЗаголовка.Низ = 1;
	Диаграмма.ОбластьПостроения.Верх = 0.01;
	Диаграмма.ОбластьЛегенды.Верх = 0.01;
	Диаграмма.ОбластьЛегенды.Прокрутка = (Диаграмма.Серии.Количество()>5);
		
КонецПроцедуры

&НаКлиенте
Процедура РасшифроватьДиаграмму(Идентификатор)
	
	ТекСтр = ДобавленныеДиаграммы.НайтиПоИдентификатору(Идентификатор);
	СтрНастроек = НастройкиДиаграмм.НайтиПоИдентификатору(ТекСтр.ИдентификаторСтрокиНастроек);
	Если НЕ ЗначениеЗаполнено(СтрНастроек.Вариант) Тогда
		Возврат;
	КонецЕсли;
	
	ОтборРасшифровки = Новый Соответствие;
	Если СтрНастроек.Остаток Тогда
		Если ТипЗнч(ТекСтр.Период)=Тип("СтандартнаяДатаНачала") Тогда
			ПериодРасшифровки = НачалоДня(ТекСтр.Период.Дата)-1;
		Иначе
			ПериодРасшифровки = '0001-01-01';
		КонецЕсли; 
		ОтборРасшифровки.Вставить("НачалоПериода", НачалоДня(ПериодРасшифровки));
		ОтборРасшифровки.Вставить("Период", ПериодРасшифровки);
		ОтборРасшифровки.Вставить("КонецПериода", ПериодРасшифровки);
	Иначе
		Если ТипЗнч(ТекСтр.Период)=Тип("СтандартныйПериод") Тогда
			ДатаНачалаРасшифровки = НачалоДня(ТекСтр.Период.ДатаНачала);
			ДатаКонцаРасшифровки = ?(ЗначениеЗаполнено(ТекСтр.Период.ДатаОкончания), КонецДня(ТекСтр.Период.ДатаОкончания), '0001-01-01');
		Иначе
			ДатаНачалаРасшифровки = '0001-01-01';
			ДатаКонцаРасшифровки = '0001-01-01';
		КонецЕсли; 
		ОтборРасшифровки.Вставить("НачалоПериода", ДатаНачалаРасшифровки);
		ОтборРасшифровки.Вставить("КонецПериода", ДатаКонцаРасшифровки);
	КонецЕсли;
	Если ТипЗнч(ТекСтр.Фильтры)=Тип("ФиксированныйМассив") Тогда
		Для каждого Фильтр Из ТекСтр.Фильтры Цикл
			СтруктураЗначения = Новый Структура;
			СтруктураЗначения.Вставить("ВидСравнения", Фильтр.ВидСравнения);
			СтруктураЗначения.Вставить("Значение", Фильтр.Значение);
			ОтборРасшифровки.Вставить(Фильтр.Поле, СтруктураЗначения);
		КонецЦикла; 
	КонецЕсли; 
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("СформироватьПриОткрытии", Истина);
	ПараметрыФормы.Вставить("ОтборРасшифровки", ОтборРасшифровки);
	ВариантыОтчетовКлиент.ОткрытьФормуОтчета(ЭтаФорма, СтрНастроек.Вариант, ПараметрыФормы);
	
КонецПроцедуры

&НаСервере
Процедура СохранитьНастройкуДиаграммыСервер()
	
	СохранитьНастройки();
	СоздатьЭлементыДиаграммы();
	ЗапуститьФоновоеЗаданиеНаСервере();
	
КонецПроцедуры

#КонецОбласти 

#Область Периоды

&НаКлиентеНаСервереБезКонтекста
Функция ПредставлениеСтандартногоПериода(Период)
	
	Если НЕ ЗначениеЗаполнено(Период) Тогда
		Возврат НСтр("ru='Не выбран';uk='Не обраний'");
	ИначеЕсли Период.Вариант=ВариантСтандартногоПериода.ПроизвольныйПериод Тогда
		Если НЕ ЗначениеЗаполнено(Период.ДатаНачала) Тогда
			Возврат НСтр("ru='до ';uk='до'")+Формат(Период.ДатаОкончания, "ДЛФ=D");
		ИначеЕсли НЕ ЗначениеЗаполнено(Период.ДатаОкончания) Тогда
			Возврат НСтр("ru='от ';uk='від '")+Формат(Период.ДатаНачала, "ДЛФ=D");
		Иначе
			Возврат ПредставлениеПериода(НачалоДня(Период.ДатаНачала), ?(ЗначениеЗаполнено(Период.ДатаОкончания), КонецДня(Период.ДатаОкончания), Период.ДатаОкончания));
		КонецЕсли; 
	ИначеЕсли Период.Вариант="ПредыдущийПлавающийПериод" Тогда
		Возврат НСтр("ru='Предыдущий период (';uk='Попередній період ('")+ПредставлениеПериода(НачалоДня(Период.Период.ДатаНачала), КонецДня(Период.Период.ДатаОкончания))+")";
	ИначеЕсли Период.Вариант="ЗаПрошлыйГод" Тогда
		Возврат НСтр("ru='За прошлый год (';uk='За минулий рік ('")+ПредставлениеПериода(НачалоДня(Период.Период.ДатаНачала), КонецДня(Период.Период.ДатаОкончания))+")";
	Иначе
		Возврат Строка(Период);
	КонецЕсли; 
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ПредставлениеСтандартнойДатыНачала(Период)
	
	Если НЕ ЗначениеЗаполнено(Период) Тогда
		Возврат НСтр("ru='Не выбрана';uk='Не обрана'");
	ИначеЕсли Период.Вариант=ВариантСтандартнойДатыНачала.ПроизвольнаяДата Тогда
		Возврат Формат(Период.Дата, "ДФ=dd.MM.yyyy");
	ИначеЕсли Период.Вариант=ВариантСтандартнойДатыНачала.НачалоЭтогоДня Тогда
		Возврат НСтр("ru='Сегодня, на начало дня';uk='Сьогодні, на початок дня'");
	ИначеЕсли Период.Вариант=ВариантСтандартнойДатыНачала.НачалоСледующегоДня Тогда
		Возврат НСтр("ru='Всегда актуально';uk='Завжди актуально'");
	ИначеЕсли Период.Вариант="ТакойЖеДеньНаПрошлойНеделе" Тогда
		Возврат НСтр("ru='Такой же день на прошлой неделе (';uk='Такий же день минулого тижня ('")+Формат(Период.Дата, "ДФ='dd.MM.yyyy, ддд'")+")";
	ИначеЕсли Период.Вариант="ТакойЖеДеньВПрошломМесяце" Тогда
		Возврат НСтр("ru='Такой же день прошлого месяца (';uk='Такий же день минулого місяця ('")+Формат(Период.Дата, "ДФ='dd.MM.yyyy, ддд'")+")";
	ИначеЕсли Период.Вариант="ТакойЖеДеньВПрошломГоду" Тогда
		Возврат НСтр("ru='Такой же день в прошлом году (';uk='Такий же день в минулому році ('")+Формат(Период.Дата, "ДФ='dd.MM.yyyy'")+")";
	Иначе
		Возврат Строка(Период);
	КонецЕсли; 
	
КонецФункции

#КонецОбласти 

&НаСервере
Процедура ОбновитьФорму()
	
	УдалитьЭлементыРекурсивно(Элементы.ГруппаДобавленныеПоказатели);
	ДобавленныеПоказатели.Очистить();
	УдалитьЭлементыРекурсивно(Элементы.ГруппаДобавленныеДиаграммы);
	ДобавленныеДиаграммы.Очистить();
	ЗагрузитьНастройки();
	СоздатьЭлементыПоказатели();
	СоздатьЭлементыДиаграммы();
	ЗапуститьФоновоеЗаданиеНаСервере();
	
КонецПроцедуры
 
&НаСервере
Процедура ОбновитьДанные()
	
	ОбновитьЗначенияПоказателей();
	ОбновитьЗначенияДиаграмм();
	
КонецПроцедуры

&НаСервере
Процедура Инициализация()
	
	// Заполнение таблицы показателей

	// Продажи
	ДобавитьПоказатель("Продажи", "Выручка", НСтр("ru='Продажи';uk='Продажі'"), НСтр("ru='Выручка';uk='Виторг'"),, Истина, "ЧДЦ=2; ЧН=-", "СКД_РН_Продажи", "Отчет.Продажи", "Основной", "Сумма");
	ДобавитьПоказатель("Продажи", "Количество", НСтр("ru='Продажи';uk='Продажі'"), НСтр("ru='Кол-во проданных товаров';uk='Кіл-ть проданих товарів'"),,, "ЧН=-", "СКД_РН_Продажи", "Отчет.Продажи", "Основной", "Количество");
	ДобавитьПоказатель("Продажи", "КоличествоДокументов", НСтр("ru='Продажи';uk='Продажі'"), НСтр("ru='Кол-во продаж (чеков)';uk='Кіл-ть продажів (чеків)'"),,, "ЧДЦ=; ЧН=-", "СКД_РН_Продажи", "Отчет.Продажи", "ВаловаяПрибыльПоМенеджерам", "КоличествоДокументов");
	ДобавитьПоказатель("Продажи", "Себестоимость", НСтр("ru='Продажи';uk='Продажі'"), НСтр("ru='Себестоимость';uk='Собівартість'"),, Истина, "ЧДЦ=2; ЧН=-", "СКД_РН_Продажи", "Отчет.Продажи", "ВаловаяПрибыль", "Себестоимость");
	ДобавитьПоказатель("Продажи", "Прибыль", НСтр("ru='Продажи';uk='Продажі'"), НСтр("ru='Прибыль';uk='Прибуток'"),, Истина, "ЧДЦ=2; ЧН=-", "СКД_РН_Продажи", "Отчет.Продажи", "ВаловаяПрибыль", "ВаловаяПрибыль");
	ДобавитьПоказатель("Продажи", "Наценка", НСтр("ru='Продажи';uk='Продажі'"), НСтр("ru='Прибыль/с-сть';uk='Прибуток/с-сть'"),,, "ЧН=-", "СКД_РН_Продажи", "Отчет.Продажи", "ВаловаяПрибыль", "Наценка");
	ДобавитьПоказатель("Продажи", "Рентабельность", НСтр("ru='Продажи';uk='Продажі'"), НСтр("ru='Прибыль/выручка';uk='Прибуток/виторг'"),,, "ЧН=-", "СКД_РН_Продажи", "Отчет.Продажи", "ВаловаяПрибыль", "Рентабельность");
	ДобавитьПоказатель("Продажи", "ВозвратыСумма", НСтр("ru='Продажи';uk='Продажі'"), НСтр("ru='Сумма возвратов';uk='Сума повернень'"),, Истина, "ЧДЦ=2; ЧН=-", "СКД_РН_Продажи", "Отчет.Продажи", "Возвраты", "Сумма");
	ДобавитьПоказатель("Продажи", "ВозвратыКоличество", НСтр("ru='Продажи';uk='Продажі'"), НСтр("ru='Кол-во возвратов';uk='Кіл-ть повернень'"),,, "ЧН=-", "СКД_РН_Продажи", "Отчет.Продажи", "Возвраты", "Количество");

	// Товары
	ДобавитьПоказатель("Товары", "КоличествоОстаток", НСтр("ru='Товары';uk='Товари'"), НСтр("ru='Остаток (кол-во)';uk='Залишок (кіл-ть)'"), Истина,, "ЧН=-", "СКД_РН_Запасы", "Отчет.Запасы", "Остатки",, "Запасы");
	ДобавитьПоказатель("Товары", "СуммаОстаток", НСтр("ru='Товары';uk='Товари'"), НСтр("ru='Остаток (сумма)';uk='Залишок (сума)'"), Истина, Истина, "ЧДЦ=2; ЧН=-", "СКД_РН_Запасы", "Отчет.Запасы", "Остатки",, "Запасы");
	ДобавитьПоказатель("Товары", "КоличествоПриход", НСтр("ru='Товары';uk='Товари'"), НСтр("ru='Приход (кол-во)';uk='Прихід (кіл-ть)'"),,, "ЧН=-", "СКД_РН_Запасы", "Отчет.Запасы", "Ведомость");
	ДобавитьПоказатель("Товары", "СуммаПриход", НСтр("ru='Товары';uk='Товари'"), НСтр("ru='Приход (сумма)';uk='Прихід (сума)'"),, Истина, "ЧДЦ=2; ЧН=-", "СКД_РН_Запасы", "Отчет.Запасы", "Ведомость");
	ДобавитьПоказатель("Товары", "КоличествоРасход", НСтр("ru='Товары';uk='Товари'"), НСтр("ru='Расход (кол-во)';uk='Витрата (кіл-ть)'"),,, "ЧН=-", "СКД_РН_Запасы", "Отчет.Запасы", "Ведомость");
	ДобавитьПоказатель("Товары", "СуммаРасход", НСтр("ru='Товары';uk='Товари'"), НСтр("ru='Расход (сумма)';uk='Витрата (сума)'"),, Истина, "ЧДЦ=2; ЧН=-", "СКД_РН_Запасы", "Отчет.Запасы", "Ведомость");

	// Деньги
	ДобавитьПоказатель("Деньги", "СуммаОстаток", НСтр("ru='Деньги';uk='Гроші'"), НСтр("ru='Остаток денег';uk='Залишок грошей'"), Истина, Истина, "ЧДЦ=2; ЧН=-", "СКД_РН_ДенежныеСредства", "Отчет.ДенежныеСредства", "Остатки",, "Деньги");
	ДобавитьПоказатель("Деньги", "ДенежныйПоток", НСтр("ru='Деньги';uk='Гроші'"), НСтр("ru='Чистый денежный поток';uk='Чистий грошовий потік'"),, Истина, "ЧДЦ=2; ЧН=-", "СКД_РН_ДенежныеСредства", "Отчет.ДенежныеСредства", "Ведомость");
	ДобавитьПоказатель("Деньги", "Поступления", НСтр("ru='Деньги';uk='Гроші'"), НСтр("ru='Поступления';uk='Надходження'"),, Истина, "ЧДЦ=2; ЧН=-", "СКД_РН_ДенежныеСредства", "Отчет.ДенежныеСредства", "Ведомость");
	ДобавитьПоказатель("Деньги", "Платежи", НСтр("ru='Деньги';uk='Гроші'"), НСтр("ru='Платежи';uk='Платежі'"),, Истина, "ЧДЦ=2; ЧН=-", "СКД_РН_ДенежныеСредства", "Отчет.ДенежныеСредства", "Ведомость");
	ДобавитьПоказатель("Деньги", "ПоступленияПлан", НСтр("ru='Деньги';uk='Гроші'"), НСтр("ru='Поступления (план)';uk='Надходження (план)'"),, Истина, "ЧДЦ=2; ЧН=-", "СКД_РН_ПлатежныйКалендарь", "Отчет.ПлановыеДвиженияДенежныхСредств", "Ведомость", "Поступления");
	ДобавитьПоказатель("Деньги", "ПлатежиПлан", НСтр("ru='Деньги';uk='Гроші'"), НСтр("ru='Платежи (план)';uk='Платежі (план)'"),, Истина, "ЧДЦ=2; ЧН=-", "СКД_РН_ПлатежныйКалендарь", "Отчет.ПлановыеДвиженияДенежныхСредств", "Ведомость", "Платежи");

	// Счета-заказы
	ДобавитьПоказатель("СчетаЗаказы", "СуммаСчетов", НСтр("ru='Счета - заказы';uk='Рахунки - замовлення'"), НСтр("ru='Выставлено счетов на сумму';uk='Виставлено рахунків на суму'"),, Истина, "ЧДЦ=2; ЧН=-", "СКД_РН_ОплатаСчетовИЗаказов", "Отчет.АнализСчетовИЗаказовВВалютеУчета", "СчетаНаОплатуКонтекст", "Сумма");
	ДобавитьПоказатель("СчетаЗаказы", "КоличествоСчетов", НСтр("ru='Счета - заказы';uk='Рахунки - замовлення'"), НСтр("ru='Выставлено счетов (штук)';uk='Виставлено рахунків (штук)'"),,, "ЧДЦ=; ЧН=-", "СКД_РН_ОплатаСчетовИЗаказов", "Отчет.АнализСчетовИЗаказовВВалютеУчета", "СчетаНаОплатуКонтекст", "КоличествоДокументов");
	ДобавитьПоказатель("СчетаЗаказы", "СуммаЗаказов", НСтр("ru='Счета - заказы';uk='Рахунки - замовлення'"), НСтр("ru='Получено заказов на сумму';uk='Отримане замовлень на суму'"),, Истина, "ЧДЦ=2; ЧН=-", "СКД_РН_ОплатаСчетовИЗаказов", "Отчет.АнализСчетовИЗаказовВВалютеУчета", "ЗаказыПокупателейКонтекст", "Сумма");
	ДобавитьПоказатель("СчетаЗаказы", "КоличествоЗаказов", НСтр("ru='Счета - заказы';uk='Рахунки - замовлення'"), НСтр("ru='Получено заказов (штук)';uk='Отримане замовлень (штук)'"),,, "ЧДЦ=; ЧН=-", "СКД_РН_ОплатаСчетовИЗаказов", "Отчет.АнализСчетовИЗаказовВВалютеУчета", "ЗаказыПокупателейКонтекст", "КоличествоДокументов");
	ДобавитьПоказатель("СчетаЗаказы", "СуммаЗаказовПоставщику", НСтр("ru='Счета - заказы';uk='Рахунки - замовлення'"), НСтр("ru='Отправлено заказов на сумму';uk='Відправлене замовлень на суму'"),, Истина, "ЧДЦ=2; ЧН=-", "СКД_РН_ОплатаСчетовИЗаказов", "Отчет.АнализСчетовИЗаказовВВалютеУчета", "ЗаказыПоставщикамКонтекст", "Сумма");
	ДобавитьПоказатель("СчетаЗаказы", "КоличествоЗаказовПоставщику", НСтр("ru='Счета - заказы';uk='Рахунки - замовлення'"), НСтр("ru='Отправлено заказов (штук)';uk='Відправлене замовлень (штук)'"),,, "ЧДЦ=; ЧН=-", "СКД_РН_ОплатаСчетовИЗаказов", "Отчет.АнализСчетовИЗаказовВВалютеУчета", "ЗаказыПоставщикамКонтекст", "КоличествоДокументов");

	// Долги
	ДобавитьПоказатель("Долги", "ДолгиОбщие", НСтр("ru='Долги';uk='Борги'"), НСтр("ru='Общий долг';uk='Загальний борг'"), Истина, Истина, "ЧДЦ=2; ЧН=-", "СКД_Расчеты", "Отчет.Взаиморасчеты", "Остатки",, "Покупатели");
	ДобавитьПоказатель("Долги", "ДолгиНам", НСтр("ru='Долги';uk='Борги'"), НСтр("ru='Долги нам';uk='Борги нам'"), Истина, Истина, "ЧДЦ=2; ЧН=-", "СКД_Расчеты", "Отчет.Взаиморасчеты", "Остатки",, "Покупатели");
	ДобавитьПоказатель("Долги", "ДолгиНаши", НСтр("ru='Долги';uk='Борги'"), НСтр("ru='Долги наши';uk='Борги наші'"), Истина, Истина, "ЧДЦ=2; ЧН=-", "СКД_Расчеты", "Отчет.Взаиморасчеты", "Остатки",, "Поставщики");

	// Фин. анализ
	ДобавитьПоказатель("ФинАнализ", "ЧистыеАктивы", НСтр("ru='Фин. анализ';uk='Фін. аналіз'"), НСтр("ru='Чистые активы';uk='Чисті активи'"), Истина, Истина, "ЧДЦ=2; ЧН=-", "СКД_РБ_Управленческий", "Отчет.ЧистыеАктивы", "ЧистыеАктивы");

	НастройкиПоказателей.Сортировать("Представление,ПредставлениеРесурса");

	// Заполнение таблицы диаграмм
	
	// Диаграмма "Динамика продаж"
	СтруктураСерий = Новый Структура;
	ДобавитьОписаниеСерииТочки(СтруктураСерий, "Сумма", НСтр("ru='Сумма продаж';uk='Сума продажів'"), НСтр("ru='Продажи';uk='Продажі'"), ТипДиаграммы.Гистограмма, Истина);
	ДобавитьОписаниеСерииТочки(СтруктураСерий, "Количество", НСтр("ru='Количество проданных товаров';uk='Кількість проданих товарів'"),, ТипДиаграммы.Гистограмма);
	ДобавитьОписаниеСерииТочки(СтруктураСерий, "КоличествоДокументов", НСтр("ru='Количество продаж (чеков)';uk='Кількість продажів (чеків)'"),, ТипДиаграммы.Гистограмма);
	ДобавитьОписаниеСерииТочки(СтруктураСерий, "Прибыль", НСтр("ru='Прибыль';uk='Прибуток'"),, ТипДиаграммы.Гистограмма, Истина);
	МассивПредставлений = Новый Массив;
	МассивПредставлений.Добавить(НСтр("ru='Себестоимость';uk='Собівартість'"));
	МассивПредставлений.Добавить(НСтр("ru='Прибыль';uk='Прибуток'"));
	ДобавитьОписаниеСерииТочки(СтруктураСерий, "ПрибыльИСебестоимость", МассивПредставлений, НСтр("ru='Себестоимость и прибыль';uk='Собівартість і прибуток'"), ТипДиаграммы.ГистограммаСНакоплением, Истина);
	ДобавитьОписаниеСерииТочки(СтруктураСерий, "ВозвратыСумма", НСтр("ru='Возвраты (сумма)';uk='Повернення (сума)'"),, ТипДиаграммы.Гистограмма, Истина);
	ДобавитьОписаниеСерииТочки(СтруктураСерий, "ВозвратыКоличество", НСтр("ru='Возвраты (количество)';uk='Повернення (кількість)'"),, ТипДиаграммы.Гистограмма);
	СтруктураТочек = Новый Структура;
	ДобавитьОписаниеСерииТочки(СтруктураТочек, "День", НСтр("ru='Дни';uk='Дні'"),,,, "ДЛФ=D");
	ДобавитьОписаниеСерииТочки(СтруктураТочек, "Неделя", НСтр("ru='Недели';uk='Тижня'"),,,, "ДЛФ=D");
	ДобавитьОписаниеСерииТочки(СтруктураТочек, "Месяц", НСтр("ru='Месяцы';uk='Місяці'"),,,, "ДФ='МММ гггг'");
	ДобавитьДиаграмму("ДинамикаПродаж", НСтр("ru='Динамика продаж';uk='Динаміка продажів'"), СтруктураСерий, СтруктураТочек, Ложь, "СКД_РН_Продажи", "Отчет.Продажи", "Основной");  
	
	// Диаграмма "Структура продаж"
	СтруктураСерий = Новый Структура;
	ДобавитьОписаниеСерииТочки(СтруктураСерий, "НоменклатураРодитель", НСтр("ru='Группы товаров';uk='Групи товарів'"),, ТипДиаграммы.Круговая);
	ДобавитьОписаниеСерииТочки(СтруктураСерий, "КатегорияНоменклатуры", НСтр("ru='Категории номенклатуры';uk='Категорії номенклатури'"),, ТипДиаграммы.Круговая);
	ДобавитьОписаниеСерииТочки(СтруктураСерий, "Склад", НСтр("ru='Склады';uk='Склади'"),, ТипДиаграммы.Круговая);
	ДобавитьОписаниеСерииТочки(СтруктураСерий, "Номенклатура", НСтр("ru='Товары';uk='Товари'"),, ТипДиаграммы.Круговая);
	ДобавитьОписаниеСерииТочки(СтруктураСерий, "Контрагент", НСтр("ru='Покупатели';uk='Покупці'"),, ТипДиаграммы.Круговая);
	ДобавитьОписаниеСерииТочки(СтруктураСерий, "Организация", НСтр("ru='Организации';uk='Організації'"),, ТипДиаграммы.Круговая);
	ДобавитьОписаниеСерииТочки(СтруктураСерий, "Подразделение", НСтр("ru='Подразделения';uk='Підрозділи'"),, ТипДиаграммы.Круговая);
	ДобавитьОписаниеСерииТочки(СтруктураСерий, "Ответственный", НСтр("ru='Менеджеры';uk='Менеджери'"),, ТипДиаграммы.Круговая);
	ДобавитьОписаниеСерииТочки(СтруктураСерий, "Валюта", НСтр("ru='Валюта';uk='Валюта'"),, ТипДиаграммы.Круговая);
	СтруктураОтбора = Новый Структура;
	СтруктураОтбора.Вставить("НаборСвойств", Справочники.НаборыДополнительныхРеквизитовИСведений.Справочник_Номенклатура);
	СтруктураНастроек = Новый Структура;
	СтруктураНастроек.Вставить("СвойствоНоменклатуры", СтруктураОтбора);
	ДобавитьОписаниеСерииТочки(СтруктураСерий, "СвойствоНоменклатуры", НСтр("ru='Доп. реквизиты номенклатуры';uk='Доп. реквізити номенклатури'"),, ТипДиаграммы.Круговая,,,, СтруктураНастроек);
	СтруктураОтбора = Новый Структура;
	СтруктураОтбора.Вставить("НаборСвойств", Справочники.НаборыДополнительныхРеквизитовИСведений.Справочник_Контрагенты);
	СтруктураНастроек = Новый Структура;
	СтруктураНастроек.Вставить("СвойствоПокупателя", СтруктураОтбора);
	ДобавитьОписаниеСерииТочки(СтруктураСерий, "СвойствоПокупателя", НСтр("ru='Доп. реквизиты покупателей';uk='Доп. реквізити покупців'"),, ТипДиаграммы.Круговая,,,, СтруктураНастроек);
	СтруктураТочек = Новый Структура;
	ДобавитьОписаниеСерииТочки(СтруктураТочек, "Сумма", НСтр("ru='Сумма продаж';uk='Сума продажів'"), НСтр("ru='Продажи';uk='Продажі'"),, Истина);
	ДобавитьОписаниеСерииТочки(СтруктураТочек, "Количество", НСтр("ru='Количество проданных товаров';uk='Кількість проданих товарів'"));
	ДобавитьОписаниеСерииТочки(СтруктураТочек, "КоличествоДокументов", НСтр("ru='Количество продаж (чеков)';uk='Кількість продажів (чеків)'"));
	ДобавитьОписаниеСерииТочки(СтруктураТочек, "Прибыль", НСтр("ru='Прибыль';uk='Прибуток'"),,, Истина);
	ДобавитьОписаниеСерииТочки(СтруктураТочек, "ВозвратыСумма", НСтр("ru='Возвраты (сумма)';uk='Повернення (сума)'"),,, Истина);
	ДобавитьОписаниеСерииТочки(СтруктураТочек, "ВозвратыКоличество", НСтр("ru='Возвраты (количество)';uk='Повернення (кількість)'"));
	ДобавитьДиаграмму("СтруктураПродаж", НСтр("ru='Структура продаж';uk='Структура продажів'"), СтруктураСерий, СтруктураТочек, Ложь, "СКД_РН_Продажи", "Отчет.Продажи", "Основной");  
	
	// Диаграмма "Динамика денег"
	СтруктураСерий = Новый Структура;
	ДобавитьОписаниеСерииТочки(СтруктураСерий, "СуммаОстаток", НСтр("ru='Остаток';uk='Залишок'"), НСтр("ru='Остаток денег';uk='Залишок грошей'"), ТипДиаграммы.Гистограмма, Истина);
	ДобавитьОписаниеСерииТочки(СтруктураСерий, "СуммаПриход", НСтр("ru='Поступления';uk='Надходження'"), НСтр("ru='Поступления денег';uk='Надходження грошей'"), ТипДиаграммы.Гистограмма, Истина);
	ДобавитьОписаниеСерииТочки(СтруктураСерий, "СуммаРасход", НСтр("ru='Списания';uk='Списання'"), НСтр("ru='Списания денег';uk='Списання грошей'"), ТипДиаграммы.Гистограмма, Истина);
	СтруктураТочек = Новый Структура;
	ДобавитьОписаниеСерииТочки(СтруктураТочек, "День", НСтр("ru='Дни';uk='Дні'"),,,, "ДЛФ=D");
	ДобавитьОписаниеСерииТочки(СтруктураТочек, "Неделя", НСтр("ru='Недели';uk='Тижня'"),,,, "ДЛФ=D");
	ДобавитьОписаниеСерииТочки(СтруктураТочек, "Месяц", НСтр("ru='Месяцы';uk='Місяці'"),,,, "ДФ='МММ гггг'");
	ДобавитьДиаграмму("ДинамикаДенег", НСтр("ru='Деньги (динамика)';uk='Гроші (динаміка)'"), СтруктураСерий, СтруктураТочек, Ложь, "СКД_РН_ДенежныеСредства", "Отчет.ДенежныеСредства", "Ведомость");  
	
	// Диаграмма "Структура денег"
	СтруктураСерий = Новый Структура;
	ДобавитьОписаниеСерииТочки(СтруктураСерий, "БанковскийСчетКасса", НСтр("ru='Банковский счет / касса';uk='Банківський рахунок / каса'"),, ТипДиаграммы.Круговая);
	ДобавитьОписаниеСерииТочки(СтруктураСерий, "Валюта", НСтр("ru='Валюта';uk='Валюта'"),, ТипДиаграммы.Круговая);
	ДобавитьОписаниеСерииТочки(СтруктураСерий, "Организация", НСтр("ru='Организация';uk='Організація'"),, ТипДиаграммы.Круговая);
	ДобавитьОписаниеСерииТочки(СтруктураСерий, "ТипДенежныхСредств", НСтр("ru='Тип денежных средств';uk='Тип грошових коштів'"),, ТипДиаграммы.Круговая);
	ДобавитьОписаниеСерииТочки(СтруктураСерий, "ХозяйственнаяОперация", НСтр("ru='Хоз. операция';uk='Госп. операція'"),, ТипДиаграммы.Круговая,,, "СуммаПриход,СуммаРасход");
	СтруктураОтбора = Новый Структура;
	МассивХозОпераций = ХозоперацииДС();
	СтруктураОтбора.Вставить("Ссылка", МассивХозОпераций);
	СтруктураНастроек = Новый Структура;
	СтруктураНастроек.Вставить("ХозяйственнаяОперация", СтруктураОтбора);
	ДобавитьОписаниеСерииТочки(СтруктураСерий, "Аналитика", НСтр("ru='Аналитика';uk='Аналітика'"),, ТипДиаграммы.Круговая,,, "СуммаПриход,СуммаРасход", СтруктураНастроек);
	СтруктураТочек = Новый Структура;
	ДобавитьОписаниеСерииТочки(СтруктураТочек, "СуммаОстаток", НСтр("ru='Остаток';uk='Залишок'"), НСтр("ru='Остаток денег';uk='Залишок грошей'"),, Истина);
	ДобавитьОписаниеСерииТочки(СтруктураТочек, "СуммаПриход", НСтр("ru='Поступления';uk='Надходження'"), НСтр("ru='Поступления денег';uk='Надходження грошей'"),, Истина);
	ДобавитьОписаниеСерииТочки(СтруктураТочек, "СуммаРасход", НСтр("ru='Списания';uk='Списання'"), НСтр("ru='Списания денег';uk='Списання грошей'"),, Истина);
	ДобавитьДиаграмму("СтруктураДенег", НСтр("ru='Структура денег';uk='Структура грошей'"), СтруктураСерий, СтруктураТочек, Ложь, "СКД_РН_ДенежныеСредстваДвижения", "Отчет.ДенежныеСредства", "Ведомость");  
	
	// Диаграмма "Динамика активов"
	СтруктураСерий = Новый Структура;
	МассивПредставлений = Новый Массив;
	МассивПредставлений.Добавить(НСтр("ru='Долги нам';uk='Борги нам'"));
	МассивПредставлений.Добавить(НСтр("ru='Наши долги';uk='Наші борги'"));
	МассивПредставлений.Добавить(НСтр("ru='Деньги';uk='Гроші'"));
	МассивПредставлений.Добавить(НСтр("ru='Товары';uk='Товари'"));
	МассивПредставлений.Добавить(НСтр("ru='Имущество';uk='Майно'"));
	ДобавитьОписаниеСерииТочки(СтруктураСерий, "Сумма", МассивПредставлений, НСтр("ru='Чистые активы';uk='Чисті активи'"), ТипДиаграммы.ГрафикСОбластямиИНакоплением, Истина);
	СтруктураТочек = Новый Структура;
	ДобавитьОписаниеСерииТочки(СтруктураТочек, "Время", НСтр("ru='Время';uk='Час'"),,,, "ДЛФ=D");
	ДобавитьДиаграмму("ДинамикаАктивов", НСтр("ru='Чистые активы (динамика)';uk='Чисті активи (динаміка)'"), СтруктураСерий, СтруктураТочек, Ложь, "СКД_РБ_Управленческий", "Отчет.ЧистыеАктивы", "ЧистыеАктивы", Истина);  

	НастройкиДиаграмм.Сортировать("Представление");

	// Прочие операции инициализации
	СимволРеглВалюты = УправлениеНебольшойФирмойПовтИсп.ПолучитьСимвольноеПредставлениеВалюты(
	УправлениеНебольшойФирмойПовтИсп.ПолучитьВалютуУчета());
	
	ОпределитьРежимыВводаОстатков();
	
КонецПроцедуры

&НаСервере
Процедура ОпределитьРежимыВводаОстатков()
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ДенежныеСредства.Регистратор
	|ИЗ
	|	РегистрНакопления.ДенежныеСредства КАК ДенежныеСредства
	|ГДЕ
	|	НЕ ДенежныеСредства.Регистратор ССЫЛКА Документ.ВводНачальныхОстатков
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ЗапасыНаСкладах.Регистратор
	|ИЗ
	|	РегистрНакопления.ЗапасыНаСкладах КАК ЗапасыНаСкладах
	|ГДЕ
	|	НЕ ЗапасыНаСкладах.Регистратор ССЫЛКА Документ.ВводНачальныхОстатков
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	РасчетыСПокупателями.Регистратор
	|ИЗ
	|	РегистрНакопления.РасчетыСПокупателями КАК РасчетыСПокупателями
	|ГДЕ
	|	НЕ РасчетыСПокупателями.Регистратор ССЫЛКА Документ.ВводНачальныхОстатков
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	РасчетыСПоставщиками.Регистратор
	|ИЗ
	|	РегистрНакопления.РасчетыСПоставщиками КАК РасчетыСПоставщиками
	|ГДЕ
	|	НЕ РасчетыСПоставщиками.Регистратор ССЫЛКА Документ.ВводНачальныхОстатков";
	Результат = Запрос.ВыполнитьПакет();
	Режимы = Новый Структура;
	Режимы.Вставить("Деньги", Результат.Получить(0).Пустой());
	Режимы.Вставить("Запасы", Результат.Получить(1).Пустой());
	Режимы.Вставить("Покупатели", Результат.Получить(0).Пустой());
	Режимы.Вставить("Поставщики", Результат.Получить(1).Пустой());
	РежимыВводаОстатков = Новый ФиксированнаяСтруктура(Режимы);
	
КонецПроцедуры

&НаСервере
Процедура СохранитьНастройки()
	
	Таб = ДобавленныеПоказатели.Выгрузить(, "Показатель,Ресурс,Представление,Период,ПериодСравнения,Фильтры,Настройки");
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить("ПульсБизнеса", "Показатели", Таб);
	Таб = ДобавленныеДиаграммы.Выгрузить(, "Диаграмма,Серия,Точка,Представление,Период,ПериодСравнения,Фильтры,Настройки");
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить("ПульсБизнеса", "Диаграммы", Таб);
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьНастройки()
	
	ТаблицаПоказателей = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить("ПульсБизнеса", "Показатели");
	ТаблицаДиаграмм = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить("ПульсБизнеса", "Диаграммы");
	Если ТаблицаПоказателей=Неопределено ИЛИ ТаблицаДиаграмм=Неопределено Тогда
		ЗаполнитьПоУмолчанию();
	Иначе
		ДобавленныеПоказатели.Очистить();
		Для каждого Стр Из ТаблицаПоказателей Цикл
			Если НЕ ПустаяСтрока(Стр.Ресурс) Тогда
				СтруктураОтбора = Новый Структура;
				СтруктураОтбора.Вставить("Показатель", Стр.Показатель);
				СтруктураОтбора.Вставить("Ресурс", Стр.Ресурс);
				Строки = НастройкиПоказателей.НайтиСтроки(СтруктураОтбора);
				Если Строки.Количество()=0 Тогда
					// Устаревший показатель
					Продолжить;
				КонецЕсли; 
			КонецЕсли; 
			НоваяСтрока = ДобавленныеПоказатели.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Стр);
			Если НЕ ПустаяСтрока(Стр.Ресурс) Тогда
				СтрНастроек = Строки[0];
				НоваяСтрока.ИдентификаторСтрокиНастроек = СтрНастроек.ПолучитьИдентификатор();
				Если НЕ ПустаяСтрока(СтрНастроек.РазделУчета) Тогда
					НоваяСтрока.ВводОстатков = РежимыВводаОстатков[СтрНастроек.РазделУчета];
				КонецЕсли; 
			КонецЕсли; 
		КонецЦикла;
		ДобавленныеДиаграммы.Очистить();
		Для каждого Стр Из ТаблицаДиаграмм Цикл
			СтруктураОтбора = Новый Структура;
			СтруктураОтбора.Вставить("Диаграмма", ?(ТаблицаДиаграмм.Колонки.Найти("Показатель")=Неопределено, Стр.Диаграмма, Стр.Показатель));
			Строки = НастройкиДиаграмм.НайтиСтроки(СтруктураОтбора);
			Если Строки.Количество()=0 Тогда
				// Устаревшая диаграмма
				Продолжить;
			КонецЕсли;
			СтрокаНастройки = Строки[0];
			Если НЕ СтрокаНастройки.Серии.Свойство(Стр.Серия) ИЛИ НЕ СтрокаНастройки.Точки.Свойство(Стр.Точка) Тогда
				// Устаревшая диаграмма
				Продолжить;
			КонецЕсли; 
			НоваяСтрока = ДобавленныеДиаграммы.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Стр);
			Если НЕ ТаблицаДиаграмм.Колонки.Найти("Показатель")=Неопределено Тогда
				НоваяСтрока.Диаграмма = Стр.Показатель;
			КонецЕсли; 
			НоваяСтрока.ИдентификаторСтрокиНастроек = Строки[0].ПолучитьИдентификатор();
			Если ЗначениеЗаполнено(НоваяСтрока.ПериодСравнения) И ТипЗнч(НоваяСтрока.ПериодСравнения)=Тип("СтандартныйПериод") Тогда
				МассивПредставлений = Новый Массив;
				МассивПредставлений.Добавить(ПредставлениеСтандартногоПериода(НоваяСтрока.Период));
				МассивПредставлений.Добавить(ПредставлениеСтандартногоПериода(НоваяСтрока.ПериодСравнения));
				НоваяСтрока.ПредставленияСерий = Новый ФиксированныйМассив(МассивПредставлений);
			ИначеЕсли ЗначениеЗаполнено(НоваяСтрока.ПериодСравнения) И ТипЗнч(НоваяСтрока.ПериодСравнения)=Тип("СтандартнаяДатаНачала") Тогда
				МассивПредставлений = Новый Массив;
				МассивПредставлений.Добавить(ПредставлениеСтандартнойДатыНачала(НоваяСтрока.Период));
				МассивПредставлений.Добавить(ПредставлениеСтандартнойДатыНачала(НоваяСтрока.ПериодСравнения));
				НоваяСтрока.ПредставленияСерий = Новый ФиксированныйМассив(МассивПредставлений);
			ИначеЕсли ЗначениеЗаполнено(НоваяСтрока.ПериодСравнения) И ТипЗнч(НоваяСтрока.ПериодСравнения)=Тип("Структура") Тогда
				МассивПредставлений = Новый Массив;
				Если НоваяСтрока.ПериодСравнения.Вариант="ТакойЖеДеньНаПрошлойНеделе" 
					ИЛИ НоваяСтрока.ПериодСравнения.Вариант="ТакойЖеДеньВПрошломМесяце" 
					ИЛИ НоваяСтрока.ПериодСравнения.Вариант="ТакойЖеДеньВПрошломГоду" Тогда
					МассивПредставлений.Добавить(ПредставлениеСтандартнойДатыНачала(НоваяСтрока.Период));
					МассивПредставлений.Добавить(ПредставлениеСтандартнойДатыНачала(НоваяСтрока.ПериодСравнения));
				Иначе
					МассивПредставлений.Добавить(ПредставлениеСтандартногоПериода(НоваяСтрока.Период));
					МассивПредставлений.Добавить(ПредставлениеСтандартногоПериода(НоваяСтрока.ПериодСравнения));
				КонецЕсли; 
				НоваяСтрока.ПредставленияСерий = Новый ФиксированныйМассив(МассивПредставлений);
			Иначе
				НоваяСтрока.ПредставленияСерий = СтрокаНастройки.Серии[Стр.Серия].Представления;
			КонецЕсли; 
			НоваяСтрока.ПредставленияТочек = СтрокаНастройки.Точки[Стр.Точка].Представления;
		КонецЦикла;
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПоУмолчанию()
	
	// Заполнение показателей по умолчанию
	ДобавленныеПоказатели.Очистить();
	ОтобразитьПоказатель(,,,, НСтр("ru='ОСТАТКИ';uk='ЗАЛИШКИ'"));
	ОтобразитьПоказатель("Деньги", "СуммаОстаток");
	ОтобразитьПоказатель("Товары", "СуммаОстаток",,, НСтр("ru='Товары';uk='Товари'"));
	ОтобразитьПоказатель("Долги", "ДолгиНам");
	ОтобразитьПоказатель("Долги", "ДолгиНаши");
	ОтобразитьПоказатель("ФинАнализ", "ЧистыеАктивы",,, НСтр("ru='Итого чистые активы';uk='Разом чисті активи'"));
	ОтобразитьПоказатель(,,,, НСтр("ru='С НАЧАЛА ГОДА';uk='З ПОЧАТКУ РОКУ'"));
	ОтобразитьПоказатель("Продажи", "Выручка", Новый СтандартныйПериод(ВариантСтандартногоПериода.СНачалаЭтогоГода), 
	Новый СтандартныйПериод(ВариантСтандартногоПериода.ПрошлыйГодДоТакойЖеДаты), НСтр("ru='Продажи';uk='Продажі'"));
	ОтобразитьПоказатель("Деньги", "Поступления", Новый СтандартныйПериод(ВариантСтандартногоПериода.СНачалаЭтогоГода), 
	Новый СтандартныйПериод(ВариантСтандартногоПериода.ПрошлыйГодДоТакойЖеДаты), НСтр("ru='Поступления';uk='Надходження'"));
	ОтобразитьПоказатель("Деньги", "Платежи", Новый СтандартныйПериод(ВариантСтандартногоПериода.СНачалаЭтогоГода), 
	Новый СтандартныйПериод(ВариантСтандартногоПериода.ПрошлыйГодДоТакойЖеДаты), НСтр("ru='Платежи';uk='Платежі'"));
	
	// Заполнение диаграмм по умолчанию
	ОтобразитьДиаграмму(
	"ДинамикаПродаж", 
	"ПрибыльИСебестоимость", 
	"Месяц", 
	Новый СтандартныйПериод(ВариантСтандартногоПериода.СНачалаЭтогоГода),, 
	НСтр("ru='Динамика продаж';uk='Динаміка продажів'"));
	
	Структура = СтруктураФильтра("ХозяйственнаяОперация",, Справочники.ХозяйственныеОперации.НаРасходы);
	МассивФильтров = Новый Массив;
	МассивФильтров.Добавить(Структура);
	ОтобразитьДиаграмму(
	"СтруктураДенег", 
	"Аналитика", 
	"СуммаРасход", 
	Новый СтандартныйПериод(ВариантСтандартногоПериода.СНачалаЭтогоГода),, 
	НСтр("ru='Структура списания денег';uk='Структура списання грошей'"), 
	МассивФильтров);
	
	СохранитьНастройки();
	
КонецПроцедуры

&НаСервере
Процедура ОтобразитьПоказатель(Показатель, Ресурс = "", Период = Неопределено, ПериодСравнения = Неопределено, Представление = "")
	
	Если ПустаяСтрока(Ресурс) Тогда
		// Добавление разделителя
		СтрДанных = ДобавленныеПоказатели.Добавить();
		СтрДанных.Представление = Представление;
	Иначе
		// Добавление показателя
		СтруктураОтбора = Новый Структура;
		СтруктураОтбора.Вставить("Показатель", Показатель);
		СтруктураОтбора.Вставить("Ресурс", Ресурс);
		Строки = НастройкиПоказателей.НайтиСтроки(СтруктураОтбора);
		Для каждого Стр Из Строки Цикл
			СтрДанных = ДобавленныеПоказатели.Добавить();
			ЗаполнитьЗначенияСвойств(СтрДанных, Стр, "Показатель, Ресурс");
			СтрДанных.Представление = ?(ПустаяСтрока(Представление), Стр.ПредставлениеРесурса, Представление);
			СтрДанных.ИдентификаторСтрокиНастроек = Стр.ПолучитьИдентификатор();
			Если ЗначениеЗаполнено(Период) Тогда
				СтрДанных.Период = Период;
			КонецЕсли; 
			Если ЗначениеЗаполнено(ПериодСравнения) Тогда
				СтрДанных.ПериодСравнения = ПериодСравнения;
			КонецЕсли;
			Если НЕ ПустаяСтрока(Стр.РазделУчета) Тогда
				СтрДанных.ВводОстатков = РежимыВводаОстатков[Стр.РазделУчета];
			КонецЕсли; 
		КонецЦикла; 
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура ОтобразитьДиаграмму(Диаграмма, Серия, Точка, Период = Неопределено, ПериодСравнения = Неопределено, Представление = "", Фильтры = Неопределено)
	
	СтруктураОтбора = Новый Структура;
	СтруктураОтбора.Вставить("Диаграмма", Диаграмма);
	Строки = НастройкиДиаграмм.НайтиСтроки(СтруктураОтбора);
	Для каждого Стр Из Строки Цикл
		НастройкиСерии = Стр.Серии[Серия];
		НастройкиТочки = Стр.Точки[Точка];
		СтрДанных = ДобавленныеДиаграммы.Добавить();
		СтрДанных.Диаграмма = Диаграмма;
		СтрДанных.Серия = Серия;
		СтрДанных.Точка = Точка;
		СтрДанных.Представление = ?(ПустаяСтрока(Представление), Стр.Представление, Представление);
		СтрДанных.ПредставленияСерий = НастройкиСерии.Представления;
		СтрДанных.ПредставленияТочек = НастройкиТочки.Представления;
		СтрДанных.ИдентификаторСтрокиНастроек = Стр.ПолучитьИдентификатор();
		Если ЗначениеЗаполнено(Период) Тогда
			СтрДанных.Период = Период;
		КонецЕсли; 
		Если ЗначениеЗаполнено(ПериодСравнения) Тогда
			СтрДанных.ПериодСравнения = ПериодСравнения;
		КонецЕсли; 
		Если ЗначениеЗаполнено(Фильтры) Тогда
			СтрДанных.Фильтры = Новый ФиксированныйМассив(Фильтры);
		КонецЕсли; 
	КонецЦикла; 
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция СтруктураФильтра(Поле, ВидСравнения = Неопределено, Значение = Неопределено)
	
	СтруктураФильтра = Новый Структура;
	СтруктураФильтра.Вставить("Поле", Поле);
	СтруктураФильтра.Вставить("ВидСравнения", ?(ВидСравнения=Неопределено, ВидСравненияКомпоновкиДанных.Равно, ВидСравнения));
	СтруктураФильтра.Вставить("Значение", Значение);
	Возврат СтруктураФильтра;
	
КонецФункции 

&НаСервере
Функция ДобавитьКоманду(ИмяГруппы, Суффикс, Элемент, Действие, Заголовок, Картинка)
	
	Команда = Команды.Добавить(ИмяГруппы+Суффикс);
	Команда.Действие = Действие;
	Команда.Картинка = Картинка;
	Команда.Заголовок = Заголовок;
	Кнопка = Элементы.Добавить(Команда.Имя, Тип("КнопкаФормы"), Элементы[Элемент.Имя+"КонтекстноеМеню"]);
	Кнопка.Вид = ВидКнопкиФормы.КнопкаКоманднойПанели;
	Кнопка.ИмяКоманды = Команда.Имя;
	Кнопка.Картинка = Картинка;
	Кнопка.Заголовок = Заголовок;
	Возврат Кнопка;
	
КонецФункции
 
&НаСервере
Функция ОтформатироватьЗначение(Значение, Стр)
	
	Если ТипЗнч(Значение)=Тип("ФорматированнаяСтрока") Тогда
		Возврат Значение;
	КонецЕсли; 
	Если ТипЗнч(Значение)=Тип("Строка") Тогда
		Возврат ВФорматированнуюСтроку(Значение);
	КонецЕсли; 
	СтрНастроек = НастройкиПоказателей.НайтиПоИдентификатору(Стр.ИдентификаторСтрокиНастроек);
	Если НЕ ЗначениеЗаполнено(Значение) Тогда
		Если ПустаяСтрока(СтрНастроек.Формат) Тогда
			Результат = Строка(Значение);
		Иначе
			Результат = Формат(Значение, СтрНастроек.Формат);
		КонецЕсли;
	Иначе
		Если ПустаяСтрока(СтрНастроек.Формат) Тогда
			Результат = ВФорматированнуюСтроку(Строка(Значение));
		Иначе
			Результат = ВФорматированнуюСтроку(Формат(Значение, СтрНастроек.Формат));
		КонецЕсли;
		Результат = Новый ФорматированнаяСтрока(
		Результат, 
		?(СтрНастроек.Валютный И ЗначениеЗаполнено(Значение), Новый ФорматированнаяСтрока(" "+СимволРеглВалюты, Новый Шрифт(Новый Шрифт,,8), ЦветаСтиля.ЦветТекстаФормы), ""));
	КонецЕсли; 
	Возврат Результат;
	
КонецФункции

&НаСервере
Функция ВФорматированнуюСтроку(Строка, Ссылка = "")
	
	РазделительДробнойЧасти = Сред(Формат(0.1), 2,1);
	Если СтрЧислоВхождений(Строка, РазделительДробнойЧасти)>1 Тогда
		Возврат Новый ФорматированнаяСтрока(Строка,,,, Ссылка);
	КонецЕсли; 
	Позиция = Найти(Строка, РазделительДробнойЧасти);
	Если Позиция=0 Тогда
		Возврат Новый ФорматированнаяСтрока(Строка, Новый Шрифт(Новый Шрифт,,, Истина), ЦветаСтиля.ЦветТекстаФормы,, Ссылка);
	Иначе
		ТекстДо = Лев(Строка, Позиция-1);
		ТекстПосле = Сред(Строка, Позиция);
		Возврат Новый ФорматированнаяСтрока(
		Новый ФорматированнаяСтрока(ТекстДо, Новый Шрифт(Новый Шрифт,,, Истина), ЦветаСтиля.ЦветТекстаФормы,, Ссылка),
		Новый ФорматированнаяСтрока(ТекстПосле, Новый Шрифт(Новый Шрифт,, 8), ЦветаСтиля.ЦветТекстаФормы,, Ссылка));
	КонецЕсли; 
	
КонецФункции

&НаСервереБезКонтекста
Функция ХозоперацииДС()
	
	МассивХозОпераций = Новый Массив;
	МассивХозОпераций.Добавить(Справочники.ХозяйственныеОперации.Аванс);
	МассивХозОпераций.Добавить(Справочники.ХозяйственныеОперации.ВозвратЗаймаСотрудником);
	МассивХозОпераций.Добавить(Справочники.ХозяйственныеОперации.ВозвратОплатыНаПлатежныеКарты);
	МассивХозОпераций.Добавить(Справочники.ХозяйственныеОперации.ВозвратОплатыПокупателю);
	МассивХозОпераций.Добавить(Справочники.ХозяйственныеОперации.ВыдачаЗаймаСотруднику);
	МассивХозОпераций.Добавить(Справочники.ХозяйственныеОперации.Зарплата);
	МассивХозОпераций.Добавить(Справочники.ХозяйственныеОперации.КредитПолученный);
	МассивХозОпераций.Добавить(Справочники.ХозяйственныеОперации.НаРасходы);
	МассивХозОпераций.Добавить(Справочники.ХозяйственныеОперации.Оплата);
	МассивХозОпераций.Добавить(Справочники.ХозяйственныеОперации.ПеремещениеВКассуККМ);
	МассивХозОпераций.Добавить(Справочники.ХозяйственныеОперации.ПеремещениеДС);
	МассивХозОпераций.Добавить(Справочники.ХозяйственныеОперации.ПеречислениеНалога);
	МассивХозОпераций.Добавить(Справочники.ХозяйственныеОперации.Подотчетнику);
	МассивХозОпераций.Добавить(Справочники.ХозяйственныеОперации.Покупателю);
	МассивХозОпераций.Добавить(Справочники.ХозяйственныеОперации.ПокупкаВалюты);
	МассивХозОпераций.Добавить(Справочники.ХозяйственныеОперации.Поставщику);
	МассивХозОпераций.Добавить(Справочники.ХозяйственныеОперации.ПоступлениеОплатыОтПокупателя);
	МассивХозОпераций.Добавить(Справочники.ХозяйственныеОперации.ПоступлениеОплатыПоКартам);
	МассивХозОпераций.Добавить(Справочники.ХозяйственныеОперации.СписаниеНаРасходы);
	Возврат МассивХозОпераций;
	
КонецФункции

#КонецОбласти
 
