
Процедура УстановитьОтборПоПериоду(СписокОтбор, ДатаНачала, ДатаОкончания, ИмяПоляОтбора = "Дата") Экспорт
	
	// Отбор на список по периоду
	ГруппаОтборПоПериоду = ОбщегоНазначенияКлиентСервер.СоздатьГруппуЭлементовОтбора(
		СписокОтбор.Элементы,
		"Период",
		ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ГруппаОтборПоПериоду,
		ИмяПоляОтбора,
		ВидСравненияКомпоновкиДанных.БольшеИлиРавно,
		ДатаНачала,
		"ДатаНачала",
		ЗначениеЗаполнено(ДатаНачала));
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ГруппаОтборПоПериоду,
		ИмяПоляОтбора,
		ВидСравненияКомпоновкиДанных.МеньшеИлиРавно,
		ДатаОкончания,
		"ДатаОкончания",
		ЗначениеЗаполнено(ДатаОкончания));
		
	КонецПроцедуры
	
Функция ОбновитьПредставлениеПериода(Период) Экспорт
	
	КодЯзыка = Локализация.КодЯзыкаИнтерфейса();
	
	Если НЕ ЗначениеЗаполнено(Период) ИЛИ (НЕ ЗначениеЗаполнено(Период.ДатаНачала) И НЕ ЗначениеЗаполнено(Период.ДатаОкончания)) Тогда
		ПредставлениеПериода = НСтр("ru='Период: за все время';uk='Період: за весь час'");
	Иначе
		ДатаОкончанияПериода = ?(ЗначениеЗаполнено(Период.ДатаОкончания), КонецДня(Период.ДатаОкончания), Период.ДатаОкончания);
		Если ДатаОкончанияПериода < Период.ДатаНачала Тогда
			#Если Клиент Тогда
			УправлениеНебольшойФирмойКлиент.СообщитьОбОшибке(Неопределено, НСтр("ru='Выбрана дата окончания периода, которая меньше даты начала!';uk='Обрана дата закінчення періоду, яка менше дати початку!'"));
			#КонецЕсли
			ПредставлениеПериода = НСтр("ru='с ';uk='з'")+Формат(Период.ДатаНачала,"ДФ=dd.MM.yyyy");
		Иначе
			ПредставлениеПериода = НСтр("ru='за ';uk='за '")+НРег(ПредставлениеПериода(Период.ДатаНачала, ДатаОкончанияПериода, "Л="+Локализация.ОпределитьКодЯзыкаДляФормат(КодЯзыка)));
		КонецЕсли; 
			КонецЕсли;
	
	Возврат ПредставлениеПериода;
	
КонецФункции

