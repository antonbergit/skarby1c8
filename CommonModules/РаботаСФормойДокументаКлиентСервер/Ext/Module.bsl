
#Область ПрограммныйИнтерфейс

Функция СформироватьНадписьДокументОснование(Знач ДокОснование) Экспорт
	
	КомпонентыФС = Новый Массив;
	
	ПредставлениеДокОснования = РаботаСФормойДокумента.ПредставлениеДокументаОснования(ДокОснование);
	
	Если ПредставлениеДокОснования = "НетОбъекта" Тогда
		
		КомпонентыФС = Новый Массив;
		КомпонентыФС.Добавить(Новый ФорматированнаяСтрока(НСтр("ru='Основание: находится в автомном рабочем месте';uk='Підстава: знаходиться в автомном робочому місці'")));
		
	ИначеЕсли ЗначениеЗаполнено(ПредставлениеДокОснования) Тогда
		
		ДокОснованиеТекст = ПредставлениеДокОснования;
		
		ДокОснованиеТекст = СтрЗаменить(ДокОснованиеТекст, НСтр("ru=' (удален)';uk=' (видалений)'"),"");
		ДокОснованиеТекст = СтрЗаменить(ДокОснованиеТекст, НСтр("ru=' (не проведен)';uk=' (не проведений)'"),"");

		КомпонентыФС = Новый Массив;
		КомпонентыФС.Добавить(Новый ФорматированнаяСтрока(НСтр("ru='Основание: ';uk='Підстава: '")));
		КомпонентыФС.Добавить(Новый ФорматированнаяСтрока(ДокОснованиеТекст, , , , "открыть"));
		КомпонентыФС.Добавить(Новый ФорматированнаяСтрока(" "));
		КомпонентыФС.Добавить(Новый ФорматированнаяСтрока(БиблиотекаКартинок.ЗаполнитьПоОснованию12х12, , , , "заполнить"));
		КомпонентыФС.Добавить(Новый ФорматированнаяСтрока(" "));
		КомпонентыФС.Добавить(Новый ФорматированнаяСтрока(БиблиотекаКартинок.Очистить, , , , "удалить"));
	Иначе
		КомпонентыФС = Новый Массив;
		КомпонентыФС.Добавить(Новый ФорматированнаяСтрока(НСтр("ru='Основание: ';uk='Підстава: '")));
		КомпонентыФС.Добавить(Новый ФорматированнаяСтрока(НСтр("ru='выбрать';uk='вибрати'"), , , , "выбрать"));
	КонецЕсли;
	
	Возврат Новый ФорматированнаяСтрока(КомпонентыФС);

КонецФункции


Функция ПолучитьЗаголовокПредоплаты(Знач ТабЧасть, Знач ВалютаДокумента) Экспорт
	
	Если ТабЧасть.Количество()>0 Тогда
		Возврат НСтр("ru='Предоплата: ';uk='Передоплата: '") + ТабЧасть.Итог("СуммаРасчетов") + " " + УправлениеНебольшойФирмойПовтИсп.ПолучитьСимвольноеПредставлениеВалюты(ВалютаДокумента); 
	Иначе
		Возврат НСтр("ru='Предоплата';uk='Передоплата'");
	КонецЕсли;
	
КонецФункции

Функция КраткоеПредставлениеТипаНалогообложенияНДС(НалогообложениеНДС) Экспорт
	
	Если НалогообложениеНДС = ПредопределенноеЗначение("Перечисление.ТипыНалогообложенияНДС.ОблагаетсяНДС") Тогда
		Возврат НСтр("ru='с НДС';uk='з ПДВ'");
	ИначеЕсли НалогообложениеНДС = ПредопределенноеЗначение("Перечисление.ТипыНалогообложенияНДС.НеОблагаетсяНДС") Тогда
		Возврат НСтр("ru='без НДС';uk='без ПДВ'");
	ИначеЕсли НалогообложениеНДС = ПредопределенноеЗначение("Перечисление.ТипыНалогообложенияНДС.НаЭкспорт") Тогда
		Возврат НСтр("ru='0% НДС';uk='0% ПДВ'");
	Иначе
		Возврат "";
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьКороткийНомерДокумента(Знач НомерДок) Экспорт
	
	СоответствиеЧисел = Новый Соответствие();
	СоответствиеЧисел.Вставить("1",1);
	СоответствиеЧисел.Вставить("2",2);
	СоответствиеЧисел.Вставить("3",3);
	СоответствиеЧисел.Вставить("4",4);
	СоответствиеЧисел.Вставить("5",5);
	СоответствиеЧисел.Вставить("6",6);
	СоответствиеЧисел.Вставить("7",7);
	СоответствиеЧисел.Вставить("8",8);
	СоответствиеЧисел.Вставить("9",9);
	СоответствиеЧисел.Вставить("0",0);
	
	НомерДок = СокрЛП(НомерДок);
	ДлинаСтроки = СтрДлина(НомерДок);
	НомерЧисло = НомерДок;
	
	Для н=1 По ДлинаСтроки Цикл
	
		Если СоответствиеЧисел.Получить(Сред(НомерДок,ДлинаСтроки-н+1,1)) = Неопределено Тогда
			НомерЧисло = Прав(НомерДок, н-1); 
			Прервать;
		КонецЕсли;
	
	КонецЦикла;
	
	возврат Число(НомерЧисло);
	
КонецФункции

Функция ПолучитьИмяФормыСтрокой(Знач ИмяФормы) Экспорт
	
	ИмяФормы = СтрЗаменить(ИмяФормы, ".ФормаСписка", "");
	ИмяФормы = СтрЗаменить(ИмяФормы, ".ФормаДокумента", "");
	ИмяФормы = СтрЗаменить(ИмяФормы, ".ФормаВыбора", "");
	ИмяФормы = СтрЗаменить(ИмяФормы, ".ФормаЭлемента", "");
	ИмяФормы = СтрЗаменить(ИмяФормы, ".Форма", "");
	
	ИмяФормы = СтрЗаменить(ИмяФормы, ".", "");
	
	Возврат ИмяФормы;
	
КонецФункции

Функция ВариантОтчета(ИмяОтчета, КлючВарианта) Экспорт
	
	Возврат РаботаСФормойДокумента.СсылкаВариантаОтчета(ИмяОтчета, КлючВарианта);
	
КонецФункции

#КонецОбласти
