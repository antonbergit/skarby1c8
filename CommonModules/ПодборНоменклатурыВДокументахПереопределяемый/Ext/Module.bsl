
// Пользовательские настройки. Начальное заполнение.

// Процедура позволяет переопределить начальное заполнение пользовательских настроек
//
Процедура ПереопределитьНачальноеЗаполнениеНастроекПодбора(Пользователь, СтандартнаяОбработка) Экспорт
	
	
	
КонецПроцедуры // ПереопределитьНачальноеЗаполнениеНастроекПодбора()

// Конец Пользовательские настройки. Начальное заполнение.

// Таблица использования

// Процедура описывает таблицу использований форм подбора под документам и табличным частям
//
// Форма таблицы ТаблицаИспользования:
//
// - "ИмяДокумента",		Строка (100), Имя документа;
// - "ИмяТабличнойЧасти",	Строка (100), Имя табличной части документа;
// - "ФормаПодбора", 		Строка (100), Полное имя формы подбора, которую необходимо использовать в качестве формы подбора;
//
Процедура ТаблицаИспользованияФормПодбора(ТаблицаИспользования) Экспорт
	
	// Реализация
	ПолноеИмяФормыПодбора = Обработки.ПодборРеализация.ПолноеИмяФормыПодбора();
	
	ДобавитьСтрокуИспользованияПодбора(ТаблицаИспользования, Метаданные.Документы.СчетНаОплату.Имя, Метаданные.Документы.СчетНаОплату.ТабличныеЧасти.Запасы.Имя, ПолноеИмяФормыПодбора);
	ДобавитьСтрокуИспользованияПодбора(ТаблицаИспользования, Метаданные.Документы.ЗаказПокупателя.Имя, Метаданные.Документы.ЗаказПокупателя.ТабличныеЧасти.Запасы.Имя, ПолноеИмяФормыПодбора);
	ДобавитьСтрокуИспользованияПодбора(ТаблицаИспользования, Метаданные.Документы.АктВыполненныхРабот.Имя, Метаданные.Документы.АктВыполненныхРабот.ТабличныеЧасти.РаботыИУслуги.Имя, ПолноеИмяФормыПодбора);
	ДобавитьСтрокуИспользованияПодбора(ТаблицаИспользования, Метаданные.Документы.РасходнаяНакладная.Имя, Метаданные.Документы.РасходнаяНакладная.ТабличныеЧасти.Запасы.Имя, ПолноеИмяФормыПодбора);
	ДобавитьСтрокуИспользованияПодбора(ТаблицаИспользования, Метаданные.Документы.ЧекККМ.Имя, Метаданные.Документы.ЧекККМ.ТабличныеЧасти.Запасы.Имя, ПолноеИмяФормыПодбора);
	
	// Поступление
	ПолноеИмяФормыПодбора = Обработки.ПодборПоступление.ПолноеИмяФормыПодбора();
	
	ДобавитьСтрокуИспользованияПодбора(ТаблицаИспользования, Метаданные.Документы.СчетНаОплатуПоставщика.Имя, Метаданные.Документы.СчетНаОплатуПоставщика.ТабличныеЧасти.Запасы.Имя, ПолноеИмяФормыПодбора);
	ДобавитьСтрокуИспользованияПодбора(ТаблицаИспользования, Метаданные.Документы.ЗаказПоставщику.Имя, Метаданные.Документы.ЗаказПоставщику.ТабличныеЧасти.Запасы.Имя, ПолноеИмяФормыПодбора);
	ДобавитьСтрокуИспользованияПодбора(ТаблицаИспользования, Метаданные.Документы.ПриходнаяНакладная.Имя, Метаданные.Документы.ПриходнаяНакладная.ТабличныеЧасти.Запасы.Имя, ПолноеИмяФормыПодбора);
	ДобавитьСтрокуИспользованияПодбора(ТаблицаИспользования, Метаданные.Документы.ПриходнаяНакладная.Имя, Метаданные.Документы.ПриходнаяНакладная.ТабличныеЧасти.Расходы.Имя, ПолноеИмяФормыПодбора);
	
	// Прочее
	ПолноеИмяФормыПодбора = Обработки.ПодборПеремещение.ПолноеИмяФормыПодбора();
	
	ДобавитьСтрокуИспользованияПодбора(ТаблицаИспользования, Метаданные.Документы.ПеремещениеЗапасов.Имя, Метаданные.Документы.ПеремещениеЗапасов.ТабличныеЧасти.Запасы.Имя, ПолноеИмяФормыПодбора);
	
КонецПроцедуры // ТаблицаИспользованияФормПодбора()

// Процедура добавляет в таблицу использования новую строку
//
Процедура ДобавитьСтрокуИспользованияПодбора(ТаблицаИспользования, ИмяДокумента, ИмяТабличнойЧасти, ПолноеИмяФормыПодбора)
	
	НоваяСтрока = ТаблицаИспользования.Добавить();
	
	НоваяСтрока.ИмяДокумента 		= ИмяДокумента;
	НоваяСтрока.ИмяТабличнойЧасти	= ИмяТабличнойЧасти;
	НоваяСтрока.ФормаПодбора		= ПолноеИмяФормыПодбора;
	
КонецПроцедуры // ДобавитьСтрокуИспользованияПодбора()

// Конец Таблица использования


// Полнотекстовый поиск

Функция ПолнотекстовыйПоискТоваров(СтрокаПоиска, РезультатПоиска)
	
	МассивШтрихкодов = Новый Массив;
	
	// Поиск данных
	РазмерПорции = 200;
	ОбластьПоиска = Новый Массив;
	ОбластьПоиска.Добавить(Метаданные.Справочники.Номенклатура);
	ОбластьПоиска.Добавить(Метаданные.Справочники.ХарактеристикиНоменклатуры);
	ОбластьПоиска.Добавить(Метаданные.РегистрыСведений.ДополнительныеСведения);
	ОбластьПоиска.Добавить(Метаданные.РегистрыСведений.ШтрихкодыНоменклатуры);
	
	СписокПоиска = ПолнотекстовыйПоиск.СоздатьСписок(СтрокаПоиска, РазмерПорции);
	СписокПоиска.ПолучатьОписание = Ложь;
	СписокПоиска.ОбластьПоиска = ОбластьПоиска;
	СписокПоиска.ПерваяЧасть();
	
	Если СписокПоиска.СлишкомМногоРезультатов() Тогда
		Возврат "СлишкомМногоРезультатов";
	КонецЕсли;
	
	КоличествоНайденныхЭлементов = СписокПоиска.ПолноеКоличество();
	Если КоличествоНайденныхЭлементов = 0 Тогда
		Возврат "НичегоНеНайдено";
	КонецЕсли;
	
	// Обработка данных
	НачальнаяПозиция	= 0;
	КонечнаяПозиция		= ?(КоличествоНайденныхЭлементов > РазмерПорции, РазмерПорции, КоличествоНайденныхЭлементов) - 1;
	ЕстьСледующаяПорция = Истина;

	Пока ЕстьСледующаяПорция Цикл
		
		Для СчетчикЭлементов = 0 По КонечнаяПозиция Цикл
			
			Элемент = СписокПоиска.Получить(СчетчикЭлементов);
			
			Если Элемент.Метаданные = Метаданные.Справочники.Номенклатура Тогда
				
				РезультатПоиска.Номенклатура.Добавить(Элемент.Значение);
				
			ИначеЕсли Элемент.Метаданные = Метаданные.Справочники.ХарактеристикиНоменклатуры Тогда
				
				РезультатПоиска.ХарактеристикиНоменклатуры.Добавить(Элемент.Значение);
				
			ИначеЕсли Элемент.Метаданные = Метаданные.РегистрыСведений.ДополнительныеСведения Тогда
				
				Если ТипЗнч(Элемент.Значение.Объект) = Тип("СправочникСсылка.Номенклатура") Тогда
					
					РезультатПоиска.Номенклатура.Добавить(Элемент.Значение.Объект);
					
				КонецЕсли;
				
			ИначеЕсли Элемент.Метаданные = Метаданные.РегистрыСведений.ШтрихкодыНоменклатуры Тогда
				
				МассивШтрихкодов.Добавить(Элемент.Значение.Штрихкод);
				
			Иначе
				
				ВызватьИсключение НСтр("ru='Неизвестная ошибка';uk='Невідома помилка'");
				
			КонецЕсли;
			
		КонецЦикла;
		
		НачальнаяПозиция    = НачальнаяПозиция + РазмерПорции;
		ЕстьСледующаяПорция = (НачальнаяПозиция < КоличествоНайденныхЭлементов - 1);
		
		Если ЕстьСледующаяПорция Тогда
			
			КонечнаяПозиция = ?(КоличествоНайденныхЭлементов > НачальнаяПозиция + РазмерПорции,
			                    РазмерПорции,
			                    КоличествоНайденныхЭлементов - НачальнаяПозиция
			                    ) - 1;
			СписокПоиска.СледующаяЧасть();
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если МассивШтрихкодов.Количество() > 0 Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ШтрихкодыНоменклатуры.Номенклатура КАК Номенклатура,
		|	ШтрихкодыНоменклатуры.Характеристика КАК Характеристика
		|ИЗ
		|	РегистрСведений.ШтрихкодыНоменклатуры КАК ШтрихкодыНоменклатуры
		|ГДЕ
		|	ШтрихкодыНоменклатуры.Штрихкод В(&МассивШтрихкодов)
		|	И ШтрихкодыНоменклатуры.Номенклатура ССЫЛКА Справочник.Номенклатура";
		
		Запрос.УстановитьПараметр("МассивШтрихкодов", МассивШтрихкодов);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			
			РезультатПоиска.Номенклатура.Добавить(Выборка.Номенклатура);
			
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат "ВыполненоУспешно";
	
КонецФункции

Функция ПоискТоваров(СтрокаПоиска, ОписаниеОшибки) Экспорт
	
	РезультатПоиска = Новый Структура;
	РезультатПоиска.Вставить("Номенклатура", Новый Массив);
	РезультатПоиска.Вставить("ХарактеристикиНоменклатуры", Новый Массив);
	
	Результат = ПолнотекстовыйПоискТоваров(СтрокаПоиска, РезультатПоиска);
	
	Если Результат = "ВыполненоУспешно" Тогда
		
		Возврат РезультатПоиска;
		
	ИначеЕсли Результат = "СлишкомМногоРезультатов" Тогда
		
		ОписаниеОшибки = НСтр("ru='Слишком много результатов. Уточните запрос.';uk='Занадто багато результатів. Уточніть запит.'");
		Возврат РезультатПоиска;
		
	ИначеЕсли Результат = "НичегоНеНайдено" Тогда
		
		ОписаниеОшибки = НСтр("ru='Ничего не найдено';uk='Нічого не знайдене'");
		Возврат РезультатПоиска;
		
	Иначе
		
		ВызватьИсключение НСтр("ru='Неизвестная ошибка';uk='Невідома помилка'");
		
	КонецЕсли;
	
КонецФункции

// Конец Полнотекстовый поиск

// Контекстный поиск

Функция СформироватьМассивОтбораПоНоменклатуре(ТекстПоиска)
	
	ТекстЗапроса = 
	"Выбрать СпрНоменклатура.Ссылка КАК НоменклатураСсылка
	|Поместить НоменклатураПоНаименование Из Справочник.Номенклатура КАК СпрНоменклатура
	|Где СпрНоменклатура.Наименование ПОДОБНО &ТекстПоиска;
	|////////////////////////////////////////////////////////////////////////////
	|Выбрать СпрНоменклатура.Ссылка КАК НоменклатураСсылка
	|Поместить НоменклатураПоПолномуНаименованию Из Справочник.Номенклатура КАК СпрНоменклатура
	|Где СпрНоменклатура.НаименованиеПолное ПОДОБНО &ТекстПоиска;
	|////////////////////////////////////////////////////////////////////////////
	|Выбрать СпрНоменклатура.Ссылка КАК НоменклатураСсылка
	|Поместить НоменклатураПоАртикулу Из Справочник.Номенклатура КАК СпрНоменклатура
	|Где СпрНоменклатура.Артикул ПОДОБНО &ТекстПоиска;
	|////////////////////////////////////////////////////////////////////////////
	|Выбрать СпрНоменклатура.Ссылка КАК НоменклатураСсылка
	|Поместить НоменклатураПоКомментарию Из Справочник.Номенклатура КАК СпрНоменклатура
	|Где СпрНоменклатура.Комментарий ПОДОБНО &ТекстПоиска;
	|////////////////////////////////////////////////////////////////////////////
	|Выбрать СпрНоменклатура.Ссылка КАК НоменклатураСсылка
	|Поместить НоменклатураПоНаименованиюЦеновойГруппы Из Справочник.Номенклатура КАК СпрНоменклатура
	|Где СпрНоменклатура.ЦеноваяГруппа.Наименование ПОДОБНО &ТекстПоиска;
	|////////////////////////////////////////////////////////////////////////////
	|Выбрать НоменклатураПоНаименование.НоменклатураСсылка КАК НоменклатураСсылка
	|Объединить
	|Выбрать НоменклатураПоПолномуНаименованию.НоменклатураСсылка
	|Объединить
	|Выбрать НоменклатураПоАртикулу.НоменклатураСсылка
	|Объединить
	|Выбрать НоменклатураПоКомментарию.НоменклатураСсылка
	|Объединить
	|Выбрать НоменклатураПоНаименованиюЦеновойГруппы.НоменклатураСсылка";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("ТекстПоиска", "%" + ТекстПоиска + "%");
	ТаблицаНоменклатуры = Запрос.Выполнить().Выгрузить();
	
	Возврат ТаблицаНоменклатуры.ВыгрузитьКолонку("НоменклатураСсылка");
	
КонецФункции

Функция СформироватьМассивОтбораПоХарактеристикам(ТекстПоиска)
	
	ТекстЗапроса = 
	"Выбрать СпрХарактеристики.Ссылка КАК ХарактеристикаСсылка
	|Из Справочник.ХарактеристикиНоменклатуры КАК СпрХарактеристики
	|Где СпрХарактеристики.Наименование ПОДОБНО &ТекстПоиска";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("ТекстПоиска", "%" + ТекстПоиска + "%");
	ТаблицаХарактеристик = Запрос.Выполнить().Выгрузить();
	
	Возврат ТаблицаХарактеристик.ВыгрузитьКолонку("ХарактеристикаСсылка");
	
КонецФункции

Функция МассивыОтборовНоменклатурыИХарактеристик(ТекстПоиска) Экспорт
	
	СтруктураМассивов = Новый Структура;
	
	СтруктураМассивов.Вставить("ОтборНоменклатура", СформироватьМассивОтбораПоНоменклатуре(ТекстПоиска));
	СтруктураМассивов.Вставить("ОтборХарактеристики", СформироватьМассивОтбораПоХарактеристикам(ТекстПоиска));
	
	Возврат СтруктураМассивов;
	
КонецФункции

// Конец Контекстный поиск