
//Преобразует код языка в формат понятный системной фунции Формат()
// Параметры
//  КодЯзыка  	 – Строка – код языка в формате uk/ru
//
// Возвращаемое значение:
//   Строка   	 – код языка в формате ru_RU/uk_UA
//
Функция ОпределитьКодЯзыкаДляФормат(КодЯзыка) Экспорт
	
	Возврат ?(КодЯзыка = "uk","uk_UA","ru_RU");
	
КонецФункции // ОпределитьКодЯзыкаДляФормат()

//По коду языка возвращает строку параметров прописи из справочника Валюты 
//(ПараметрыПрописиНаРусском или ПараметрыПрописиНаУкраинском).
Функция ПараметрыПрописи(Валюта, КодЯзыка) Экспорт
	
	Возврат ?(КодЯзыка = "uk",Валюта.ПараметрыПрописиНаУкраинском,Валюта.ПараметрыПрописиНаРусском);
	
КонецФункции // ПараметрыПрописи()
       
// Возвращает код языка информационной базы, который указан 
// в региональных настройках (в конфигураторе)
Функция КодЯзыкаИнформационнойБазы() Экспорт
	
	// любой язык кроме украинского будем считать русским
	КодЛокализацииИБ = КодЛокализации();
	Возврат ?((КодЛокализацииИБ = "uk_UA") или (КодЛокализацииИБ = "uk"),"uk","ru");
	
КонецФункции //КодЯзыкаИнформационнойБазы()

// Возвращает код языка интерфейса в формате ru/uk
Функция КодЯзыкаИнтерфейса() Экспорт
	
	//Возврат ТекущийЯзык().КодЯзыка;
	Возврат ИнтернетПоддержкаПользователей.КодЯзыкаИнтерфейсаКонфигурации();
	
КонецФункции // КодЯзыкаИнтерфейса()

// Функция возвращает имя языка для формирования нерегламентных печатных форм
// документов и некоторых справочников с учетом установленного режима в настройках
// пользователя. По-умолчанию это язык информционной базы.
//
// Параметры
//     	РежимПользователя - Установленное значение в настройках пользователя
//
// Возвращаемое значение:
//		Строка   – код языка
//
Функция ПолучитьЯзыкФормированияПечатныхФорм(РежимПользователя = Неопределено) Экспорт

	Если РежимПользователя = Неопределено Тогда
		
		РежимПользователя = УправлениеНебольшойФирмойПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(Пользователи.ТекущийПользователь(), "РежимФормированияПечатныхФорм")
		
	КонецЕсли; 
	
	Если РежимПользователя = Перечисления.РежимФормированияПечатныхФорм.НаРусскомЯзыке Тогда
		
		КодЯзыкаПечать = "ru";
		
	ИначеЕсли РежимПользователя = Перечисления.РежимФормированияПечатныхФорм.НаУкраинскомЯзыке Тогда
		
		КодЯзыкаПечать = "uk";
		
	ИначеЕсли РежимПользователя = Перечисления.РежимФормированияПечатныхФорм.НаЯзыкеИнтерфейсаПользователя Тогда		
		
		КодЯзыкаПечать = КодЯзыкаИнтерфейса();
		
	Иначе
		
		КодЯзыкаПечать = КодЯзыкаИнформационнойБазы();
		
	КонецЕсли;

	Возврат КодЯзыкаПечать;
	
КонецФункции // ПолучитьЯзыкФормированияПечатныхФорм()