////////////////////////////////////////////////////////////////////////////////
// Подсистема "Отчеты УНФ" (вызов сервера).
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// См. описание процедуры ОтчетыУНФ.ДобавитьОтчетВИсторию()
Процедура ДобавитьОтчетВИсторию(Вариант) Экспорт
	
	ОтчетыУНФ.ДобавитьОтчетВИсторию(Вариант);
	
КонецПроцедуры

#КонецОбласти

// Гунько 2021-01-18
Функция гвСоздатьЗаказПокупателяНаСервере(АдресРасшифровкиВХранилище, Расшифровка, ДанныеФормы) Экспорт 
	
	ДанныеРасшифровкиОбъект = ПолучитьИзВременногоХранилища(АдресРасшифровкиВХранилище);
	лПолеРасшифровки = ДанныеРасшифровкиОбъект.Элементы[Расшифровка].ПолучитьПоля()[0];
	лСерийныйНомер = лПолеРасшифровки.Значение;
	
	Если ЗначениеЗаполнено(лСерийныйНомер) И ТипЗнч(лСерийныйНомер) = Тип("СправочникСсылка.СерийныеНомера") Тогда
		
		ДанныеФормы.ДатаОтгрузки = ТекущаяДатаСеанса();
		
		лСтрокаЗапасы = ДанныеФормы.Запасы.Добавить(); 	
		лСтрокаЗапасы.Номенклатура = лСерийныйНомер.Владелец;
		лСтрокаЗапасы.гвСерийныйНомер = лСерийныйНомер;
		лСтрокаЗапасы.гвЗарезервировано = Истина;
		лСтрокаЗапасы.Количество = 1;
		
		Возврат ДанныеФормы;
		
	Иначе
		
		Возврат Неопределено;
		
	КонецЕсли;
	
КонецФункции

Функция гвСоздатьЗаказНаПроизводствоНаСервере(АдресРасшифровкиВХранилище, Расшифровка, ДанныеФормы) Экспорт
	
	ДанныеРасшифровкиОбъект = ПолучитьИзВременногоХранилища(АдресРасшифровкиВХранилище);
	лПолеРасшифровки = ДанныеРасшифровкиОбъект.Элементы[Расшифровка].ПолучитьПоля()[0];
	лСерийныйНомер = лПолеРасшифровки.Значение;
	
	Если ЗначениеЗаполнено(лСерийныйНомер) И ТипЗнч(лСерийныйНомер) = Тип("СправочникСсылка.СерийныеНомера") Тогда		
	
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	СерийныеНомераОстатки.Номенклатура,
		|	СерийныеНомераОстатки.СтруктурнаяЕдиница,
		|	СерийныеНомераОстатки.СтруктурнаяЕдиница.МОЛ КАК МОЛ 
		|ИЗ
		|	РегистрНакопления.СерийныеНомера.Остатки(, СерийныйНомер = &СерийныйНомер) КАК СерийныеНомераОстатки";
		
		Запрос.УстановитьПараметр("СерийныйНомер", лСерийныйНомер);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		Выборка = РезультатЗапроса.Выбрать();
		
		Если Выборка.Следующий() Тогда
			
			ДанныеФормы.СтруктурнаяЕдиница 	= Выборка.СтруктурнаяЕдиница;
			ДанныеФормы.гвИсполнитель 		= Выборка.МОЛ;
			
			ДанныеФормы.Старт = НачалоДня(ТекущаяДатаСеанса());
			ДанныеФормы.Финиш = ДанныеФормы.Старт + 3 * 86000;
			
			лСтрокаПродукция = ДанныеФормы.Продукция.Добавить(); 	
			лСтрокаПродукция.Номенклатура = лСерийныйНомер.Владелец;
			лСтрокаПродукция.гвСерийныйНомер = лСерийныйНомер;
			
			СтруктураДанные = Новый Структура;
			СтруктураДанные.Вставить("Номенклатура", лСтрокаПродукция.Номенклатура);
			
			СтруктураДанные = гвПолучитьДанныеНоменклатураПриИзменении(СтруктураДанные);
			
			лСтрокаПродукция.ЕдиницаИзмерения = СтруктураДанные.ЕдиницаИзмерения;
			лСтрокаПродукция.Количество = 1;
			лСтрокаПродукция.Спецификация = СтруктураДанные.Спецификация;
			
			лСтрокаПродукция.ТипНоменклатуры = СтруктураДанные.ТипНоменклатуры;
			
			Возврат ДанныеФормы;
			
		Иначе	
			Возврат Неопределено;
		КонецЕсли;
		
	Иначе		
		Возврат Неопределено;		
	КонецЕсли;	
	
КонецФункции

Функция гвПолучитьДанныеНоменклатураПриИзменении(СтруктураДанные)
	
	СтруктураДанные.Вставить("ЕдиницаИзмерения", СтруктураДанные.Номенклатура.ЕдиницаИзмерения);
	
	Если СтруктураДанные.Свойство("Характеристика") Тогда
		СтруктураДанные.Вставить("Спецификация", УправлениеНебольшойФирмойСервер.ПолучитьПоУмолчаниюСпецификацию(СтруктураДанные.Номенклатура, СтруктураДанные.Характеристика));
	Иначе
		СтруктураДанные.Вставить("Спецификация", УправлениеНебольшойФирмойСервер.ПолучитьПоУмолчаниюСпецификацию(СтруктураДанные.Номенклатура));
	КонецЕсли;
	
	СтруктураДанные.Вставить("ТипНоменклатуры", СтруктураДанные.Номенклатура.ТипНоменклатуры);
	
	Возврат СтруктураДанные;
	
КонецФункции // ПолучитьДанныеНоменклатураПриИзменении()

Функция гвСоздатьПеремещениеЗапасовНаСервере(АдресРасшифровкиВХранилище, Расшифровка, ДанныеФормы) Экспорт
	
	ДанныеРасшифровкиОбъект = ПолучитьИзВременногоХранилища(АдресРасшифровкиВХранилище);
	лПолеРасшифровки = ДанныеРасшифровкиОбъект.Элементы[Расшифровка].ПолучитьПоля()[0];
	лСерийныйНомер = лПолеРасшифровки.Значение;
	
	Если ЗначениеЗаполнено(лСерийныйНомер) И ТипЗнч(лСерийныйНомер) = Тип("СправочникСсылка.СерийныеНомера") Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	СерийныеНомераОстатки.СтруктурнаяЕдиница 
		|ИЗ
		|	РегистрНакопления.СерийныеНомера.Остатки(, СерийныйНомер = &СерийныйНомер) КАК СерийныеНомераОстатки";
		
		Запрос.УстановитьПараметр("СерийныйНомер", лСерийныйНомер);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		Выборка = РезультатЗапроса.Выбрать();
		
		Если Выборка.Следующий() Тогда
			
			ДанныеФормы.СтруктурнаяЕдиница = Выборка.СтруктурнаяЕдиница;
			
			лСтрокаЗапасы = ДанныеФормы.Запасы.Добавить(); 	
			лСтрокаЗапасы.Номенклатура = лСерийныйНомер.Владелец;
			
			СтруктураДанные = Новый Структура();
			СтруктураДанные.Вставить("Номенклатура", лСтрокаЗапасы.Номенклатура);
			СтруктураДанные.Вставить("ЕдиницаИзмерения", СтруктураДанные.Номенклатура.ЕдиницаИзмерения);
			СтруктураДанные.Вставить("СтранаПроисхождения", СтруктураДанные.Номенклатура.СтранаПроисхождения);
			
			лСтрокаЗапасы.ЕдиницаИзмерения = СтруктураДанные.ЕдиницаИзмерения;
			лСтрокаЗапасы.СтранаПроисхождения = СтруктураДанные.СтранаПроисхождения;
			лСтрокаЗапасы.НомерГТД = Неопределено;
			лСтрокаЗапасы.Количество = 1;
			
			РаботаССерийнымиНомерамиКлиентСервер.ДобавитьСерийныйНомерВСтроку(лСтрокаЗапасы, лСерийныйНомер, ДанныеФормы);
						
			Возврат ДанныеФормы;
			
		Иначе	
			Возврат Неопределено;
		КонецЕсли;
		
	Иначе
		
		Возврат Неопределено;
		
	КонецЕсли;	
	
КонецФункции	

Функция гвСерийныйНомерЗарезервирован(СерийныйНомер)
		
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	гвРезервированиеТоваровПоСерийныхНомерахОстатки.СерийныйНомер
	|ИЗ
	|	РегистрНакопления.гвРезервированиеТоваровПоСерийныхНомерах.Остатки(, СерийныйНомер = &СерийныйНомер) КАК гвРезервированиеТоваровПоСерийныхНомерахОстатки
	|ГДЕ
	|	гвРезервированиеТоваровПоСерийныхНомерахОстатки.КоличествоОстаток > 0";
	
	Запрос.УстановитьПараметр("СерийныйНомер", СерийныйНомер);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат НЕ РезультатЗапроса.Пустой();		
	
КонецФункции	
// ...Гунько 2021-01-18
