////////////////////////////////////////////////////////////////////////////////
// Подсистема "Банки".
//
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныйПрограммныйИнтерфейс

// Вызывается после запуска конфигурации, подключает обработчик ожидания.
//
Процедура ПослеНачалаРаботыСистемы() Экспорт
	
	ПараметрыКлиента = СтандартныеПодсистемыКлиентПовтИсп.ПараметрыРаботыКлиентаПриЗапуске();
	Если ПараметрыКлиента.Свойство("Банки") И ПараметрыКлиента.Банки.ВыводитьОповещениеОНеактуальности Тогда
		ПодключитьОбработчикОжидания("РаботаСБанкамиВывестиОповещениеОНеактуальности", 45, Истина);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

////////////////////////////////////////////////////////////////////////////////
// Обновление классификатора банков.

// Выводит соответствующее оповещение.
//
Процедура ОповеститьКлассификаторУстарел() Экспорт
	
	ПоказатьОповещениеПользователя(
		НСтр("ru='Классификатор банков устарел';uk='Класифікатор банків застарів'"),
		НавигационнаяСсылкаФормыЗагрузки(),
		НСтр("ru='Обновить классификатор банков';uk='Оновити класифікатор банків'"),
		БиблиотекаКартинок.Предупреждение32);
	
КонецПроцедуры

// Выводит соответствующее оповещение.
//
Процедура ОповеститьКлассификаторУспешноОбновлен() Экспорт
	
	ПоказатьОповещениеПользователя(
		НСтр("ru='Классификатор банков успешно обновлен';uk='Класифікатор банків успішно оновлений'"),
		НавигационнаяСсылкаФормыЗагрузки(),
		НСтр("ru='Классификатор банков обновлен';uk='Класифікатор банків оновлений'"),
		БиблиотекаКартинок.Информация32);
	
КонецПроцедуры

// Выводит соответствующее оповещение.
//
Процедура ОповеститьКлассификаторАктуален() Экспорт
	
	ПоказатьПредупреждение(,НСтр("ru='Классификатор банков актуален.';uk='Класифікатор банків актуальний.'"));
	
КонецПроцедуры

// Возвращает навигационную ссылку для оповещений.
//
Функция НавигационнаяСсылкаФормыЗагрузки()
	Возврат "e1cib/data/Справочник.КлассификаторБанковРФ.Форма.ЗагрузкаКлассификатора";
КонецФункции

#КонецОбласти
