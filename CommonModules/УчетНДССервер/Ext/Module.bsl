////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБРАБОТКИ ОШИБОК ОКРУГЛЕНИЯ НДС

// Выполняет округление числовых значений с накоплением погрешностей округления, образовавшихся
//		в результате предыдущих вызовов функции
//
// Параметры
//  Число 		– Число. Округляемое значение
//  Точность	– Число. Точность округления
//	Погрешность	- Число. Переменная, в которой накапливается погрешность с предыдущих вызовов
//
// Возвращаемое значение:
//   Число   – округленное значение
//
Функция ОкруглитьСУчетомПогрешности(Число, Точность, Погрешность = 0, 
	               СоответствиеПогрешностей = Неопределено, Ключ = Неопределено) Экспорт

	Если НЕ СоответствиеПогрешностей = Неопределено  И ЗначениеЗаполнено(Ключ) Тогда
	
		// считываем погрешность округления, накопленную ранее при расчетах
		Погрешность = СоответствиеПогрешностей[Ключ];
		// погрешности округления еще нет -- первая сумма
		Если Погрешность = Неопределено Тогда
			Погрешность = 0;
		КонецЕсли;
		// округлим с учетом погрешности
		Округленное = ОкруглитьСУчетомПогрешности(Число, Точность, Погрешность);
		// сохраним погрешность округления
		СоответствиеПогрешностей.Вставить(Ключ, Погрешность);
	
	Иначе
		
		Если Число = 0 Тогда
			Возврат 0;
		КонецЕсли; 
	
		// выравнивание разрядности
		Число = Окр(Число, 27, ?(Число<0, РежимОкругления.Окр15как10, РежимОкругления.Окр15как20));
		
		// сумма с учетом погрешности предыдущих вычислений
		Округляемое = Число + Погрешность;

		// для отрицательного числа меняем направление округления, чтобы избежать ошибки Окр(-0.5) = -1
		Округленное	= Окр(Округляемое, Точность, ?(Округляемое<0, РежимОкругления.Окр15как10, РежимОкругления.Окр15как20));
		
		// рассчитаем новую погрешность округления
		Погрешность	= Округляемое - Округленное;
		
	КонецЕсли;
	
	Возврат Округленное;

КонецФункции // ОкруглитьСУчетомПогрешности()


// Рассчитывает сумму НДС исходя из суммы и флагов налогообложения
//
// Параметры: 
//  Сумма            - число, сумма от которой надо рассчитывать налоги, 
//  НДСВключатьВСтоимость  - булево, признак учета НДС в сумме, 
//  СуммаВключаетНДС - булево, признак включения НДС в сумму ("внутри" или "сверху"),
//  СтавкаНДС        - число , процентная ставка НДС,
//
// Возвращаемое значение:
//  Число, полученная сумма НДС
//
Функция РассчитатьСуммуНДСсУчетомПогрешности(Сумма, УчитыватьНДС, СуммаВключаетНДС, СправочникСтавкаНДС,
	               СоответствиеПогрешностей = Неопределено) Экспорт

	СтавкаНДС = УправлениеНебольшойФирмойПовтИсп.ПолучитьЗначениеСтавкиНДС(СправочникСтавкаНДС);
				   
	Если УчитыватьНДС Тогда 
		Если СуммаВключаетНДС Тогда
			СуммаНДС = ОкруглитьСУчетомПогрешности(Сумма * СтавкаНДС / (100 + СтавкаНДС), 2, , СоответствиеПогрешностей, СправочникСтавкаНДС);
		Иначе
			СуммаНДС = ОкруглитьСУчетомПогрешности(Сумма * СтавкаНДС / 100              , 2, , СоответствиеПогрешностей, СправочникСтавкаНДС);
		КонецЕсли;
	Иначе
		СуммаНДС = 0;
	КонецЕсли;

	Возврат СуммаНДС;

КонецФункции // РассчитатьСуммуНДССУчетомПогрешности()

// Процедура корректирует построчные ошибки округления НДС так, 
// чтобы итоговый НДС по ставке совпадал с расчитанным НДС по базе
//
// Параметры:
//  ТабличнаяЧасть       - табличная часть, для которой следует пересчитать НДС
//  ДокументОбъект       - объект редактируемого документа.
//
Процедура ПересчитатьНДСсУчетомПогрешностиОкругления(ТабличнаяЧасть, ДокументОбъект, ПогрешностиОкругления, ПредставлениеТабличнойЧасти = "", ИмяРеквизитаСумма    = "Сумма", ИмяРеквизитаСуммаНДС = "СуммаНДС", ИмяРеквизитаСуммаВсего = "Всего", ДополнениеКТексту ="") Экспорт
	
	Перем СуммаНДСДоОкругления;    // для проверки наличия изменения в сумме НДС по документу
    Перем СуммаНДСПослеОкругления; // для проверки наличия изменения в сумме НДС по документу
	
	УчитыватьНДС = ДокументОбъект.НалогообложениеНДС = Перечисления.ТипыНалогообложенияНДС.ОблагаетсяНДС;
	
	// если НДС не учитывается, то и пересчитывать нечего
	Если Не УчитыватьНДС Тогда
		Возврат
	КонецЕсли;

	// Запомним сумма и сумму НДС до округления
	СуммаНДСДоОкругления = ТабличнаяЧасть.Итог(ИмяРеквизитаСуммаНДС);
	СуммаВсегоДоОкругления    = ТабличнаяЧасть.Итог(ИмяРеквизитаСуммаВсего);
	
	Для Каждого СтрокаТаблицы Из ТабличнаяЧасть Цикл
		
		СтрокаТаблицы[ИмяРеквизитаСуммаНДС] = РассчитатьСуммуНДСсУчетомПогрешности(СтрокаТаблицы[ИмяРеквизитаСумма],
	                                                   УчитыватьНДС,
	                                                   ДокументОбъект.СуммаВключаетНДС,
	                                                   СтрокаТаблицы.СтавкаНДС,
	                                                   ПогрешностиОкругления);
		СтрокаТаблицы[ИмяРеквизитаСуммаВсего] = СтрокаТаблицы[ИмяРеквизитаСумма] + ?(ДокументОбъект.СуммаВключаетНДС, 0, СтрокаТаблицы[ИмяРеквизитаСуммаНДС]);		
	КонецЦикла;
	
	// Рассчитаем сумму НДС после устранения ошибок округления
	СуммаНДСПослеОкругления = ТабличнаяЧасть.Итог(ИмяРеквизитаСуммаНДС);
	СуммаВсегоПослеОкругления    = ТабличнаяЧасть.Итог(ИмяРеквизитаСуммаВсего);

	// Сравним суммы до и после округления
	Если СуммаНДСПослеОкругления <> СуммаНДСДоОкругления Тогда
		// Если суммы отличаются, сообщим об этом пользователю
		ТекстСообщения = "После исправления погрешностей округления сумма НДС"+ДополнениеКТексту+" табличной части " + ПредставлениеТабличнойЧасти + " изменилась (было "
		+ УправлениеНебольшойФирмойСервер.ФорматСумм(СуммаНДСДоОкругления, ДокументОбъект.ВалютаДокумента) + ", стало " + УправлениеНебольшойФирмойСервер.ФорматСумм(СуммаНДСПослеОкругления, ДокументОбъект.ВалютаДокумента) + ")";
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		
		// изменилась общая сумма документа - сообщим об этом пользователю 
		Если (УчитыватьНДС)
			И (Не ДокументОбъект.СуммаВключаетНДС) Тогда
			ТекстСообщения = "После исправления погрешностей округления общая сумма"+ДополнениеКТексту+" табличной части " + ПредставлениеТабличнойЧасти + " изменилась (было "
			+ УправлениеНебольшойФирмойСервер.ФорматСумм(СуммаВсегоДоОкругления, ДокументОбъект.ВалютаДокумента) + ", стало " + УправлениеНебольшойФирмойСервер.ФорматСумм(СуммаВсегоПослеОкругления, ДокументОбъект.ВалютаДокумента) + ")";
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
			
		КонецЕсли;	
	КонецЕсли;

КонецПроцедуры // ПересчитатьНДСсУчетомПогрешностиОкругления

Функция ВДокументеЕстьСтавкаНДС7(Документ) Экспорт
                                                       
	Отбор = Новый Структура("СтавкаНДС", УправлениеНебольшойФирмойПовтИсп.ПолучитьСтавкуНДС7());
	
	Если    (Документ.Запасы.НайтиСтроки(Отбор).Количество() > 0)
		ИЛИ (Документ.ОС.НайтиСтроки(Отбор).Количество() > 0)
		Тогда
		
		Возврат Истина;
		
	Иначе
		
		Возврат Ложь;
		
	КонецЕсли;

КонецФункции

Функция ВДокументеЕстьСтавкаНДС7ИДругиеСтавки(Документ, ВыдаватьСообщенение = Истина) Экспорт
                                                       
	Отбор = Новый Структура("СтавкаНДС", УправлениеНебольшойФирмойПовтИсп.ПолучитьСтавкуНДС7());
	
	Если    (Документ.Запасы.НайтиСтроки(Отбор).Количество() > 0 И Документ.Запасы.НайтиСтроки(Отбор).Количество() <> Документ.Запасы.Количество())
		ИЛИ (Документ.ОС.НайтиСтроки(Отбор).Количество() > 0 И Документ.ОС.НайтиСтроки(Отбор).Количество() <> Документ.ОС.Количество())
		Тогда
		
			Если ВыдаватьСообщенение = Истина Тогда
			
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Форма налоговой накладной, утвержденная 14.01.2014 приказом Миндоходов № 10, не предусматривает наличие ставки НДС  7%. "
"В данной налоговой накладной присутствует номенклатура, которая облагается как по ставке 7% так и по другим ставкам."
"Согласно рекомендации Миндоходов, изложенной в письме  от 04.04.2014  № 7860/7/99-99-19-04-02-17, на номенклатуру, облагаемую НДС по ставке 7%, необходимо выписывать отдельную налоговую накладную!';uk='Форма податкової накладної, затверджена 14.01.2014 наказом Міндоходів № 10, не передбачає наявність ставки ПДВ  7%. "
"У даній податковій накладній присутня номенклатура, яка обкладається як по ставці 7% так і по інших ставках."
"Згідно з рекомендацією Міндоходів, викладеної в листі  від 04.04.2014  № 7860/7/99-99-19-04-02-17, на номенклатуру, оподатковувану ПДВ по ставці 7%, необхідно виписувати окрему податкову накладну!'"));
				
			КонецЕсли;
		
		Возврат Истина;
		
	Иначе
		
		Возврат Ложь;
		
	КонецЕсли;

КонецФункции

Функция ДатаВступленияВСилуПриказа1307() Экспорт
	Возврат '2016-04-01';
КонецФункции // ДатаВступленияВСилуПриказа1307()

Функция КодЕдиницыИзмеренияДляПечатиНалоговых(Код) Экспорт

	Код = СтрЗаменить(Строка(Код), " ", "");
	
	Если НЕ СтрДлина(Код) = 4 Тогда
	
		Возврат "";	
		
	Иначе
		
		Для НомСимв = 1 По 4 Цикл
			КодСимвола = КодСимвола(Код, НомСимв);
			Если НЕ (КодСимвола >= 48 И КодСимвола <= 57) Тогда
				Возврат "";
			КонецЕсли; 
		КонецЦикла; 		
		
		Возврат Код;
		
	КонецЕсли;	

КонецФункции
