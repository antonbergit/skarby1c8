////////////////////////////////////////////////////////////////////////////////
// Подсистема "Работа с контрагентами".
// Процедуры и функции проверки корректности заполнения регламентированных данных.
//  
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Проверяет соответствие ИНН требованиям.
//
// Параметры:
//  ИНН                - Строка - Проверяемый индивидуальный номер налогоплательщика.
//  ЭтоЮридическоеЛицо - Булево - признак, является ли владелец ИНН юридическим лицом.
//  ТекстСообщения     - Строка - Текст сообщения о найденных ошибках.
//
// Возвращаемое значение:
//  Истина       - ИНН соответствует требованиям;
//  Ложь         - ИНН не соответствует требованиям.
//
Функция ИННСоответствуетТребованиям(Знач ИНН, ЭтоЮридическоеЛицо, ТекстСообщения = "") Экспорт

	СоответствуетТребованиям = Истина;
	ТекстСообщения = "";

	ИНН      = СокрЛП(ИНН);
	ДлинаИНН = СтрДлина(ИНН);

	Если ЭтоЮридическоеЛицо = Неопределено Тогда
		ТекстСообщения = ТекстСообщения + НСтр("ru='Не определен тип владельца ИНН.';uk='Не визначений тип власника ІПН.'");
		Возврат Ложь;
	КонецЕсли;
	
	Если НЕ СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(ИНН) Тогда
		СоответствуетТребованиям = Ложь;
		ТекстСообщения = ТекстСообщения + НСтр("ru='ИНН должен состоять только из цифр.';uk='ІПН повинен полягати тільки із цифр.'");
	КонецЕсли;

	Если  ЭтоЮридическоеЛицо И ДлинаИНН <> 12 Тогда
		СоответствуетТребованиям = Ложь;
		ТекстСообщения = ТекстСообщения + ?(ЗначениеЗаполнено(ТекстСообщения), Символы.ПС, "")
			+ НСтр("ru='ИНН юридического лица должен состоять из 12 цифр.';uk='ІПН юридичної особи повинен складатися з 12 цифр.'");
	ИначеЕсли НЕ ЭтоЮридическоеЛицо И ДлинаИНН <> 10 Тогда
		СоответствуетТребованиям = Ложь;
		ТекстСообщения = ТекстСообщения + ?(ЗначениеЗаполнено(ТекстСообщения), Символы.ПС, "")
			+ НСтр("ru='ИНН физического лица должен состоять из 10 цифр.';uk='ІПН фізичної особи повинен складатися з 10 цифр.'");
	КонецЕсли;

	Возврат СоответствуетТребованиям;

КонецФункции 

// Проверяет соответствие кода ЕДРПОУ требованиям стандартов.
//
// Параметры:
//  КодПоЕДРПОУ            - Строка - проверяемый код ЕДРПОУ.
//  ЭтоЮридическоеЛицо     - Булево - признак, является ли владелец кода ЕДРПОУ юридическим лицом.
//  ТекстСообщения         - Строка - текст сообщения о найденных ошибках.
//
// Возвращаемое значение:
//  Истина       - КодПоЕДРПОУ соответствует требованиям;
//  Ложь         - КодПоЕДРПОУ не соответствует требованиям.
//
Функция ЕДРПОУСоответствуетТребованиям(Знач КодПоЕДРПОУ, ЭтоЮридическоеЛицо, ТекстСообщения = "") Экспорт
	
	СоответствуетТребованиям = Истина;
	ТекстСообщения = "";

	КодПоЕДРПОУ       = СокрЛП(КодПоЕДРПОУ);
	ДлинаКодаПоЕДРПОУ = СтрДлина(КодПоЕДРПОУ);

	Если ЭтоЮридическоеЛицо = Неопределено Тогда
		ТекстСообщения = ТекстСообщения + НСтр("ru='Не определен тип владельца кода по ЕДРПОУ.';uk='Не визначений тип власника коду по ЄДРПОУ.'");
		Возврат Ложь;
	КонецЕсли;
	
	Если НЕ СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(КодПоЕДРПОУ) Тогда
		СоответствуетТребованиям = Ложь;
		ТекстСообщения = ТекстСообщения + НСтр("ru='Код по ЕДРПОУ должен состоять только из цифр.';uk='Код по ЄДРПОУ повинен полягати тільки із цифр.'");
	КонецЕсли;

	Если ДлинаКодаПоЕДРПОУ < 8 Тогда
		СоответствуетТребованиям = Ложь;
		ТекстСообщения = ТекстСообщения + ?(ЗначениеЗаполнено(ТекстСообщения), Символы.ПС, "")
		+ НСтр("ru='Код по ЕДРПОУ должен содержать не менее 8 цифр.';uk='Код по ЄДРПОУ повинен містити не менш 8 цифр.'");
	КонецЕсли;
	
	Возврат СоответствуетТребованиям;
	
КонецФункции 
	
#КонецОбласти
