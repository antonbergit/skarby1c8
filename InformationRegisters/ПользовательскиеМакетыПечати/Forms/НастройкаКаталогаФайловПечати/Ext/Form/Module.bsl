
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	КаталогДляСохраненияДанныхПечати = УправлениеПечатью.ПолучитьЛокальныйКаталогФайловПечати();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПутьККаталогуНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Если ПодключитьРасширениеРаботыСФайлами() Тогда
		ДиалогОткрытияФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога);
		ДиалогОткрытияФайла.ПолноеИмяФайла = "";
		ДиалогОткрытияФайла.Каталог = КаталогДляСохраненияДанныхПечати;
		ДиалогОткрытияФайла.МножественныйВыбор = Ложь;
		ДиалогОткрытияФайла.Заголовок = НСтр("ru='Выберите путь к каталогу для файлов печати.';uk='Виберіть шлях до каталогу для файлів друку.'");
		
		ДиалогОткрытияФайла.Показать(Новый ОписаниеОповещения("ПутьККаталогуНачалоВыбораЗавершение", ЭтотОбъект, Новый Структура("ДиалогОткрытияФайла", ДиалогОткрытияФайла)));
	Иначе
		ПоказатьПредупреждение(, НСтр("ru='Для выбора каталога необходимо установить расширение для работы с файлами в Веб-клиенте.';uk='Для вибору каталогу необхідно встановити розширення для роботи з файлами у Веб-клієнтові.'"));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПутьККаталогуНачалоВыбораЗавершение(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
    
    ДиалогОткрытияФайла = ДополнительныеПараметры.ДиалогОткрытияФайла;
    
    Если (ВыбранныеФайлы <> Неопределено) Тогда
        КаталогДляСохраненияДанныхПечати = ДиалогОткрытияФайла.Каталог + "\";
    КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	УправлениеПечатьюВызовСервера.СохранитьЛокальныйКаталогФайловПечати(КаталогДляСохраненияДанныхПечати);
	ОповеститьОВыборе(КаталогДляСохраненияДанныхПечати);
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	КаталогДляСохраненияДанныхПечати = "";
	ОповеститьОВыборе(КаталогДляСохраненияДанныхПечати);
	
КонецПроцедуры

#КонецОбласти
