#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

////////////////////////////////////////////////////////////////////////////////
// Групповое изменение объектов.

// Возвращает реквизиты объекта, которые разрешается редактировать
// с помощью обработки группового изменения реквизитов.
//
// Возвращаемое значение:
//  Массив - список имен реквизитов объекта.
Функция РеквизитыРедактируемыеВГрупповойОбработке() Экспорт
	
	Результат = Новый Массив;
	Результат.Добавить("СпособУстановкиКурса");
	Результат.Добавить("Наценка");
	Результат.Добавить("ОсновнаяВалюта");
	Результат.Добавить("ФормулаРасчетаКурса");
	Возврат Результат;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Загрузка данных из файла

// Запрещает загрузку данных в этот справочник из подсистемы "ЗагрузкаДанныхИзФайла".
// Т.к. справочник реализует свой метод обновления данных.
//
Функция ИспользоватьЗагрузкуДанныхИзФайла() Экспорт
	Возврат Ложь;
КонецФункции

// УНФ
Процедура ЗагрузитьКурсыВалют(ПараметрыЗагрузки = Неопределено, Адрес = Неопределено) Экспорт
	
	Если ТипЗнч(ПараметрыЗагрузки) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ПараметрыЗагрузки.Свойство("Валюты") Тогда
		Возврат;
	КонецЕсли;
	
	РаботаСКурсамиВалют.ЗагрузитьКурсыВалютПоПараметрам(
	ПараметрыЗагрузки.Валюты,
	ПараметрыЗагрузки.НачалоПериодаЗагрузки,
	ПараметрыЗагрузки.ОкончаниеПериодаЗагрузки);
	
КонецПроцедуры

// Возвращает объект по коду. Если объект не найден в справочнике, создаёт новый объет и заполняет его из классификатора.
// Сам объект в базу при этом не записывается.
//
// Параметры:
//   КодВалюты - Строка - код валюты.
//
// Возвращаемое значение:
//   СправочникОбъект.Валюты - объект справочника валюты.
//
Функция ОбъектПоКоду(Знач КодВалюты) Экспорт
	
	ВалютаСсылка = Справочники.Валюты.НайтиПоКоду(КодВалюты);
	Если ЗначениеЗаполнено(ВалютаСсылка) Тогда
		Возврат ВалютаСсылка.ПолучитьОбъект();
	КонецЕсли;
	
	Результат = Справочники.Валюты.СоздатьЭлемент();
	
	КлассификаторXML = Справочники.Валюты.ПолучитьМакет("КлассификаторВалют").ПолучитьТекст();
	
	КлассификаторТаблица = ОбщегоНазначения.ПрочитатьXMLВТаблицу(КлассификаторXML).Данные;
	
	ЗаписьОКВ = КлассификаторТаблица.Найти(КодВалюты, "Code"); 
	Если ЗаписьОКВ = Неопределено Тогда
		Возврат Результат;
	КонецЕсли;
	
	Результат.Код         			  = ЗаписьОКВ.Code;
	Результат.Наименование        	  = ЗаписьОКВ.CodeSymbol;
	Результат.НаименованиеПолное        = ЗаписьОКВ.Name;
	Если ЗаписьОКВ.RBCLoading Тогда
		Результат.СпособУстановкиКурса = Перечисления.СпособыУстановкиКурсаВалюты.ЗагрузкаИзИнтернета;
	Иначе
		Результат.СпособУстановкиКурса = Перечисления.СпособыУстановкиКурсаВалюты.РучнойВвод;
	КонецЕсли;
	Результат.ПараметрыПрописиНаРусском = ЗаписьОКВ.RusNumerationItemOptions;
	Результат.ПараметрыПрописиНаУкраинском = ЗаписьОКВ.UkrNumerationItemOptions;
	
	Возврат Результат;
	
КонецФункции

// Конец УНФ

#КонецОбласти

#КонецЕсли