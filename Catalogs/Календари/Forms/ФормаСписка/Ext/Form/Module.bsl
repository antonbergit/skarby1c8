
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Если Параметры.РежимВыбора Тогда
		Элементы.Список.РежимВыбора = Истина;
	КонецЕсли;
	
	УстановитьПараметрыСписка();
		
	ЗаполнитьУсловноеОформлениеСписка();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ИзменитьВыделенные(Команда)
	МодульГрупповоеИзменениеОбъектовКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("ГрупповоеИзменениеОбъектовКлиент");
	МодульГрупповоеИзменениеОбъектовКлиент.ИзменитьВыделенные(Элементы.Список);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьПараметрыСписка()
	
	Список.Параметры.УстановитьЗначениеПараметра("ТекущаяДата", ТекущаяДатаСеанса());
	
	Список.Параметры.УстановитьЗначениеПараметра("КалендарьНеЗаполнен", НСтр("ru='Производственный календарь не заполнен';uk='Виробничий календар не заповнений'"));
	Список.Параметры.УстановитьЗначениеПараметра("КалендарьНеЗаполненНаГод", НСтр("ru='Производственный календарь не заполнен на очередной календарный год';uk='Виробничий календар не заповнений на черговий календарний рік'"));
	Список.Параметры.УстановитьЗначениеПараметра("ГрафикНеЗаполнен", НСтр("ru='График не был заполнен на очередной календарный год';uk='Графік не був заповнений на черговий календарний рік'"));
	Список.Параметры.УстановитьЗначениеПараметра("ПериодГрафикаОграничен", НСтр("ru='Период заполнения графика ограничен (см. поле «Дата окончания»)';uk='Період заповнення графіку обмежений (див. поле ""Дата закінчення"")'"));
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		Список, "ВладелецГрафика", , ВидСравненияКомпоновкиДанных.НеЗаполнено, , ,
		РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Обычный);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьУсловноеОформлениеСписка()
	
	НовыйЭлемент = Список.УсловноеОформление.Элементы.Добавить();
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(НовыйЭлемент.Отбор, "ТребуетЗаполнения", Истина);
	
	Оформление = НовыйЭлемент.Оформление.Элементы.Найти("ЦветТекста");
	Оформление.Значение = ЦветаСтиля.ПросроченныеДанныеЦвет;
	Оформление.Использование = Истина;
	
КонецПроцедуры

#КонецОбласти
