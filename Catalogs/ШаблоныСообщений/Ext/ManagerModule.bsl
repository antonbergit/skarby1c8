#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Формирует массив метаданных объектов, для которых определены наборы данных, используемые в шаблонах сообщений.
// Для указанных объектов должен быть определен макет СКД с именем "СКД_ДанныеШаблонаСообщений",
// принимающий параметр - ссылку с типом соответствующего объекта
//
// Возвращаемое значение:
//   Массив   - сформированный массив
//
Функция ОбъектыМетаданныхПоставляющиеПараметрыШаблонов() Экспорт
	
	Результат = Новый Массив;
	
	Результат.Добавить(Метаданные.Документы.ЗаказПокупателя);
	Результат.Добавить(Метаданные.Документы.ЗаказПоставщику);
	Результат.Добавить(Метаданные.Документы.ЗаказНаПроизводство);
	Результат.Добавить(Метаданные.Документы.Событие);
	Результат.Добавить(Метаданные.Документы.ЗаданиеНаРаботу);
	Результат.Добавить(Метаданные.Документы.ПриемИПередачаВРемонт);
	Результат.Добавить(Метаданные.Документы.СчетНаОплату);
	
	Возврат Результат;
	
КонецФункции

Функция СформироватьПоляСообщения(Шаблон, Основание) Экспорт
	
	Результат = Новый Структура;
	
	РеквизитыШаблона = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
		Шаблон,
		"КартинкиТекстаHTML, ПолноеИмяОснования, ВидОснования, СпособОтправки, ТекстШаблона, ТекстШаблонаHTML, ТемаШаблона"
	);
	
	Если ЗначениеЗаполнено(Основание) И Основание.Метаданные().ПолноеИмя() <> РеквизитыШаблона.ПолноеИмяОснования Тогда
		ВызватьИсключение НСтр("ru='Шаблон предназначен для другого типа оснований';uk='Шаблон призначений для іншого типу підстав'");
	КонецЕсли;
	
	Если ПустаяСтрока(РеквизитыШаблона.ВидОснования) Тогда
		СКД = ПолучитьСхемуДанныхШаблонаСообщения(Основание.Метаданные());
	Иначе
		СКД = ПолучитьСхемуДанныхШаблонаСообщения(Основание.Метаданные(), "СКД_ДанныеШаблонаСообщенийЗаказНаряд");
	КонецЕсли;
	
	ЗначенияПараметров = Новый Соответствие;
	Если РеквизитыШаблона.СпособОтправки = Перечисления.ВидыКаналовСвязи.Email Тогда
		
		Результат.Вставить("Тема",		РеквизитыШаблона.ТемаШаблона);
		Результат.Вставить("Текст",		РеквизитыШаблона.ТекстШаблона);
		Результат.Вставить("ТекстHTML",	РеквизитыШаблона.ТекстШаблонаHTML);
		Результат.Вставить("КартинкиТекстаHTML", РеквизитыШаблона.КартинкиТекстаHTML.Получить());
		
		ОбщегоНазначенияКлиентСервер.ДополнитьСоответствие(
			ЗначенияПараметров,
			ШаблоныСообщенийКлиентСервер.ОпределитьПараметрыСообщения(РеквизитыШаблона.ТемаШаблона),
			Истина
		);
		ОбщегоНазначенияКлиентСервер.ДополнитьСоответствие(
			ЗначенияПараметров,
			ШаблоныСообщенийКлиентСервер.ОпределитьПараметрыСообщения(РеквизитыШаблона.ТекстШаблона),
			Истина
		);
		ОбщегоНазначенияКлиентСервер.ДополнитьСоответствие(
			ЗначенияПараметров,
			ШаблоныСообщенийКлиентСервер.ОпределитьПараметрыСообщения(РеквизитыШаблона.ТекстШаблонаHTML),
			Истина
		);
	ИначеЕсли РеквизитыШаблона.СпособОтправки = Перечисления.ВидыКаналовСвязи.SMS Тогда
		
		Результат.Вставить("Текст",		РеквизитыШаблона.ТекстШаблона);
		
		ОбщегоНазначенияКлиентСервер.ДополнитьСоответствие(
			ЗначенияПараметров,
			ШаблоныСообщенийКлиентСервер.ОпределитьПараметрыСообщения(РеквизитыШаблона.ТекстШаблона),
			Истина
		);
	КонецЕсли;
	
	Компоновщик = Новый КомпоновщикНастроекКомпоновкиДанных();
	Компоновщик.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(СКД));
	Компоновщик.ЗагрузитьНастройки(СКД.НастройкиПоУмолчанию);
	Компоновщик.Настройки.ПараметрыДанных.УстановитьЗначениеПараметра("Основание", Основание);
	
	Компоновщик.Настройки.Выбор.Элементы.Очистить();
	Для Каждого КлючИЗначение Из ЗначенияПараметров Цикл
		
		ПолеКД = Новый ПолеКомпоновкиДанных(КлючИЗначение.Ключ);
		Если Компоновщик.Настройки.ДоступныеПоляВыбора.НайтиПоле(ПолеКД) = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ВыбранноеПолеКД = Компоновщик.Настройки.Выбор.Элементы.Добавить(Тип("ВыбранноеПолеКомпоновкиДанных"));
		ВыбранноеПолеКД.Поле = ПолеКД;
		
	КонецЦикла;
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных();
	МакетКомпоновкиДанных = КомпоновщикМакета.Выполнить(СКД, Компоновщик.ПолучитьНастройки(),,,Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"));
	
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных();
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновкиДанных);
	
	РезультатСКД = Новый ТаблицаЗначений;
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;
	ПроцессорВывода.УстановитьОбъект(РезультатСКД);
	ПроцессорВывода.Вывести(ПроцессорКомпоновки);
	
	Если РезультатСКД.Количество() = 1 Тогда
		ДанныеПараметров = РезультатСКД[0];
		Для Каждого Колонка из РезультатСКД.Колонки Цикл
			Если РеквизитыШаблона.СпособОтправки = Перечисления.ВидыКаналовСвязи.Email Тогда
				Результат.Тема		= СтрЗаменить(Результат.Тема, "[" + Колонка.Имя + "]", ДанныеПараметров[Колонка.Имя]);
				Результат.Текст		= СтрЗаменить(Результат.Текст, "[" + Колонка.Имя + "]", ДанныеПараметров[Колонка.Имя]);
				Результат.ТекстHTML	= СтрЗаменить(Результат.ТекстHTML, "[" + Колонка.Имя + "]", ДанныеПараметров[Колонка.Имя]);
			ИначеЕсли РеквизитыШаблона.СпособОтправки = Перечисления.ВидыКаналовСвязи.SMS Тогда
				Результат.Текст		= СтрЗаменить(Результат.Текст, "[" + Колонка.Имя + "]", ДанныеПараметров[Колонка.Имя]);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ПолучитьСхемуДанныхШаблонаСообщения(ОписаниеОбъектаМетаданных, ИмяМакетаШаблона = "СКД_ДанныеШаблонаСообщений") Экспорт
	
	ТипОписанияОбъектаМетаданных = ТипЗнч(ОписаниеОбъектаМетаданных);
	
	Если ТипОписанияОбъектаМетаданных = Тип("Тип") Тогда
		МетаданныеОбъекта = Метаданные.НайтиПоТипу(ОписаниеОбъектаМетаданных);
	ИначеЕсли ТипОписанияОбъектаМетаданных = Тип("Строка") Тогда
		МетаданныеОбъекта = Метаданные.НайтиПоПолномуИмени(ОписаниеОбъектаМетаданных);
	Иначе
		МетаданныеОбъекта = ОписаниеОбъектаМетаданных;
	КонецЕсли;
	
	Если МетаданныеОбъекта = Неопределено Тогда
		ВызватьИсключение НСтр("ru='Неизвестный объект метаданных для шаблона сообщения';uk='Невідомий об`єкт метаданих для шаблону повідомлення'");
	КонецЕсли;
	
	Если МетаданныеОбъекта.Макеты.Найти(ИмяМакетаШаблона) = Неопределено Тогда
		ВызватьИсключение НСтр("ru='Не найдена схема компоновки данных для шаблона сообщения';uk='Не знайдена схема компонування даних для шаблону повідомлення'");
	КонецЕсли;
	
	Возврат ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(МетаданныеОбъекта.ПолноеИмя()).ПолучитьМакет(ИмяМакетаШаблона);
	
КонецФункции

#КонецОбласти

#КонецЕсли