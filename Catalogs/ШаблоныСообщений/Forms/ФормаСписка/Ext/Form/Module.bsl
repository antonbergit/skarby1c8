
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Если Параметры.Отбор.Свойство("СпособОтправки") Тогда
		
		АвтоЗаголовок = Ложь;
		Заголовок = Метаданные.Справочники.ШаблоныСообщений.Представление() + " "  + Строка(Параметры.Отбор.СпособОтправки);
		
		Элементы.ФормаСоздатьШаблонEmail.Видимость	= Параметры.Отбор.СпособОтправки = Перечисления.ВидыКаналовСвязи.Email;
		Элементы.ФормаСоздатьШаблонSMS.Видимость	= Параметры.Отбор.СпособОтправки = Перечисления.ВидыКаналовСвязи.SMS;
		
	КонецЕсли;
	
	Если Параметры.Отбор.Свойство("ПолноеИмяОснования") И ЗначениеЗаполнено(Параметры.Отбор.ПолноеИмяОснования) Тогда
		
		ПолноеИмяОснования = Параметры.Отбор.ПолноеИмяОснования;
		
		Если Параметры.Отбор.Свойство("ВидОснования") Тогда
			ВидОснования = Параметры.Отбор.ВидОснования;
		КонецЕсли;
		
		МетаданныеОснования = Метаданные.НайтиПоПолномуИмени(ПолноеИмяОснования);
		Если МетаданныеОснования <> Неопределено Тогда
			
			АвтоЗаголовок = Ложь;
			Заголовок = Заголовок + " " + НСтр("ru='по основанию:';uk='по підставі:'") + " ";
			Если Параметры.Отбор.Свойство("ВидОснования") И Параметры.Отбор.ВидОснования = "ЗаказНаряд" Тогда
				Заголовок = Заголовок + НСтр("ru='Заказ-наряд';uk='Замовлення-наряд'");
			Иначе
				Заголовок = Заголовок + МетаданныеОснования.Представление();
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	// Установим настройки формы для случая открытия в режиме выбора
	Элементы.Список.РежимВыбора = Параметры.РежимВыбора;
	Элементы.Список.МножественныйВыбор = ?(Параметры.ЗакрыватьПриВыборе = Неопределено, Ложь, Не Параметры.ЗакрыватьПриВыборе);
	Если Параметры.РежимВыбора Тогда
		КлючНазначенияИспользования = "ВыборПодбор";
		РежимОткрытияОкна = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
	Иначе
		КлючНазначенияИспользования = "Список";
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СоздатьШаблонEmail(Команда)
	
	СоздатьШаблон(ПредопределенноеЗначение("Перечисление.ВидыКаналовСвязи.Email"));
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьШаблонSMS(Команда)
	
	СоздатьШаблон(ПредопределенноеЗначение("Перечисление.ВидыКаналовСвязи.SMS"));
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура СоздатьШаблон(СпособОтправки)
	
	ЗначенияЗаполнения = Новый Структура;
	ЗначенияЗаполнения.Вставить("СпособОтправки", СпособОтправки);
	Если Не ПустаяСтрока(ПолноеИмяОснования) Тогда
		ЗначенияЗаполнения.Вставить("ПолноеИмяОснования", ПолноеИмяОснования);
	КонецЕсли;
	Если Не ПустаяСтрока(ВидОснования) Тогда
		ЗначенияЗаполнения.Вставить("ВидОснования", ВидОснования);
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура("ЗначенияЗаполнения", ЗначенияЗаполнения);
	ОткрытьФорму("Справочник.ШаблоныСообщений.ФормаОбъекта", ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти
