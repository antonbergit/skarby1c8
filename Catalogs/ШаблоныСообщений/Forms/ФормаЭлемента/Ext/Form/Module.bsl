
#Область ПеременныеФормы

&НаКлиенте
Перем НачалоСтроки, НачалоКолонки, КонецСтроки, КонецКолонки; // Границы выделения текста шаблона SMS

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Элементы.ПолноеИмяОснования.Видимость		= Не ЗначениеЗаполнено(Объект.ПолноеИмяОснования);
	Элементы.ПредставлениеОснования.Видимость	= ЗначениеЗаполнено(Объект.ПолноеИмяОснования);
	
	Если Параметры.Ключ.Пустая() Тогда
		ПриСозданииПриЧтенииНаСервере();
	КонецЕсли;
	
	Элементы.ОбщаяКомандаНастроитьОтправкуSMS.Видимость = Пользователи.ЭтоПолноправныйПользователь();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_НастройкиОтправкиSMS" Тогда
		НастройкиSMSВыполнены = НастройкаОтправкиSMSВыполненаСервер();
		УправлениеФормой(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	КартинкиТекстаHTML = ТекущийОбъект.КартинкиТекстаHTML.Получить();
	Если КартинкиТекстаHTML = Неопределено Тогда
		КартинкиТекстаHTML = Новый Структура;
	КонецЕсли;
	
	ТекстФорматированныйДокумент.УстановитьHTML(ТекущийОбъект.ТекстШаблонаHTML, КартинкиТекстаHTML);
	
	ТемаФорматированныйДокумент.Удалить(ТемаФорматированныйДокумент.ПолучитьЗакладкуНачала(),
										ТемаФорматированныйДокумент.ПолучитьЗакладкуКонца());
	ТемаФорматированныйДокумент.Добавить(Объект.ТемаШаблона);
	
	ПриСозданииПриЧтенииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	ПроверитьПравильностьШаблона(Отказ, Ложь);
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если ТекущийОбъект.СпособОтправки = Перечисления.ВидыКаналовСвязи.Email Тогда
		
		ТекстШаблонаHTML	= "";
		КартинкиТекстаHTML	= Новый Структура;
		ТекстФорматированныйДокумент.ПолучитьHTML(ТекстШаблонаHTML, КартинкиТекстаHTML);
		
		ТекущийОбъект.ТекстШаблонаHTML		= ТекстШаблонаHTML;
		ТекущийОбъект.КартинкиТекстаHTML	= Новый ХранилищеЗначения(КартинкиТекстаHTML);
		ТекущийОбъект.ТекстШаблона			= ТекстФорматированныйДокумент.ПолучитьТекст();
		ТекущийОбъект.ТемаШаблона			= ТемаФорматированныйДокумент.ПолучитьТекст();
		
	ИначеЕсли ТекущийОбъект.СпособОтправки = Перечисления.ВидыКаналовСвязи.SMS Тогда
		
		НастройкиSMSВыполнены = ОтправкаSMS.НастройкаОтправкиSMSВыполнена();
		Если Не НастройкиSMSВыполнены Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Для отправки сообщений SMS по шаблону необходимо выполнить настройку SMS';uk='Для відправки повідомлень SMS за шаблоном необхідно здійснити всі необхідні налаштування SMS'"),,,,Отказ);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура СпособОтправкиПриИзменении(Элемент)
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолноеИмяОснованияПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Объект.ПолноеИмяОснования) Тогда
		ПерезаполнитьПараметрыПоОснованию();
	Иначе
		ДеревоДоступныхПолей.ПолучитьЭлементы().Очистить();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоДоступныхПолейВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтрокаДерева = ДеревоДоступныхПолей.НайтиПоИдентификатору(ВыбраннаяСтрока);
	ТекстПараметра = "[" + СтрокаДерева.Имя + "]";
	
	Если Объект.СпособОтправки = ПредопределенноеЗначение("Перечисление.ВидыКаналовСвязи.Email") Тогда
		
		ТекущийЭлемент = Элементы.ТекстФорматированныйДокумент;
		Начало = Неопределено;
		Конец = Неопределено;
		Элементы.ТекстФорматированныйДокумент.ПолучитьГраницыВыделения(Начало, Конец);
		
		ПозицияВставки = ТекстФорматированныйДокумент.ПолучитьПозициюПоЗакладке(Конец);
		
		ЭлементВставки = ТекстФорматированныйДокумент.Вставить(ТекстФорматированныйДокумент.ПолучитьЗакладкуПоПозиции(ПозицияВставки), ТекстПараметра);
		ТекстФорматированныйДокумент.Удалить(Начало, Конец);
		
		НайденнаяОбласть = ТекстФорматированныйДокумент.НайтиТекст(ТекстПараметра);
		Элементы.ТекстФорматированныйДокумент.УстановитьГраницыВыделения(НайденнаяОбласть.ЗакладкаНачала, НайденнаяОбласть.ЗакладкаКонца);
		
	Иначе
		
		ТекущийЭлемент = Элементы.ТекстШаблонаSMS;
		Элементы.ТекстШаблонаSMS.ПолучитьГраницыВыделения(НачалоСтроки, НачалоКолонки, КонецСтроки, КонецКолонки);
		
		массивСтрок = СтрРазделить(Объект.ТекстШаблона, Символы.ПС, Истина);
		массивСтрок[НачалоСтроки-1] = Лев(массивСтрок[НачалоСтроки-1], НачалоКолонки-1) + ТекстПараметра + Сред(массивСтрок[НачалоСтроки-1], КонецКолонки);
		
		Объект.ТекстШаблона = СтрСоединить(массивСтрок, Символы.ПС);
		
		КонецКолонки = НачалоКолонки + СтрДлина(ТекстПараметра);
		ПодключитьОбработчикОжидания("УстановитьГраницыВыделенияДобавленногоПараметра", 0.1, Истина);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоДоступныхПолейНачалоПеретаскивания(Элемент, ПараметрыПеретаскивания, Выполнение)
	
	Если ПараметрыПеретаскивания.Значение = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СтрокаДерева = ДеревоДоступныхПолей.НайтиПоИдентификатору(ПараметрыПеретаскивания.Значение);
	СтрокаРодитель = СтрокаДерева.ПолучитьРодителя();
	Если СтрокаРодитель = Неопределено Тогда
		ПараметрыПеретаскивания.Значение = "[" + СтрокаДерева.Имя + "]";
	Иначе
		ПараметрыПеретаскивания.Значение = "[" + СтрокаРодитель.Имя + "." + СтрокаДерева.Имя + "]";
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КомментарийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(Элемент.ТекстРедактирования, ЭтотОбъект, "Объект.Комментарий");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПроверитьШаблон(Команда)
	
	ПроверитьПравильностьШаблона();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПриСозданииПриЧтенииНаСервере()
	
	ЗаполнитьДоступныеОснования();
	НастройкиSMSВыполнены = ОтправкаSMS.НастройкаОтправкиSMSВыполнена();
	ПерезаполнитьПараметрыПоОснованию();
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	ЭтоШаблонEmail = Форма.Объект.СпособОтправки = ПредопределенноеЗначение("Перечисление.ВидыКаналовСвязи.Email");
	
	Форма.Элементы.ОтправкаEmail.Видимость	= ЭтоШаблонEmail;
	Форма.Элементы.ОтправкаSMS.Видимость	= Не ЭтоШаблонEmail;
	
	Форма.Элементы.ИнформацияОНастройкеSMS.Видимость = Не ЭтоШаблонEmail И Не Форма.НастройкиSMSВыполнены;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДоступныеОснования()
	
	Элементы.ПолноеИмяОснования.СписокВыбора.Очистить();
	МассивОбъектовМетаданных = Справочники.ШаблоныСообщений.ОбъектыМетаданныхПоставляющиеПараметрыШаблонов();
	
	Для Каждого ОбъектМетаданных Из МассивОбъектовМетаданных Цикл
		
		Элементы.ПолноеИмяОснования.СписокВыбора.Добавить(
			ОбъектМетаданных.ПолноеИмя(),
			ОбъектМетаданных.Синоним
		);
	КонецЦикла;
	
	Если ЗначениеЗаполнено(Объект.ПолноеИмяОснования) Тогда
		НайденныйЭлемент = Элементы.ПолноеИмяОснования.СписокВыбора.НайтиПоЗначению(Объект.ПолноеИмяОснования);
		Если НайденныйЭлемент <> Неопределено Тогда
			ПредставлениеОснования = НайденныйЭлемент.Представление;
		КонецЕсли;
	КонецЕсли;
	
	Если Объект.ВидОснования = "ЗаказНаряд" Тогда
		ПредставлениеОснования = НСтр("ru='Заказ-наряд';uk='Замовлення-наряд'");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПерезаполнитьПараметрыПоОснованию()
	
	ЭлементыДерева = ДеревоДоступныхПолей.ПолучитьЭлементы();
	ЭлементыДерева.Очистить();
	
	ОбъектМетаданных = Метаданные.НайтиПоПолномуИмени(Объект.ПолноеИмяОснования);
	Если ОбъектМетаданных = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	МассивОбъектовМетаданных = Справочники.ШаблоныСообщений.ОбъектыМетаданныхПоставляющиеПараметрыШаблонов();
	
	Если МассивОбъектовМетаданных.Найти(ОбъектМетаданных) = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ПустаяСтрока(Объект.ВидОснования) Тогда
		СКД = Справочники.ШаблоныСообщений.ПолучитьСхемуДанныхШаблонаСообщения(ОбъектМетаданных);
	Иначе
		СКД = Справочники.ШаблоныСообщений.ПолучитьСхемуДанныхШаблонаСообщения(ОбъектМетаданных, "СКД_ДанныеШаблонаСообщенийЗаказНаряд");
	КонецЕсли;
	
	Компоновщик = Новый КомпоновщикНастроекКомпоновкиДанных();
	Компоновщик.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(ПоместитьВоВременноеХранилище(СКД, УникальныйИдентификатор)));
	
	Для Каждого ДоступноеПоле ИЗ Компоновщик.Настройки.ДоступныеПоляВыбора.Элементы Цикл
		
		Если ДоступноеПоле.Папка Тогда
			Продолжить;
		КонецЕсли;
		
		Параметр = ЭлементыДерева.Добавить();
		
		Параметр.Имя			= ДоступноеПоле.Поле;
		Параметр.Представление	= ДоступноеПоле.Заголовок;
		Параметр.ИндексКартинки	= 3;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьПравильностьШаблона(Отказ = Ложь, ВыдаватьСообщениеОКорректности = Истина)
	
	ОчиститьСообщения();
	
	Если Объект.СпособОтправки = ПредопределенноеЗначение("Перечисление.ВидыКаналовСвязи.Email") Тогда
		ПроверитьПараметрыШаблона("ТекстФорматированныйДокумент", Элементы.ТекстФорматированныйДокумент, Отказ);
		ПроверитьПараметрыШаблона("ТемаФорматированныйДокумент", Элементы.ТемаФорматированныйДокумент, Отказ);
	Иначе
		ПроверитьПараметрыШаблона("Объект.ТекстШаблона", Элементы.ТекстШаблонаSMS, Отказ);
	КонецЕсли;
	
	Если ВыдаватьСообщениеОКорректности И Не Отказ Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Параметры шаблона заполнены корректно.';uk='Параметри шаблону заповнені коректно.'"));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьПараметрыШаблона(знач ПутьКДаннымВФорме, Поле, Отказ)
	
	ЭтоФорматированныйДокумент = СтрНайти(ПутьКДаннымВФорме, "ФорматированныйДокумент") > 0;
	
	ЧастиПути = СтрРазделить(ПутьКДаннымВФорме, ".");
	ЗначениеРеквизита = ЭтотОбъект;
	Для Каждого ЧастьПути Из ЧастиПути Цикл
		ЗначениеРеквизита = ЗначениеРеквизита[ЧастьПути];
	КонецЦикла;
	
	ТекстШаблона = ?(ЭтоФорматированныйДокумент, ЗначениеРеквизита.ПолучитьТекст(), ЗначениеРеквизита);
	
	ПараметрыСообщения = ШаблоныСообщенийКлиентСервер.ОпределитьПараметрыСообщения(ТекстШаблона);
	
	Для Каждого ПараметрСообщения Из ПараметрыСообщения Цикл
		
		ИдентификаторСтроки = -1;
		ОбщегоНазначенияКлиентСервер.ПолучитьИдентификаторСтрокиДереваПоЗначениюПоля(
			"Имя",
			ИдентификаторСтроки,
			ДеревоДоступныхПолей.ПолучитьЭлементы(),
			ПараметрСообщения.Ключ,
			Ложь
		);
		
		Если ИдентификаторСтроки = -1 Тогда
			
			ТекстСообщения = НСтр("ru='Неверный параметр в шаблоне сообщения';uk='Невірний параметр в шаблоні повідомлення'") + ": " + ПараметрСообщения.Ключ;
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , ПутьКДаннымВФорме,, Отказ);
			ПодстрокаПоиска = "[" + ПараметрСообщения.Ключ + "]";
			
			Если ЭтоФорматированныйДокумент Тогда
				НайденнаяОбласть = ЭтотОбъект[ПутьКДаннымВФорме].НайтиТекст(ПодстрокаПоиска);
				Поле.УстановитьГраницыВыделения(НайденнаяОбласть.ЗакладкаНачала, НайденнаяОбласть.ЗакладкаКонца);
			Иначе
				НачальнаяПозиция	= СтрНайти(ТекстШаблона, ПодстрокаПоиска);
				КонечнаяПозиция		= НачальнаяПозиция + СтрДлина(ПодстрокаПоиска);
				Поле.УстановитьГраницыВыделения(НачальнаяПозиция, КонечнаяПозиция);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция НастройкаОтправкиSMSВыполненаСервер()
	
	Возврат ОтправкаSMS.НастройкаОтправкиSMSВыполнена();
	
КонецФункции

&НаКлиенте
Процедура УстановитьГраницыВыделенияДобавленногоПараметра()
	
	Элементы.ТекстШаблонаSMS.УстановитьГраницыВыделения(НачалоСтроки, НачалоКолонки, НачалоСтроки, КонецКолонки);
	
КонецПроцедуры

#КонецОбласти
