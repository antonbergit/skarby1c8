
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("Отбор") И Параметры.Отбор.Свойство("Владелец") Тогда
		
		ОбъектВладелец = Параметры.Отбор.Владелец;
		
		Если ТипЗнч(ОбъектВладелец) = Тип("СправочникСсылка.Номенклатура") Тогда
			
			Если НЕ ЗначениеЗаполнено(ОбъектВладелец)
				ИЛИ НЕ ОбъектВладелец.ТипНоменклатуры = Перечисления.ТипыНоменклатуры.Запас
				И НЕ ОбъектВладелец.ТипНоменклатуры = Перечисления.ТипыНоменклатуры.Услуга
				И НЕ ОбъектВладелец.ТипНоменклатуры = Перечисления.ТипыНоменклатуры.Работа Тогда
				
				АвтоЗаголовок = Ложь;
				Заголовок = НСтр("ru='Характеристики хранятся только для запасов, услуг и работ';uk='Характеристики зберігаються тільки для запасів, послуг і робіт'");
				
				Элементы.Список.ТолькоПросмотр = Истина;
				
			КонецЕсли;
			
			НаборДопРеквизитов = ОбъектВладелец.КатегорияНоменклатуры.НаборСвойствХарактеристики;
			
		ИначеЕсли ТипЗнч(ОбъектВладелец) = Тип("СправочникСсылка.КатегорииНоменклатуры") Тогда
			
			НаборДопРеквизитов = ОбъектВладелец.НаборСвойствХарактеристики;
			
		Иначе
			
			Элементы.ИзменитьНаборДополнительныхРеквизитовИСведений.Видимость = Ложь;
			
		КонецЕсли;
		
	Иначе
		
		Элементы.ИзменитьНаборДополнительныхРеквизитовИСведений.Видимость = Ложь;
		
	КонецЕсли;
	
	// СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
	ДополнительныеОтчетыИОбработки.ПриСозданииНаСервере(ЭтаФорма);
	// Конец СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
	
КонецПроцедуры // ПриСозданииНаСервере()

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
// Процедура - обработчик события Выполнить команды ИзменитьНаборДополнительныхРеквизитовИСведений.
//
Процедура ИзменитьНаборДополнительныхРеквизитовИСведений(Команда)
	
	Если ЗначениеЗаполнено(НаборДопРеквизитов) Тогда
		ПараметрыФормыНабораСвойств = Новый Структура("Ключ", НаборДопРеквизитов);
		ОткрытьФорму("Справочник.НаборыДополнительныхРеквизитовИСведений.Форма.ФормаЭлемента", ПараметрыФормыНабораСвойств);
	Иначе
		ПоказатьПредупреждение(Неопределено,НСтр("ru='Нельзя получить набор свойств объекта. Возможно не заполнены необходимые реквизиты.';uk='Не можна одержати набір властивостей об`єкта. Можливо не заповнені необхідні реквізити.'"));
	КонецЕсли;
	
КонецПроцедуры // ИзменитьНаборДополнительныхРеквизитовИСведений()

#КонецОбласти

#Область ЗамерыПроизводительности

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	ОценкаПроизводительностиКлиент.НачатьЗамерВремени(Истина, "СозданиеФормы" + РаботаСФормойДокументаКлиентСервер.ПолучитьИмяФормыСтрокой(ЭтотОбъект.ИмяФормы));
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПередНачаломИзменения(Элемент, Отказ)
	
	ОценкаПроизводительностиКлиент.НачатьЗамерВремени(Истина, "ОткрытиеФормы" + РаботаСФормойДокументаКлиентСервер.ПолучитьИмяФормыСтрокой(ЭтотОбъект.ИмяФормы));
	
КонецПроцедуры

#КонецОбласти
