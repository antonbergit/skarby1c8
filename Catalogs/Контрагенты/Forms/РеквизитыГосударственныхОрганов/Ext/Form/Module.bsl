
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Свойство("КодГосударственногоОргана")
		И Параметры.Свойство("ВидГосударственногоОргана") Тогда
		КодГосударственногоОргана = Параметры.КодГосударственногоОргана;
		ВидГосударственногоОргана = Параметры.ВидГосударственногоОргана;
	ИначеЕсли Параметры.Свойство("РегистрацияВНалоговомОргане")
		И ЗначениеЗаполнено(Параметры.РегистрацияВНалоговомОргане) Тогда
		КодГосударственногоОргана = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Параметры.РегистрацияВНалоговомОргане, "Код");
		ВидГосударственногоОргана = Перечисления.ВидыГосударственныхОрганов.НалоговыйОрган;
	Иначе
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	Элементы.КодГосударственногоОргана.ТолькоПросмотр = Параметры.ЗапретРедактированияКода;
	
	Если ВидГосударственногоОргана = Перечисления.ВидыГосударственныхОрганов.НалоговыйОрган Тогда
		ЭтаФорма.Заголовок = НСтр("ru='Платежные реквизиты налоговой инспекции';uk='Платіжні реквізити податкової інспекції'");
	ИначеЕсли ВидГосударственногоОргана = Перечисления.ВидыГосударственныхОрганов.ОрганПФР Тогда
		ЭтаФорма.Заголовок = НСтр("ru='Платежные реквизиты отделения ПФР';uk='Платіжні реквізити відділення ПФР'");
	ИначеЕсли ВидГосударственногоОргана = Перечисления.ВидыГосударственныхОрганов.ОрганФСС Тогда
		ЭтаФорма.Заголовок = НСтр("ru='Платежные реквизиты отделения ФСС';uk='Платіжні реквізити відділення ФСС'");
	Иначе
		ЭтаФорма.Заголовок = НСтр("ru='Платежные реквизиты';uk='Платіжні реквізити'");
	КонецЕсли;
	
	Данные = ДанныеГосударственныхОрганов.ГосударственныйОрган(ВидГосударственногоОргана, КодГосударственногоОргана);
	
	ГосударственныйОрганБанк = Данные.ПлатежныеРеквизиты.Банк;
	ГосударственныйОрганМФО  = Данные.ПлатежныеРеквизиты.МФО;
	ГосударственныйОрганИНН  = Данные.ИНН;
	ГосударственныйОрганЕДРПОУ  = Данные.КодПоЕДРПОУ;
	ГосударственныйОрганНаименованиеКраткое = Данные.Наименование;
	ГосударственныйОрганНаименование    = Данные.ПолноеНаименование;
	ГосударственныйОрганРасчетныйСчет   = Данные.ПлатежныеРеквизиты.РасчетныйСчет;
	ГосударственныйОрганТекстПолучателя = Данные.ПлатежныеРеквизиты.ПолучательПлатежа;
	ТекущийКодГосударственногоОргана = КодГосударственногоОргана;
	
	Если Параметры.Свойство("НаименованиеГосударственногоОргана")
		И НЕ ЗначениеЗаполнено(ГосударственныйОрганНаименование) Тогда
		ГосударственныйОрганНаименование = Параметры.НаименованиеГосударственногоОргана;
	КонецЕсли;
	
	Если НЕ ПравоДоступа("Изменение", Метаданные.Справочники.Контрагенты) Тогда
		ТолькоПросмотр = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовФормы
&НаКлиенте
Процедура МФОБанкаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ОткрытьФормуВыбораБанка(Истина);
КонецПроцедуры

&НаКлиенте
Процедура МФОБанкаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ГосударственныйОрганБанк = ВыбранноеЗначение;
	ЗаполнитьДанныеБанка(ГосударственныйОрганМФО, ГосударственныйОрганБанк);
	
	Если ПустаяСтрока(ГосударственныйОрганМФО) Тогда
		
		ЭтаФорма.ТекущийЭлемент = Элементы.МФОБанка;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура МФОБанкаОчистка(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка     = Ложь;
	ГосударственныйОрганБанк = Неопределено;
	ГосударственныйОрганМФО = "";
	
КонецПроцедуры

&НаКлиенте
Процедура МФОБанкаОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	#Если ВебКлиент Тогда
		
		Если СтрДлина(Текст) > 9 Тогда
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = НСтр("ru='Введенное значение превышает допустимую длину МФО 9 символов!';uk='Введене значення перевищує припустиму довжину МФО 9 символів!'");
			Сообщение.Сообщить();
			
			СтандартнаяОбработка = Ложь;
			
			Возврат;
			
		КонецЕсли;
		
	#КонецЕсли
	
	СписокНайденныхБанков = НайтиБанки(Текст, Элемент.Имя);
	Если ТипЗнч(СписокНайденныхБанков) = Тип("СписокЗначений") Тогда
		
		Если СписокНайденныхБанков.Количество() = 1 Тогда
			
			ОповеститьОбИзменении(Тип("СправочникСсылка.Банки"));
			
			ГосударственныйОрганБанк = СписокНайденныхБанков[0].Значение;
			ЗаполнитьДанныеБанка(ГосударственныйОрганМФО, ГосударственныйОрганБанк);
			
		ИначеЕсли СписокНайденныхБанков.Количество() > 1 Тогда
			
			ОповеститьОбИзменении(Тип("СправочникСсылка.Банки"));
			
			ОткрытьФормуВыбораБанка(Истина, СписокНайденныхБанков);
			
		Иначе
			
			ОткрытьФормуВыбораБанка(Истина);
			
		КонецЕсли;
		
	Иначе
		
		ЭтаФорма.ТекущийЭлемент = Элемент;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОК(Команда)
	
	ЗаписатьДанныеОПлатежныхРеквизитах();
	
	Закрыть(Новый Структура("Код, Наименование, НаименованиеПолное", КодГосударственногоОргана, ГосударственныйОрганНаименованиеКраткое, ГосударственныйОрганНаименование));
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОтмена(Команда)
	
	Модифицированность = Ложь;
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Процедура заполняет значения полей МФО.
//
&НаСервереБезКонтекста
Процедура ЗаполнитьДанныеБанка(Мфо, Банк)
	
	Если НЕ ЗначениеЗаполнено(Банк) Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Мфо = Банк.Код;
	
КонецПроцедуры 

// Функция возвращает список значений с банком/банками подходящих условию поиска
// 
// В случае неудачи возвращает Неопределено или пустой список значений.
//
&НаКлиенте
Функция НайтиБанки(ТекстДляПоиска, Поле, Валютный = Ложь)
	
	Перем ТекстОшибки;
	
	ЭтоБанк = Истина;
	ГосударственныйОрганБанк = Неопределено;
	ГосударственныйОрганМФО = "";
	
	Если ПустаяСтрока(ТекстДляПоиска) Тогда
		
		ОчиститьСообщения();
		
		ТекстСообщения = НСтр("ru='Поле МФО заполнено не корректно.';uk='Поле МФО заповнене не коректно.'"); 
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,, Поле);
		
		Возврат Неопределено;
		
	КонецЕсли;
	
	ОбластьПоиска = "Код";
	
	
	СписокНайденныхБанков = ПолучитьСписокБанковПоРеквизитам(ОбластьПоиска, ТекстДляПоиска);
	Если СписокНайденныхБанков.Количество() = 0 Тогда
		
		Если ОбластьПоиска = "Код" Тогда
			
			Если НЕ ПроверитьКорректностьМФО(ТекстДляПоиска, ТекстОшибки) Тогда
				
				ОчиститьСообщения();
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,, Поле);
				Возврат Неопределено;
				
			КонецЕсли;
			
			ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='Банк с МФО ""%1"" не найден в справочнике банков';uk='Банк з МФО ""%1"" не знайдений у довіднику банків'"), ТекстДляПоиска);
			
		ИначеЕсли ОбластьПоиска = "КоррСчет" Тогда
			
			Если НЕ ПроверитьКорректностьНомераСчета(ТекстДляПоиска, Валютный, ТекстОшибки) Тогда
				ОчиститьСообщения();
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,, Поле);
				Возврат Неопределено;
			КонецЕсли;
			
			ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='Банк с корр. счетом ""%1"" не найден в справочнике банков';uk='Банк з кор. рахунком ""%1"" не знайдений у довіднику банків'"), ТекстДляПоиска);
			
		КонецЕсли;
		
		// Сформируем варианты
		Кнопки	= Новый СписокЗначений;
		Кнопки.Добавить("Выбрать",     НСтр("ru='Выбрать из справочника';uk='Вибрати з довідника'"));
		Кнопки.Добавить("Отменить",   НСтр("ru='Отменить ввод';uk='Скасувати введення'"));
		
		// Обработка выбора
		ОписаниеОповещения = Новый ОписаниеОповещения("ОпределитьНеобходимостьВыбораБанкаИзСправочника", ЭтотОбъект, Новый Структура("ЭтоБанк", ЭтоБанк));
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, Кнопки,, "Выбрать", НСтр("ru='Банк не найден';uk='Банк не знайдений'"));
		Возврат Неопределено;
		
		
	КонецЕсли;
	
	Возврат СписокНайденныхБанков;
	
КонецФункции // НайтиБанки()

&НаСервереБезКонтекста
Функция ПолучитьСписокБанковПоРеквизитам(Знач Поле, Знач Значение) Экспорт

	СписокБанков = Новый СписокЗначений;
	
	Если ПустаяСтрока(Значение) Тогда
	
		Возврат СписокБанков;
		
	КонецЕсли;
	
	ТаблицаБанков = Справочники.Банки.ПолучитьТаблицуБанковПоРеквизитам(Поле, Значение);
	
	СписокБанков.ЗагрузитьЗначения(ТаблицаБанков.ВыгрузитьКолонку("Ссылка"));
	
	Возврат СписокБанков;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ПроверитьКорректностьНомераСчета(Номер, ВалютныйСчет = Ложь, ТекстОшибки = "")

	Результат = Истина;
	
	Если ПустаяСтрока(Номер) Тогда
		Возврат Результат;
	КонецЕсли;
	
	ТекстОшибки = "";
	Если НЕ ВалютныйСчет И СтрДлина(Номер) <> 20 Тогда
		
		ТекстОшибки = НСтр("ru='Возможно номер счета указан не полностью';uk='Можливо номер рахунку зазначений не повністю'");
		Результат = Ложь;
		
	ИначеЕсли НЕ СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(Номер) Тогда
		
		ТекстОшибки = ТекстОшибки + ?(ПустаяСтрока(ТекстОшибки), "", " ") +
			НСтр("ru='В номере банковского счета присутствуют не только цифры."
"Возможно, номер указан неверно';uk='У номері банківського рахунку присутні не тільки цифри."
"Можливо, номер зазначений невірно'");
		Результат = Ложь;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ПроверитьКорректностьМФО(МФО, ТекстОшибки = "")
	
	Если ПустаяСтрока(МФО) Тогда
		
		Возврат Истина;
		
	КонецЕсли;
	
	ТекстОшибки = "";
	Если СтрДлина(МФО) <> 9 Тогда
		
		ТекстОшибки = НСтр("ru='По указанному МФО банк не найден. Возможно МФО указан не полностью.';uk='По зазначеному МФО банк не знайдений. Можливо МФО зазначений не повністю.'");
		
	ИначеЕсли НЕ СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(МФО) Тогда
		
		ТекстОшибки = ТекстОшибки + ?(ПустаяСтрока(ТекстОшибки), "", " ") +
			НСтр("ru='В составе МФО банка должны быть только цифры';uk='У складі МФО банку повинні бути тільки цифри'");
		
	ИначеЕсли НЕ Лев(МФО, 2) = "04" Тогда
		
		ТекстОшибки = ТекстОшибки + ?(ПустаяСтрока(ТекстОшибки), "", " ") +
			НСтр("ru='Первые 2 цифры МФО банка должны быть ""04""';uk='Перші 2 цифри МФО банку повинні бути ""04""'");
		
	КонецЕсли;
	
	Возврат ПустаяСтрока(ТекстОшибки);
	
КонецФункции

// Процедура открывает форму списка банков для ручного выбора
//
&НаКлиенте
Процедура ОткрытьФормуВыбораБанка(ЭтоБанк, СписокНайденныхБанков = Неопределено)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ТекущаяСтрока", ГосударственныйОрганБанк);
	ПараметрыФормы.Вставить("ПараметрВыборГруппИЭлементов", ИспользованиеГруппИЭлементов.Элементы);
	ПараметрыФормы.Вставить("ЗакрыватьПриВыборе", Истина);
	ПараметрыФормы.Вставить("МножественныхВыбор", Ложь);
	
	Если СписокНайденныхБанков <> Неопределено Тогда
		
		ПараметрыФормы.Вставить("СписокНайденныхБанков", СписокНайденныхБанков);
		
	КонецЕсли;
	
	ОткрытьФорму("Справочник.Банки.ФормаВыбора", ПараметрыФормы, Элементы.МФОБанка);
	
КонецПроцедуры // ОткрытьФормуВыбораБанка()

&НаСервере
Процедура ЗаписатьДанныеОПлатежныхРеквизитах();
	
	ГосударственныйОрган = ДанныеГосударственныхОрганов.ГосударственныйОрган(ВидГосударственногоОргана, ?(ПустаяСтрока(ТекущийКодГосударственногоОргана), КодГосударственногоОргана, ТекущийКодГосударственногоОргана));
	ГосударственныйОрган.Вид = ВидГосударственногоОргана;
	ГосударственныйОрган.Код = КодГосударственногоОргана;
	ГосударственныйОрган.ПолноеНаименование = ГосударственныйОрганНаименование;
	ГосударственныйОрган.ИНН = ГосударственныйОрганИНН;
	ГосударственныйОрган.КодПоЕДРПОУ = ГосударственныйОрганЕДРПОУ;
	ГосударственныйОрган.Наименование = ?(ПустаяСтрока(ГосударственныйОрганНаименованиеКраткое), ГосударственныйОрганНаименование, ГосударственныйОрганНаименованиеКраткое);
	ГосударственныйОрган.ПлатежныеРеквизиты.ПолучательПлатежа = ГосударственныйОрганТекстПолучателя;
	ГосударственныйОрган.ПлатежныеРеквизиты.РасчетныйСчет = ГосударственныйОрганРасчетныйСчет;
	ГосударственныйОрган.ПлатежныеРеквизиты.КоррСчет = "";
	ГосударственныйОрган.ПлатежныеРеквизиты.МФО = ГосударственныйОрганМФО;
	
	ДанныеГосударственныхОрганов.ОбновитьДанныеГосударственногоОргана(ГосударственныйОрган);
	
КонецПроцедуры


#КонецОбласти

