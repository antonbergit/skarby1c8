#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") И Не ЭтоГруппа Тогда
		
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения);
		
	КонецЕсли;
	
	ДозаполнитьПоУмолчанию();
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	Если Не Справочники.ГруппыДоступаКонтрагентов.ИспользуютсяГруппыДоступа() Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ГруппаДоступа");
	КонецЕсли;
	
	Если Не ВидКонтрагента = Перечисления.ВидыКонтрагентов.ГосударственныйОрган Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ВидГосударственногоОргана");
		МассивНепроверяемыхРеквизитов.Добавить("КодГосударственногоОргана");
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	// 1. Действия, выполняемые всегда, в том числе и при обмене данными
	
	ДополнительныеСвойства.Вставить("НужноЗаписыватьВРегистрПриЗаписи", Ложь);
	
	// Не выполнять дальнейшие действия при обмене данными
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	// 2. Дальнейшие действия не выполняются в случае, когда запись инициирована механизмом обмена данными
	
	// Проверим согласованность внесенных изменений с другими объектами базы
	Если Не ЭтоНовый() Тогда
		ПроверитьВозможностьИзменений(Отказ);
	КонецЕсли;
	
	// Не выполнять дальнейшие действия при отказе в записи
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	// 3. Дальнейшие действия не выполняются при отказе в записи объекта
	
	// Проверим корректность ИНН, ЕГРПОУ и зарегистрируем дубли контрагентов,
	// кроме случая загрузки/выгрузки из локальной версии в модель сервиса.
	Если Не (ДополнительныеСвойства.Свойство("РегистрироватьДублиКонтрагентов") И ДополнительныеСвойства.РегистрироватьДублиКонтрагентов = Ложь) Тогда
		ЗарегистрироватьДублиКонтрагентов();
	КонецЕсли;
	
	// Дозаполним реквизиты
	Если Не ЭтоГруппа И ЭтоНовый() Тогда
		ДатаСоздания = ТекущаяДатаСеанса();
	КонецЕсли;
	Если Не ЭтоГруппа Тогда
		СформироватьОсновныеСведения();
	КонецЕсли;
	
	// Заполним договор по умолчанию: подставляем любой существующий или создаем новый
	Если Не ЭтоГруппа И Не ЗначениеЗаполнено(ДоговорПоУмолчанию) Тогда
		
		НадоСоздатьДоговор = Истина;
		
		Если Не ЭтоНовый() Тогда
			
			Запрос = Новый Запрос;
			Запрос.Текст = 
				"ВЫБРАТЬ ПЕРВЫЕ 1
				|	ДоговорыКонтрагентов.Ссылка КАК Договор
				|ИЗ
				|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
				|ГДЕ
				|	ДоговорыКонтрагентов.Владелец = &Владелец
				|	И ДоговорыКонтрагентов.ПометкаУдаления = ЛОЖЬ
				|
				|УПОРЯДОЧИТЬ ПО
				|	ДоговорыКонтрагентов.НомерДоговора УБЫВ";
			
			Запрос.УстановитьПараметр("Владелец", Ссылка);
			
			РезультатЗапроса = Запрос.Выполнить();
			
			Если Не РезультатЗапроса.Пустой() Тогда
				
				Выборка = РезультатЗапроса.Выбрать();
				Выборка.Следующий();
				ДоговорПоУмолчанию = Выборка.Договор;
				
				НадоСоздатьДоговор = Ложь;
				
			КонецЕсли;
			
		КонецЕсли;
		
		Если НадоСоздатьДоговор Тогда
			ДоговорПоУмолчанию = Справочники.ДоговорыКонтрагентов.ПолучитьСсылку();
			ДополнительныеСвойства.Вставить("НовыйОсновнойДоговор", ДоговорПоУмолчанию);
		КонецЕсли;
		
	КонецЕсли;
	
	// Настройка данных реквизитов в зависимости от других реквизитов
	ПривестиДанныеКСогласованномуСостоянию();
	
	//ITF 170823...
	Если Не ЭтоГруппа Тогда
		Если НЕ ЗначениеЗаполнено(Родитель) Тогда
			Сообщить("Не заповнено групу.");
			Отказ = Истина			
		КонецЕсли;
	КонецЕсли;
	//...ITF 170823
	//ITF 210723...
	//Если ЗначениеЗаполнено(МобНомерТелефона) Тогда
	//тепер номер маэ бути завжди
	Если Не ЭтоГруппа Тогда	
		ДлинаНомера = СтрДлина(СокрЛП(МобНомерТелефона)); 
		Если Не ДлинаНомера = 13 Тогда
			Сообщить("Номер містить менше 13 символів. Прохання перевірити.");
			Отказ = Истина;
		Иначе
			ПервыеТриСимвола = Лев(МобНомерТелефона,3);
			Если Не ПервыеТриСимвола = "+38" Тогда
				Сообщить("Номер має починатись на +38. Прохання перевірити.");
				Отказ = Истина;				
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	//...ITF 210723
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ДополнительныеСвойства.НужноЗаписыватьВРегистрПриЗаписи Тогда
		Справочники.Контрагенты.ВыполнитьДвиженияПоРегиструДублей(Ссылка, ИНН, КодПоЕДРПОУ, Ложь);
	КонецЕсли;
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	// Создадим договор контрагента по ссылке, созданной в событии ПередЗаписью()
	Если Не ЭтоГруппа И ДополнительныеСвойства.Свойство("НовыйОсновнойДоговор") Тогда
		
		ДоговорОбъект = Справочники.ДоговорыКонтрагентов.СоздатьЭлемент();
		ДоговорОбъект.Заполнить(Ссылка);
		
		ДоговорОбъект.УстановитьСсылкуНового(ДополнительныеСвойства.НовыйОсновнойДоговор);
		ДоговорОбъект.Записать();
		
		ДополнительныеСвойства.Удалить("НовыйОсновнойДоговор");
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	Если НЕ ЭтоГруппа Тогда
		БанковскийСчетПоУмолчанию	= Неопределено;
		ДоговорПоУмолчанию			= Неопределено;
		КонтактноеЛицо				= Неопределено;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередУдалением(Отказ)
	
	Если ОбменДанными.Загрузка = Истина Тогда
		Возврат;
	КонецЕсли;
	
	УдалитьРегистрациюДублейПередУдалением();
	
КонецПроцедуры

#КонецОбласти

#Область ПрограммныйИнтерфейс

// Процедура заполняет вспомогательный реквизит "ОсновныеСведения"
//
Процедура СформироватьОсновныеСведения() Экспорт
	
	МассивСтрок = Новый Массив;
	
	Если Не ПустаяСтрока(НаименованиеПолное) Тогда
		МассивСтрок.Добавить(НаименованиеПолное);
	КонецЕсли;
	
	Если Не ПустаяСтрока(ИНН) Тогда
		МассивСтрок.Добавить(НСтр("ru='ИНН';uk='ІПН'") + " " + ИНН);
	КонецЕсли;
		
	Если Не ПустаяСтрока(КодПоЕДРПОУ) Тогда
		МассивСтрок.Добавить(НСтр("ru='ЕГРПОУ';uk='ЄДРПОУ'") + " " + КодПоЕДРПОУ);
	КонецЕсли;
	
	Если Не ПустаяСтрока(НомерСвидетельства) Тогда
		МассивСтрок.Добавить(НСтр("ru='Свидетельство';uk='Свідоцтво'") + " " + НомерСвидетельства);
	КонецЕсли;
	
	Если Не ПустаяСтрока(ДокументУдостоверяющийЛичность) Тогда
		МассивСтрок.Добавить(НСтр("ru='Документ';uk='Документ'") + " " + ДокументУдостоверяющийЛичность);
	КонецЕсли;
	
	КИ = КонтактнаяИнформация.Выгрузить();
	КИ.Сортировать("Вид");
	Для Каждого СтрокаКИ Из КИ Цикл
		Если ПустаяСтрока(СтрокаКИ.Представление) Тогда
			Продолжить;
		КонецЕсли;
		МассивСтрок.Добавить(СтрокаКИ.Представление);
	КонецЦикла;
	
	Если ДополнительныеСвойства.Свойство("ОсновныеСведенияКонтактныхЛиц") Тогда
		
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивСтрок, ДополнительныеСвойства.ОсновныеСведенияКонтактныхЛиц);
		
	Иначе
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	КонтактныеЛица.Наименование КАК Наименование,
			|	КонтактныеЛица.КонтактнаяИнформация.(
			|		Представление КАК Представление,
			|		Вид КАК ВидКИ
			|	)
			|ИЗ
			|	Справочник.КонтактныеЛица КАК КонтактныеЛица
			|ГДЕ
			|	КонтактныеЛица.Владелец = &Контрагент
			|	И КонтактныеЛица.ПометкаУдаления = ЛОЖЬ
			|
			|УПОРЯДОЧИТЬ ПО
			|	Наименование,
			|	ВидКИ";
		
		Запрос.УстановитьПараметр("Контрагент", Ссылка);
		
		ВыборкаКЛ = Запрос.Выполнить().Выбрать();
		Пока ВыборкаКЛ.Следующий() Цикл
			
			Если МассивСтрок.Количество() > 0 Тогда
				МассивСтрок.Добавить(Символы.ПС);
			КонецЕсли;
			МассивСтрок.Добавить(ВыборкаКЛ.Наименование);
			
			ВыборкаКИ = ВыборкаКЛ.КонтактнаяИнформация.Выбрать();
			Пока ВыборкаКИ.Следующий() Цикл
				Если ПустаяСтрока(ВыборкаКИ.Представление) Тогда
					Продолжить;
				КонецЕсли;
				МассивСтрок.Добавить(ВыборкаКИ.Представление);
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Если Не ПустаяСтрока(Комментарий) Тогда
		МассивСтрок.Добавить(Комментарий);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Ответственный) Тогда
		МассивСтрок.Добавить(ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ответственный, "Наименование"));
	КонецЕсли;
	
	ОсновныеСведения = СтрСоединить(МассивСтрок, Символы.ПС);
	
КонецПроцедуры

// Процедура заполняет вспомогательный реквизит "НомерТелефона"
//
Процедура ЗаполнитьНомерТелефона() Экспорт
	
	НомераТелефонов = Новый Массив;
	
	Для Каждого СтрокаКИ Из КонтактнаяИнформация Цикл
		Если СтрокаКИ.Тип = Перечисления.ТипыКонтактнойИнформации.Телефон Тогда
			НомераТелефонов.Добавить(СтрокаКИ.Представление);
		КонецЕсли;
	КонецЦикла;
	
	НомерТелефона = СтрСоединить(НомераТелефонов, ", ");
	
КонецПроцедуры

// См. описание в комментарии к одноименной процедуре в модуле УправлениеДоступом.
//
Процедура ЗаполнитьНаборыЗначенийДоступа(Таблица) Экспорт
	
	// Логика ограничения:
	// Чтения, Изменение: объект разрешен по виду доступа ГруппыДоступаКонтрагентов.
	
	Строка = Таблица.Добавить();
	Строка.ЗначениеДоступа = Ссылка;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ДозаполнитьПоУмолчанию()
	
	Если ЭтоГруппа Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Ответственный) Тогда
		Ответственный = УправлениеНебольшойФирмойПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(Пользователи.ТекущийПользователь(), "ОсновнойОтветственный");
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ГруппаДоступа) Тогда
		ГруппаДоступа = Справочники.ГруппыДоступаКонтрагентов.ГруппаДоступаПоУмолчанию();
	КонецЕсли;
	
КонецПроцедуры

// Процедура проверяет корректность ИНН, ЕГРПОУ и фиксирует наличие дублей контрагента
//
Процедура ЗарегистрироватьДублиКонтрагентов()
	
	Если ЭтоГруппа Тогда
		Возврат;
	КонецЕсли;
	
	НужноВыполнятьПроверку = ЭтоНовый();
	
	ЭтоЮрЛицо = ВидКонтрагента = Перечисления.ВидыКонтрагентов.ЮридическоеЛицо;
	
	ИзменилсяИНН = НужноВыполнятьПроверку;
	ИзменилсяЕДРПОУ = НужноВыполнятьПроверку;
	
	Если НЕ НужноВыполнятьПроверку Тогда
		
		СтруктураПрежнихЗначений = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Ссылка, 
																			  "ИНН,
																			  |КодПоЕДРПОУ,
																			  |ИННВведенКорректно,
																			  |ЕДРПОУВведенКорректно, 
																			  |ВидКонтрагента");
																			  
		Если НЕ СтруктураПрежнихЗначений.ИНН = ИНН 
			Или НЕ СтруктураПрежнихЗначений.КодПоЕДРПОУ = КодПоЕДРПОУ 
			Или НЕ СтруктураПрежнихЗначений.ВидКонтрагента = ВидКонтрагента Тогда
			
			НужноВыполнятьПроверку = Истина;
			
		КонецЕсли;
		
		ИзменилсяИНН = НЕ СтруктураПрежнихЗначений.ИНН = ИНН;
		ИзменилсяЕДРПОУ = НЕ СтруктураПрежнихЗначений.КодПоЕДРПОУ = КодПоЕДРПОУ;
		
		ЭтоЮрЛицоБыло = СтруктураПрежнихЗначений.ВидКонтрагента = Перечисления.ВидыКонтрагентов.ЮридическоеЛицо;
		
		Если НужноВыполнятьПроверку 
			И СтруктураПрежнихЗначений.ИННВведенКорректно 
			И (СтруктураПрежнихЗначений.ЕДРПОУВведенКорректно Или ПустаяСтрока(СтруктураПрежнихЗначений.КодПоЕДРПОУ)) Тогда
			
			Если НЕ СтруктураПрежнихЗначений.ИНН = ИНН
				Или НЕ СтруктураПрежнихЗначений.КодПоЕДРПОУ = КодПоЕДРПОУ Тогда
			
				Блокировка = Новый БлокировкаДанных;
				
				Если НЕ СтруктураПрежнихЗначений.ИНН = ИНН Тогда
			
					ЭлементБлокировкиПоПрежнемуИНН = Блокировка.Добавить("РегистрСведений.НаличиеДублейУКонтрагентов");
					ЭлементБлокировкиПоПрежнемуИНН.УстановитьЗначение("ИНН", СтруктураПрежнихЗначений.ИНН);
					ЭлементБлокировкиПоПрежнемуИНН.Режим = РежимБлокировкиДанных.Исключительный;
					
				КонецЕсли;
				
				Если НЕ СтруктураПрежнихЗначений.КодПоЕДРПОУ = КодПоЕДРПОУ И ЭтоЮрЛицоБыло Тогда
			
					ЭлементБлокировкиПоПрежнемуЕДРПОУ = Блокировка.Добавить("РегистрСведений.НаличиеДублейУКонтрагентов");
					ЭлементБлокировкиПоПрежнемуЕДРПОУ.УстановитьЗначение("ЕДРПОУ", СтруктураПрежнихЗначений.КодПоЕДРПОУ);
					ЭлементБлокировкиПоПрежнемуЕДРПОУ.Режим = РежимБлокировкиДанных.Исключительный;
					
				КонецЕсли;
			
				Блокировка.Заблокировать();
				
			КонецЕсли;
			
			ПрежнийМассивДублей = Справочники.Контрагенты.ЕстьЗаписиВРегистреДублей(СокрЛП(СтруктураПрежнихЗначений.ИНН), 
																								  СокрЛП(СтруктураПрежнихЗначений.КодПоЕДРПОУ), 
																								  Ссылка);
		Иначе
			ПрежнийМассивДублей = Новый Массив;
		КонецЕсли;
		
	Иначе
		
		ПрежнийМассивДублей = Новый Массив;
		
	КонецЕсли;
	
	Если НужноВыполнятьПроверку Тогда
		
		ОписаниеОшибки = "";
		Если ПустаяСтрока(ИНН) Тогда
			ИННВведенКорректно = Истина;
		Иначе
			ИННВведенКорректно = РегламентированныеДанныеКлиентСервер.ИННСоответствуетТребованиям(ИНН, ЭтоЮрЛицо, ОписаниеОшибки);
		КонецЕсли;
		
		Если ПустаяСтрока(КодПоЕДРПОУ) Тогда
			ЕДРПОУВведенКорректно = Истина;
		Иначе
			ЕДРПОУВведенКорректно = РегламентированныеДанныеКлиентСервер.ЕДРПОУСоответствуетТребованиям(КодПоЕДРПОУ, ОписаниеОшибки);
		КонецЕсли;
		
		Если (ИзменилсяИНН Или ИзменилсяЕДРПОУ)
			И (ИННВведенКорректно И Не ПустаяСтрока(ИНН))
			И (Не ЭтоЮрЛицо Или (ЕДРПОУВведенКорректно И Не ПустаяСтрока(КодПоЕДРПОУ))) Тогда
			
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировкиПоИНН = Блокировка.Добавить("РегистрСведений.НаличиеДублейУКонтрагентов");
			ЭлементБлокировкиПоИНН.УстановитьЗначение("ИНН", ИНН);
			ЭлементБлокировкиПоИНН.Режим = РежимБлокировкиДанных.Исключительный;
			
			Если ЭтоЮрЛицо Тогда
				
				ЭлементБлокировкиПоЕДРПОУ = Блокировка.Добавить("РегистрСведений.НаличиеДублейУКонтрагентов");
				ЭлементБлокировкиПоЕДРПОУ.УстановитьЗначение("ЕДРПОУ", КодПоЕДРПОУ);
				ЭлементБлокировкиПоЕДРПОУ.Режим = РежимБлокировкиДанных.Исключительный;
				
			КонецЕсли;
			
			Блокировка.Заблокировать();
			
			МассивДублей = Справочники.Контрагенты.ПроверитьДублиСправочникаКонтрагентыПоИННЕДРПОУ(СокрЛП(ИНН), 
																								СокрЛП(КодПоЕДРПОУ), 
																								Ссылка, Истина);
																								
			Если МассивДублей.Количество() > 0 Тогда
				
				// Для нового элемента Ссылка будет доступна только ПриЗаписи, там и запишем.
				ДополнительныеСвойства.НужноЗаписыватьВРегистрПриЗаписи = Истина;
				
				Для Каждого ЭлементМассива Из МассивДублей Цикл
					Справочники.Контрагенты.ВыполнитьДвиженияПоРегиструДублей(ЭлементМассива, ИНН, КодПоЕДРПОУ, Ложь);
				КонецЦикла;
				
			КонецЕсли;
			
			Если ПрежнийМассивДублей.Количество() > 0 Тогда
				
				Справочники.Контрагенты.ВыполнитьДвиженияПоРегиструДублей(Ссылка, СтруктураПрежнихЗначений.ИНН, СтруктураПрежнихЗначений.КодПоЕДРПОУ, Истина);
				
				Если ПрежнийМассивДублей.Количество() = 1 Тогда
					Справочники.Контрагенты.ВыполнитьДвиженияПоРегиструДублей(ПрежнийМассивДублей[0], СтруктураПрежнихЗначений.ИНН, СтруктураПрежнихЗначений.КодПоЕДРПОУ, Истина);
				КонецЕсли;
				
			КонецЕсли;
			
		Иначе
			
			Если ПрежнийМассивДублей.Количество() > 0 Тогда
				
				Справочники.Контрагенты.ВыполнитьДвиженияПоРегиструДублей(Ссылка, СтруктураПрежнихЗначений.ИНН, СтруктураПрежнихЗначений.КодПоЕДРПОУ, Истина);
			
				Если ПрежнийМассивДублей.Количество() = 1 Тогда
					Справочники.Контрагенты.ВыполнитьДвиженияПоРегиструДублей(ПрежнийМассивДублей[0], СтруктураПрежнихЗначений.ИНН, СтруктураПрежнихЗначений.КодПоЕДРПОУ, Истина);
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Процедура очищает регистр НаличиеДублейУКонтрагентов
//
Процедура УдалитьРегистрациюДублейПередУдалением()
	
	МассивДублей = Справочники.Контрагенты.ЕстьЗаписиВРегистреДублей(СокрЛП(ИНН), СокрЛП(КодПоЕДРПОУ), Ссылка);
	
	Если МассивДублей.Количество() = 1 Тогда
		
		ЭтоЮрЛицо = (ВидКонтрагента = Перечисления.ВидыКонтрагентов.ЮридическоеЛицо) Или (ВидКонтрагента = Перечисления.ВидыКонтрагентов.ГосударственныйОрган);
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировкиПоПрежнемуИНН = Блокировка.Добавить("РегистрСведений.НаличиеДублейУКонтрагентов");
		ЭлементБлокировкиПоПрежнемуИНН.УстановитьЗначение("ИНН", ИНН);
		ЭлементБлокировкиПоПрежнемуИНН.Режим = РежимБлокировкиДанных.Исключительный;
		
		Если ЭтоЮрЛицо Тогда
			
			ЭлементБлокировкиПоПрежнемуКодПоЕДРПОУ = Блокировка.Добавить("РегистрСведений.НаличиеДублейУКонтрагентов");
			ЭлементБлокировкиПоПрежнемуКодПоЕДРПОУ.УстановитьЗначение("КодПоЕДРПОУ", КодПоЕДРПОУ);
			ЭлементБлокировкиПоПрежнемуКодПоЕДРПОУ.Режим = РежимБлокировкиДанных.Исключительный;
			
		КонецЕсли;
		
		Блокировка.Заблокировать();
		
		Справочники.Контрагенты.ВыполнитьДвиженияПоРегиструДублей(МассивДублей[0], ИНН, КодПоЕДРПОУ, Истина);
		
	КонецЕсли;
	
КонецПроцедуры

// Процедура проверяет согласованность данных в ИБ с измененным состоянием контрагента
//
// Параметры:
//  Отказ	 - 	Булево - Устанавливается Истина в случае несогласованных данных
//
Процедура ПроверитьВозможностьИзменений(Отказ)
	
	// Запрет изменения аналитики учета взаиморасчетов по контрагенту при наличии движений по регистрам взаиморасчетов.
	
	ПредыдущиеЗначения = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Ссылка,
		"ВестиРасчетыПоДоговорам,ВестиРасчетыПоДокументам,ВестиРасчетыПоЗаказам");
		
	Если ВестиРасчетыПоДоговорам <> ПредыдущиеЗначения.ВестиРасчетыПоДоговорам
		Или ВестиРасчетыПоДокументам <> ПредыдущиеЗначения.ВестиРасчетыПоДокументам
		Или ВестиРасчетыПоЗаказам <> ПредыдущиеЗначения.ВестиРасчетыПоЗаказам Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	РасчетыСПокупателями.Контрагент
		|ИЗ
		|	РегистрНакопления.РасчетыСПокупателями КАК РасчетыСПокупателями
		|ГДЕ
		|	РасчетыСПокупателями.Контрагент = &Контрагент
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	РасчетыСПоставщиками.Контрагент
		|ИЗ
		|	РегистрНакопления.РасчетыСПоставщиками КАК РасчетыСПоставщиками
		|ГДЕ
		|	РасчетыСПоставщиками.Контрагент = &Контрагент";
		
		Если ВестиРасчетыПоДоговорам <> ПредыдущиеЗначения.ВестиРасчетыПоДоговорам Тогда
			Запрос.Текст = Запрос.Текст + "
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ ПЕРВЫЕ 1
			|	РасчетыСПрочимиКонтрагентами.Контрагент
			|ИЗ
			|	РегистрНакопления.РасчетыСПрочимиКонтрагентами КАК РасчетыСПрочимиКонтрагентами
			|ГДЕ
			|	РасчетыСПрочимиКонтрагентами.Контрагент = &Контрагент";
		КонецЕсли;
		
		Запрос.УстановитьПараметр("Контрагент", Ссылка);
		
		УстановитьПривилегированныйРежим(Истина);
		РезультатЗапроса = Запрос.Выполнить();
		УстановитьПривилегированныйРежим(Ложь);
		
		Если Не РезультатЗапроса.Пустой() Тогда
			ТекстСообщения = НСтр("ru='В базе присутствуют движения по взаиморасчетам с контрагентом. Изменение аналитики учета взаиморасчетов запрещено.';uk='У базі присутні рухи по взаєморозрахунках з контрагентом. Зміна аналітики обліку взаєморозрахунків заборонена.'");
			УправлениеНебольшойФирмойСервер.СообщитьОбОшибке(ЭтотОбъект, ТекстСообщения,,,, Отказ);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Процедура согласовывает состояние одних реквизитов объекта в зависимости от других
//
Процедура ПривестиДанныеКСогласованномуСостоянию()
	
	Если ВидКонтрагента = Перечисления.ВидыКонтрагентов.ЮридическоеЛицо Тогда
		
		ФИО = "";
		ИНН = Лев(ИНН, 12); // ИНН 12 цифр
		ВидГосударственногоОргана = Неопределено;
		КодГосударственногоОргана = "";
		СвидетельствоСерияНомер = "";
		СвидетельствоДатаВыдачи = '00010101';
		ДокументУдостоверяющийЛичность = "";
		Пол = Неопределено;
		ДатаРождения = '00010101';
		
		Если СтранаРегистрации = Справочники.СтраныМира.Украина Тогда
			РегистрационныйНомер = Лев(РегистрационныйНомер, 13);
			КодПоЕДРПОУ = Лев(КодПоЕДРПОУ, 12); // ЕГРПОУ не менее 8 цифр
		Иначе
			НомерСвидетельства = "";
			КодПоЕДРПОУ = "";
		КонецЕсли;
		
	ИначеЕсли ВидКонтрагента = Перечисления.ВидыКонтрагентов.ИндивидуальныйПредприниматель Тогда
		
		СтранаРегистрации = Неопределено;
		НомерСвидетельства = "";
		РегистрационныйНомер = Лев(РегистрационныйНомер, 15);
		ВидГосударственногоОргана = Неопределено;
		КодГосударственногоОргана = "";
		
	ИначеЕсли ВидКонтрагента = Перечисления.ВидыКонтрагентов.ФизическоеЛицо Тогда
		
		СтранаРегистрации = Неопределено;
		НомерСвидетельства = "";
		КодПоЕДРПОУ = "";
		РегистрационныйНомер = "";
		ВидГосударственногоОргана = Неопределено;
		КодГосударственногоОргана = "";
		СвидетельствоСерияНомер = "";
		СвидетельствоДатаВыдачи = '00010101';
		
	ИначеЕсли ВидКонтрагента = Перечисления.ВидыКонтрагентов.ГосударственныйОрган Тогда
		
		СтранаРегистрации = Неопределено;
		ФИО = "";
		КодПоЕДРПОУ = "";
		РегистрационныйНомер = "";
		СвидетельствоСерияНомер = "";
		СвидетельствоДатаВыдачи = '00010101';
		ДокументУдостоверяющийЛичность = "";
		Пол = Неопределено;
		ДатаРождения = '00010101';
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли