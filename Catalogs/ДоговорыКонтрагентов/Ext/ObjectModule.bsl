#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПриКопировании(ОбъектКопирования)
	
	// Биллинг
	ДоговорОбслуживанияНаправлениеДеятельности = Неопределено;
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.Контрагенты") Тогда
		// Программное заполнение по методу Заполнить()
		
		СтандартнаяОбработка = Ложь;
		
		ОрганизацияПоУмолчанию = УправлениеНебольшойФирмойПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(ПользователиКлиентСервер.АвторизованныйПользователь(), "ОсновнаяОрганизация");
		Если Не ЗначениеЗаполнено(ОрганизацияПоУмолчанию) Тогда
			ОрганизацияПоУмолчанию = Справочники.Организации.ОсновнаяОрганизация;
		КонецЕсли;
		
		Наименование		= "Основной договор";
		ВалютаРасчетов		= Константы.НациональнаяВалюта.Получить();
		Организация			= ОрганизацияПоУмолчанию;
		ВидДоговора			= Перечисления.ВидыДоговоров.СПокупателем;
		ВидЦен				= Справочники.ВидыЦен.ПолучитьОсновнойВидЦенПродажи();
		Владелец			= ДанныеЗаполнения;
		СрокОплатыПоставщику = Константы.СрокОплатыПоставщику.Получить();
		СрокОплатыПокупателя = Константы.СрокОплатыПокупателя.Получить();
		
		// Заполним вид цен котрагента
		Если ПолучитьФункциональнуюОпцию("УчетЦенКонтрагентов") Тогда
			
			УстановитьПривилегированныйРежим(Истина);
			
			НовыйВидЦенКонтрагентов = Справочники.ВидыЦенКонтрагентов.ВидЦенКонтрагентаПоУмолчанию(ДанныеЗаполнения);
			Если НЕ ЗначениеЗаполнено(НовыйВидЦенКонтрагентов) Тогда 
				
				НовыйВидЦенКонтрагентов = Справочники.ВидыЦенКонтрагентов.НайтиЛюбойПервыйВидЦенКонтрагента(ДанныеЗаполнения);
				
				Если НЕ ЗначениеЗаполнено(НовыйВидЦенКонтрагентов) Тогда
					
					НовыйВидЦенКонтрагентов = Справочники.ВидыЦенКонтрагентов.СоздатьВидЦенКонтрагента(ДанныеЗаполнения, ВалютаРасчетов);
					
				КонецЕсли;
				
			КонецЕсли;
			
			УстановитьПривилегированныйРежим(Ложь);
			
			ВидЦенКонтрагента = НовыйВидЦенКонтрагентов;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	// Вид цен.
	Если ЗначениеЗаполнено(ВидСкидкиНаценки) Тогда
		ПроверяемыеРеквизиты.Добавить("ВидЦен");
	КонецЕсли;
	
	// Биллинг
	Если ЭтоДоговорОбслуживания Тогда
		ПроверяемыеРеквизиты.Добавить("ВидЦен");
		ПроверяемыеРеквизиты.Добавить("ДоговорОбслуживанияДатаНачала");
		ПроверяемыеРеквизиты.Добавить("ДоговорОбслуживанияТарифныйПлан");
		ПроверяемыеРеквизиты.Добавить("ДоговорОбслуживанияПериодичность");
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ПометкаУдаления Тогда
		
		ЗначенияРеквизитовКонтрагента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Владелец, "ПометкаУдаления, ДоговорПоУмолчанию");
		
		Если Не ЗначенияРеквизитовКонтрагента.ПометкаУдаления И ЗначенияРеквизитовКонтрагента.ДоговорПоУмолчанию = Ссылка Тогда
			ТекстСообщения = НСтр("ru='Договор контрагента, установленный в качестве основного, не может быть помечен на удаление.';uk='Договір контрагента, встановлений в якості основного, не може бути позначений на видалення.'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, Ссылка,,, Отказ);
		КонецЕсли;
		
	КонецЕсли;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДоговорыКонтрагентов.Ссылка
	|ИЗ
	|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|ГДЕ
	|	ДоговорыКонтрагентов.Ссылка <> &Ссылка
	|	И ДоговорыКонтрагентов.Владелец = &Владелец
	|	И НЕ ДоговорыКонтрагентов.Владелец.ВестиРасчетыПоДоговорам");
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Владелец", Владелец);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		ТекстСообщения = НСтр("ru='Для контрагента не ведется учет по договорам.';uk='Для контрагента не ведеться облік по договорах.'");
		УправлениеНебольшойФирмойСервер.СообщитьОбОшибке(
			ЭтотОбъект,
			ТекстСообщения,
			,
			,
			,
			Отказ
		);
	КонецЕсли;
	
	// Биллинг
	Если ЭтоДоговорОбслуживания И ЭтоНовый()
		ИЛИ ЭтоДоговорОбслуживания И НЕ ЭтоНовый() И НЕ Ссылка.ЭтоДоговорОбслуживания Тогда
		
		ИспользоватьНаправленияДеятельности = Константы.БиллингВестиУчетРасходовПоДоговорамОбслуживания.Получить();
		Если ИспользоватьНаправленияДеятельности Тогда
			ДоговорОбслуживанияНаправлениеДеятельности = 
				Справочники.ДоговорыКонтрагентов.СоздатьНаправлениеДеятельностиДляДоговораОбслуживания(Владелец, Наименование);
		КонецЕсли;
	КонецЕсли;
	
	Если ЭтоДоговорОбслуживания И НЕ ПометкаУдаления Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТарифныеПланыДоговоровОбслуживанияУчетНоменклатуры.Номенклатура,
		|	ТарифныеПланыДоговоровОбслуживанияУчетНоменклатуры.Характеристика,
		|	ЕСТЬNULL(ЦеныНоменклатурыСрезПоследних.Цена, 0) КАК Цена
		|ПОМЕСТИТЬ ВТНоменклатураИЦены
		|ИЗ
		|	Справочник.ТарифныеПланыДоговоровОбслуживания.УчетНоменклатуры КАК ТарифныеПланыДоговоровОбслуживанияУчетНоменклатуры
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры.СрезПоследних(&ДатаЗаключенияДоговора, ВидЦен = &ВидЦен) КАК ЦеныНоменклатурыСрезПоследних
		|		ПО ТарифныеПланыДоговоровОбслуживанияУчетНоменклатуры.Номенклатура = ЦеныНоменклатурыСрезПоследних.Номенклатура
		|			И ТарифныеПланыДоговоровОбслуживанияУчетНоменклатуры.Характеристика = ЦеныНоменклатурыСрезПоследних.Характеристика
		|ГДЕ
		|	ТарифныеПланыДоговоровОбслуживанияУчетНоменклатуры.Ссылка = &ТарифныйПлан
		|	И ТарифныеПланыДоговоровОбслуживанияУчетНоменклатуры.ФормированиеЦены = ЗНАЧЕНИЕ(Перечисление.БиллингФормированиеЦеныНоменклатуры.ПоВидуЦенДоговора)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТНоменклатураИЦены.Номенклатура,
		|	ВТНоменклатураИЦены.Характеристика,
		|	ВТНоменклатураИЦены.Цена
		|ИЗ
		|	ВТНоменклатураИЦены КАК ВТНоменклатураИЦены
		|ГДЕ
		|	ВТНоменклатураИЦены.Цена = 0";
		
		Запрос.УстановитьПараметр("ДатаЗаключенияДоговора", ДоговорОбслуживанияДатаНачала);
		Запрос.УстановитьПараметр("ВидЦен", ВидЦен);
		Запрос.УстановитьПараметр("ТарифныйПлан", ДоговорОбслуживанияТарифныйПлан);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				СтрШаблон(НСтр("ru='Для номенклатуры ""%1"" не установлена цена по виду цен ""%2"" на дату начала действия договора (%3)!';uk='Для номенклатури ""%1"" не встановлена ​​ціна за видом цін ""%2"" на дату початку дії договору (%3)!'"),
					УправлениеНебольшойФирмойСервер.ПредставлениеНоменклатуры(Выборка.Номенклатура, Выборка.Характеристика),
					ВидЦен,
					Формат(ДоговорОбслуживанияДатаНачала, "ДЛФ=Д")
				),
				Выборка.Номенклатура,
				,,
				Отказ
			);
			
		КонецЦикла;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Ссылка) Тогда
		ДополнительныеСвойства.Вставить("ПометкаУдаления", Ссылка.ПометкаУдаления);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	// Биллинг
	Если ЭтоДоговорОбслуживания Тогда
		Если ЗначениеЗаполнено(ДоговорОбслуживанияНаправлениеДеятельности) Тогда
			// Если договор помечается (снимается пометка) на удаление, так же помечаем и связанное с ним направление деятельности.
			Если ДополнительныеСвойства.Свойство("ПометкаУдаления") Тогда
				Если ДополнительныеСвойства.ПометкаУдаления = Ложь И ПометкаУдаления = Истина
					ИЛИ ДополнительныеСвойства.ПометкаУдаления = Истина И ПометкаУдаления = Ложь Тогда
					
					НаправлениеДеятельностиОбъект = ДоговорОбслуживанияНаправлениеДеятельности.ПолучитьОбъект();
					НаправлениеДеятельностиОбъект.ПометкаУдаления = ПометкаУдаления;
					НаправлениеДеятельностиОбъект.Записать();
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура СформироватьНаименование() Экспорт
	
	Если Не ПустаяСтрока(НомерДоговора) Тогда
		Наименование = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='№ %1 %2 (%3)';uk='№ %1 %2 (%3)'"),
			СокрЛП(НомерДоговора),
			?(ЗначениеЗаполнено(ДатаДоговора), "от " + Формат(ДатаДоговора, "ДЛФ=D"), ""),
			Строка(ВалютаРасчетов));
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
