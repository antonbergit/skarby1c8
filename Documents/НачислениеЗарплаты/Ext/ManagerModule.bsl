#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

// Инициализирует таблицы значений, содержащие данные табличных частей документа.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура ИнициализироватьДанныеДокумента(ДокументСсылкаНачислениеЗарплаты, СтруктураДополнительныеСвойства) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	&Организация КАК Организация,
	|	НачислениеЗарплатыНачисленияУдержания.НомерСтроки КАК НомерСтроки,
	|	НачислениеЗарплатыНачисленияУдержания.Ссылка.Дата КАК Период,
	|	НачислениеЗарплатыНачисленияУдержания.Ссылка.ПериодРегистрации КАК ПериодРегистрации,
	|	НачислениеЗарплатыНачисленияУдержания.Ссылка.ВалютаДокумента КАК Валюта,
	|	НачислениеЗарплатыНачисленияУдержания.Ссылка.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	НачислениеЗарплатыНачисленияУдержания.Сотрудник КАК Сотрудник,
	|	НачислениеЗарплатыНачисленияУдержания.СчетЗатрат КАК СчетЗатрат,
	|	НачислениеЗарплатыНачисленияУдержания.ЗаказПокупателя КАК ЗаказПокупателя,
	|	НачислениеЗарплатыНачисленияУдержания.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	НачислениеЗарплатыНачисленияУдержания.ДатаНачала КАК ДатаНачала,
	|	НачислениеЗарплатыНачисленияУдержания.ДатаОкончания КАК ДатаОкончания,
	|	НачислениеЗарплатыНачисленияУдержания.ОтработаноДней КАК ОтработаноДней,
	|	НачислениеЗарплатыНачисленияУдержания.ОтработаноЧасов КАК ОтработаноЧасов,
	|	НачислениеЗарплатыНачисленияУдержания.Размер КАК Размер,
	|	НачислениеЗарплатыНачисленияУдержания.ВидНачисленияУдержания КАК ВидНачисленияУдержания,
	|	ВЫРАЗИТЬ(НачислениеЗарплатыНачисленияУдержания.Сумма * КурсыВалютРасчетов.Курс * КурсыВалютУчета.Кратность / (КурсыВалютУчета.Курс * КурсыВалютРасчетов.Кратность) КАК ЧИСЛО(15, 2)) КАК Сумма,
	|	НачислениеЗарплатыНачисленияУдержания.Сумма КАК СуммаВал
	|ПОМЕСТИТЬ ТаблицаНачисления
	|ИЗ
	|	Документ.НачислениеЗарплаты.НачисленияУдержания КАК НачислениеЗарплатыНачисленияУдержания
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(
	|				&МоментВремени,
	|				Валюта В
	|					(ВЫБРАТЬ
	|						Константы.ВалютаУчета
	|					ИЗ
	|						Константы КАК Константы)) КАК КурсыВалютУчета
	|		ПО (ИСТИНА)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Константы КАК Константы
	|		ПО (ИСТИНА)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&МоментВремени, ) КАК КурсыВалютРасчетов
	|		ПО НачислениеЗарплатыНачисленияУдержания.Ссылка.ВалютаДокумента = КурсыВалютРасчетов.Валюта
	|ГДЕ
	|	НачислениеЗарплатыНачисленияУдержания.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Организация,
	|	НачислениеЗарплатыНачисленияУдержания.НомерСтроки,
	|	НачислениеЗарплатыНачисленияУдержания.Ссылка.Дата,
	|	НачислениеЗарплатыНачисленияУдержания.Ссылка.ПериодРегистрации,
	|	НачислениеЗарплатыНачисленияУдержания.Ссылка.ВалютаДокумента,
	|	НачислениеЗарплатыНачисленияУдержания.Ссылка.СтруктурнаяЕдиница,
	|	НачислениеЗарплатыНачисленияУдержания.Сотрудник,
	|	НачислениеЗарплатыНачисленияУдержания.ВидНачисленияУдержания.ВидНалога.СчетУчета,
	|	ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка),
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка),
	|	НачислениеЗарплатыНачисленияУдержания.Ссылка.ПериодРегистрации,
	|	КОНЕЦПЕРИОДА(НачислениеЗарплатыНачисленияУдержания.Ссылка.ПериодРегистрации, МЕСЯЦ),
	|	0,
	|	0,
	|	0,
	|	НачислениеЗарплатыНачисленияУдержания.ВидНачисленияУдержания,
	|	ВЫРАЗИТЬ(НачислениеЗарплатыНачисленияУдержания.Сумма * КурсыВалютРасчетов.Курс * КурсыВалютУчета.Кратность / (КурсыВалютУчета.Курс * КурсыВалютРасчетов.Кратность) КАК ЧИСЛО(15, 2)),
	|	НачислениеЗарплатыНачисленияУдержания.Сумма
	|ИЗ
	|	Документ.НачислениеЗарплаты.НалогиНаДоходы КАК НачислениеЗарплатыНачисленияУдержания
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(
	|				&МоментВремени,
	|				Валюта В
	|					(ВЫБРАТЬ
	|						Константы.ВалютаУчета
	|					ИЗ
	|						Константы КАК Константы)) КАК КурсыВалютУчета
	|		ПО (ИСТИНА)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Константы КАК Константы
	|		ПО (ИСТИНА)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&МоментВремени, ) КАК КурсыВалютРасчетов
	|		ПО НачислениеЗарплатыНачисленияУдержания.Ссылка.ВалютаДокумента = КурсыВалютРасчетов.Валюта
	|ГДЕ
	|	НачислениеЗарплатыНачисленияУдержания.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&Организация КАК Организация,
	|	НачислениеЗарплатыНачисленияУдержания.Период КАК Период,
	|	НачислениеЗарплатыНачисленияУдержания.ПериодРегистрации КАК ПериодРегистрации,
	|	НачислениеЗарплатыНачисленияУдержания.Валюта КАК Валюта,
	|	НачислениеЗарплатыНачисленияУдержания.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	НачислениеЗарплатыНачисленияУдержания.Сотрудник КАК Сотрудник,
	|	НачислениеЗарплатыНачисленияУдержания.ДатаНачала КАК ДатаНачала,
	|	НачислениеЗарплатыНачисленияУдержания.ДатаОкончания КАК ДатаОкончания,
	|	НачислениеЗарплатыНачисленияУдержания.ОтработаноДней КАК ОтработаноДней,
	|	НачислениеЗарплатыНачисленияУдержания.ОтработаноЧасов КАК ОтработаноЧасов,
	|	НачислениеЗарплатыНачисленияУдержания.Размер КАК Размер,
	|	НачислениеЗарплатыНачисленияУдержания.ВидНачисленияУдержания КАК ВидНачисленияУдержания,
	|	НачислениеЗарплатыНачисленияУдержания.Сумма КАК Сумма,
	|	НачислениеЗарплатыНачисленияУдержания.СуммаВал КАК СуммаВал
	|ИЗ
	|	ТаблицаНачисления КАК НачислениеЗарплатыНачисленияУдержания
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&Организация КАК Организация,
	|	НачислениеЗарплатыНачисленияУдержания.Период КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	НачислениеЗарплатыНачисленияУдержания.ПериодРегистрации КАК ПериодРегистрации,
	|	НачислениеЗарплатыНачисленияУдержания.Валюта КАК Валюта,
	|	НачислениеЗарплатыНачисленияУдержания.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	НачислениеЗарплатыНачисленияУдержания.Сотрудник КАК Сотрудник,
	|	ВЫБОР
	|		КОГДА НачислениеЗарплатыНачисленияУдержания.ВидНачисленияУдержания.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыНачисленийИУдержаний.Начисление)
	|			ТОГДА НачислениеЗарплатыНачисленияУдержания.СуммаВал
	|		ИНАЧЕ -1 * НачислениеЗарплатыНачисленияУдержания.СуммаВал
	|	КОНЕЦ КАК СуммаВал,
	|	ВЫБОР
	|		КОГДА НачислениеЗарплатыНачисленияУдержания.ВидНачисленияУдержания.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыНачисленийИУдержаний.Начисление)
	|			ТОГДА НачислениеЗарплатыНачисленияУдержания.Сумма
	|		ИНАЧЕ -1 * НачислениеЗарплатыНачисленияУдержания.Сумма
	|	КОНЕЦ КАК Сумма,
	|	НачислениеЗарплатыНачисленияУдержания.Сотрудник.СчетРасчетовСПерсоналом КАК СчетУчета,
	|	ЗНАЧЕНИЕ(ВидДвиженияБухгалтерии.Кредит) КАК ВидДвиженияУправленческий,
	|	ВЫБОР
	|		КОГДА НачислениеЗарплатыНачисленияУдержания.ВидНачисленияУдержания.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыНачисленийИУдержаний.Налог)
	|			ТОГДА ВЫРАЗИТЬ(&НачисленНалог КАК СТРОКА(100))
	|		ИНАЧЕ ВЫРАЗИТЬ(&НачислениеЗарплаты КАК СТРОКА(100))
	|	КОНЕЦ КАК СодержаниеПроводки
	|ИЗ
	|	ТаблицаНачисления КАК НачислениеЗарплатыНачисленияУдержания
	|ГДЕ
	|	НачислениеЗарплатыНачисленияУдержания.СуммаВал <> 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НачислениеЗарплатыНачисленияУдержания.НомерСтроки КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	НачислениеЗарплатыНачисленияУдержания.ПериодРегистрации КАК Период,
	|	&Организация КАК Организация,
	|	НачислениеЗарплатыНачисленияУдержания.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	НачислениеЗарплатыНачисленияУдержания.СчетЗатрат КАК СчетЗатрат,
	|	НачислениеЗарплатыНачисленияУдержания.СчетЗатрат КАК СчетУчета,
	|	НачислениеЗарплатыНачисленияУдержания.ЗаказПокупателя КАК ЗаказПокупателя,
	|	ВЫБОР
	|		КОГДА НачислениеЗарплатыНачисленияУдержания.ВидНачисленияУдержания.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыНачисленийИУдержаний.Удержание)
	|			ТОГДА -1 * НачислениеЗарплатыНачисленияУдержания.Сумма
	|		ИНАЧЕ НачислениеЗарплатыНачисленияУдержания.Сумма
	|	КОНЕЦ КАК Сумма,
	|	ЗНАЧЕНИЕ(ВидДвиженияБухгалтерии.Дебет) КАК ВидДвиженияУправленческий,
	|	ИСТИНА КАК ФиксированнаяСтоимость,
	|	ВЫРАЗИТЬ(&НачислениеЗарплаты КАК СТРОКА(100)) КАК СодержаниеПроводки
	|ИЗ
	|	ТаблицаНачисления КАК НачислениеЗарплатыНачисленияУдержания
	|ГДЕ
	|	НачислениеЗарплатыНачисленияУдержания.ВидНачисленияУдержания.Тип <> ЗНАЧЕНИЕ(Перечисление.ТипыНачисленийИУдержаний.Налог)
	|	И (НачислениеЗарплатыНачисленияУдержания.СчетЗатрат.ТипСчета = ЗНАЧЕНИЕ(Перечисление.ТипыСчетов.НезавершенноеПроизводство)
	|			ИЛИ НачислениеЗарплатыНачисленияУдержания.СчетЗатрат.ТипСчета = ЗНАЧЕНИЕ(Перечисление.ТипыСчетов.КосвенныеЗатраты))
	|	И НачислениеЗарплатыНачисленияУдержания.СуммаВал <> 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НачислениеЗарплатыНачисленияУдержания.НомерСтроки КАК НомерСтроки,
	|	НачислениеЗарплатыНачисленияУдержания.ПериодРегистрации КАК Период,
	|	&Организация КАК Организация,
	|	ВЫБОР
	|		КОГДА НачислениеЗарплатыНачисленияУдержания.СчетЗатрат.ТипСчета = ЗНАЧЕНИЕ(Перечисление.ТипыСчетов.Расходы)
	|			ТОГДА НачислениеЗарплатыНачисленияУдержания.НаправлениеДеятельности
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.Прочее)
	|	КОНЕЦ КАК НаправлениеДеятельности,
	|	ВЫБОР
	|		КОГДА НачислениеЗарплатыНачисленияУдержания.СчетЗатрат.ТипСчета = ЗНАЧЕНИЕ(Перечисление.ТипыСчетов.Расходы)
	|			ТОГДА НачислениеЗарплатыНачисленияУдержания.СтруктурнаяЕдиница
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК СтруктурнаяЕдиница,
	|	НачислениеЗарплатыНачисленияУдержания.СчетЗатрат КАК СчетУчета,
	|	НачислениеЗарплатыНачисленияУдержания.СчетЗатрат КАК СчетЗатрат,
	|	НачислениеЗарплатыНачисленияУдержания.Сотрудник КАК Аналитика,
	|	ВЫБОР
	|		КОГДА НачислениеЗарплатыНачисленияУдержания.ВидНачисленияУдержания.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыНачисленийИУдержаний.Начисление)
	|			ТОГДА НачислениеЗарплатыНачисленияУдержания.Сумма
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаРасходов,
	|	ВЫБОР
	|		КОГДА НачислениеЗарплатыНачисленияУдержания.ВидНачисленияУдержания.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыНачисленийИУдержаний.Удержание)
	|			ТОГДА НачислениеЗарплатыНачисленияУдержания.Сумма
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаДоходов,
	|	ВЫБОР
	|		КОГДА НачислениеЗарплатыНачисленияУдержания.ВидНачисленияУдержания.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыНачисленийИУдержаний.Удержание)
	|			ТОГДА -1 * НачислениеЗарплатыНачисленияУдержания.Сумма
	|		ИНАЧЕ НачислениеЗарплатыНачисленияУдержания.Сумма
	|	КОНЕЦ КАК Сумма,
	|	ВЫБОР
	|		КОГДА НачислениеЗарплатыНачисленияУдержания.СчетЗатрат.ТипСчета = ЗНАЧЕНИЕ(Перечисление.ТипыСчетов.Расходы)
	|			ТОГДА НачислениеЗарплатыНачисленияУдержания.ЗаказПокупателя
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК ЗаказПокупателя,
	|	ЗНАЧЕНИЕ(ВидДвиженияБухгалтерии.Дебет) КАК ВидДвиженияУправленческий,
	|	ВЫРАЗИТЬ(&НачислениеЗарплаты КАК СТРОКА(100)) КАК СодержаниеПроводки
	|ИЗ
	|	ТаблицаНачисления КАК НачислениеЗарплатыНачисленияУдержания
	|ГДЕ
	|	НачислениеЗарплатыНачисленияУдержания.ВидНачисленияУдержания.Тип <> ЗНАЧЕНИЕ(Перечисление.ТипыНачисленийИУдержаний.Налог)
	|	И НачислениеЗарплатыНачисленияУдержания.СчетЗатрат.ТипСчета В (ЗНАЧЕНИЕ(Перечисление.ТипыСчетов.Расходы), ЗНАЧЕНИЕ(Перечисление.ТипыСчетов.ПрочиеРасходы), ЗНАЧЕНИЕ(Перечисление.ТипыСчетов.Доходы), ЗНАЧЕНИЕ(Перечисление.ТипыСчетов.ПрочиеДоходы))
	|	И НачислениеЗарплатыНачисленияУдержания.СуммаВал <> 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&Организация КАК Организация,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	НачислениеЗарплатыНачисленияУдержания.ПериодРегистрации КАК Период,
	|	НачислениеЗарплатыНачисленияУдержания.ВидНачисленияУдержания.ВидНалога КАК ВидНалога,
	|	НачислениеЗарплатыНачисленияУдержания.ВидНачисленияУдержания.ВидНалога.СчетУчета КАК СчетУчета,
	|	НачислениеЗарплатыНачисленияУдержания.Сумма,
	|	ВЫРАЗИТЬ(&НачисленНалог КАК СТРОКА(100)) КАК СодержаниеПроводки
	|ИЗ
	|	ТаблицаНачисления КАК НачислениеЗарплатыНачисленияУдержания
	|ГДЕ
	|	НачислениеЗарплатыНачисленияУдержания.ВидНачисленияУдержания.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыНачисленийИУдержаний.Налог)
	|	И НачислениеЗарплатыНачисленияУдержания.СуммаВал <> 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НачислениеЗарплатыНачисленияУдержания.НомерСтроки КАК НомерСтроки,
	|	НачислениеЗарплатыНачисленияУдержания.ПериодРегистрации КАК Период,
	|	&Организация КАК Организация,
	|	ЗНАЧЕНИЕ(Справочник.СценарииПланирования.Фактический) КАК СценарийПланирования,
	|	ВЫБОР
	|		КОГДА НачислениеЗарплатыНачисленияУдержания.ВидНачисленияУдержания.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыНачисленийИУдержаний.Начисление)
	|			ТОГДА НачислениеЗарплатыНачисленияУдержания.СчетЗатрат
	|		ИНАЧЕ НачислениеЗарплатыНачисленияУдержания.Сотрудник.СчетРасчетовСПерсоналом
	|	КОНЕЦ КАК СчетДт,
	|	ВЫБОР
	|		КОГДА (НачислениеЗарплатыНачисленияУдержания.ВидНачисленияУдержания.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыНачисленийИУдержаний.Удержание)
	|					ИЛИ НачислениеЗарплатыНачисленияУдержания.ВидНачисленияУдержания.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыНачисленийИУдержаний.Налог))
	|					И НачислениеЗарплатыНачисленияУдержания.Сотрудник.СчетРасчетовСПерсоналом.Валютный
	|				ИЛИ НачислениеЗарплатыНачисленияУдержания.ВидНачисленияУдержания.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыНачисленийИУдержаний.Начисление)
	|					И НачислениеЗарплатыНачисленияУдержания.СчетЗатрат.Валютный
	|			ТОГДА НачислениеЗарплатыНачисленияУдержания.Валюта
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК ВалютаДт,
	|	ВЫБОР
	|		КОГДА (НачислениеЗарплатыНачисленияУдержания.ВидНачисленияУдержания.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыНачисленийИУдержаний.Удержание)
	|					ИЛИ НачислениеЗарплатыНачисленияУдержания.ВидНачисленияУдержания.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыНачисленийИУдержаний.Налог))
	|					И НачислениеЗарплатыНачисленияУдержания.Сотрудник.СчетРасчетовСПерсоналом.Валютный
	|				ИЛИ НачислениеЗарплатыНачисленияУдержания.ВидНачисленияУдержания.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыНачисленийИУдержаний.Начисление)
	|					И НачислениеЗарплатыНачисленияУдержания.СчетЗатрат.Валютный
	|			ТОГДА НачислениеЗарплатыНачисленияУдержания.СуммаВал
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаВалДт,
	|	ВЫБОР
	|		КОГДА НачислениеЗарплатыНачисленияУдержания.ВидНачисленияУдержания.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыНачисленийИУдержаний.Начисление)
	|			ТОГДА НачислениеЗарплатыНачисленияУдержания.Сотрудник.СчетРасчетовСПерсоналом
	|		ИНАЧЕ НачислениеЗарплатыНачисленияУдержания.СчетЗатрат
	|	КОНЕЦ КАК СчетКт,
	|	ВЫБОР
	|		КОГДА (НачислениеЗарплатыНачисленияУдержания.ВидНачисленияУдержания.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыНачисленийИУдержаний.Удержание)
	|					ИЛИ НачислениеЗарплатыНачисленияУдержания.ВидНачисленияУдержания.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыНачисленийИУдержаний.Налог))
	|					И НачислениеЗарплатыНачисленияУдержания.СчетЗатрат.Валютный
	|				ИЛИ НачислениеЗарплатыНачисленияУдержания.ВидНачисленияУдержания.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыНачисленийИУдержаний.Начисление)
	|					И НачислениеЗарплатыНачисленияУдержания.Сотрудник.СчетРасчетовСПерсоналом.Валютный
	|			ТОГДА НачислениеЗарплатыНачисленияУдержания.Валюта
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК ВалютаКт,
	|	ВЫБОР
	|		КОГДА (НачислениеЗарплатыНачисленияУдержания.ВидНачисленияУдержания.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыНачисленийИУдержаний.Удержание)
	|					ИЛИ НачислениеЗарплатыНачисленияУдержания.ВидНачисленияУдержания.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыНачисленийИУдержаний.Налог))
	|					И НачислениеЗарплатыНачисленияУдержания.СчетЗатрат.Валютный
	|				ИЛИ НачислениеЗарплатыНачисленияУдержания.ВидНачисленияУдержания.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыНачисленийИУдержаний.Начисление)
	|					И НачислениеЗарплатыНачисленияУдержания.Сотрудник.СчетРасчетовСПерсоналом.Валютный
	|			ТОГДА НачислениеЗарплатыНачисленияУдержания.СуммаВал
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаВалКт,
	|	НачислениеЗарплатыНачисленияУдержания.Сумма КАК Сумма,
	|	ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА НачислениеЗарплатыНачисленияУдержания.ВидНачисленияУдержания.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыНачисленийИУдержаний.Налог)
	|				ТОГДА &НачисленНалог
	|			ИНАЧЕ &НачислениеЗарплаты
	|		КОНЕЦ КАК СТРОКА(100)) КАК Содержание
	|ИЗ
	|	ТаблицаНачисления КАК НачислениеЗарплатыНачисленияУдержания
	|ГДЕ
	|	НачислениеЗарплатыНачисленияУдержания.СуммаВал <> 0";
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаНачислениеЗарплаты);
	Запрос.УстановитьПараметр("МоментВремени", Новый Граница(СтруктураДополнительныеСвойства.ДляПроведения.МоментВремени, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("Организация", СтруктураДополнительныеСвойства.ДляПроведения.Организация);
	Запрос.УстановитьПараметр("ОрганизацияДляРегОтчетности", ДокументСсылкаНачислениеЗарплаты.Организация);
	Запрос.УстановитьПараметр("НачислениеЗарплаты", НСтр("ru='Начисление зарплаты';uk='Нарахування зарплати'"));
	Запрос.УстановитьПараметр("НачисленНалог", НСтр("ru='Начислен налог';uk='Нарахований податок'"));
	Запрос.УстановитьПараметр("НачислениеВзносов", НСтр("ru='Начислен взнос';uk='Нарахований внесок'"));
	Запрос.УстановитьПараметр("УдержаниеВСчетПогашенияЗаймаИПроцентов", НСтр("ru='Удержание в счет погашения займа и процентов';uk='Утримання в рахунок погашення позики й відсотків'"));
	Запрос.УстановитьПараметр("УправленческиеРасходы", ПланыСчетов.Управленческий.УправленческиеРасходы);
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаНачисленияИУдержания", МассивРезультатов[1].Выгрузить());
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаРасчетыСПерсоналом", МассивРезультатов[2].Выгрузить());
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаЗапасы", МассивРезультатов[3].Выгрузить());
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаДоходыИРасходы", МассивРезультатов[4].Выгрузить());
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаРасчетыПоНалогам", МассивРезультатов[5].Выгрузить());
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаУправленческий", МассивРезультатов[6].Выгрузить());
	
	// Налоги и взносы
	СформироватьТаблицуВзносы(ДокументСсылкаНачислениеЗарплаты, СтруктураДополнительныеСвойства);
	СформироватьТаблицуВзносыФОТ(ДокументСсылкаНачислениеЗарплаты, СтруктураДополнительныеСвойства);
	СформироватьТаблицуВзносыНДФЛ(ДокументСсылкаНачислениеЗарплаты, СтруктураДополнительныеСвойства);
	СформироватьТаблицуНДФЛ(ДокументСсылкаНачислениеЗарплаты, СтруктураДополнительныеСвойства);
	// Конец Налоги и взносы
	
	// Прочие Расчеты
	СформироватьТаблицаРасчетыПоКредитамИЗаймам(ДокументСсылкаНачислениеЗарплаты, СтруктураДополнительныеСвойства);
	СформироватьТаблицыПоУчетуЗаймов(ДокументСсылкаНачислениеЗарплаты, СтруктураДополнительныеСвойства);
	// Конец Прочие Расчеты
	
КонецПроцедуры // ИнициализироватьДанныеДокумента()

// Выполняет контроль возникновения отрицательных остатков.
//
Процедура ВыполнитьКонтроль(ДокументСсылкаНачислениеЗарплаты, ДополнительныеСвойства, Отказ, УдалениеПроведения = Ложь) Экспорт
	
	Если Не УправлениеНебольшойФирмойСервер.ВыполнитьКонтрольОстатков() Тогда
		Возврат;
	КонецЕсли;

	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	
КонецПроцедуры

#Область ИнтерфейсПечати

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	
	
КонецПроцедуры

#КонецОбласти

#Область РасчетыПоКредитамИЗаймам

Процедура СформироватьТаблицаРасчетыПоКредитамИЗаймам(ДокументСсылкаНачислениеЗарплаты, СтруктураДополнительныеСвойства)
	
	Если ДокументСсылкаНачислениеЗарплаты.ПогашениеЗаймов.Количество() = 0 Тогда
		СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаРасчетыПоКредитамИЗаймам", Новый ТаблицаЗначений);
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Организация", СтруктураДополнительныеСвойства.ДляПроведения.Организация);
	Запрос.УстановитьПараметр("УчетРасчетовПоЗаймамСотрудникам", НСтр("ru='Учет расчетов по займам сотрудникам';uk='Облік розрахунків по позиках співробітникам'"));
	Запрос.УстановитьПараметр("УдержаниеОсновногоДолгаПоЗайму", НСтр("ru='Удержание основного долга по займу';uk='Утримання основного боргу по позиці'"));
	Запрос.УстановитьПараметр("УдержаниеПроцентовПоЗайму", НСтр("ru='Удержание процентов по займу';uk='Утримання відсотків по позиці'"));
	Запрос.УстановитьПараметр("НачислениеПроцентовПоЗайму", НСтр("ru='Начисление процентов по займу';uk='Нарахування відсотків по позиці'"));
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаНачислениеЗарплаты);
	Запрос.УстановитьПараметр("МоментВремени", Новый Граница(СтруктураДополнительныеСвойства.ДляПроведения.МоментВремени, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("ПериодКонтроля", СтруктураДополнительныеСвойства.ДляПроведения.МоментВремени.Дата);
	Запрос.УстановитьПараметр("КурсоваяРазница", НСтр("ru='Курсовая разница';uk='Курсова різниця'"));
	
	Запрос.УстановитьПараметр("ВалютаУчета", Константы.ВалютаУчета.Получить());
	Запрос.УстановитьПараметр("ВалютаДС", ДокументСсылкаНачислениеЗарплаты.ВалютаДокумента);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	&Организация,
	|	НачислениеЗарплатыПогашениеЗаймов.НомерСтроки,
	|	НачислениеЗарплатыПогашениеЗаймов.Ссылка,
	|	НачислениеЗарплатыПогашениеЗаймов.Ссылка.Дата КАК Период,
	|	НачислениеЗарплатыПогашениеЗаймов.Ссылка.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	НачислениеЗарплатыПогашениеЗаймов.Ссылка.ПериодРегистрации,
	|	НачислениеЗарплатыПогашениеЗаймов.Ссылка.ВалютаДокумента КАК Валюта,
	|	НачислениеЗарплатыПогашениеЗаймов.Сотрудник,
	|	НачислениеЗарплатыПогашениеЗаймов.Сотрудник.СчетРасчетовСПерсоналом КАК СчетУчетаСотрудник,
	|	НачислениеЗарплатыПогашениеЗаймов.ДоговорЗаймаСотруднику КАК ДоговорЗаймаСотруднику,
	|	НачислениеЗарплатыПогашениеЗаймов.ДоговорЗаймаСотруднику.СчетУчета КАК СчетУчетаДоговорЗайма,
	|	НачислениеЗарплатыПогашениеЗаймов.ДоговорЗаймаСотруднику.ВалютаРасчетов КАК ВалютаДоговораЗайма,
	|	НачислениеЗарплатыПогашениеЗаймов.ДоговорЗаймаСотруднику.СчетУчетаПроцентов КАК СчетУчетаПроцентов,
	|	НачислениеЗарплатыПогашениеЗаймов.ДоговорЗаймаСотруднику.СчетЗатрат КАК СчетЗатрат,
	|	НачислениеЗарплатыПогашениеЗаймов.Сотрудник.СчетРасчетовСПерсоналом КАК СчетРасчетовСПерсоналом,
	|	НачислениеЗарплатыПогашениеЗаймов.ДоговорЗаймаСотруднику.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	НачислениеЗарплатыПогашениеЗаймов.ПогашеноЗайма + НачислениеЗарплатыПогашениеЗаймов.ПогашеноПроцентов КАК СуммаВал,
	|	ВЫРАЗИТЬ((НачислениеЗарплатыПогашениеЗаймов.ПогашеноЗайма + НачислениеЗарплатыПогашениеЗаймов.ПогашеноПроцентов) * КурсыВалютРасчетов.Курс * КурсыВалютУчета.Кратность / (КурсыВалютУчета.Курс * КурсыВалютРасчетов.Кратность) КАК ЧИСЛО(15, 2)) КАК Сумма,
	|	ВЫРАЗИТЬ((НачислениеЗарплатыПогашениеЗаймов.ПогашеноЗайма + НачислениеЗарплатыПогашениеЗаймов.ПогашеноПроцентов) * КурсыВалютРасчетов.Курс * КурсыВалютДоговораЗайма.Кратность / (КурсыВалютДоговораЗайма.Курс * КурсыВалютРасчетов.Кратность) КАК ЧИСЛО(15, 2)) КАК СуммаВалДоговора,
	|	НачислениеЗарплатыПогашениеЗаймов.ПогашеноЗайма КАК ПогашеноЗаймаВал,
	|	ВЫРАЗИТЬ(НачислениеЗарплатыПогашениеЗаймов.ПогашеноЗайма * КурсыВалютРасчетов.Курс * КурсыВалютУчета.Кратность / (КурсыВалютУчета.Курс * КурсыВалютРасчетов.Кратность) КАК ЧИСЛО(15, 2)) КАК ПогашеноЗайма,
	|	ВЫРАЗИТЬ(НачислениеЗарплатыПогашениеЗаймов.ПогашеноЗайма * КурсыВалютРасчетов.Курс * КурсыВалютДоговораЗайма.Кратность / (КурсыВалютДоговораЗайма.Курс * КурсыВалютРасчетов.Кратность) КАК ЧИСЛО(15, 2)) КАК ПогашеноЗаймаВалДоговора,
	|	НачислениеЗарплатыПогашениеЗаймов.ПогашеноПроцентов КАК ПогашеноПроцентовВал,
	|	ВЫРАЗИТЬ(НачислениеЗарплатыПогашениеЗаймов.ПогашеноПроцентов * КурсыВалютРасчетов.Курс * КурсыВалютУчета.Кратность / (КурсыВалютУчета.Курс * КурсыВалютРасчетов.Кратность) КАК ЧИСЛО(15, 2)) КАК ПогашеноПроцентов,
	|	ВЫРАЗИТЬ(НачислениеЗарплатыПогашениеЗаймов.ПогашеноПроцентов * КурсыВалютРасчетов.Курс * КурсыВалютДоговораЗайма.Кратность / (КурсыВалютДоговораЗайма.Курс * КурсыВалютРасчетов.Кратность) КАК ЧИСЛО(15, 2)) КАК ПогашеноПроцентовВалДоговора,
	|	НачислениеЗарплатыПогашениеЗаймов.НачисленоПроцентов КАК НачисленоПроцентовВал,
	|	ВЫРАЗИТЬ(НачислениеЗарплатыПогашениеЗаймов.НачисленоПроцентов * КурсыВалютРасчетов.Курс * КурсыВалютУчета.Кратность / (КурсыВалютУчета.Курс * КурсыВалютРасчетов.Кратность) КАК ЧИСЛО(15, 2)) КАК НачисленоПроцентов,
	|	ВЫРАЗИТЬ(НачислениеЗарплатыПогашениеЗаймов.НачисленоПроцентов * КурсыВалютРасчетов.Курс * КурсыВалютДоговораЗайма.Кратность / (КурсыВалютДоговораЗайма.Курс * КурсыВалютРасчетов.Кратность) КАК ЧИСЛО(15, 2)) КАК НачисленоПроцентовВалДоговора
	|ПОМЕСТИТЬ ТаблицаЗаймы
	|ИЗ
	|	Документ.НачислениеЗарплаты.ПогашениеЗаймов КАК НачислениеЗарплатыПогашениеЗаймов
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(
	|				&МоментВремени,
	|				Валюта В
	|					(ВЫБРАТЬ
	|						Константы.ВалютаУчета
	|					ИЗ
	|						Константы КАК Константы)) КАК КурсыВалютУчета
	|		ПО (ИСТИНА)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Константы КАК Константы
	|		ПО (ИСТИНА)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&МоментВремени, ) КАК КурсыВалютРасчетов
	|		ПО НачислениеЗарплатыПогашениеЗаймов.Ссылка.ВалютаДокумента = КурсыВалютРасчетов.Валюта
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&МоментВремени, ) КАК КурсыВалютДоговораЗайма
	|		ПО НачислениеЗарплатыПогашениеЗаймов.ДоговорЗаймаСотруднику.ВалютаРасчетов = КурсыВалютДоговораЗайма.Валюта
	|ГДЕ
	|	НачислениеЗарплатыПогашениеЗаймов.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&Организация КАК Организация,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	ТаблицаЗаймы.Период КАК Дата,
	|	ТаблицаЗаймы.Период КАК Период,
	|	&УдержаниеОсновногоДолгаПоЗайму КАК СодержаниеПроводки,
	|	ТаблицаЗаймы.Сотрудник КАК Контрагент,
	|	ТаблицаЗаймы.ПогашеноЗаймаВалДоговора КАК ОсновнойДолгВал,
	|	ТаблицаЗаймы.ПогашеноЗайма КАК ОсновнойДолг,
	|	ТаблицаЗаймы.ПогашеноЗаймаВалДоговора КАК ОсновнойДолгВалДляОстатка,
	|	ТаблицаЗаймы.ПогашеноЗайма КАК ОсновнойДолгДляОстатка,
	|	0 КАК ПроцентыВал,
	|	0 КАК Проценты,
	|	0 КАК ПроцентыВалДляОстатка,
	|	0 КАК ПроцентыДляОстатка,
	|	0 КАК КомиссияВал,
	|	0 КАК Комиссия,
	|	0 КАК КомиссияВалДляОстатка,
	|	0 КАК КомиссияДляОстатка,
	|	ТаблицаЗаймы.ДоговорЗаймаСотруднику КАК ДоговорКредитаЗайма,
	|	ТаблицаЗаймы.ВалютаДоговораЗайма КАК Валюта,
	|	ТаблицаЗаймы.СчетУчетаДоговорЗайма КАК СчетУчета,
	|	ИСТИНА КАК УдержаноИзЗарплаты,
	|	ТаблицаЗаймы.ДоговорЗаймаСотруднику.ВидДоговора КАК ВидДоговора,
	|	ТаблицаЗаймы.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	ТаблицаЗаймы.ПогашеноЗаймаВалДоговора КАК СуммаВал,
	|	ТаблицаЗаймы.ПогашеноЗайма КАК Сумма
	|ПОМЕСТИТЬ ВременнаяТаблицаРасчетыПоКредитамИЗаймам
	|ИЗ
	|	ТаблицаЗаймы КАК ТаблицаЗаймы
	|ГДЕ
	|	ТаблицаЗаймы.ПогашеноЗаймаВал <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Организация,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход),
	|	ТаблицаЗаймы.Период,
	|	ТаблицаЗаймы.Период,
	|	&УдержаниеПроцентовПоЗайму,
	|	ТаблицаЗаймы.Сотрудник,
	|	0,
	|	0,
	|	0,
	|	0,
	|	ТаблицаЗаймы.ПогашеноПроцентовВалДоговора,
	|	ТаблицаЗаймы.ПогашеноПроцентов,
	|	ТаблицаЗаймы.ПогашеноПроцентовВалДоговора,
	|	ТаблицаЗаймы.ПогашеноПроцентов,
	|	0,
	|	0,
	|	0,
	|	0,
	|	ТаблицаЗаймы.ДоговорЗаймаСотруднику,
	|	ТаблицаЗаймы.ВалютаДоговораЗайма,
	|	ТаблицаЗаймы.СчетУчетаПроцентов,
	|	ИСТИНА,
	|	ТаблицаЗаймы.ДоговорЗаймаСотруднику.ВидДоговора,
	|	ТаблицаЗаймы.СтруктурнаяЕдиница,
	|	ТаблицаЗаймы.ПогашеноПроцентовВалДоговора,
	|	ТаблицаЗаймы.ПогашеноПроцентов
	|ИЗ
	|	ТаблицаЗаймы КАК ТаблицаЗаймы
	|ГДЕ
	|	ТаблицаЗаймы.ПогашеноПроцентовВал <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Организация,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход),
	|	ТаблицаЗаймы.Период,
	|	ТаблицаЗаймы.Период,
	|	&НачислениеПроцентовПоЗайму,
	|	ТаблицаЗаймы.Сотрудник,
	|	0,
	|	0,
	|	0,
	|	0,
	|	ТаблицаЗаймы.НачисленоПроцентовВалДоговора,
	|	ТаблицаЗаймы.НачисленоПроцентов,
	|	-ТаблицаЗаймы.НачисленоПроцентовВалДоговора,
	|	-ТаблицаЗаймы.НачисленоПроцентов,
	|	0,
	|	0,
	|	0,
	|	0,
	|	ТаблицаЗаймы.ДоговорЗаймаСотруднику,
	|	ТаблицаЗаймы.ВалютаДоговораЗайма,
	|	ТаблицаЗаймы.СчетУчетаПроцентов,
	|	ЛОЖЬ,
	|	ТаблицаЗаймы.ДоговорЗаймаСотруднику.ВидДоговора,
	|	ТаблицаЗаймы.СтруктурнаяЕдиница,
	|	ТаблицаЗаймы.НачисленоПроцентовВалДоговора,
	|	ТаблицаЗаймы.НачисленоПроцентов
	|ИЗ
	|	ТаблицаЗаймы КАК ТаблицаЗаймы
	|ГДЕ
	|	ТаблицаЗаймы.НачисленоПроцентовВал <> 0";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	// Установка исключительной блокировки контролируемых остатков расчетов с подотчетными лицами.
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВременнаяТаблицаРасчетыПоКредитамИЗаймам.Организация КАК Организация,
	|	ВременнаяТаблицаРасчетыПоКредитамИЗаймам.Контрагент КАК Контрагент,
	|	ВременнаяТаблицаРасчетыПоКредитамИЗаймам.ДоговорКредитаЗайма КАК ДоговорКредитаЗайма
	|ИЗ
	|	ВременнаяТаблицаРасчетыПоКредитамИЗаймам КАК ВременнаяТаблицаРасчетыПоКредитамИЗаймам";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.РасчетыПоКредитамИЗаймам");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = РезультатЗапроса;
	
	Для каждого КолонкаРезультатЗапроса Из РезультатЗапроса.Колонки Цикл
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных(КолонкаРезультатЗапроса.Имя, КолонкаРезультатЗапроса.Имя);
	КонецЦикла;
	Блокировка.Заблокировать();
	
	НомерЗапроса = 0;
	
	ЕстьСтруктурнаяЕдиница = Истина;
	Запрос.Текст = УправлениеНебольшойФирмойСервер.ПолучитьТекстЗапросаКурсовыеРазницыРасчетыПоКредитамИЗаймам(Запрос.МенеджерВременныхТаблиц, НомерЗапроса, ЕстьСтруктурнаяЕдиница);
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаРасчетыПоКредитамИЗаймам", МассивРезультатов[НомерЗапроса].Выгрузить());
	
КонецПроцедуры

Процедура СформироватьТаблицыПоУчетуЗаймов(ДокументСсылкаНачислениеЗарплаты, СтруктураДополнительныеСвойства)
	
	Если ДокументСсылкаНачислениеЗарплаты.ПогашениеЗаймов.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаНачислениеЗарплаты);
	Запрос.УстановитьПараметр("МоментВремени", Новый Граница(СтруктураДополнительныеСвойства.ДляПроведения.МоментВремени, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("Организация", СтруктураДополнительныеСвойства.ДляПроведения.Организация);
	Запрос.УстановитьПараметр("НачислениеЗарплаты", НСтр("ru='Начисление зарплаты';uk='Нарахування зарплати'"));
	Запрос.УстановитьПараметр("НачисленНалог", НСтр("ru='Начислен налог';uk='Нарахований податок'"));
	Запрос.УстановитьПараметр("УдержаниеВСчетПогашенияЗаймаИПроцентов", НСтр("ru='Удержание в счет погашения займа и процентов';uk='Утримання в рахунок погашення позики й відсотків'"));
	Запрос.УстановитьПараметр("УдержаниеПроцентовПоЗайму", НСтр("ru='Удержание процентов по займу';uk='Утримання відсотків по позиці'"));
	Запрос.УстановитьПараметр("КурсоваяРазница", НСтр("ru='Курсовая разница';uk='Курсова різниця'"));
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	&Организация,
	|	ТаблицаЗаймы.Период КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ТаблицаЗаймы.ПериодРегистрации,
	|	ТаблицаЗаймы.Валюта,
	|	ТаблицаЗаймы.СтруктурнаяЕдиница,
	|	ТаблицаЗаймы.Сотрудник,
	|	-ТаблицаЗаймы.СуммаВал КАК СуммаВал,
	|	-ТаблицаЗаймы.Сумма КАК Сумма,
	|	ТаблицаЗаймы.СчетУчетаДоговорЗайма,
	|	ЗНАЧЕНИЕ(ВидДвиженияБухгалтерии.Кредит) КАК ВидДвиженияУправленческий,
	|	ВЫРАЗИТЬ(&УдержаниеВСчетПогашенияЗаймаИПроцентов КАК СТРОКА(100)) КАК СодержаниеПроводки
	|ИЗ
	|	ТаблицаЗаймы КАК ТаблицаЗаймы
	|ГДЕ
	|	ТаблицаЗаймы.Сумма <> 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаЗаймы.НомерСтроки,
	|	ТаблицаЗаймы.ПериодРегистрации КАК Период,
	|	ТаблицаЗаймы.Организация,
	|	ВЫБОР
	|		КОГДА ТаблицаЗаймы.СчетУчетаПроцентов.ТипСчета = ЗНАЧЕНИЕ(Перечисление.ТипыСчетов.Расходы)
	|			ТОГДА ТаблицаЗаймы.НаправлениеДеятельности
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.Прочее)
	|	КОНЕЦ КАК НаправлениеДеятельности,
	|	ВЫБОР
	|		КОГДА ТаблицаЗаймы.СчетУчетаПроцентов.ТипСчета = ЗНАЧЕНИЕ(Перечисление.ТипыСчетов.Расходы)
	|			ТОГДА ТаблицаЗаймы.СтруктурнаяЕдиница
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК СтруктурнаяЕдиница,
	|	ТаблицаЗаймы.СчетЗатрат КАК СчетУчета,
	|	ТаблицаЗаймы.Сотрудник КАК Аналитика,
	|	0 КАК СуммаРасходов,
	|	ТаблицаЗаймы.НачисленоПроцентов КАК СуммаДоходов,
	|	ВЫРАЗИТЬ(&УдержаниеПроцентовПоЗайму КАК СТРОКА(100)) КАК СодержаниеПроводки
	|ИЗ
	|	ТаблицаЗаймы КАК ТаблицаЗаймы
	|ГДЕ
	|	ТаблицаЗаймы.НачисленоПроцентов <> 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДокумента.Дата,
	|	ТаблицаДокумента.Организация,
	|	ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА ТаблицаДокумента.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|				ТОГДА ТаблицаДокумента.Контрагент.СчетРасчетовСПерсоналом
	|			ИНАЧЕ ТаблицаДокумента.СчетУчета
	|		КОНЕЦ КАК ПланСчетов.Управленческий) КАК СчетДт,
	|	ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА ТаблицаДокумента.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|				ТОГДА ТаблицаДокумента.СчетУчета
	|			ИНАЧЕ ТаблицаДокумента.ДоговорКредитаЗайма.СчетЗатрат
	|		КОНЕЦ КАК ПланСчетов.Управленческий) КАК СчетКт,
	|	ТаблицаДокумента.Валюта,
	|	ТаблицаДокумента.СуммаВал,
	|	ТаблицаДокумента.Сумма,
	|	ВЫРАЗИТЬ(ТаблицаДокумента.СодержаниеПроводки КАК СТРОКА(100)) КАК СодержаниеПроводки
	|ПОМЕСТИТЬ ВременнаяТаблицаРасчетыПоКредитамИЗаймамДляПроводок
	|ИЗ
	|	ВременнаяТаблицаРасчетыПоКредитамИЗаймам КАК ТаблицаДокумента
	|ГДЕ
	|	ТаблицаДокумента.Сумма <> 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	1 КАК НомерСтроки,
	|	ТаблицаДокумента.Дата КАК Период,
	|	ТаблицаДокумента.Организация КАК Организация,
	|	ЗНАЧЕНИЕ(Справочник.СценарииПланирования.Фактический) КАК СценарийПланирования,
	|	ТаблицаДокумента.СчетДт КАК СчетДт,
	|	ТаблицаДокумента.СчетКт КАК СчетКт,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СчетДт.Валютный
	|			ТОГДА ТаблицаДокумента.Валюта
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК ВалютаДт,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СчетКт.Валютный
	|			ТОГДА ТаблицаДокумента.Валюта
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК ВалютаКт,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СчетДт.Валютный
	|			ТОГДА ТаблицаДокумента.СуммаВал
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаВалДт,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СчетКт.Валютный
	|			ТОГДА ТаблицаДокумента.СуммаВал
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаВалКт,
	|	ТаблицаДокумента.Сумма КАК Сумма,
	|	ВЫРАЗИТЬ(ТаблицаДокумента.СодержаниеПроводки КАК СТРОКА(100)) КАК Содержание
	|ИЗ
	|	ВременнаяТаблицаРасчетыПоКредитамИЗаймамДляПроводок КАК ТаблицаДокумента
	|ГДЕ
	|	ТаблицаДокумента.Сумма <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	1,
	|	ТаблицаДокумента.Дата,
	|	ТаблицаДокумента.Организация,
	|	ЗНАЧЕНИЕ(Справочник.СценарииПланирования.Фактический),
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА ТаблицаДокумента.СчетУчета
	|		ИНАЧЕ ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ПрочиеДоходы)
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ПрочиеРасходы)
	|		ИНАЧЕ ТаблицаДокумента.СчетУчета
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|				И ТаблицаДокумента.СчетУчета.Валютный
	|			ТОГДА ТаблицаДокумента.Валюта
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы < 0
	|				И ТаблицаДокумента.СчетУчета.Валютный
	|			ТОГДА ТаблицаДокумента.Валюта
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	0,
	|	0,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА ТаблицаДокумента.СуммаКурсовойРазницы
	|		ИНАЧЕ -ТаблицаДокумента.СуммаКурсовойРазницы
	|	КОНЕЦ,
	|	ВЫРАЗИТЬ(&КурсоваяРазница КАК СТРОКА(100))
	|ИЗ
	|	ВременнаяТаблицаКурсовыхРазницРасчетыПоКредитамИЗаймам КАК ТаблицаДокумента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&Организация,
	|	НачислениеЗарплатыНачисленияУдержания.НомерСтроки КАК НомерСтроки,
	|	НачислениеЗарплатыНачисленияУдержания.Ссылка.Дата КАК Период,
	|	НачислениеЗарплатыНачисленияУдержания.Ссылка.ПериодРегистрации КАК ПериодРегистрации,
	|	НачислениеЗарплатыНачисленияУдержания.Ссылка.ВалютаДокумента КАК Валюта,
	|	НачислениеЗарплатыНачисленияУдержания.Ссылка.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	НачислениеЗарплатыНачисленияУдержания.Сотрудник КАК Сотрудник,
	|	НачислениеЗарплатыНачисленияУдержания.ДоговорЗаймаСотруднику.СчетУчета КАК СчетЗатрат,
	|	ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка) КАК ЗаказПокупателя,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельности,
	|	НачислениеЗарплатыНачисленияУдержания.Ссылка.ПериодРегистрации КАК ДатаНачала,
	|	КОНЕЦПЕРИОДА(НачислениеЗарплатыНачисленияУдержания.Ссылка.ПериодРегистрации, МЕСЯЦ) КАК ДатаОкончания,
	|	0 КАК ОтработаноДней,
	|	0 КАК ОтработаноЧасов,
	|	0 КАК Размер,
	|	ВЫБОР
	|		КОГДА НачислениеЗарплатыНачисленияУдержания.ДоговорЗаймаСотруднику.УдержаниеОсновнойДолг = НЕОПРЕДЕЛЕНО
	|				ИЛИ НачислениеЗарплатыНачисленияУдержания.ДоговорЗаймаСотруднику.УдержаниеОсновнойДолг = ЗНАЧЕНИЕ(Справочник.ВидыНачисленийИУдержаний.ПустаяСсылка)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.ВидыНачисленийИУдержаний.ПогашениеЗаймаИзЗарплаты)
	|		ИНАЧЕ НачислениеЗарплатыНачисленияУдержания.ДоговорЗаймаСотруднику.УдержаниеОсновнойДолг
	|	КОНЕЦ КАК ВидНачисленияУдержания,
	|	ВЫРАЗИТЬ(НачислениеЗарплатыНачисленияУдержания.ПогашеноЗайма * КурсыВалютРасчетов.Курс * КурсыВалютУчета.Кратность / (КурсыВалютУчета.Курс * КурсыВалютРасчетов.Кратность) КАК ЧИСЛО(15, 2)) КАК Сумма,
	|	НачислениеЗарплатыНачисленияУдержания.ПогашеноЗайма КАК СуммаВал
	|ИЗ
	|	Документ.НачислениеЗарплаты.ПогашениеЗаймов КАК НачислениеЗарплатыНачисленияУдержания
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(
	|				&МоментВремени,
	|				Валюта В
	|					(ВЫБРАТЬ
	|						Константы.ВалютаУчета
	|					ИЗ
	|						Константы КАК Константы)) КАК КурсыВалютУчета
	|		ПО (ИСТИНА)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Константы КАК Константы
	|		ПО (ИСТИНА)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&МоментВремени, ) КАК КурсыВалютРасчетов
	|		ПО НачислениеЗарплатыНачисленияУдержания.Ссылка.ВалютаДокумента = КурсыВалютРасчетов.Валюта
	|ГДЕ
	|	НачислениеЗарплатыНачисленияУдержания.Ссылка = &Ссылка
	|	И НачислениеЗарплатыНачисленияУдержания.ПогашеноЗайма > 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Организация,
	|	НачислениеЗарплатыНачисленияУдержания.НомерСтроки,
	|	НачислениеЗарплатыНачисленияУдержания.Ссылка.Дата,
	|	НачислениеЗарплатыНачисленияУдержания.Ссылка.ПериодРегистрации,
	|	НачислениеЗарплатыНачисленияУдержания.Ссылка.ВалютаДокумента,
	|	НачислениеЗарплатыНачисленияУдержания.Ссылка.СтруктурнаяЕдиница,
	|	НачислениеЗарплатыНачисленияУдержания.Сотрудник,
	|	НачислениеЗарплатыНачисленияУдержания.ДоговорЗаймаСотруднику.СчетУчета,
	|	ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка),
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка),
	|	НачислениеЗарплатыНачисленияУдержания.Ссылка.ПериодРегистрации,
	|	КОНЕЦПЕРИОДА(НачислениеЗарплатыНачисленияУдержания.Ссылка.ПериодРегистрации, МЕСЯЦ),
	|	0,
	|	0,
	|	0,
	|	ВЫБОР
	|		КОГДА НачислениеЗарплатыНачисленияУдержания.ДоговорЗаймаСотруднику.УдержаниеПроценты = НЕОПРЕДЕЛЕНО
	|				ИЛИ НачислениеЗарплатыНачисленияУдержания.ДоговорЗаймаСотруднику.УдержаниеПроценты = ЗНАЧЕНИЕ(Справочник.ВидыНачисленийИУдержаний.ПустаяСсылка)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.ВидыНачисленийИУдержаний.ПроцентыПоЗайму)
	|		ИНАЧЕ НачислениеЗарплатыНачисленияУдержания.ДоговорЗаймаСотруднику.УдержаниеПроценты
	|	КОНЕЦ,
	|	ВЫРАЗИТЬ(НачислениеЗарплатыНачисленияУдержания.ПогашеноПроцентов * КурсыВалютРасчетов.Курс * КурсыВалютУчета.Кратность / (КурсыВалютУчета.Курс * КурсыВалютРасчетов.Кратность) КАК ЧИСЛО(15, 2)),
	|	НачислениеЗарплатыНачисленияУдержания.ПогашеноПроцентов
	|ИЗ
	|	Документ.НачислениеЗарплаты.ПогашениеЗаймов КАК НачислениеЗарплатыНачисленияУдержания
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(
	|				&МоментВремени,
	|				Валюта В
	|					(ВЫБРАТЬ
	|						Константы.ВалютаУчета
	|					ИЗ
	|						Константы КАК Константы)) КАК КурсыВалютУчета
	|		ПО (ИСТИНА)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Константы КАК Константы
	|		ПО (ИСТИНА)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&МоментВремени, ) КАК КурсыВалютРасчетов
	|		ПО НачислениеЗарплатыНачисленияУдержания.Ссылка.ВалютаДокумента = КурсыВалютРасчетов.Валюта
	|ГДЕ
	|	НачислениеЗарплатыНачисленияУдержания.Ссылка = &Ссылка
	|	И НачислениеЗарплатыНачисленияУдержания.ПогашеноПроцентов > 0";
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	ТекущаяТаблица = МассивРезультатов[0].Выгрузить();
	Если ЗначениеЗаполнено(СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаРасчетыСПерсоналом) Тогда
		Для каждого ТекущаяСтрока Из ТекущаяТаблица Цикл
			НоваяСтрока = СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаРасчетыСПерсоналом.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущаяСтрока);
		КонецЦикла;
	Иначе
		СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаРасчетыСПерсоналом", ТекущаяТаблица);
	КонецЕсли;
	
	ТекущаяТаблица = МассивРезультатов[1].Выгрузить();
	Если ЗначениеЗаполнено(СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаДоходыИРасходы) Тогда
		Для каждого ТекущаяСтрока Из ТекущаяТаблица Цикл
			НоваяСтрока = СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаДоходыИРасходы.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущаяСтрока);
		КонецЦикла;
	Иначе
		СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаДоходыИРасходы", ТекущаяТаблица);
	КонецЕсли;
	
	ТекущаяТаблица = МассивРезультатов[3].Выгрузить();
	Если ЗначениеЗаполнено(СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаУправленческий) Тогда
		Для каждого ТекущаяСтрока Из ТекущаяТаблица Цикл
			НоваяСтрока = СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаУправленческий.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущаяСтрока);
		КонецЦикла;
	Иначе
		СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаУправленческий", ТекущаяТаблица);
	КонецЕсли;
	
	ТекущаяТаблица = МассивРезультатов[4].Выгрузить();
	Если ЗначениеЗаполнено(СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаНачисленияИУдержания) Тогда
		Для каждого ТекущаяСтрока Из ТекущаяТаблица Цикл
			НоваяСтрока = СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаНачисленияИУдержания.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущаяСтрока);
		КонецЦикла;
	Иначе
		СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаНачисленияИУдержания", ТекущаяТаблица);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область НалогиИВзносы

Процедура СформироватьТаблицуВзносы(ДокументСсылкаНачислениеЗарплаты, СтруктураДополнительныеСвойства)
	
	ТаблицаВзносы = Новый ТаблицаЗначений;
	
	ТаблицаВзносы.Колонки.Добавить("ВидДвижения");
	ТаблицаВзносы.Колонки.Добавить("Организация");
	ТаблицаВзносы.Колонки.Добавить("Период");
	ТаблицаВзносы.Колонки.Добавить("НалоговыйПериод");
	ТаблицаВзносы.Колонки.Добавить("Налог");
	ТаблицаВзносы.Колонки.Добавить("Ставка");
	ТаблицаВзносы.Колонки.Добавить("База");
	ТаблицаВзносы.Колонки.Добавить("Результат");
	ТаблицаВзносы.Колонки.Добавить("Предел");
	ТаблицаВзносы.Колонки.Добавить("ФизЛицо");
	
	Если ДокументСсылкаНачислениеЗарплаты.Взносы.Количество() = 0 Тогда
		СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаВзносы", ТаблицаВзносы);
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаНачислениеЗарплаты);
	Запрос.УстановитьПараметр("Организация", СтруктураДополнительныеСвойства.ДляПроведения.Организация);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	&Организация,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	НачислениеЗарплатыВзносы.Ссылка.Дата КАК Период,
	|	НачислениеЗарплатыВзносы.Ссылка.ПериодРегистрации КАК НалоговыйПериод,
	|	НачислениеЗарплатыВзносы.Сотрудник КАК ФизЛицо,
	|	НачислениеЗарплатыВзносы.ВидНачисленияУдержания КАК Налог,
	|	НачислениеЗарплатыВзносы.База,
	|	НачислениеЗарплатыВзносы.Сумма КАК Результат,
	|	НачислениеЗарплатыВзносы.Ставка1,
	|	НачислениеЗарплатыВзносы.Ставка2,
	|	НачислениеЗарплатыВзносы.Порог
	|ИЗ
	|	Документ.НачислениеЗарплаты.Взносы КАК НачислениеЗарплатыВзносы
	|ГДЕ
	|	НачислениеЗарплатыВзносы.Ссылка = &Ссылка";
	
	тчВзносы = Запрос.Выполнить().Выгрузить();
	
	Для Каждого ТекСтрокаВзносыФОТ Из тчВзносы Цикл
		Если (ТекСтрокаВзносыФОТ.Результат <> 0) Тогда
			// регистр ВзаиморасчетыПоВзносамВФонды Приход
			Если ((ТекСтрокаВзносыФОТ.Налог = Справочники.ВидыНачисленийИУдержаний.Пенсионный1_2)
				ИЛИ (ТекСтрокаВзносыФОТ.Налог = Справочники.ВидыНачисленийИУдержаний.Пенсионный1_5)
				ИЛИ (ТекСтрокаВзносыФОТ.Налог = Справочники.ВидыНачисленийИУдержаний.ПенсионныйФОТ)
				ИЛИ (ТекСтрокаВзносыФОТ.Налог = Справочники.ВидыНачисленийИУдержаний.ПенсионныйФОТИнв))
				И (ТекСтрокаВзносыФОТ.База > ТекСтрокаВзносыФОТ.Порог) Тогда
				
				Ставка1 = ТекСтрокаВзносыФОТ.Ставка1;
				База1 = ТекСтрокаВзносыФОТ.Порог;
				Результат1 = База1 * Ставка1 / 100;
				НоваяСтрока = ТаблицаВзносы.Добавить();
			    ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекСтрокаВзносыФОТ);
				НоваяСтрока.Результат = Результат1; 
				НоваяСтрока.База = База1;
				НоваяСтрока.Ставка = Ставка1;
				НоваяСтрока.Предел = ТекСтрокаВзносыФОТ.Порог;

				Ставка2 = ТекСтрокаВзносыФОТ.Ставка2;
				База2 = ТекСтрокаВзносыФОТ.База - ТекСтрокаВзносыФОТ.Порог;
				Результат2 = База2 * Ставка2 / 100;
				НоваяСтрока = ТаблицаВзносы.Добавить();
			    ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекСтрокаВзносыФОТ);
				НоваяСтрока.Результат = Результат2; 
				НоваяСтрока.База = База2; 
				НоваяСтрока.Ставка = Ставка2;
				НоваяСтрока.Предел = ТекСтрокаВзносыФОТ.Порог;
			Иначе
				Если ТекСтрокаВзносыФОТ.База >= ТекСтрокаВзносыФОТ.Порог Тогда
					Ставка = ТекСтрокаВзносыФОТ.Ставка1;
				Иначе
					Ставка = ТекСтрокаВзносыФОТ.Ставка2;
				КонецЕсли;
				
				База = ТекСтрокаВзносыФОТ.База;
				Результат = ТекСтрокаВзносыФОТ.Результат;
				НоваяСтрока = ТаблицаВзносы.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекСтрокаВзносыФОТ);
				НоваяСтрока.Результат = Результат; 
				НоваяСтрока.База = База; 
				НоваяСтрока.Ставка = Ставка;
				НоваяСтрока.Предел = ТекСтрокаВзносыФОТ.Порог;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаВзносы", ТаблицаВзносы);
	
КонецПроцедуры

Процедура СформироватьТаблицуВзносыФОТ(ДокументСсылкаНачислениеЗарплаты, СтруктураДополнительныеСвойства)
	
	Если ДокументСсылкаНачислениеЗарплаты.ВзносыФОТ.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Организация", СтруктураДополнительныеСвойства.ДляПроведения.Организация);
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаНачислениеЗарплаты);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	&Организация,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	НачислениеЗарплатыВзносы.Ссылка.Дата КАК Период,
	|	НачислениеЗарплатыВзносы.Ссылка.ПериодРегистрации КАК НалоговыйПериод,
	|	НачислениеЗарплатыВзносы.Сотрудник КАК ФизЛицо,
	|	НачислениеЗарплатыВзносы.ВидНачисленияУдержания КАК Налог,
	|	НачислениеЗарплатыВзносы.База,
	|	НачислениеЗарплатыВзносы.Сумма КАК Результат,
	|	НачислениеЗарплатыВзносы.Ставка1,
	|	НачислениеЗарплатыВзносы.Ставка2,
	|	НачислениеЗарплатыВзносы.Порог
	|ИЗ
	|	Документ.НачислениеЗарплаты.ВзносыФОТ КАК НачислениеЗарплатыВзносы
	|ГДЕ
	|	НачислениеЗарплатыВзносы.Ссылка = &Ссылка";
	
	тчВзносыФОТ = Запрос.Выполнить().Выгрузить();
	
	ТаблицаВзносыФОТ = Новый ТаблицаЗначений;
	
	ТаблицаВзносыФОТ.Колонки.Добавить("ВидДвижения");
	ТаблицаВзносыФОТ.Колонки.Добавить("Организация");
	ТаблицаВзносыФОТ.Колонки.Добавить("Период");
	ТаблицаВзносыФОТ.Колонки.Добавить("НалоговыйПериод");
	ТаблицаВзносыФОТ.Колонки.Добавить("Налог");
	ТаблицаВзносыФОТ.Колонки.Добавить("Ставка");
	ТаблицаВзносыФОТ.Колонки.Добавить("База");
	ТаблицаВзносыФОТ.Колонки.Добавить("Результат");
	ТаблицаВзносыФОТ.Колонки.Добавить("Предел");
	ТаблицаВзносыФОТ.Колонки.Добавить("ФизЛицо");
	
	Для Каждого ТекСтрокаВзносыФОТ Из тчВзносыФОТ Цикл
		Если (ТекСтрокаВзносыФОТ.Результат <> 0) Тогда
			// регистр ВзаиморасчетыПоВзносамВФонды Приход
			Если ((ТекСтрокаВзносыФОТ.Налог = Справочники.ВидыНачисленийИУдержаний.Пенсионный1_2)
				ИЛИ (ТекСтрокаВзносыФОТ.Налог = Справочники.ВидыНачисленийИУдержаний.Пенсионный1_5)
				ИЛИ (ТекСтрокаВзносыФОТ.Налог = Справочники.ВидыНачисленийИУдержаний.ПенсионныйФОТ)
				ИЛИ (ТекСтрокаВзносыФОТ.Налог = Справочники.ВидыНачисленийИУдержаний.ПенсионныйФОТИнв))
				И (ТекСтрокаВзносыФОТ.База > ТекСтрокаВзносыФОТ.Порог) Тогда
				
				Ставка1 = ТекСтрокаВзносыФОТ.Ставка1;
				База1 = ТекСтрокаВзносыФОТ.Порог;
				Результат1 = База1 * Ставка1 / 100;
				НоваяСтрока = ТаблицаВзносыФОТ.Добавить();
			    ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекСтрокаВзносыФОТ);
				НоваяСтрока.Результат = Результат1; 
				НоваяСтрока.База = База1;
				НоваяСтрока.Ставка = Ставка1;
				НоваяСтрока.Предел = ТекСтрокаВзносыФОТ.Порог;

				Ставка2 = ТекСтрокаВзносыФОТ.Ставка2;
				База2 = ТекСтрокаВзносыФОТ.База - ТекСтрокаВзносыФОТ.Порог;
				Результат2 = База2 * Ставка2 / 100;
				НоваяСтрока = ТаблицаВзносыФОТ.Добавить();
			    ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекСтрокаВзносыФОТ);
				НоваяСтрока.Результат = Результат2; 
				НоваяСтрока.База = База2; 
				НоваяСтрока.Ставка = Ставка2;
				НоваяСтрока.Предел = ТекСтрокаВзносыФОТ.Порог;
			Иначе
				Если ТекСтрокаВзносыФОТ.База >= ТекСтрокаВзносыФОТ.Порог Тогда
					Ставка = ТекСтрокаВзносыФОТ.Ставка1;
				Иначе
					Ставка = ТекСтрокаВзносыФОТ.Ставка2;
				КонецЕсли;
				
				База = ТекСтрокаВзносыФОТ.База;
				Результат = ТекСтрокаВзносыФОТ.Результат;
				НоваяСтрока = ТаблицаВзносыФОТ.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекСтрокаВзносыФОТ);
				НоваяСтрока.Результат = Результат; 
				НоваяСтрока.База = База; 
				НоваяСтрока.Ставка = Ставка;
				НоваяСтрока.Предел = ТекСтрокаВзносыФОТ.Порог;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ТаблицаВзносыФОТ, СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаВзносы); 
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаВзносы", СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаВзносы);
	
КонецПроцедуры

Процедура СформироватьТаблицуВзносыНДФЛ(ДокументСсылкаНачислениеЗарплаты, СтруктураДополнительныеСвойства)
	
	Если ДокументСсылкаНачислениеЗарплаты.НалогиНаДоходы.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Организация", СтруктураДополнительныеСвойства.ДляПроведения.Организация);
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаНачислениеЗарплаты);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	&Организация,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	НачислениеЗарплатыНалогиНаДоходы.Ссылка.Дата КАК Период,
	|	НачислениеЗарплатыНалогиНаДоходы.Ссылка.ПериодРегистрации КАК НалоговыйПериод,
	|	НачислениеЗарплатыНалогиНаДоходы.Сотрудник КАК ФизЛицо,
	|	НачислениеЗарплатыНалогиНаДоходы.ВидНачисленияУдержания,
	|	НачислениеЗарплатыНалогиНаДоходы.Сумма,
	|	НачислениеЗарплатыНалогиНаДоходы.Доход,
	|	НачислениеЗарплатыНалогиНаДоходы.Льгота,
	|	НачислениеЗарплатыНалогиНаДоходы.Ставка1
	|ИЗ
	|	Документ.НачислениеЗарплаты.НалогиНаДоходы КАК НачислениеЗарплатыНалогиНаДоходы
	|ГДЕ
	|	НачислениеЗарплатыНалогиНаДоходы.Ссылка = &Ссылка";
	
	тчВзносыНДФЛ = Запрос.Выполнить().Выгрузить();
	
	ТаблицаВзносыНДФЛ = Новый ТаблицаЗначений;
	
	ТаблицаВзносыНДФЛ.Колонки.Добавить("ВидДвижения");
	ТаблицаВзносыНДФЛ.Колонки.Добавить("Организация");
	ТаблицаВзносыНДФЛ.Колонки.Добавить("Период");
	ТаблицаВзносыНДФЛ.Колонки.Добавить("НалоговыйПериод");
	ТаблицаВзносыНДФЛ.Колонки.Добавить("Налог");
	ТаблицаВзносыНДФЛ.Колонки.Добавить("Ставка");
	ТаблицаВзносыНДФЛ.Колонки.Добавить("База");
	ТаблицаВзносыНДФЛ.Колонки.Добавить("Результат");
	ТаблицаВзносыНДФЛ.Колонки.Добавить("Предел");
	ТаблицаВзносыНДФЛ.Колонки.Добавить("ФизЛицо");
	
	Для Каждого ТекСтрокаНДФЛ Из тчВзносыНДФЛ Цикл
		Если ТекСтрокаНДФЛ.Сумма > 0 Тогда
			НоваяСтрока = ТаблицаВзносыНДФЛ.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекСтрокаНДФЛ);
			НоваяСтрока.Результат = ТекСтрокаНДФЛ.Сумма; 
			НоваяСтрока.База = ТекСтрокаНДФЛ.Доход;
			НоваяСтрока.Ставка = ТекСтрокаНДФЛ.Ставка1;
			НоваяСтрока.Предел = 0;
			НоваяСтрока.Налог = ТекСтрокаНДФЛ.ВидНачисленияУдержания;
		КонецЕсли;
	КонецЦикла;
	
	ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ТаблицаВзносыНДФЛ, СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаВзносы); 
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаВзносы", СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаВзносы);
	
КонецПроцедуры

Процедура СформироватьТаблицуНДФЛ(ДокументСсылкаНачислениеЗарплаты, СтруктураДополнительныеСвойства)
	
	Если ДокументСсылкаНачислениеЗарплаты.НалогиНаДоходы.Количество() = 0 Тогда
		СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаНДФЛ", Новый ТаблицаЗначений);
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Организация", СтруктураДополнительныеСвойства.ДляПроведения.Организация);
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаНачислениеЗарплаты);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	&Организация,
	|	НачислениеЗарплатыНалогиНаДоходы.Ссылка.Дата КАК Период,
	|	НачислениеЗарплатыНалогиНаДоходы.Ссылка.ПериодРегистрации КАК НалоговыйПериод,
	|	НачислениеЗарплатыНалогиНаДоходы.Сотрудник КАК ФизЛицо,
	|	НачислениеЗарплатыНалогиНаДоходы.ВидНачисленияУдержания,
	|	НачислениеЗарплатыНалогиНаДоходы.Сумма,
	|	НачислениеЗарплатыНалогиНаДоходы.Доход,
	|	НачислениеЗарплатыНалогиНаДоходы.Льгота,
	|	НачислениеЗарплатыНалогиНаДоходы.Ставка1
	|ИЗ
	|	Документ.НачислениеЗарплаты.НалогиНаДоходы КАК НачислениеЗарплатыНалогиНаДоходы
	|ГДЕ
	|	НачислениеЗарплатыНалогиНаДоходы.Ссылка = &Ссылка";
	
	тчНДФЛ = Запрос.Выполнить().Выгрузить();
	
	ТаблицаНДФЛ = Новый ТаблицаЗначений;
	
	ТаблицаНДФЛ.Колонки.Добавить("Период");
	ТаблицаНДФЛ.Колонки.Добавить("Организация");
	ТаблицаНДФЛ.Колонки.Добавить("ФизЛицо");
	ТаблицаНДФЛ.Колонки.Добавить("НалоговыйПериод");
	ТаблицаНДФЛ.Колонки.Добавить("ДоходНДФЛ");
	ТаблицаНДФЛ.Колонки.Добавить("Налог");
	ТаблицаНДФЛ.Колонки.Добавить("Доход");
	ТаблицаНДФЛ.Колонки.Добавить("Льгота");
	
	Для Каждого ТекСтрокаНДФЛ Из тчНДФЛ Цикл
		Если ТекСтрокаНДФЛ.Сумма > 0 Тогда
			НоваяСтрока = ТаблицаНДФЛ.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекСтрокаНДФЛ);
			НоваяСтрока.Налог = ТекСтрокаНДФЛ.Сумма; 
			НоваяСтрока.Доход = ТекСтрокаНДФЛ.Доход;
			НоваяСтрока.Льгота = ТекСтрокаНДФЛ.Льгота;
			НоваяСтрока.ДоходНДФЛ = ТекСтрокаНДФЛ.ВидНачисленияУдержания;
		КонецЕсли;
	КонецЦикла;
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаНДФЛ", ТаблицаНДФЛ);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли