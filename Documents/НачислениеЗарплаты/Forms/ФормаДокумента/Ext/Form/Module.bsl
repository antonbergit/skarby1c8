
#Region СлужебныеПроцедурыИФункции

&НаСервере
// Процедура заполняет структуру данных для выбора счета расчета
//
Процедура ПолучитьДанныеДляВыбораСчетаРасчетов(СтруктураДанных)
	
	ДоступныеТипыСчетовУчета = Новый Массив;
	ВидНачисленияУдержания = СтруктураДанных.ВидНачисленияУдержания;
	Если НЕ ЗначениеЗаполнено(ВидНачисленияУдержания) Тогда
		
		ДоступныеТипыСчетовУчета.Добавить(Перечисления.ТипыСчетов.НезавершенноеПроизводство);
		ДоступныеТипыСчетовУчета.Добавить(Перечисления.ТипыСчетов.КосвенныеЗатраты);
		ДоступныеТипыСчетовУчета.Добавить(Перечисления.ТипыСчетов.Расходы);
		ДоступныеТипыСчетовУчета.Добавить(Перечисления.ТипыСчетов.ПрочиеРасходы);
		ДоступныеТипыСчетовУчета.Добавить(Перечисления.ТипыСчетов.ПрочиеВнеоборотныеАктивы);
		ДоступныеТипыСчетовУчета.Добавить(Перечисления.ТипыСчетов.ПрочиеДоходы);
		
	ИначеЕсли ВидНачисленияУдержания.Тип = Перечисления.ТипыНачисленийИУдержаний.Начисление Тогда
		
		ДоступныеТипыСчетовУчета.Добавить(Перечисления.ТипыСчетов.НезавершенноеПроизводство);
		ДоступныеТипыСчетовУчета.Добавить(Перечисления.ТипыСчетов.КосвенныеЗатраты);
		ДоступныеТипыСчетовУчета.Добавить(Перечисления.ТипыСчетов.Расходы);
		ДоступныеТипыСчетовУчета.Добавить(Перечисления.ТипыСчетов.ПрочиеРасходы);
		ДоступныеТипыСчетовУчета.Добавить(Перечисления.ТипыСчетов.ПрочиеВнеоборотныеАктивы);
		
	ИначеЕсли ВидНачисленияУдержания.Тип = Перечисления.ТипыНачисленийИУдержаний.Удержание Тогда
		
		ДоступныеТипыСчетовУчета.Добавить(Перечисления.ТипыСчетов.ПрочиеДоходы);
		
	КонецЕсли;
	
	СтруктураДанных.Вставить("ДоступныеТипыСчетовУчета", ДоступныеТипыСчетовУчета);
	
КонецПроцедуры // ПолучитьДанныеДляВыбораСчетаРасчетов()

// Процедура заполняет таблицу показателей параметрами.
//
&НаСервере
Функция ЗаполнитьПоказатели(ВидНачисленияУдержания)

	СтруктураВозврата = Новый Структура;
	СтруктураВозврата.Вставить("Показатель1", "");
	СтруктураВозврата.Вставить("Представление1", Справочники.ПараметрыРасчетов.ПустаяСсылка());
	СтруктураВозврата.Вставить("Значение1", 0);
	СтруктураВозврата.Вставить("Показатель2", "");
	СтруктураВозврата.Вставить("Представление2", Справочники.ПараметрыРасчетов.ПустаяСсылка());
	СтруктураВозврата.Вставить("Значение2", 0);
	СтруктураВозврата.Вставить("Показатель3", "");
	СтруктураВозврата.Вставить("Представление3", Справочники.ПараметрыРасчетов.ПустаяСсылка());
	СтруктураВозврата.Вставить("Значение3", 0);
	
	// 1. Проверка
	Если НЕ ЗначениеЗаполнено(ВидНачисленияУдержания) Тогда
		Возврат СтруктураВозврата;
	КонецЕсли; 
	
	// 2. Поиск всех параметров-идентификаторов формулы
	СтруктураПараметров = Новый Структура;
	УправлениеНебольшойФирмойСервер.ДобавитьПараметрыВСтруктуру(ВидНачисленияУдержания.Формула, СтруктураПараметров);
		
	// 3. Добавление показателя
	Счетчик = 0;
	Для каждого ПараметрСтруктуры Из СтруктураПараметров Цикл
		
		Если ПараметрСтруктуры.Ключ = "ОтработаноДней" 
			ИЛИ ПараметрСтруктуры.Ключ = "ОтработаноЧасов"
			ИЛИ ПараметрСтруктуры.Ключ = "ТарифнаяСтавка" Тогда
			
			Продолжить;
			
		КонецЕсли; 
		
		ПараметрРасчета = Справочники.ПараметрыРасчетов.НайтиПоРеквизиту("Идентификатор", ПараметрСтруктуры.Ключ);
		Если НЕ ЗначениеЗаполнено(ПараметрРасчета) Тогда
			
			Сообщение = Новый СообщениеПользователю();
			Сообщение.Текст = НСтр("ru='Не найден параметр ';uk='Не знайдений параметр'") + ПараметрРасчета + НСтр("ru=' для формулы начисления (удержания) ';uk=' для формули нарахування (утримання)'") + ВидНачисленияУдержания;
			Сообщение.Сообщить();
			Продолжить;
			
		КонецЕсли; 
		
		Счетчик = Счетчик + 1;
		
		Если Счетчик > 3 Тогда
			
			Прервать;
			
		КонецЕсли; 
		
		СтруктураВозврата["Показатель" + Счетчик] = ПараметрСтруктуры.Ключ;
		СтруктураВозврата["Представление" + Счетчик] = ПараметрРасчета;
		
	КонецЦикла;
	
	Возврат СтруктураВозврата;
	
КонецФункции // ЗаполнитьПоказатели()

&НаСервере
// Функция формирует таблицу начислений.
//
Функция СформироватьТаблицуНачислений()

	ТаблицаНачислений = Новый ТаблицаЗначений;

    Массив = Новый Массив;
	
	Массив.Добавить(Тип("СправочникСсылка.Сотрудники"));
	ОписаниеТипов = Новый ОписаниеТипов(Массив, ,);
	Массив.Очистить();

	ТаблицаНачислений.Колонки.Добавить("Сотрудник", ОписаниеТипов);

	Массив.Добавить(Тип("СправочникСсылка.Должности"));
	ОписаниеТипов = Новый ОписаниеТипов(Массив, ,);
	Массив.Очистить();

	ТаблицаНачислений.Колонки.Добавить("Должность", ОписаниеТипов);
	
	Массив.Добавить(Тип("СправочникСсылка.ВидыНачисленийИУдержаний"));
	ОписаниеТипов = Новый ОписаниеТипов(Массив, ,);
	Массив.Очистить();

	ТаблицаНачислений.Колонки.Добавить("ВидНачисленияУдержания", ОписаниеТипов);

	Массив.Добавить(Тип("Дата"));
	ОписаниеТипов = Новый ОписаниеТипов(Массив, ,);
	Массив.Очистить();
	
	ТаблицаНачислений.Колонки.Добавить("ДатаНачала", ОписаниеТипов);
	  
	Массив.Добавить(Тип("Дата"));
	ОписаниеТипов = Новый ОписаниеТипов(Массив, ,);
	Массив.Очистить();
	
	ТаблицаНачислений.Колонки.Добавить("ДатаОкончания", ОписаниеТипов);
		        
	Массив.Добавить(Тип("ПланСчетовСсылка.Управленческий"));
	ОписаниеТипов = Новый ОписаниеТипов(Массив, ,);
	Массив.Очистить();

	ТаблицаНачислений.Колонки.Добавить("СчетЗатрат", ОписаниеТипов);

	Массив.Добавить(Тип("Число"));
	ОписаниеТипов = Новый ОписаниеТипов(Массив, ,);
	Массив.Очистить();

	ТаблицаНачислений.Колонки.Добавить("Размер", ОписаниеТипов);

	Для каждого СтрокаТЧ Из Объект.НачисленияУдержания Цикл

		НоваяСтрока = ТаблицаНачислений.Добавить();
        НоваяСтрока.Сотрудник = СтрокаТЧ.Сотрудник;
        НоваяСтрока.Должность = СтрокаТЧ.Должность;
		НоваяСтрока.ВидНачисленияУдержания = СтрокаТЧ.ВидНачисленияУдержания;
        НоваяСтрока.ДатаНачала = СтрокаТЧ.ДатаНачала;
        НоваяСтрока.ДатаОкончания = СтрокаТЧ.ДатаОкончания;
        НоваяСтрока.СчетЗатрат = СтрокаТЧ.СчетЗатрат;
        НоваяСтрока.Размер = СтрокаТЧ.Размер;

	КонецЦикла;
    	    
	Возврат ТаблицаНачислений;

КонецФункции // СформироватьТаблицуНачислений()

&НаСервере
// Процедура заполняет табличную часть "Сотрудники" с отбором по подразделению.
//
Процедура ЗаполнитьПоПодразделению()

	Объект.НачисленияУдержания.Очистить();
	Объект.НалогиНаДоходы.Очистить();
	Объект.Взносы.Очистить();
	Объект.ВзносыФОТ.Очистить();
	
	Запрос = Новый Запрос;
	
	Запрос.Параметры.Вставить("НачалоМесяца", 		Объект.ПериодРегистрации);
	Запрос.Параметры.Вставить("ОкончаниеМесяца",	КонецМесяца(Объект.ПериодРегистрации));
	Запрос.Параметры.Вставить("Организация", 		УправлениеНебольшойФирмойСервер.ПолучитьОрганизацию(Объект.Организация));
	Запрос.Параметры.Вставить("СтруктурнаяЕдиница", Объект.СтруктурнаяЕдиница);
	Запрос.Параметры.Вставить("Валюта", 			Объект.ВалютаДокумента);
		
	// 1. Определяем интересующих сотрудников	
	// 2. Определяем все движения интересующих сотрудников и начисления в нужном подразделении.
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВложенныйЗапрос.Сотрудник КАК Сотрудник
	|ПОМЕСТИТЬ СотрудникиПодразделения
	|ИЗ
	|	(ВЫБРАТЬ
	|		СотрудникиСрезПоследних.Сотрудник КАК Сотрудник
	|	ИЗ
	|		РегистрСведений.Сотрудники.СрезПоследних(&НачалоМесяца, Организация = &Организация) КАК СотрудникиСрезПоследних
	|	ГДЕ
	|		СотрудникиСрезПоследних.СтруктурнаяЕдиница = &СтруктурнаяЕдиница
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Сотрудники.Сотрудник
	|	ИЗ
	|		РегистрСведений.Сотрудники КАК Сотрудники
	|	ГДЕ
	|		Сотрудники.СтруктурнаяЕдиница = &СтруктурнаяЕдиница
	|		И Сотрудники.Период МЕЖДУ &НачалоМесяца И &ОкончаниеМесяца
	|		И Сотрудники.Организация = &Организация) КАК ВложенныйЗапрос
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВложенныйЗапрос.Сотрудник КАК Сотрудник,
	|	ВложенныйЗапрос.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	ВложенныйЗапрос.Должность КАК Должность,
	|	ПлановыеНачисленияИУдержания.ВидНачисленияУдержания КАК ВидНачисленияУдержания,
	|	ПлановыеНачисленияИУдержания.Сумма КАК Сумма,
	|	ПлановыеНачисленияИУдержания.СчетЗатрат КАК СчетЗатрат,
	|	ПлановыеНачисленияИУдержания.Актуальность КАК Актуальность,
	|	ВложенныйЗапрос.Период КАК Период,
	|	ВложенныйЗапрос.ДрПодрУвольнение КАК ДрПодрУвольнение
	|ПОМЕСТИТЬ ДвиженияСотрудники
	|ИЗ
	|	(ВЫБРАТЬ
	|		СотрудникиПодразделения.Сотрудник КАК Сотрудник,
	|		Сотрудники.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|		Сотрудники.Должность КАК Должность,
	|		МАКСИМУМ(ПлановыеНачисленияИУдержания.Период) КАК ПериодНачисления,
	|		Сотрудники.Период КАК Период,
	|		ВЫБОР
	|			КОГДА Сотрудники.СтруктурнаяЕдиница = &СтруктурнаяЕдиница
	|				ТОГДА ЛОЖЬ
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ КАК ДрПодрУвольнение,
	|		ПлановыеНачисленияИУдержания.ВидНачисленияУдержания КАК ВидНачисленияУдержания,
	|		ПлановыеНачисленияИУдержания.Валюта КАК Валюта
	|	ИЗ
	|		СотрудникиПодразделения КАК СотрудникиПодразделения
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.Сотрудники КАК Сотрудники
	|				ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПлановыеНачисленияИУдержания КАК ПлановыеНачисленияИУдержания
	|				ПО Сотрудники.Организация = ПлановыеНачисленияИУдержания.Организация
	|					И Сотрудники.Сотрудник = ПлановыеНачисленияИУдержания.Сотрудник
	|					И Сотрудники.Период >= ПлановыеНачисленияИУдержания.Период
	|					И (Сотрудники.СтруктурнаяЕдиница = &СтруктурнаяЕдиница)
	|					И (ПлановыеНачисленияИУдержания.Валюта = &Валюта)
	|					И (ПлановыеНачисленияИУдержания.ВидНачисленияУдержания <> ЗНАЧЕНИЕ(Справочник.ВидыНачисленийИУдержаний.СдельнаяОплата))
	|					И (ПлановыеНачисленияИУдержания.ВидНачисленияУдержания <> ЗНАЧЕНИЕ(Справочник.ВидыНачисленийИУдержаний.СдельнаяОплатаПроцент))
	|					И (ПлановыеНачисленияИУдержания.ВидНачисленияУдержания <> ЗНАЧЕНИЕ(Справочник.ВидыНачисленийИУдержаний.ФиксированнаяСумма))
	|			ПО СотрудникиПодразделения.Сотрудник = Сотрудники.Сотрудник
	|	ГДЕ
	|		Сотрудники.Организация = &Организация
	|		И Сотрудники.Период МЕЖДУ ДОБАВИТЬКДАТЕ(&НачалоМесяца, ДЕНЬ, 1) И &ОкончаниеМесяца
	|	
	|	СГРУППИРОВАТЬ ПО
	|		Сотрудники.СтруктурнаяЕдиница,
	|		СотрудникиПодразделения.Сотрудник,
	|		Сотрудники.Должность,
	|		Сотрудники.Период,
	|		ПлановыеНачисленияИУдержания.ВидНачисленияУдержания,
	|		ПлановыеНачисленияИУдержания.Валюта,
	|		ВЫБОР
	|			КОГДА Сотрудники.СтруктурнаяЕдиница = &СтруктурнаяЕдиница
	|				ТОГДА ЛОЖЬ
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ) КАК ВложенныйЗапрос
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПлановыеНачисленияИУдержания КАК ПлановыеНачисленияИУдержания
	|		ПО ВложенныйЗапрос.Сотрудник = ПлановыеНачисленияИУдержания.Сотрудник
	|			И (ПлановыеНачисленияИУдержания.Валюта = &Валюта)
	|			И (ПлановыеНачисленияИУдержания.Организация = &Организация)
	|			И ВложенныйЗапрос.ПериодНачисления = ПлановыеНачисленияИУдержания.Период
	|			И ВложенныйЗапрос.ВидНачисленияУдержания = ПлановыеНачисленияИУдержания.ВидНачисленияУдержания
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВложенныйЗапрос.Сотрудник,
	|	ВложенныйЗапрос.Период,
	|	ВложенныйЗапрос.ВидНачисленияУдержания,
	|	ВложенныйЗапрос.Сумма,
	|	ВложенныйЗапрос.СчетЗатрат,
	|	ВложенныйЗапрос.Актуальность,
	|	Сотрудники.СтруктурнаяЕдиница,
	|	Сотрудники.Должность
	|ПОМЕСТИТЬ ДвиженияПлановыеНачисления
	|ИЗ
	|	(ВЫБРАТЬ
	|		ПлановыеНачисленияИУдержания.Сотрудник КАК Сотрудник,
	|		ПлановыеНачисленияИУдержания.Период КАК Период,
	|		ПлановыеНачисленияИУдержания.ВидНачисленияУдержания КАК ВидНачисленияУдержания,
	|		ПлановыеНачисленияИУдержания.Сумма КАК Сумма,
	|		ВЫБОР
	|			КОГДА ПлановыеНачисленияИУдержания.СчетЗатрат = ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ПустаяСсылка)
	|				ТОГДА ПлановыеНачисленияИУдержания.ВидНачисленияУдержания.СчетЗатрат
	|			ИНАЧЕ ПлановыеНачисленияИУдержания.СчетЗатрат
	|		КОНЕЦ КАК СчетЗатрат,
	|		ПлановыеНачисленияИУдержания.Актуальность КАК Актуальность,
	|		МАКСИМУМ(Сотрудники.Период) КАК ПериодСотрудники
	|	ИЗ
	|		СотрудникиПодразделения КАК СотрудникиПодразделения
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПлановыеНачисленияИУдержания КАК ПлановыеНачисленияИУдержания
	|				ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.Сотрудники КАК Сотрудники
	|				ПО ПлановыеНачисленияИУдержания.Сотрудник = Сотрудники.Сотрудник
	|					И ПлановыеНачисленияИУдержания.Период >= Сотрудники.Период
	|					И (Сотрудники.Организация = &Организация)
	|			ПО СотрудникиПодразделения.Сотрудник = ПлановыеНачисленияИУдержания.Сотрудник
	|				И (ПлановыеНачисленияИУдержания.Валюта = &Валюта)
	|				И (ПлановыеНачисленияИУдержания.Период МЕЖДУ ДОБАВИТЬКДАТЕ(&НачалоМесяца, ДЕНЬ, 1) И &ОкончаниеМесяца)
	|				И (ПлановыеНачисленияИУдержания.ВидНачисленияУдержания <> ЗНАЧЕНИЕ(Справочник.ВидыНачисленийИУдержаний.СдельнаяОплата))
	|				И (ПлановыеНачисленияИУдержания.ВидНачисленияУдержания <> ЗНАЧЕНИЕ(Справочник.ВидыНачисленийИУдержаний.СдельнаяОплатаПроцент))
	|				И (ПлановыеНачисленияИУдержания.ВидНачисленияУдержания <> ЗНАЧЕНИЕ(Справочник.ВидыНачисленийИУдержаний.ФиксированнаяСумма))
	|				И (ПлановыеНачисленияИУдержания.Организация = &Организация)
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ПлановыеНачисленияИУдержания.Актуальность,
	|		ВЫБОР
	|			КОГДА ПлановыеНачисленияИУдержания.СчетЗатрат = ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ПустаяСсылка)
	|				ТОГДА ПлановыеНачисленияИУдержания.ВидНачисленияУдержания.СчетЗатрат
	|			ИНАЧЕ ПлановыеНачисленияИУдержания.СчетЗатрат
	|		КОНЕЦ,
	|		ПлановыеНачисленияИУдержания.Период,
	|		ПлановыеНачисленияИУдержания.ВидНачисленияУдержания,
	|		ПлановыеНачисленияИУдержания.Сотрудник,
	|		ПлановыеНачисленияИУдержания.Сумма) КАК ВложенныйЗапрос
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.Сотрудники КАК Сотрудники
	|		ПО ВложенныйЗапрос.ПериодСотрудники = Сотрудники.Период
	|			И (Сотрудники.Организация = &Организация)
	|			И ВложенныйЗапрос.Сотрудник = Сотрудники.Сотрудник
	|ГДЕ
	|	Сотрудники.СтруктурнаяЕдиница = &СтруктурнаяЕдиница
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВложенныйЗапрос.Сотрудник КАК Сотрудник,
	|	ВложенныйЗапрос.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	ВложенныйЗапрос.Должность КАК Должность,
	|	ВложенныйЗапрос.ДатаДействияНачало КАК ДатаДействияНачало,
	|	ВложенныйЗапрос.ВидНачисленияУдержания КАК ВидНачисленияУдержания,
	|	ВложенныйЗапрос.Размер КАК Размер,
	|	ВложенныйЗапрос.СчетЗатрат КАК СчетЗатрат,
	|	ВложенныйЗапрос.Актуальность,
	|	ВЫБОР
	|		КОГДА ВложенныйЗапрос.СтруктурнаяЕдиница = &СтруктурнаяЕдиница
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ДругоеПодрУвольнение,
	|	ВЫБОР
	|		КОГДА ВложенныйЗапрос.ВидНачисленияУдержания.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыНачисленийИУдержаний.Налог)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЭтоНалог
	|ИЗ
	|	(ВЫБРАТЬ
	|		СотрудникиПодразделения.Сотрудник КАК Сотрудник,
	|		СотрудникиСрезПоследних.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|		СотрудникиСрезПоследних.Должность КАК Должность,
	|		&НачалоМесяца КАК ДатаДействияНачало,
	|		ПлановыеНачисленияИУдержанияСрезПоследних.ВидНачисленияУдержания КАК ВидНачисленияУдержания,
	|		ПлановыеНачисленияИУдержанияСрезПоследних.Сумма КАК Размер,
	|		ВЫБОР
	|			КОГДА ПлановыеНачисленияИУдержанияСрезПоследних.СчетЗатрат = ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ПустаяСсылка)
	|				ТОГДА ПлановыеНачисленияИУдержанияСрезПоследних.ВидНачисленияУдержания.СчетЗатрат
	|			ИНАЧЕ ПлановыеНачисленияИУдержанияСрезПоследних.СчетЗатрат
	|		КОНЕЦ КАК СчетЗатрат,
	|		ИСТИНА КАК Актуальность
	|	ИЗ
	|		СотрудникиПодразделения КАК СотрудникиПодразделения
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.Сотрудники.СрезПоследних(&НачалоМесяца, Организация = &Организация) КАК СотрудникиСрезПоследних
	|			ПО СотрудникиПодразделения.Сотрудник = СотрудникиСрезПоследних.Сотрудник
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПлановыеНачисленияИУдержания.СрезПоследних(
	|					&НачалоМесяца,
	|					Организация = &Организация
	|						И Валюта = &Валюта) КАК ПлановыеНачисленияИУдержанияСрезПоследних
	|			ПО СотрудникиПодразделения.Сотрудник = ПлановыеНачисленияИУдержанияСрезПоследних.Сотрудник
	|	ГДЕ
	|		ПлановыеНачисленияИУдержанияСрезПоследних.Актуальность
	|		И ПлановыеНачисленияИУдержанияСрезПоследних.ВидНачисленияУдержания <> ЗНАЧЕНИЕ(Справочник.ВидыНачисленийИУдержаний.СдельнаяОплата)
	|		И ПлановыеНачисленияИУдержанияСрезПоследних.ВидНачисленияУдержания <> ЗНАЧЕНИЕ(Справочник.ВидыНачисленийИУдержаний.СдельнаяОплатаПроцент)
	|		И ПлановыеНачисленияИУдержанияСрезПоследних.ВидНачисленияУдержания <> ЗНАЧЕНИЕ(Справочник.ВидыНачисленийИУдержаний.ФиксированнаяСумма)
	|		И СотрудникиСрезПоследних.СтруктурнаяЕдиница = &СтруктурнаяЕдиница
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВЫБОР
	|			КОГДА Сотрудники.Сотрудник ЕСТЬ NULL 
	|				ТОГДА ПлановыеНачисленияИУдержания.Сотрудник
	|			ИНАЧЕ Сотрудники.Сотрудник
	|		КОНЕЦ,
	|		ВЫБОР
	|			КОГДА Сотрудники.Сотрудник ЕСТЬ NULL 
	|				ТОГДА ПлановыеНачисленияИУдержания.СтруктурнаяЕдиница
	|			ИНАЧЕ Сотрудники.СтруктурнаяЕдиница
	|		КОНЕЦ,
	|		ВЫБОР
	|			КОГДА Сотрудники.Сотрудник ЕСТЬ NULL 
	|				ТОГДА ПлановыеНачисленияИУдержания.Должность
	|			ИНАЧЕ Сотрудники.Должность
	|		КОНЕЦ,
	|		ВЫБОР
	|			КОГДА Сотрудники.Сотрудник ЕСТЬ NULL 
	|				ТОГДА ПлановыеНачисленияИУдержания.Период
	|			ИНАЧЕ Сотрудники.Период
	|		КОНЕЦ,
	|		ВЫБОР
	|			КОГДА Сотрудники.Сотрудник ЕСТЬ NULL 
	|				ТОГДА ПлановыеНачисленияИУдержания.ВидНачисленияУдержания
	|			ИНАЧЕ Сотрудники.ВидНачисленияУдержания
	|		КОНЕЦ,
	|		ВЫБОР
	|			КОГДА Сотрудники.Сотрудник ЕСТЬ NULL 
	|				ТОГДА ПлановыеНачисленияИУдержания.Сумма
	|			ИНАЧЕ Сотрудники.Сумма
	|		КОНЕЦ,
	|		ВЫБОР
	|			КОГДА Сотрудники.Сотрудник ЕСТЬ NULL 
	|				ТОГДА ВЫБОР
	|						КОГДА ПлановыеНачисленияИУдержания.СчетЗатрат = ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ПустаяСсылка)
	|							ТОГДА ПлановыеНачисленияИУдержания.ВидНачисленияУдержания.СчетЗатрат
	|						ИНАЧЕ ПлановыеНачисленияИУдержания.СчетЗатрат
	|					КОНЕЦ
	|			ИНАЧЕ ВЫБОР
	|					КОГДА Сотрудники.СчетЗатрат = ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ПустаяСсылка)
	|						ТОГДА Сотрудники.ВидНачисленияУдержания.СчетЗатрат
	|					ИНАЧЕ Сотрудники.СчетЗатрат
	|				КОНЕЦ
	|		КОНЕЦ,
	|		ВЫБОР
	|			КОГДА Сотрудники.Сотрудник ЕСТЬ NULL 
	|				ТОГДА ПлановыеНачисленияИУдержания.Актуальность
	|			ИНАЧЕ Сотрудники.Актуальность
	|		КОНЕЦ
	|	ИЗ
	|		ДвиженияСотрудники КАК Сотрудники
	|			ПОЛНОЕ СОЕДИНЕНИЕ ДвиженияПлановыеНачисления КАК ПлановыеНачисленияИУдержания
	|			ПО Сотрудники.Сотрудник = ПлановыеНачисленияИУдержания.Сотрудник
	|				И Сотрудники.Период = ПлановыеНачисленияИУдержания.Период
	|				И Сотрудники.ВидНачисленияУдержания = ПлановыеНачисленияИУдержания.ВидНачисленияУдержания) КАК ВложенныйЗапрос
	|
	|УПОРЯДОЧИТЬ ПО
	|	Сотрудник,
	|	ДатаДействияНачало
	|ИТОГИ ПО
	|	Сотрудник";
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	// 3. Определяем даты окончания периодов, заполняем таблицу значений.
	
	КонецМесяца = НачалоДня(КонецМесяца(Объект.ПериодРегистрации));
	ВыборкаСотрудник = МассивРезультатов[3].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Сотрудник");
	Пока ВыборкаСотрудник.Следующий() Цикл
		
		Выборка = ВыборкаСотрудник.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			
			Если Выборка.ДругоеПодрУвольнение Тогда
				ЗаменитьДатуМассив = Объект.НачисленияУдержания.НайтиСтроки(Новый Структура("ДатаОкончания, Сотрудник", КонецМесяца, Выборка.Сотрудник));
				Для каждого ЭлементМассива Из ЗаменитьДатуМассив Цикл
					ЭлементМассива.ДатаОкончания = Выборка.ДатаДействияНачало - 60*60*24;
				КонецЦикла;
				Продолжить;
			КонецЕсли; 
			
			ЗаменитьДатуМассив = Объект.НачисленияУдержания.НайтиСтроки(Новый Структура("ДатаОкончания, Сотрудник, ВидНачисленияУдержания", КонецМесяца, Выборка.Сотрудник, Выборка.ВидНачисленияУдержания));
			Для каждого ЭлементМассива Из ЗаменитьДатуМассив Цикл
				ЭлементМассива.ДатаОкончания = Выборка.ДатаДействияНачало - 60*60*24;
			КонецЦикла;
			
			Если ЗначениеЗаполнено(Выборка.ВидНачисленияУдержания) И Выборка.Актуальность Тогда
			
				Если Выборка.ЭтоНалог Тогда
										
					НоваяСтрока							= Объект.НалогиНаДоходы.Добавить();
					НоваяСтрока.Сотрудник 				= Выборка.Сотрудник;
					НоваяСтрока.ВидНачисленияУдержания 	= Выборка.ВидНачисленияУдержания;
				
				Иначе
				
					НоваяСтрока							= Объект.НачисленияУдержания.Добавить();
					НоваяСтрока.Сотрудник 				= Выборка.Сотрудник;
					НоваяСтрока.Должность 				= Выборка.Должность;
											
					НоваяСтрока.ВидНачисленияУдержания 	= Выборка.ВидНачисленияУдержания;
					НоваяСтрока.ДатаНачала 				= Выборка.ДатаДействияНачало;
					НоваяСтрока.ДатаОкончания 			= КонецМесяца;
					НоваяСтрока.Размер 					= Выборка.Размер;
					
					ТипСчета = Выборка.СчетЗатрат.ТипСчета;
					Если Объект.СтруктурнаяЕдиница.ТипСтруктурнойЕдиницы = Перечисления.ТипыСтруктурныхЕдиниц.Подразделение
						И  НЕ (ТипСчета = Перечисления.ТипыСчетов.КосвенныеЗатраты
						ИЛИ ТипСчета = Перечисления.ТипыСчетов.Расходы
						ИЛИ ТипСчета = Перечисления.ТипыСчетов.ПрочиеОборотныеАктивы
						ИЛИ ТипСчета = Перечисления.ТипыСчетов.КосвенныеЗатраты
						ИЛИ ТипСчета = Перечисления.ТипыСчетов.НезавершенноеПроизводство
						ИЛИ ТипСчета = Перечисления.ТипыСчетов.ПрочиеОборотныеАктивы
						ИЛИ ТипСчета = Перечисления.ТипыСчетов.Кредиторы) Тогда
					
						НоваяСтрока.СчетЗатрат = ПланыСчетов.Управленческий.ПустаяСсылка();
						
					Иначе
						
						НоваяСтрока.СчетЗатрат = Выборка.СчетЗатрат;
						
					КонецЕсли;
					
				КонецЕсли;
				
			КонецЕсли; 
			
		КонецЦикла;
		
	КонецЦикла;
	
	// 4. Заполненяем отработанное время
		
	Запрос.Параметры.Вставить("ТаблицаНачисленияУдержания", СформироватьТаблицуНачислений());
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаНачисленияУдержания.Сотрудник,
	|	ТаблицаНачисленияУдержания.Должность,
	|	ТаблицаНачисленияУдержания.ВидНачисленияУдержания,
	|	ТаблицаНачисленияУдержания.ДатаНачала,
	|	ТаблицаНачисленияУдержания.ДатаОкончания,
	|	ТаблицаНачисленияУдержания.Размер,
	|	ТаблицаНачисленияУдержания.СчетЗатрат
	|ПОМЕСТИТЬ ТаблицаНачисленияУдержания
	|ИЗ
	|	&ТаблицаНачисленияУдержания КАК ТаблицаНачисленияУдержания
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДокументНачисление.Сотрудник КАК Сотрудник,
	|	ДокументНачисление.Должность КАК Должность,
	|	ДокументНачисление.ВидНачисленияУдержания КАК ВидНачисленияУдержания,
	|	ДокументНачисление.ДатаНачала КАК ДатаНачала,
	|	ДокументНачисление.ДатаОкончания КАК ДатаОкончания,
	|	ДокументНачисление.Размер КАК Размер,
	|	ДокументНачисление.СчетЗатрат КАК СчетЗатрат,
	|	ДанныеТабеля.ОтработаноДней КАК ОтработаноДней,
	|	ДанныеТабеля.ОтработаноЧасов,
	|	ЕстьNULL(ДанныеТабеля.ВЦеломЗаПериод, Ложь) КАК ВЦеломЗаПериод
	|ИЗ
	|	ТаблицаНачисленияУдержания КАК ДокументНачисление
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			ДокументНачисление.Сотрудник КАК Сотрудник,
	|			СУММА(Табель.Дней) КАК ОтработаноДней,
	|			СУММА(Табель.Часов) КАК ОтработаноЧасов,
	|			ДокументНачисление.ДатаНачала КАК ДатаНачала,
	|			ДокументНачисление.ДатаОкончания КАК ДатаОкончания,
	|			МАКСИМУМ(Табель.ВЦеломЗаПериод) КАК ВЦеломЗаПериод
	|		ИЗ
	|			(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|				ДокументНачисление.Сотрудник КАК Сотрудник,
	|				ДокументНачисление.ДатаНачала КАК ДатаНачала,
	|				ДокументНачисление.ДатаОкончания КАК ДатаОкончания
	|			ИЗ
	|				ТаблицаНачисленияУдержания КАК ДокументНачисление) КАК ДокументНачисление
	|				ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.Табель КАК Табель
	|				ПО ДокументНачисление.Сотрудник = Табель.Сотрудник
	|					И (Табель.ВидВремени = ЗНАЧЕНИЕ(Справочник.ВидыРабочегоВремени.Работа))
	|					И (Табель.Организация = &Организация)
	|					И (Табель.СтруктурнаяЕдиница = &СтруктурнаяЕдиница)
	|					И ((НЕ Табель.ВЦеломЗаПериод)
	|							И ДокументНачисление.ДатаНачала <= Табель.Период
	|							И ДокументНачисление.ДатаОкончания >= Табель.Период
	|						ИЛИ Табель.ВЦеломЗаПериод
	|							И Табель.Период = НАЧАЛОПЕРИОДА(ДокументНачисление.ДатаНачала, МЕСЯЦ))
	|		
	|		СГРУППИРОВАТЬ ПО
	|			ДокументНачисление.Сотрудник,
	|			ДокументНачисление.ДатаНачала,
	|			ДокументНачисление.ДатаОкончания) КАК ДанныеТабеля
	|		ПО ДокументНачисление.Сотрудник = ДанныеТабеля.Сотрудник
	|			И ДокументНачисление.ДатаНачала = ДанныеТабеля.ДатаНачала
	|			И ДокументНачисление.ДатаОкончания = ДанныеТабеля.ДатаОкончания";
	
	РезультатЗапроса = Запрос.ВыполнитьПакет()[1].Выгрузить();
	Объект.НачисленияУдержания.Загрузить(РезультатЗапроса); 
	
	Объект.НачисленияУдержания.Сортировать("Сотрудник Возр, ДатаНачала Возр, ВидНачисленияУдержания Возр");
	
	Для каждого СтрокаТабличнойЧасти Из Объект.НачисленияУдержания Цикл
		
		// 1. Проверка
		Если НЕ ЗначениеЗаполнено(СтрокаТабличнойЧасти.ВидНачисленияУдержания) Тогда
			Продолжить;
		КонецЕсли; 
		МассивПовторов = РезультатЗапроса.НайтиСтроки(Новый Структура("Сотрудник, ВидНачисленияУдержания", СтрокаТабличнойЧасти.Сотрудник, СтрокаТабличнойЧасти.ВидНачисленияУдержания));
		Если МассивПовторов.Количество() > 1 И МассивПовторов[0].ВЦеломЗаПериод Тогда
			
			СтрокаТабличнойЧасти.ОтработаноДней = 0;
			СтрокаТабличнойЧасти.ОтработаноЧасов = 0;
			
			ТекстСообщения = НСтр("ru='%Сотрудник%, %ВидНачисления%: Данные об отработанном времени введены сводно. Расчет времени по каждому виду начисления (удержания) невозможен!';uk='%Сотрудник%, %ВидНачисления%: Дані про відпрацьований час уведені сводно. Розрахунок часу по кожному виду нарахування (утримання) неможливий!'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Сотрудник%", СтрокаТабличнойЧасти.Сотрудник);
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ВидНачисления%", СтрокаТабличнойЧасти.ВидНачисленияУдержания);
			ПолеСообщения = "Объект.НачисленияУдержания[" + Объект.НачисленияУдержания.Индекс(СтрокаТабличнойЧасти) + "].Сотрудник";
			
			УправлениеНебольшойФирмойСервер.СообщитьОбОшибке(Объект, ТекстСообщения,,,ПолеСообщения);
			
		КонецЕсли;
		
		// 2. Очистка
		Для Счетчик = 1 По 3 Цикл
			
			СтрокаТабличнойЧасти["Показатель" + Счетчик] = "";
			СтрокаТабличнойЧасти["Представление" + Счетчик] = Справочники.ПараметрыРасчетов.ПустаяСсылка();
			СтрокаТабличнойЧасти["Значение" + Счетчик] = 0;
			
		КонецЦикла;
		
		// 3. Поиск всех параметров-идентификаторов формулы
		СтруктураПараметров = Новый Структура;
		УправлениеНебольшойФирмойСервер.ДобавитьПараметрыВСтруктуру(СтрокаТабличнойЧасти.ВидНачисленияУдержания.Формула, СтруктураПараметров);
		
		// 4. Добавление показателя
		Счетчик = 0;
		Для каждого ПараметрСтруктуры Из СтруктураПараметров Цикл
			
			Если ПараметрСтруктуры.Ключ = "ОтработаноДней"
				ИЛИ ПараметрСтруктуры.Ключ = "ОтработаноЧасов"
				ИЛИ ПараметрСтруктуры.Ключ = "ТарифнаяСтавка" Тогда
				
				Продолжить;
				
			КонецЕсли;
			
			ПараметрРасчета = Справочники.ПараметрыРасчетов.НайтиПоРеквизиту("Идентификатор", ПараметрСтруктуры.Ключ);
			Если НЕ ЗначениеЗаполнено(ПараметрРасчета) Тогда
				Сообщение = Новый СообщениеПользователю();
				Сообщение.Текст = НСтр("ru='Не найден параметр ';uk='Не знайдений параметр'") + ПараметрРасчета + НСтр("ru=' для сотрудника в строке № ';uk=' для співробітника в рядку № '") + (Объект.НачисленияУдержания.Индекс(СтрокаТабличнойЧасти) + 1);
				Сообщение.Сообщить();
			КонецЕсли; 
			
			Счетчик = Счетчик + 1;
			
			Если Счетчик > 3 Тогда
				Прервать;
			КонецЕсли; 
			
			СтрокаТабличнойЧасти["Показатель" + Счетчик] = ПараметрСтруктуры.Ключ;
			СтрокаТабличнойЧасти["Представление" + Счетчик] = ПараметрРасчета;
			
			Если ПараметрРасчета.ЗадаватьЗначениеПриРасчетеЗП Тогда
				Продолжить;
			КонецЕсли; 
			
		// 5. Расчет показателя
			
			СтруктураОтборов = Новый Структура;
			СтруктураОтборов.Вставить("ПериодРегистрации", 		Объект.ПериодРегистрации);
			СтруктураОтборов.Вставить("Организация", 			УправлениеНебольшойФирмойСервер.ПолучитьОрганизацию(Объект.Организация));
			СтруктураОтборов.Вставить("Валюта", 				Объект.ВалютаДокумента);
			СтруктураОтборов.Вставить("Подразделение", 			Объект.СтруктурнаяЕдиница);
			СтруктураОтборов.Вставить("СтруктурнаяЕдиница", 	Объект.СтруктурнаяЕдиница);
			СтруктураОтборов.Вставить("МоментВремени", 			КонецДня(СтрокаТабличнойЧасти.ДатаОкончания));
			СтруктураОтборов.Вставить("НачалоПериода", 			СтрокаТабличнойЧасти.ДатаНачала);
			СтруктураОтборов.Вставить("КонецПериода", 			КонецДня(СтрокаТабличнойЧасти.ДатаОкончания));
			СтруктураОтборов.Вставить("Сотрудник",		 		СтрокаТабличнойЧасти.Сотрудник);
			СтруктураОтборов.Вставить("ТипЗанятости",		 	СтрокаТабличнойЧасти.Сотрудник.ТипЗанятости);
			СтруктураОтборов.Вставить("ТабельныйНомер",		 	СтрокаТабличнойЧасти.Сотрудник.Код);
			СтруктураОтборов.Вставить("ТабНомер",		 		СтрокаТабличнойЧасти.Сотрудник.Код);
			СтруктураОтборов.Вставить("Исполнитель",		 	СтрокаТабличнойЧасти.Сотрудник);
			СтруктураОтборов.Вставить("ФизЛицо",		 		СтрокаТабличнойЧасти.Сотрудник.Физлицо);
			СтруктураОтборов.Вставить("ФизическоеЛицо",		 	СтрокаТабличнойЧасти.Сотрудник.Физлицо);
			СтруктураОтборов.Вставить("Должность", 				СтрокаТабличнойЧасти.Должность);
			СтруктураОтборов.Вставить("ВидНачисленияУдержания", СтрокаТабличнойЧасти.ВидНачисленияУдержания);
			СтруктураОтборов.Вставить("ЗаказПокупателя", 		СтрокаТабличнойЧасти.ЗаказПокупателя);
			СтруктураОтборов.Вставить("Заказ", 					СтрокаТабличнойЧасти.ЗаказПокупателя);
			СтруктураОтборов.Вставить("Проект", 				СтрокаТабличнойЧасти.ЗаказПокупателя.Проект);
			СтруктураОтборов.Вставить("СчетЗатрат", 			СтрокаТабличнойЧасти.СчетЗатрат);
			СтруктураОтборов.Вставить("НаправлениеДеятельности",СтрокаТабличнойЧасти.НаправлениеДеятельности);
			СтруктураОтборов.Вставить("Размер",					СтрокаТабличнойЧасти.Размер);
			СтруктураОтборов.Вставить("ОтработаноДней",			СтрокаТабличнойЧасти.ОтработаноДней);
			СтруктураОтборов.Вставить("ОтработаноЧасов",		СтрокаТабличнойЧасти.ОтработаноЧасов);
			
			// СуммаПродажВНациональнойВалюте
			ВалютаУчета = Константы.ВалютаУчета.Получить();
			Если ВалютаУчета = Объект.ВалютаДокумента Тогда
				
				СтруктураОтборов.Вставить("ВалютаУчетаКратность", 1);
				СтруктураОтборов.Вставить("ВалютаУчетаКурс", 1);
				СтруктураОтборов.Вставить("ВалютаДокументаКратность", 1);
				СтруктураОтборов.Вставить("ВалютаДокументаКурс", 1);
				
			Иначе
				
				СтруктураКурсаВалюты = РаботаСКурсамиВалют.ПолучитьКурсВалюты(ВалютаУчета, Объект.Дата);
				СтруктураОтборов.Вставить("ВалютаУчетаКратность", СтруктураКурсаВалюты.Кратность);
				СтруктураОтборов.Вставить("ВалютаУчетаКурс", СтруктураКурсаВалюты.Курс);
				
				СтруктураКурсаВалюты = РаботаСКурсамиВалют.ПолучитьКурсВалюты(Объект.ВалютаДокумента, Объект.Дата);
				СтруктураОтборов.Вставить("ВалютаДокументаКратность", СтруктураКурсаВалюты.Кратность);
				СтруктураОтборов.Вставить("ВалютаДокументаКурс", СтруктураКурсаВалюты.Курс);
				
			КонецЕсли;
			
			СтрокаТабличнойЧасти["Значение" + Счетчик] = УправлениеНебольшойФирмойСервер.РассчитатьЗначениеПараметра(СтруктураОтборов, ПараметрРасчета, НСтр("ru=' для сотрудника в строке №';uk=' для співробітника в рядку №'") + (Объект.НачисленияУдержания.Индекс(СтрокаТабличнойЧасти) + 1));
		
		КонецЦикла;
		
	КонецЦикла; 
	
	ЗаполнитьЗаймыСотрудникам(); // Прочие расчеты. Займы сотрудникам.
	РассчитатьПоФормулам();
	РассчитатьНалогиИВзносы();
	ОбновитьПодвалФормы();
	
КонецПроцедуры // ЗаполнитьПоПодразделению()

&НаСервере
// Процедура выполняет расчет размера начисления или удержания по формуле.
//
Процедура РассчитатьПоФормулам()

	Для каждого СтрокаНачислений Из Объект.НачисленияУдержания Цикл
		
		Если СтрокаНачислений.РучнаяКорректировка ИЛИ НЕ ЗначениеЗаполнено(СтрокаНачислений.ВидНачисленияУдержания.Формула) Тогда
			Продолжить;
		КонецЕсли; 
		
		// 1. Добавляем параметры и значения в структуру
		
		СтруктураПараметров = Новый Структура;
		СтруктураПараметров.Вставить("ТарифнаяСтавка", СтрокаНачислений.Размер);
		СтруктураПараметров.Вставить("ОтработаноДней", СтрокаНачислений.ОтработаноДней);
		СтруктураПараметров.Вставить("ОтработаноЧасов", СтрокаНачислений.ОтработаноЧасов);
		
		Для Счетчик = 1 По 3 Цикл
			Если ЗначениеЗаполнено(СтрокаНачислений["Представление" + Счетчик]) Тогда
				СтруктураПараметров.Вставить(СтрокаНачислений["Показатель" + Счетчик], СтрокаНачислений["Значение" + Счетчик]);
			КонецЕсли; 
		КонецЦикла; 
		
		
		// 2. Рассчитываем по формулам
			 
		Формула = СтрокаНачислений.ВидНачисленияУдержания.Формула;
		Для каждого Параметр Из СтруктураПараметров Цикл
			Формула = СтрЗаменить(Формула, "[" + Параметр.Ключ + "]", Формат(Параметр.Значение, "ЧРД=.; ЧН=0; ЧГ=0"));
		КонецЦикла;
		Попытка
			РассчитаннаяСумма = Вычислить(Формула);
		Исключение
			ТекстСообщения = НСтр("ru='Не удалось рассчитать сумму начисления  в строке №%НомерСтроки%. Возможно, формула содержит ошибку или не заполнены показатели.';uk='Не вдалося розрахувати суму нарахування  в рядку №%НомерСтроки%. Можливо, формула містить помилку або не заповнені показники.'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%НомерСтроки%", (Объект.НачисленияУдержания.Индекс(СтрокаНачислений) + 1));
			ПолеСообщения = "Объект.НачисленияУдержания[" + Объект.НачисленияУдержания.Индекс(СтрокаНачислений) + "].ВидНачисленияУдержания";
			
			УправлениеНебольшойФирмойСервер.СообщитьОбОшибке(Объект, ТекстСообщения,,,ПолеСообщения);
			
			РассчитаннаяСумма = 0;
		КонецПопытки;
		СтрокаНачислений.Сумма = Окр(РассчитаннаяСумма, 2); 

	КонецЦикла;
	
	ОбновитьПодвалФормы();

КонецПроцедуры // РассчитатьПоФормулам()

&НаСервере
// Процедура выполняет расчет размера налога и взносов.
//
Процедура РассчитатьНалогиИВзносы()
	
	Если РассчитыватьБезВзносов Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ТранзакцияАктивна() Тогда
		НачатьТранзакцию();
		ОтменятьЗапись = Истина;
	КонецЕсли;
	
	Объект.Взносы.Очистить();
	Объект.Взносы.Загрузить(ЗаполнитьВзносы(Справочники.ВидыНалогов.Взносы));
	РассчитатьВзносы();
	
	Объект.ВзносыФОТ.Очистить();
	Объект.ВзносыФОТ.Загрузить(ЗаполнитьВзносы(Справочники.ВидыНалогов.ВзносыФОТ));
	РассчитатьВзносыФОТ();
	
	Объект.НалогиНаДоходы.Очистить();
	ЗаполнитьНДФЛ(Перечисления.ТипыНачисленийИУдержаний.Налог);
	РассчитатьНДФЛ();
	
КонецПроцедуры

&НаСервере
Процедура РассчитатьПоФормуламИНалогиИВзносы()
	
	РассчитатьПоФормулам();
	РассчитатьНалогиИВзносы();
	ОбновитьПодвалФормы();
	
КонецПроцедуры

// Получает набор данных с сервера для реквизита СчетЗатрат табличной части НачисленияИУдержания
//
&НаСервереБезКонтекста
Функция ПолучитьДанныеСчетаЗатрат(СчетЗатрат)
	
	СтруктураДанных = Новый Структура("ТипСчета", Неопределено);
	Если ЗначениеЗаполнено(СчетЗатрат) Тогда
		
		СтруктураДанных.ТипСчета = СчетЗатрат.ТипСчета;
		
	КонецЕсли;
	
	Возврат СтруктураДанных;
	
КонецФункции // ПолучитьДанныеСчетаЗатрат()

&НаСервереБезКонтекста
// Получает набор данных с сервера для процедуры ДатаПриИзменении.
//
Функция ПолучитьДанныеДатаПриИзменении(ДокументСсылка, ДатаНовая, ДатаПередИзменением)
	
	СтруктураДанные = Новый Структура();
	СтруктураДанные.Вставить("РазностьДат", УправлениеНебольшойФирмойСервер.ПроверитьНомерДокумента(ДокументСсылка, ДатаНовая, ДатаПередИзменением));
	
	Возврат СтруктураДанные;
	
КонецФункции // ПолучитьДанныеДатаПриИзменении()

&НаСервереБезКонтекста
// Получает набор данных с сервера для процедуры ДоговорПриИзменении.
//
Функция ПолучитьДанныеОрганизацияПриИзменении(Организация)
	
	СтруктураДанные = Новый Структура();
	СтруктураДанные.Вставить("Компания", УправлениеНебольшойФирмойСервер.ПолучитьОрганизацию(Организация));
	
	Возврат СтруктураДанные;
	
КонецФункции // ПолучитьДанныеОрганизацияПриИзменении()

&НаСервере
// Процедура обновляет данные в подвале формы.
//
Процедура ОбновитьПодвалФормы()
	
	Документ = РеквизитФормыВЗначение("Объект");
	СтруктураИтогов = Документ.ПолучитьСуммуДокумента();
	СуммаДокумента = СтруктураИтогов.СуммаДокумента;
	СуммаНачислено = СтруктураИтогов.СуммаНачислено;
	СуммаУдержано = СтруктураИтогов.СуммаУдержано;
	СуммаПогашено = СтруктураИтогов.СуммаПогашено;
	СуммаВзносов = СтруктураИтогов.СуммаВзносов + СтруктураИтогов.СуммаВзносовФОТ;
	ЗначениеВРеквизитФормы(Документ, "Объект");
	
КонецПроцедуры // ОбновитьПодвалФормы()

&НаСервереБезКонтекста
Функция ПроверитьЗаполнениеРеквизитовОрганизацииДляРасчетаВзносов(Организация, ДатаАнализа, ТекстВопроса, РассчитыватьБезВзносов, Расшифровка)
	
	УчетНалогов = ПолучитьФункциональнуюОпцию("ВестиУчетНалогаНаДоходыИВзносов");
	Если Не УчетНалогов Тогда
		РассчитыватьБезВзносов = Истина;
		Возврат Истина;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Организации.ИспользуетсяОтчетность
	|ИЗ
	|	Справочник.Организации КАК Организации
	|ГДЕ
	|	Организации.Ссылка = &Организация";
	Запрос.УстановитьПараметр("Организация", Организация);
	
	Расшифровка = Новый Структура;
	Расшифровка.Вставить("ТипРасшифровки", "Объектная");
	Расшифровка.Вставить("ЗначениеРасшифровки", Организация);
	ПоляСОшибками = Новый Соответствие;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Если Не Выборка.ИспользуетсяОтчетность Тогда
			РассчитыватьБезВзносов = Истина;
			Возврат Истина;
		Иначе
			Возврат Истина;
		КонецЕсли;
		
	Иначе
		Возврат Истина;
	КонецЕсли;
	
КонецФункции

&НаСервереБезКонтекста
Процедура ПолучитьСчетЗатратВзноса(СтруктураПараметров)
	
	УправлениеНебольшойФирмойСервер.ПолучитьСчетЗатратВидаНачисления(СтруктураПараметров);
	СтруктураПараметров.СчетЗатрат = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтруктураПараметров.ВидНачисленияУдержания, "СчетЗатрат");
	
КонецПроцедуры

// Заполняет Взносы результатом выборки из запроса.
//
Функция ЗаполнитьВзносы(ВидУдержания)
	
	ТЗСотрудники = Объект.НачисленияУдержания.Выгрузить(, "Сотрудник");
	ТЗСотрудники.Свернуть("Сотрудник");
	МассивСотрудников = ТЗСотрудники.ВыгрузитьКолонку("Сотрудник");
	
	НачисленияИУдержания = Объект.НачисленияУдержания.Выгрузить();
	
	спНалогиНеДляПенсионера = Новый СписокЗначений;
	спНалогиНеДляПенсионера.Добавить(Справочники.ВидыНачисленийИУдержаний.Безработица);
	спНалогиНеДляПенсионера.Добавить(Справочники.ВидыНачисленийИУдержаний.БезработицаФОТ);
	
	спНалогиНеДляИнвалида = Новый СписокЗначений;
	спНалогиНеДляИнвалида.Добавить(Справочники.ВидыНачисленийИУдержаний.ПенсионныйФОТ);
	спНалогиНеДляИнвалида.Добавить(Справочники.ВидыНачисленийИУдержаний.Безработица);
	спНалогиНеДляИнвалида.Добавить(Справочники.ВидыНачисленийИУдержаний.ЕСВ_ФОТ);
	
	спНалогиДляИнвалида = Новый СписокЗначений;
	спНалогиДляИнвалида.Добавить(Справочники.ВидыНачисленийИУдержаний.ПенсионныйФОТИнв);
	спНалогиДляИнвалида.Добавить(Справочники.ВидыНачисленийИУдержаний.ЕСВ_ФОТ_инв);
	
	спНалогиЕСВ = Новый СписокЗначений;
	спНалогиЕСВ.Добавить(Справочники.ВидыНачисленийИУдержаний.ЕСВ_работник);
	спНалогиЕСВ.Добавить(Справочники.ВидыНачисленийИУдержаний.ЕСВ_ФОТ);
	спНалогиЕСВ.Добавить(Справочники.ВидыНачисленийИУдержаний.ЕСВ_ФОТ_инв);
	
	Запрос= Новый Запрос;
	Запрос.УстановитьПараметр("ДатаДокумента", Объект.Дата);
	Запрос.УстановитьПараметр("ПериодРегистрации", Объект.ПериодРегистрации);
	Запрос.УстановитьПараметр("ДатаЕСВ", Дата("20110101"));
	Запрос.УстановитьПараметр("Работники", МассивСотрудников);
	Запрос.УстановитьПараметр("ВидНачисления", ВидУдержания);
	Запрос.УстановитьПараметр("НачисленияИУдержания",НачисленияИУдержания);
	Запрос.УстановитьПараметр("НалогиНеДляПенсионера", спНалогиНеДляПенсионера);
	Запрос.УстановитьПараметр("НалогиНеДляИнвалида", спНалогиНеДляИнвалида);
	Запрос.УстановитьПараметр("НалогиДляИнвалида", спНалогиДляИнвалида);
	Запрос.УстановитьПараметр("НалогиЕСВ", спНалогиЕСВ);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	НачислениеЗаработнойПлаты.Сотрудник,
	|	НачислениеЗаработнойПлаты.Сумма
	|ПОМЕСТИТЬ ВтНачисления
	|ИЗ
	|	&НачисленияИУдержания КАК НачислениеЗаработнойПлаты
	|;
	|
	|ВЫБРАТЬ
	|	РаботникиСрезПоследних.Сотрудник,
	|	НачисленияИУдержанияСрезПоследних.Налог КАК ВидНачисленияУдержания,
	|	НачисленияИУдержанияСрезПоследних.Порог,
	|	НачисленияИУдержанияСрезПоследних.Ставка1,
	|	НачисленияИУдержанияСрезПоследних.Ставка2,
	|	РазмерБазы.База
	|ИЗ
	|	РегистрСведений.НДФЛЛьготыСотрудников.СрезПоследних(&ДатаДокумента, Сотрудник В (&Работники)) КАК РаботникиСрезПоследних
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			НачислениеЗаработнойПлатыНачисления.Сотрудник КАК Сотрудник,
	|			НачислениеЗаработнойПлатыНачисления.Сумма КАК База
	|		ИЗ
	|			ВтНачисления КАК НачислениеЗаработнойПлатыНачисления) КАК РазмерБазы
	|		ПО РаботникиСрезПоследних.Сотрудник = РазмерБазы.Сотрудник,
	|	РегистрСведений.НДФЛСтавкиНалогов.СрезПоследних(&ДатаДокумента, Налог.ВидНалога = &ВидНачисления) КАК НачисленияИУдержанияСрезПоследних
	|ГДЕ
	|	ВЫБОР
	|			КОГДА РаботникиСрезПоследних.Пенсионер
	|				ТОГДА (НЕ НачисленияИУдержанияСрезПоследних.Налог В (&НалогиНеДляПенсионера))
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ
	|	И ВЫБОР
	|			КОГДА РаботникиСрезПоследних.Инвалид
	|				ТОГДА (НЕ НачисленияИУдержанияСрезПоследних.Налог В (&НалогиНеДляИнвалида))
	|			ИНАЧЕ (НЕ НачисленияИУдержанияСрезПоследних.Налог В (&НалогиДляИнвалида))
	|		КОНЕЦ
	|	И ВЫБОР
	|			КОГДА &ПериодРегистрации >= &ДатаЕСВ
	|				ТОГДА НачисленияИУдержанияСрезПоследних.Налог В (&НалогиЕСВ)
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ
	|
	|СГРУППИРОВАТЬ ПО
	|	РаботникиСрезПоследних.Сотрудник,
	|	НачисленияИУдержанияСрезПоследних.Налог,
	|	НачисленияИУдержанияСрезПоследних.Порог,
	|	НачисленияИУдержанияСрезПоследних.Ставка1,
	|	НачисленияИУдержанияСрезПоследних.Ставка2,
	|	РазмерБазы.База";
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

&НаСервере
// НДФЛ данными Запроса.
//
Процедура ЗаполнитьНДФЛ(ВидУдержания)
	
	// Получим МинОплату, ПрожМинимум и ПределЛьготы
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ОкладМинимумСрезПоследних.Значение КАК МинОплатаТруда,
	|	ПрожиточныйМинимумСрезПоследних.Значение КАК ПрожиточныйМинимум
	|ИЗ
	|	РегистрСведений.МРОТ.СрезПоследних(&НачалоГода) КАК ОкладМинимумСрезПоследних,
	|	РегистрСведений.ПрожиточныйМинимум.СрезПоследних(&НачалоГода) КАК ПрожиточныйМинимумСрезПоследних";
	Запрос.УстановитьПараметр("НачалоГода", НачалоГода(Объект.ПериодРегистрации));
	ВыборкаЗапроса = Запрос.Выполнить().Выбрать();
	Если ВыборкаЗапроса.Следующий() Тогда
		МинОплатаТруда = ВыборкаЗапроса.МинОплатаТруда;
		ПрожиточныйМинимум = ВыборкаЗапроса.ПрожиточныйМинимум;
	Иначе
		МинОплатаТруда = 0;
		ПрожиточныйМинимум = 0;
	КонецЕсли;
	ПределЛьготыНДФЛОсн = Окр(ПрожиточныйМинимум * 1.4, -1);
	
	
	// получим ставку НДФЛ
	Запрос= Новый Запрос;
	Запрос.Текст=
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	НачисленияИУдержанияСрезПоследних.Ставка1
	|ИЗ
	|	РегистрСведений.НДФЛСтавкиНалогов.СрезПоследних(
	|			&ДатаНачисления,
	|			Налог = ЗНАЧЕНИЕ(Справочник.ВидыНачисленийИУдержаний.НДФЛ)
	|				И Налог.Тип = &ВидНачисленияУдержания) КАК НачисленияИУдержанияСрезПоследних
	|
	|УПОРЯДОЧИТЬ ПО
	|	НачисленияИУдержанияСрезПоследних.Период УБЫВ";
	Запрос.УстановитьПараметр("ДатаНачисления", Объект.ПериодРегистрации);
	Запрос.УстановитьПараметр("ВидНачисленияУдержания", ВидУдержания);
	ВыборкаСтавка=Запрос.Выполнить().Выбрать();
	СтавкаНДФЛ=0;
	Если ВыборкаСтавка.Следующий() Тогда
		СтавкаНДФЛ = ВыборкаСтавка.Ставка1;
	КонецЕсли;
	
	// получим ставку Военного сбора
	Запрос= Новый Запрос;
	Запрос.Текст=
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	НачисленияИУдержанияСрезПоследних.Ставка1
	|ИЗ
	|	РегистрСведений.НДФЛСтавкиНалогов.СрезПоследних(
	|			&ДатаНачисления,
	|			Налог = ЗНАЧЕНИЕ(Справочник.ВидыНачисленийИУдержаний.ВоенныйСбор)
	|				И Налог.Тип = &ВидНачисленияУдержания) КАК НачисленияИУдержанияСрезПоследних
	|
	|УПОРЯДОЧИТЬ ПО
	|	НачисленияИУдержанияСрезПоследних.Период УБЫВ";
	Запрос.УстановитьПараметр("ДатаНачисления", Объект.ПериодРегистрации);
	Запрос.УстановитьПараметр("ВидНачисленияУдержания", Перечисления.ТипыНачисленийИУдержаний.Налог);
	ВыборкаСтавка=Запрос.Выполнить().Выбрать();
	СтавкаВС=0;
	Если ВыборкаСтавка.Следующий() Тогда
		СтавкаВС = ВыборкаСтавка.Ставка1;
	КонецЕсли;
	
	НачисленияИУдержания = Объект.НачисленияУдержания.Выгрузить();
	
	// Получим сумму льготы
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	НачислениеЗаработнойПлаты.Сотрудник
	|ПОМЕСТИТЬ ВтНачисления
	|ИЗ
	|	&НачисленияИУдержания КАК НачислениеЗаработнойПлаты
	|;
	|
	|ВЫБРАТЬ
	|	НачислениеЗаработнойПлатыРаботники.Сотрудник,
	|	РаботникиСрезПоследних.ОсновнаяЛьгота,
	|	РаботникиСрезПоследних.СобственнаяЛьгота,
	|	ЕСТЬNULL(РаботникиСрезПоследних.КоличествоДетей, 0) КАК КоличествоДетей
	|ИЗ
	|	ВтНачисления КАК НачислениеЗаработнойПлатыРаботники
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НДФЛЛьготыСотрудников.СрезПоследних(&ДатаНачисления, ) КАК РаботникиСрезПоследних
	|		ПО НачислениеЗаработнойПлатыРаботники.Сотрудник = РаботникиСрезПоследних.Сотрудник";
	Запрос.УстановитьПараметр("ДатаНачисления", Объект.ПериодРегистрации);
	Запрос.УстановитьПараметр("НачисленияИУдержания",НачисленияИУдержания);
	ВыборкаРаботники = Запрос.Выполнить().Выбрать();
	Пока ВыборкаРаботники.Следующий() Цикл
		НоваяСтрока = Объект.НалогиНаДоходы.Добавить();
		НоваяСтрока.Сотрудник = ВыборкаРаботники.Сотрудник;
		НоваяСтрока.Ставка1 = СтавкаНДФЛ;
		
		ТЗСотрудники = Объект.НачисленияУдержания.Выгрузить();
		
		// определим базу
		СтрокаПоиска = ТЗСотрудники.Найти(ВыборкаРаботники.Сотрудник);
		Если НЕ СтрокаПоиска = Неопределено Тогда
			НоваяСтрока.Доход = СтрокаПоиска.Сумма;
		КонецЕсли;
		
		// определим предел льготы доп.
		ПределЛьготыНДФЛДоп = ПределЛьготыНДФЛОсн * ВыборкаРаботники.КоличествоДетей;
		
		СуммаЛьготы = 0;
		Если НоваяСтрока.Доход <= ПределЛьготыНДФЛОсн Тогда
			//Льгота основная предоставляется
			ОсновнаяЛьгота = ПосчитатьСуммуЛьготыНДФЛ(ВыборкаРаботники.ОсновнаяЛьгота);
			СуммаЛьготы = СуммаЛьготы + ОсновнаяЛьгота;
		КонецЕсли;
		Если НоваяСтрока.Доход <= ПределЛьготыНДФЛДоп Тогда
			//Льгота основная предоставляется
			ДополнительнаяЛьгота = ПосчитатьСуммуЛьготыНДФЛ(ВыборкаРаботники.СобственнаяЛьгота);
			СуммаЛьготы = СуммаЛьготы + ДополнительнаяЛьгота;
		КонецЕсли;
		
		НоваяСтрока.Льгота = СуммаЛьготы;
		НоваяСтрока.ВидНачисленияУдержания = Справочники.ВидыНачисленийИУдержаний.НДФЛ;
		Если СтавкаВС <> 0 Тогда
			НоваяСтрока = Объект.НалогиНаДоходы.Добавить();
			НоваяСтрока.Сотрудник = ВыборкаРаботники.Сотрудник;
			НоваяСтрока.Ставка1 = СтавкаВС;
			
			// определим базу
			СтрокаПоиска = ТЗСотрудники.Найти(ВыборкаРаботники.Сотрудник);
			Если НЕ СтрокаПоиска = Неопределено Тогда
				НоваяСтрока.Доход = СтрокаПоиска.Сумма;
			КонецЕсли;
			
			// определим предел льготы доп.
			ПределЛьготыНДФЛДоп = ПределЛьготыНДФЛОсн * ВыборкаРаботники.КоличествоДетей;
			
			СуммаЛьготы = 0;
			Если НоваяСтрока.Доход <= ПределЛьготыНДФЛОсн Тогда
				//Льгота основная предоставляется
				ОсновнаяЛьгота = ПосчитатьСуммуЛьготыНДФЛ(ВыборкаРаботники.ОсновнаяЛьгота);
				СуммаЛьготы = СуммаЛьготы + ОсновнаяЛьгота;
			КонецЕсли;
			Если НоваяСтрока.Доход <= ПределЛьготыНДФЛДоп Тогда
				//Льгота основная предоставляется
				ДополнительнаяЛьгота = ПосчитатьСуммуЛьготыНДФЛ(ВыборкаРаботники.СобственнаяЛьгота);
				СуммаЛьготы = СуммаЛьготы + ДополнительнаяЛьгота;
			КонецЕсли;
			
			НоваяСтрока.Льгота = СуммаЛьготы;
			НоваяСтрока.ВидНачисленияУдержания = Справочники.ВидыНачисленийИУдержаний.ВоенныйСбор;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
// Возвращает сумму льготы НДФЛ по переданному элементу справочника "ЛьготыПоНДФЛ"
//
Функция ПосчитатьСуммуЛьготыНДФЛ(ВыбраннаяЛьгота)
	
	Если ТипЗнч(ВыбраннаяЛьгота) <> Тип("СправочникСсылка.ЛьготыПоНДФЛ") Тогда
		Возврат 0;
	КонецЕсли;
	
	// получим базу для льготы (это сумма базовой льготы)
	БазоваяЛьгота = Справочники.ЛьготыПоНДФЛ.НДФЛ_611;
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЕСТЬNULL(ПрожМинОкладСрезПоследних.Значение, 0) КАК РазмерСумма
	|ИЗ
	|	РегистрСведений.МРОТ.СрезПоследних(&НачалоГода) КАК ПрожМинОкладСрезПоследних";
	Запрос.УстановитьПараметр("НачалоГода", НачалоГода(Объект.ПериодРегистрации));
	ВыборкаЗапроса = Запрос.Выполнить().Выбрать();
	Если ВыборкаЗапроса.Следующий() Тогда
		РазмерБазы = ВыборкаЗапроса.РазмерСумма;
	Иначе
		РазмерБазы = 0;
	КонецЕсли;
	Если ВыбраннаяЛьгота <> БазоваяЛьгота Тогда
		РазмерБазы = РазмерБазы * БазоваяЛьгота.РазмерНДФЛ / 100;
	КонецЕсли;
	
	// посчитаем сумму льготы
	РазмерЛьготы = РазмерБазы * ВыбраннаяЛьгота.РазмерНДФЛ / 100;
	Возврат РазмерЛьготы;
	
КонецФункции // ПосчитатьСуммуЛьготыНДФЛ()

&НаСервере
// Процедура расчета результата табличной части Взносы
//
Процедура РассчитатьВзносы()
	
	Для Каждого Строка Из Объект.Взносы Цикл
		
		Если Строка.ВидНачисленияУдержания = Справочники.ВидыНачисленийИУдержаний.Пенсионный1_2 Тогда
			
			Если (Строка.База < Строка.Порог)
				ИЛИ (Строка.Ставка2 = 0) Тогда
				Строка.Сумма = Строка.База * Строка.Ставка1 * 0.01;
			ИначеЕсли Строка.База > Строка.Порог Тогда
				Строка.Сумма = (Строка.База - Строка.Порог) * Строка.Ставка2 * 0.01 + Строка.Порог * Строка.Ставка1 * 0.01;
			КонецЕсли;
			
		ИначеЕсли (Строка.ВидНачисленияУдержания = Справочники.ВидыНачисленийИУдержаний.Соцстрах)
			ИЛИ (Строка.ВидНачисленияУдержания = Справочники.ВидыНачисленийИУдержаний.Пенсионный1_5) Тогда
			
			Если (Строка.База < Строка.Порог)
				ИЛИ (Строка.Ставка2 = 0) Тогда
				Строка.Сумма = Строка.База * Строка.Ставка1 * 0.01;
			ИначеЕсли Строка.База > Строка.Порог Тогда
				Строка.Сумма = Строка.База * Строка.Ставка2 * 0.01;
			КонецЕсли;
			
		ИначеЕсли Строка.ВидНачисленияУдержания = Справочники.ВидыНачисленийИУдержаний.Безработица
			ИЛИ Строка.ВидНачисленияУдержания = Справочники.ВидыНачисленийИУдержаний.ЕСВ_работник Тогда
			
			Строка.Сумма = Строка.База * Строка.Ставка1 * 0.01;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
// Процедура расчета результата табличной части ВзносыФОТ
//
Процедура РассчитатьВзносыФОТ()
	
	Данные = Новый Структура("Организация",Объект.Организация);
	ДанныеПоОрганизации = РегистрыСведений.СистемыНалогообложенияОрганизаций.СрезПоследних(Объект.Дата, Данные);
	Если ДанныеПоОрганизации.Количество() > 0 Тогда
		СумЕдиногоНалога = ДанныеПоОрганизации[0].СуммаЕдиногоНалога;
	Иначе 
		СумЕдиногоНалога = 0;
	КонецЕсли;
	
	Для Каждого Строка Из Объект.ВзносыФОТ Цикл 
		
		Если Строка.ВидНачисленияУдержания = Справочники.ВидыНачисленийИУдержаний.ПенсионныйФОТ Тогда
			Строка.Сумма = Строка.База * Строка.Ставка1 * 0.01 - СумЕдиногоНалога * Строка.Ставка2 * 0.01;
		ИначеЕсли Строка.ВидНачисленияУдержания = Справочники.ВидыНачисленийИУдержаний.СоцстрахНесчФОТ
			ИЛИ Строка.ВидНачисленияУдержания = Справочники.ВидыНачисленийИУдержаний.ЕСВ_ФОТ
			ИЛИ Строка.ВидНачисленияУдержания = Справочники.ВидыНачисленийИУдержаний.ЕСВ_ФОТ_инв Тогда
			
			Строка.Сумма = Строка.База * Строка.Ставка1 * 0.01;
			
		ИначеЕсли (Строка.ВидНачисленияУдержания = Справочники.ВидыНачисленийИУдержаний.СоцстрахФОТ)
			ИЛИ (Строка.ВидНачисленияУдержания = Справочники.ВидыНачисленийИУдержаний.ПенсионныйФОТИнв)
			ИЛИ (Строка.ВидНачисленияУдержания = Справочники.ВидыНачисленийИУдержаний.БезработицаФОТ) Тогда
			
			Если (Строка.База < Строка.Порог)
				ИЛИ (Строка.Ставка2 = 0) Тогда
				Строка.Сумма = Строка.База * Строка.Ставка1 * 0.01;
			ИначеЕсли Строка.База > Строка.Порог Тогда
				Строка.Сумма = Строка.База * Строка.Ставка2 * 0.01;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
// Процедура расчета  налога табличной части НДФЛ
//
Процедура РассчитатьНДФЛ()
	
	Для Каждого Строка ИЗ Объект.НалогиНаДоходы Цикл
		
		СтруктураОтбора = Новый Структура("Сотрудник", Строка.Сотрудник);
		тзВзносы = Объект.Взносы.Выгрузить(СтруктураОтбора);
		Строка.Сумма=Макс((Строка.Доход - тзВзносы.Итог("Сумма") - Строка.Льгота), 0) * Строка.Ставка1 * 0.01;
		
	КонецЦикла;
	
КонецПроцедуры

#EndRegion

#Region ОбработчикиСобытийФормы

&НаСервере
// Процедура - обработчик события ПриСозданииНаСервере формы.
// В процедуре осуществляется
// - инициализация параметров формы,
// - установка параметров функциональных опций формы.
//
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	КодЯзыка = Локализация.КодЯзыкаИнтерфейса();
	ОтображениеПериодаРегистрации = Формат(Объект.ПериодРегистрации, "Л="+Локализация.ОпределитьКодЯзыкаДляФормат(КодЯзыка)+";ДФ='MMMM yyyy'");
	
	// Установка реквизитов формы.
	ДатаДокумента = Объект.Дата;
	Если НЕ ЗначениеЗаполнено(ДатаДокумента) Тогда
		ДатаДокумента = ТекущаяДата();
	КонецЕсли;
	
	ВалютаДокумента = Объект.ВалютаДокумента;
	Компания = УправлениеНебольшойФирмойСервер.ПолучитьОрганизацию(Объект.Организация);
	
	ОбновитьПодвалФормы();
	
	Если НЕ Константы.ФункциональнаяОпцияИспользоватьСовместительство.Получить() Тогда
		
		Если Элементы.Найти("НачисленияУдержанияСотрудникКод") <> Неопределено Тогда
			
			Элементы.НачисленияУдержанияСотрудникКод.Видимость = Ложь;
			
		КонецЕсли;
		
		Если Элементы.Найти("НалогиНаДоходыСотрудникКод") <> Неопределено Тогда
			
			Элементы.НалогиНаДоходыСотрудникКод.Видимость = Ложь;
			
		КонецЕсли;
		
		Если Элементы.Найти("СтраховыеВзносыСотрудникКод") <> Неопределено Тогда
			
			Элементы.СтраховыеВзносыСотрудникКод.Видимость = Ложь;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если Объект.НачисленияУдержания.Количество() > 0 Тогда
		
		Для каждого СтрокаДанных Из Объект.НачисленияУдержания Цикл
			
			Если ЗначениеЗаполнено(СтрокаДанных.СчетЗатрат) Тогда
				
				СтрокаДанных.ТипСчета = СтрокаДанных.СчетЗатрат.ТипСчета;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Если Объект.Взносы.Количество() > 0 Тогда
		
		Для каждого СтрокаДанных Из Объект.Взносы Цикл
			
			Если ЗначениеЗаполнено(СтрокаДанных.СчетЗатрат) Тогда
				
				СтрокаДанных.ТипСчета = СтрокаДанных.СчетЗатрат.ТипСчета;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	ОтчетыУНФ.ПриСозданииНаСервереФормыСвязанногоОбъекта(ЭтотОбъект);
	
	// СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
	ДополнительныеОтчетыИОбработки.ПриСозданииНаСервере(ЭтаФорма);
	// Конец СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
	
	// СтандартныеПодсистемы.Печать
	УправлениеПечатью.ПриСозданииНаСервере(ЭтаФорма, Элементы.ГруппаВажныеКоманды);
	// Конец СтандартныеПодсистемы.Печать
	
КонецПроцедуры // ПриСозданииНаСервере()

// Процедура - обработчик события ПриЧтенииНаСервере.
//
&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);
	
КонецПроцедуры // ПриЧтенииНаСервере()

&НаКлиенте
// Процедура - обработчик события формы ОбработкаВыбора
//
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	Если ТипЗнч(ИсточникВыбора) = Тип("УправляемаяФорма")
		И СтрНайти(ИсточникВыбора.ИмяФормы, "ФормаКалендаря") > 0 Тогда
		
		Объект.ПериодРегистрации = КонецДня(ВыбранноеЗначение);
		УправлениеНебольшойФирмойКлиент.ПриИзмененииПериодаРегистрации(ЭтаФорма);
		
	КонецЕсли;
	
КонецПроцедуры // ОбработкаВыбора()

#EndRegion

#Region ОбработчикиКомандФормы

&НаКлиенте
// Процедура - обработчик команды Рассчитать.
//
Процедура Рассчитать(Команда)
	
	ТекстВопроса = "";
	РассчитыватьБезВзносов = Ложь;
	Расшифровка = Новый Структура;
	Если Не ПроверитьЗаполнениеРеквизитовОрганизацииДляРасчетаВзносов(Объект.Организация, Объект.ПериодРегистрации, ТекстВопроса, РассчитыватьБезВзносов, Расшифровка) Тогда
		Возврат; 
	КонецЕсли;
	
	РассчитатьПоФормуламИНалогиИВзносы();
	
КонецПроцедуры

&НаКлиенте
// Процедура заполняет табличную часть "Сотрудники" с отбором по подразделению.
//
Процедура Заполнить(Команда)
	
	Если НЕ ЗначениеЗаполнено(Объект.СтруктурнаяЕдиница) Тогда
		
		Сообщение = Новый СообщениеПользователю();
		Сообщение.Текст = НСтр("ru='Не заполнено подразделение! Заполнение документа отменено.';uk='Не заповнений підрозділ! Заповнення документа скасоване.'");
		Сообщение.Поле = "Объект.СтруктурнаяЕдиница";
		Сообщение.Сообщить();
		
		Возврат;
		
	КонецЕсли;
	
	Если Объект.НачисленияУдержания.Количество() > 0 И Объект.НалогиНаДоходы.Количество() > 0 Тогда
		
		Ответ = Неопределено;
		
		ПоказатьВопрос(Новый ОписаниеОповещения("ЗаполнитьЗавершение1", ЭтотОбъект), НСтр("ru='Табличные части документа будут очищены! Продолжить выполнение операции?';uk='Табличні частини документа будуть очищені! Продовжити виконання операції?'"), РежимДиалогаВопрос.ДаНет, 0);
		Возврат;
		
	ИначеЕсли Объект.НачисленияУдержания.Количество() > 0 ИЛИ Объект.НалогиНаДоходы.Количество() > 0 ИЛИ
		Объект.ПогашениеЗаймов.Количество() > 0 Тогда // Прочие расчеты
		
		ПоказатьВопрос(Новый ОписаниеОповещения("ЗаполнитьЗавершение", ЭтотОбъект), НСтр("ru='Табличная часть документа будет очищена! Продолжить выполнение операции?';uk='Таблична частина документа буде очищена! Продовжити виконання операції?'"), РежимДиалогаВопрос.ДаНет, 0);
		Возврат; 
		
	КонецЕсли;
	
	ТекстВопроса = "";
	РассчитыватьБезВзносов = Ложь;
	Расшифровка = Новый Структура;
	Если Не ПроверитьЗаполнениеРеквизитовОрганизацииДляРасчетаВзносов(Объект.Организация, Объект.ПериодРегистрации, ТекстВопроса, РассчитыватьБезВзносов, Расшифровка) Тогда
		
		Возврат;
		
	КонецЕсли;
	
	ЗаполнитьФрагмент1();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьЗавершение1(Результат, ДополнительныеПараметры) Экспорт
	
	Ответ = Результат;
	
	Если Ответ <> КодВозвратаДиалога.Да Тогда
		
		Возврат;
		
	КонецЕсли;
	
	ТекстВопроса = "";
	РассчитыватьБезВзносов = Ложь;
	Расшифровка = Новый Структура;
	Если Не ПроверитьЗаполнениеРеквизитовОрганизацииДляРасчетаВзносов(Объект.Организация, Объект.ПериодРегистрации, ТекстВопроса, РассчитыватьБезВзносов, Расшифровка) Тогда
		Возврат; 
	КонецЕсли;
	
	ЗаполнитьФрагмент1();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьРеквизитыОрганизацииЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Ответ = Результат;
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		
		ПроверкаДанныхКлиент.ПровестиРасшифровку(ДополнительныеПараметры.Расшифровка, Объект.Организация);
		Возврат;
		
	ИначеЕсли Ответ = КодВозвратаДиалога.Отмена Тогда
		Возврат;
	КонецЕсли;
	
	РассчитыватьБезВзносов = Истина;
	Если ДополнительныеПараметры.ПроцедураВыполнения = "ЗаполнитьПоПодразделению" Тогда
		ЗаполнитьПоПодразделению();
	ИначеЕсли ДополнительныеПараметры.ПроцедураВыполнения = "РассчитатьПоФормуламИНалогиИВзносы" Тогда
		РассчитатьПоФормуламИНалогиИВзносы();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьФрагмент1()
	
	ЗаполнитьФрагмент();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Ответ = Результат;
	
	Если Ответ <> КодВозвратаДиалога.Да Тогда
		Возврат;
		
	КонецЕсли; 
	
	ТекстВопроса = "";
	РассчитыватьБезВзносов = Ложь;
	Расшифровка = Новый Структура;
	Если Не ПроверитьЗаполнениеРеквизитовОрганизацииДляРасчетаВзносов(Объект.Организация, Объект.ПериодРегистрации, ТекстВопроса, РассчитыватьБезВзносов, Расшифровка) Тогда
		Возврат; 
	КонецЕсли;
	
	ЗаполнитьФрагмент();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьФрагмент()
	
	ЗаполнитьПоПодразделению();
	
КонецПроцедуры

#EndRegion

#Region ОбработчикиСобытийРеквизитовФормы

&НаКлиенте
// Процедура - обработчик события ПриИзменении поля ввода Дата.
// В процедуре определяется ситуация, когда при изменении своей даты документ 
// оказывается в другом периоде нумерации документов, и в этом случае
// присваивает документу новый уникальный номер.
// Переопределяет соответствующий параметр формы.
//
Процедура ДатаПриИзменении(Элемент)
	
	// Обработка события изменения даты.
	ДатаПередИзменением = ДатаДокумента;
	ДатаДокумента = Объект.Дата;
	Если Объект.Дата <> ДатаПередИзменением Тогда
		СтруктураДанные = ПолучитьДанныеДатаПриИзменении(Объект.Ссылка, Объект.Дата, ДатаПередИзменением);
		Если СтруктураДанные.РазностьДат <> 0 Тогда
			Объект.Номер = "";
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // ДатаПриИзменении()

&НаКлиенте
// Процедура - обработчик события ПриИзменении поля ввода Организация.
// В процедуре осуществляется очистка номера документа,
// а также производится установка параметров функциональных опций формы.
// Переопределяет соответствующий параметр формы.
//
Процедура ОрганизацияПриИзменении(Элемент)

	// Обработка события изменения организации.
	Объект.Номер = "";
	СтруктураДанные = ПолучитьДанныеОрганизацияПриИзменении(Объект.Организация);
	Компания = СтруктураДанные.Компания;
	
КонецПроцедуры // ОрганизацияПриИзменении()

&НаКлиенте
// Процедура - обработчик события ПриИзменении поля ввода ВалютаДокумента.
//
Процедура ВалютаДокументаПриИзменении(Элемент)
	
	Если Объект.ВалютаДокумента = ВалютаДокумента Тогда
		Возврат;
	КонецЕсли; 
	
	Если Объект.НачисленияУдержания.Количество() > 0 
		ИЛИ Объект.ПогашениеЗаймов.Количество() > 0 Тогда
		
		Режим = РежимДиалогаВопрос.ДаНет;
		Ответ = Неопределено;
		
		ПоказатьВопрос(Новый ОписаниеОповещения("ВалютаДокументаПриИзмененииЗавершение", ЭтотОбъект), НСтр("ru='Табличные части будут очищены! Продолжить выполнение операции?';uk='Табличні частини будуть очищені! Продовжити виконання операції?'"), Режим, 0);
		Возврат;
		
	КонецЕсли; 
	
	ВалютаДокументаПриИзмененииФрагмент();
КонецПроцедуры

//Процедура обработчик события регулирования поля ПериодРегистрации
//
&НаКлиенте
Процедура ПериодРегистрацииРегулирование(Элемент, Направление, СтандартнаяОбработка)
	
	УправлениеНебольшойФирмойКлиент.ПриРегулированииПериодаРегистрации(ЭтаФорма, Направление);
	УправлениеНебольшойФирмойКлиент.ПриИзмененииПериодаРегистрации(ЭтаФорма);
	
КонецПроцедуры //ПериодРегистрацииРегулирование()

//Процедура обработчик события начала ввода данных поля ПериодРегистрации
//
&НаКлиенте
Процедура ПериодРегистрацииНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка	 = Ложь;
	
	ДатаКалендаряПриОткрытии = ?(ЗначениеЗаполнено(Объект.ПериодРегистрации), Объект.ПериодРегистрации, УправлениеНебольшойФирмойПовтИсп.ПолучитьТекущуюДатаСеанса());
	
	ОткрытьФорму("ОбщаяФорма.ФормаКалендаря", УправлениеНебольшойФирмойКлиент.ПолучитьПараметрыОткрытияФормыКалендаря(ДатаКалендаряПриОткрытии), ЭтаФорма);
	
КонецПроцедуры //ПериодРегистрацииНачалоВыбора()

&НаКлиенте
Процедура ВалютаДокументаПриИзмененииЗавершение(Результат, ДополнительныеПараметры) Экспорт
    
    Ответ = Результат;
    Если Ответ = КодВозвратаДиалога.Да Тогда
        Объект.НачисленияУдержания.Очистить();
		Объект.НалогиНаДоходы.Очистить();
		Объект.ПогашениеЗаймов.Очистить();
	КонецЕсли;
    
    
    ВалютаДокументаПриИзмененииФрагмент();

КонецПроцедуры

&НаКлиенте
Процедура ВалютаДокументаПриИзмененииФрагмент()
    
    ВалютаДокумента = Объект.ВалютаДокумента;

КонецПроцедуры // ВалютаДокументаПриИзменении()

&НаКлиенте
Процедура КомментарийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(Элемент.ТекстРедактирования, ЭтотОбъект, "Объект.Комментарий");
		
КонецПроцедуры

&НаКлиенте
Процедура НачисленияУдержанияСчетЗатратПриИзменении(Элемент)
	
	ДанныеТекущейСтроки = Элементы.НачисленияУдержания.ТекущиеДанные;
	Если ДанныеТекущейСтроки <> Неопределено Тогда
		
		СтруктураДанных = ПолучитьДанныеСчетаЗатрат(ДанныеТекущейСтроки.СчетЗатрат);
		ДанныеТекущейСтроки.ТипСчета = СтруктураДанных.ТипСчета;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НачисленияУдержанияСчетЗатратНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ДанныеТекущейСтроки = Элементы.НачисленияУдержания.ТекущиеДанные;
	
	СтруктураДанных = Новый Структура;
	СтруктураДанных.Вставить("ВидНачисленияУдержания", 
		?(ДанныеТекущейСтроки = Неопределено, Неопределено, ДанныеТекущейСтроки.ВидНачисленияУдержания));
		
	ПолучитьДанныеДляВыбораСчетаРасчетов(СтруктураДанных);
	
	НовыйМассив = Новый Массив;
	НовыйПараметр = Новый ПараметрВыбора("Отбор.ТипСчета", Новый ФиксированныйМассив(СтруктураДанных.ДоступныеТипыСчетовУчета));
	НовыйМассив.Добавить(НовыйПараметр);
	Элементы.НачисленияУдержанияСчетЗатрат.ПараметрыВыбора = Новый ФиксированныйМассив(НовыйМассив);
	
КонецПроцедуры

&НаКлиенте
// Процедура - обработчик события ПриНачалеРедактирования табличной части Начисления.
//
Процедура НачисленияУдержанияПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока Тогда
		
		Если НЕ Копирование Тогда
			
			ТекущиеДанные 				= Элементы.НачисленияУдержания.ТекущиеДанные;
			
			ТекущиеДанные.ДатаНачала 	= Объект.ПериодРегистрации;
			ТекущиеДанные.ДатаОкончания = КонецМесяца(Объект.ПериодРегистрации);
			ТекущиеДанные.РучнаяКорректировка = Истина;
			
		КонецЕсли; 
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
// Процедура - обработчик события ПриИзменении табличной части Начисления.
//
Процедура НачисленияУдержанияПриИзменении(Элемент)
	
	ОбновитьПодвалФормы();
	
КонецПроцедуры

&НаКлиенте
// Процедура - обработчик события ПриИзменении табличной части Начисления.
//
Процедура НалогиНаДоходыПриИзменении(Элемент)
	
	ОбновитьПодвалФормы();
	
КонецПроцедуры

&НаКлиенте
// Процедура - обработчик события ПриИзменении табличной части Начисления.
//
Процедура СтраховыеВзносыПриИзменении(Элемент)
	
	ОбновитьПодвалФормы();
	
КонецПроцедуры

&НаКлиенте
// Процедура - обработчик события ПриИзменении табличной части Начисления.
//
Процедура СтраховыеВзносыФОТПриИзменении(Элемент)
	
	ОбновитьПодвалФормы();
	
КонецПроцедуры

&НаКлиенте
// Процедура - обработчик события ПриИзменении реквизита ВидНачисленияУдержания табличной части НачисленияУдержания.
//
Процедура НачисленияУдержанияВидНачисленияУдержанияПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.НачисленияУдержания.ТекущиеДанные;
	
	СтруктураДанных = ЗаполнитьПоказатели(ТекущаяСтрока.ВидНачисленияУдержания);
	ЗаполнитьЗначенияСвойств(ТекущаяСтрока, СтруктураДанных);
	УправлениеНебольшойФирмойКлиент.ПроставитьСчетЗатратПоУмолчанию(ЭтаФорма, Объект.СтруктурнаяЕдиница);
	
КонецПроцедуры

&НаКлиенте
Процедура СтраховыеВзносыСчетЗатратПриИзменении(Элемент)
	
	ДанныеТекущейСтроки = Элементы.СтраховыеВзносы.ТекущиеДанные;
	Если ДанныеТекущейСтроки <> Неопределено Тогда
		
		СтруктураДанных = ПолучитьДанныеСчетаЗатрат(ДанныеТекущейСтроки.СчетЗатрат);
		ДанныеТекущейСтроки.ТипСчета = СтруктураДанных.ТипСчета;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СтраховыеВзносыСчетЗатратНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ДанныеТекущейСтроки = Элементы.СтраховыеВзносы.ТекущиеДанные;
	
	СтруктураДанных = Новый Структура;
	СтруктураДанных.Вставить("ВидНачисленияУдержания", 
		?(ДанныеТекущейСтроки = Неопределено, Неопределено, ДанныеТекущейСтроки.ВидНачисленияУдержания));
		
	ПолучитьДанныеДляВыбораСчетаРасчетов(СтруктураДанных);
	
	НовыйМассив = Новый Массив;
	НовыйПараметр = Новый ПараметрВыбора("Отбор.ТипСчета", Новый ФиксированныйМассив(СтруктураДанных.ДоступныеТипыСчетовУчета));
	НовыйМассив.Добавить(НовыйПараметр);
	Элементы.НачисленияУдержанияСчетЗатрат.ПараметрыВыбора = Новый ФиксированныйМассив(НовыйМассив);
	
КонецПроцедуры

&НаКлиенте
Процедура СтраховыеВзносыВидНачисленияУдержанияПриИзменении(Элемент)
	
	ДанныеТекущейСтроки = Элементы.СтраховыеВзносы.ТекущиеДанные;
	СтруктураПараметров = Новый Структура("СчетЗатрат, ТипСчета");
	СтруктураПараметров.Вставить("ВидНачисленияУдержания", ДанныеТекущейСтроки.ВидНачисленияУдержания);
	СтруктураПараметров.Вставить("СтруктурнаяЕдиница", Объект.СтруктурнаяЕдиница);
	
	Если ЗначениеЗаполнено(ДанныеТекущейСтроки.ВидНачисленияУдержания) Тогда
		
		ПолучитьСчетЗатратВзноса(СтруктураПараметров);
		ДанныеТекущейСтроки.СчетЗатрат = СтруктураПараметров.СчетЗатрат;
		
	КонецЕсли;
	
	Если ДанныеТекущейСтроки.Свойство("ТипСчета") Тогда
		
		ДанныеТекущейСтроки.ТипСчета = СтруктураПараметров.ТипСчета;
		
	КонецЕсли;
КонецПроцедуры

#EndRegion

#Область ОбработчикиБиблиотек

// СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
&НаКлиенте
Процедура Подключаемый_ВыполнитьНазначаемуюКоманду(Команда)
	Если НЕ ДополнительныеОтчетыИОбработкиКлиент.ВыполнитьНазначаемуюКомандуНаКлиенте(ЭтаФорма, Команда.Имя) Тогда
		РезультатВыполнения = Неопределено;
		ДополнительныеОтчетыИОбработкиВыполнитьНазначаемуюКомандуНаСервере(Команда.Имя, РезультатВыполнения);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ДополнительныеОтчетыИОбработкиВыполнитьНазначаемуюКомандуНаСервере(ИмяЭлемента, РезультатВыполнения)
	ДополнительныеОтчетыИОбработки.ВыполнитьНазначаемуюКомандуНаСервере(ЭтаФорма, ИмяЭлемента, РезультатВыполнения);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки

// СтандартныеПодсистемы.Печать
&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуПечати(Команда)
	УправлениеПечатьюКлиент.ВыполнитьПодключаемуюКомандуПечати(Команда, ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.Печать

#КонецОбласти

#Область ЗаймыСотрудникам

&НаСервере
Процедура ЗаполнитьЗаймыСотрудникам()

	Объект.ПогашениеЗаймов.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВложенныйЗапрос.Сотрудник КАК Сотрудник
	|ПОМЕСТИТЬ СотрудникиПодразделения
	|ИЗ
	|	(ВЫБРАТЬ
	|		СотрудникиСрезПоследних.Сотрудник КАК Сотрудник
	|	ИЗ
	|		РегистрСведений.Сотрудники.СрезПоследних(&НачалоМесяца, Организация = &Организация) КАК СотрудникиСрезПоследних
	|	ГДЕ
	|		СотрудникиСрезПоследних.СтруктурнаяЕдиница = &СтруктурнаяЕдиница
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Сотрудники.Сотрудник
	|	ИЗ
	|		РегистрСведений.Сотрудники КАК Сотрудники
	|	ГДЕ
	|		Сотрудники.СтруктурнаяЕдиница = &СтруктурнаяЕдиница
	|		И Сотрудники.Период МЕЖДУ &НачалоМесяца И &ОкончаниеМесяца
	|		И Сотрудники.Организация = &Организация) КАК ВложенныйЗапрос
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Сотрудник
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РасчетыПоКредитамИЗаймамОбороты.ДоговорКредитаЗайма КАК ДоговорЗаймаСотруднику,
	|	РасчетыПоКредитамИЗаймамОбороты.ДоговорКредитаЗайма.ВалютаРасчетов КАК ВалютаРасчетов,
	|	РасчетыПоКредитамИЗаймамОбороты.ОсновнойДолгВалПриход КАК ОсновнойДолгВалВыплачено,
	|	РасчетыПоКредитамИЗаймамОбороты.ОсновнойДолгВалРасход КАК ОсновнойДолгПогашеноРанее,
	|	РасчетыПоКредитамИЗаймамОбороты.ПроцентыВалПриход КАК ПроцентыВалНачислено,
	|	РасчетыПоКредитамИЗаймамОбороты.ПроцентыВалРасход КАК ПроцентыВалПогашено,
	|	РасчетыПоКредитамИЗаймамОбороты.КомиссияВалПриход КАК КомиссияВалНачислено,
	|	РасчетыПоКредитамИЗаймамОбороты.КомиссияВалРасход КАК КомиссияВалПогашено
	|ПОМЕСТИТЬ ВременнаяТаблицаСуммыНачисленныеИПогашенныеСУчетомДвижений
	|ИЗ
	|	РегистрНакопления.РасчетыПоКредитамИЗаймам.Обороты(
	|			,
	|			,
	|			,
	|			ДоговорКредитаЗайма.ПогашатьИзЗаработнойПлаты
	|				И Организация = &Организация
	|				И ДоговорКредитаЗайма.ВидДоговора = &ВидДоговораДоговорЗаймаСотруднику
	|				И ВидДоговора = &ВидДоговораДоговорЗаймаСотруднику
	|				И ДоговорКредитаЗайма.ВалютаРасчетов = &Валюта
	|				И Контрагент В
	|					(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|						СотрудникиПодразделения.Сотрудник
	|					ИЗ
	|						СотрудникиПодразделения КАК СотрудникиПодразделения)) КАК РасчетыПоКредитамИЗаймамОбороты
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	РасчетыПоКредитамИЗаймам.ДоговорКредитаЗайма,
	|	РасчетыПоКредитамИЗаймам.ДоговорКредитаЗайма.ВалютаРасчетов,
	|	ВЫБОР
	|		КОГДА РасчетыПоКредитамИЗаймам.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА -РасчетыПоКредитамИЗаймам.ОсновнойДолгВал
	|		ИНАЧЕ РасчетыПоКредитамИЗаймам.ОсновнойДолгВал
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА РасчетыПоКредитамИЗаймам.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|			ТОГДА -РасчетыПоКредитамИЗаймам.ОсновнойДолгВал
	|		ИНАЧЕ РасчетыПоКредитамИЗаймам.ОсновнойДолгВал
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА РасчетыПоКредитамИЗаймам.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА -РасчетыПоКредитамИЗаймам.ПроцентыВал
	|		ИНАЧЕ РасчетыПоКредитамИЗаймам.ПроцентыВал
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА РасчетыПоКредитамИЗаймам.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|			ТОГДА -РасчетыПоКредитамИЗаймам.ПроцентыВал
	|		ИНАЧЕ РасчетыПоКредитамИЗаймам.ПроцентыВал
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА РасчетыПоКредитамИЗаймам.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА -РасчетыПоКредитамИЗаймам.КомиссияВал
	|		ИНАЧЕ РасчетыПоКредитамИЗаймам.КомиссияВал
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА РасчетыПоКредитамИЗаймам.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|			ТОГДА -РасчетыПоКредитамИЗаймам.КомиссияВал
	|		ИНАЧЕ РасчетыПоКредитамИЗаймам.КомиссияВал
	|	КОНЕЦ
	|ИЗ
	|	РегистрНакопления.РасчетыПоКредитамИЗаймам КАК РасчетыПоКредитамИЗаймам
	|ГДЕ
	|	РасчетыПоКредитамИЗаймам.Регистратор = &Ссылка
	|	И РасчетыПоКредитамИЗаймам.ДоговорКредитаЗайма.ПогашатьИзЗаработнойПлаты
	|	И РасчетыПоКредитамИЗаймам.Организация = &Организация
	|	И РасчетыПоКредитамИЗаймам.ДоговорКредитаЗайма.ВидДоговора = &ВидДоговораДоговорЗаймаСотруднику
	|	И РасчетыПоКредитамИЗаймам.ВидДоговора = &ВидДоговораДоговорЗаймаСотруднику
	|	И РасчетыПоКредитамИЗаймам.Контрагент В
	|			(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|				СотрудникиПодразделения.Сотрудник
	|			ИЗ
	|				СотрудникиПодразделения КАК СотрудникиПодразделения)
	|	И РасчетыПоКредитамИЗаймам.ДоговорКредитаЗайма.ВалютаРасчетов = &Валюта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВременнаяТаблицаСуммыНачисленныеИПогашенныеСУчетомДвижений.ДоговорЗаймаСотруднику,
	|	ВременнаяТаблицаСуммыНачисленныеИПогашенныеСУчетомДвижений.ВалютаРасчетов,
	|	СУММА(ВременнаяТаблицаСуммыНачисленныеИПогашенныеСУчетомДвижений.ОсновнойДолгВалВыплачено) КАК ОсновнойДолгВалВыплачено,
	|	СУММА(ВременнаяТаблицаСуммыНачисленныеИПогашенныеСУчетомДвижений.ОсновнойДолгПогашеноРанее) КАК ОсновнойДолгПогашеноРанее,
	|	СУММА(ВременнаяТаблицаСуммыНачисленныеИПогашенныеСУчетомДвижений.ПроцентыВалНачислено) КАК ПроцентыВалНачислено,
	|	СУММА(ВременнаяТаблицаСуммыНачисленныеИПогашенныеСУчетомДвижений.ПроцентыВалПогашено) КАК ПроцентыВалПогашено,
	|	СУММА(ВременнаяТаблицаСуммыНачисленныеИПогашенныеСУчетомДвижений.КомиссияВалНачислено) КАК КомиссияВалНачислено,
	|	СУММА(ВременнаяТаблицаСуммыНачисленныеИПогашенныеСУчетомДвижений.КомиссияВалПогашено) КАК КомиссияВалПогашено
	|ПОМЕСТИТЬ ВременнаяТаблицаСуммыНачисленныеИПогашенные
	|ИЗ
	|	ВременнаяТаблицаСуммыНачисленныеИПогашенныеСУчетомДвижений КАК ВременнаяТаблицаСуммыНачисленныеИПогашенныеСУчетомДвижений
	|
	|СГРУППИРОВАТЬ ПО
	|	ВременнаяТаблицаСуммыНачисленныеИПогашенныеСУчетомДвижений.ДоговорЗаймаСотруднику,
	|	ВременнаяТаблицаСуммыНачисленныеИПогашенныеСУчетомДвижений.ВалютаРасчетов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ГрафикПогашенияКредитовИЗаймов.ДоговорКредитаЗайма,
	|	ГрафикПогашенияКредитовИЗаймов.ДоговорКредитаЗайма.ВалютаРасчетов,
	|	СУММА(ГрафикПогашенияКредитовИЗаймов.СуммаОсновногоДолга) КАК СуммаОсновногоДолгаГрафик,
	|	СУММА(ГрафикПогашенияКредитовИЗаймов.СуммаПроцентов) КАК СуммаПроцентовГрафик,
	|	СУММА(ГрафикПогашенияКредитовИЗаймов.СуммаКомиссии) КАК СуммаКомиссииГрафик
	|ПОМЕСТИТЬ ВременнаяТаблицаСуммыКоторыеДолжныПогасить
	|ИЗ
	|	СотрудникиПодразделения КАК СотрудникиПодразделения
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ГрафикПогашенияКредитовИЗаймов КАК ГрафикПогашенияКредитовИЗаймов
	|		ПО СотрудникиПодразделения.Сотрудник = ГрафикПогашенияКредитовИЗаймов.ДоговорКредитаЗайма.Сотрудник
	|ГДЕ
	|	ГрафикПогашенияКредитовИЗаймов.Период <= &ОкончаниеМесяца
	|	И ГрафикПогашенияКредитовИЗаймов.ДоговорКредитаЗайма.ВалютаРасчетов = &Валюта
	|
	|СГРУППИРОВАТЬ ПО
	|	ГрафикПогашенияКредитовИЗаймов.ДоговорКредитаЗайма,
	|	ГрафикПогашенияКредитовИЗаймов.ДоговорКредитаЗайма.ВалютаРасчетов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВременнаяТаблицаСуммыКоторыеДолжныПогасить.ДоговорКредитаЗайма КАК ДоговорЗаймаСотруднику,
	|	ВременнаяТаблицаСуммыКоторыеДолжныПогасить.ДоговорКредитаЗаймаВалютаРасчетов КАК ВалютаРасчетов,
	|	ВременнаяТаблицаСуммыКоторыеДолжныПогасить.СуммаОсновногоДолгаГрафик,
	|	ВременнаяТаблицаСуммыКоторыеДолжныПогасить.СуммаПроцентовГрафик,
	|	ВременнаяТаблицаСуммыКоторыеДолжныПогасить.СуммаКомиссииГрафик,
	|	ЕСТЬNULL(ВременнаяТаблицаСуммыНачисленныеИПогашенные.ОсновнойДолгВалВыплачено, 0) КАК ОсновнойДолгВалВыплачено,
	|	ЕСТЬNULL(ВременнаяТаблицаСуммыНачисленныеИПогашенные.ОсновнойДолгПогашеноРанее, 0) КАК ОсновнойДолгПогашеноРанее,
	|	ЕСТЬNULL(ВременнаяТаблицаСуммыНачисленныеИПогашенные.ПроцентыВалНачислено, 0) КАК ПроцентыВалНачислено,
	|	ЕСТЬNULL(ВременнаяТаблицаСуммыНачисленныеИПогашенные.ПроцентыВалПогашено, 0) КАК ПроцентыВалПогашено,
	|	ЕСТЬNULL(ВременнаяТаблицаСуммыНачисленныеИПогашенные.КомиссияВалНачислено, 0) КАК КомиссияВалНачислено,
	|	ЕСТЬNULL(ВременнаяТаблицаСуммыНачисленныеИПогашенные.КомиссияВалПогашено, 0) КАК КомиссияВалПогашено,
	|	ВременнаяТаблицаСуммыКоторыеДолжныПогасить.ДоговорКредитаЗайма.Сотрудник КАК Сотрудник,
	|	ВременнаяТаблицаСуммыКоторыеДолжныПогасить.ДоговорКредитаЗайма.СуммаДокумента КАК ОбщаяСуммаЗайма
	|ИЗ
	|	ВременнаяТаблицаСуммыКоторыеДолжныПогасить КАК ВременнаяТаблицаСуммыКоторыеДолжныПогасить
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаСуммыНачисленныеИПогашенные КАК ВременнаяТаблицаСуммыНачисленныеИПогашенные
	|		ПО ВременнаяТаблицаСуммыКоторыеДолжныПогасить.ДоговорКредитаЗайма = ВременнаяТаблицаСуммыНачисленныеИПогашенные.ДоговорЗаймаСотруднику";
	
	Запрос.УстановитьПараметр("ВидДоговораДоговорЗаймаСотруднику", Перечисления.ВидыДоговоровКредитаИЗайма.ДоговорЗаймаСотруднику);
	Запрос.Параметры.Вставить("НачалоМесяца", 		Объект.ПериодРегистрации);
	Запрос.Параметры.Вставить("ОкончаниеМесяца",	КонецМесяца(Объект.ПериодРегистрации));
	Запрос.Параметры.Вставить("Организация", 		УправлениеНебольшойФирмойСервер.ПолучитьОрганизацию(Объект.Организация));
	Запрос.Параметры.Вставить("СтруктурнаяЕдиница", Объект.СтруктурнаяЕдиница);
	Запрос.Параметры.Вставить("Валюта", 			Объект.ВалютаДокумента);
	Запрос.Параметры.Вставить("Ссылка", 			Объект.Ссылка);
		
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		// Если деньги по договору займа ещё не выплатили, то погашать не будем.
		Если Выборка.ОсновнойДолгВалВыплачено = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		НужноПогаситьЗайма = Выборка.СуммаОсновногоДолгаГрафик - Выборка.ОсновнойДолгПогашеноРанее;
		НужноПогаситьЗайма = ?(НужноПогаситьЗайма < 0, 0, НужноПогаситьЗайма);
		НужноПогаситьЗайма = ?(Выборка.ОсновнойДолгПогашеноРанее > Выборка.ОбщаяСуммаЗайма, 0, НужноПогаситьЗайма);
		
		НачисленоПроцентов = (Выборка.СуммаПроцентовГрафик + Выборка.СуммаКомиссииГрафик) - (Выборка.ПроцентыВалНачислено + Выборка.КомиссияВалНачислено);
		НачисленоПроцентов = ?(НачисленоПроцентов < 0, 0, НачисленоПроцентов);
		
		ПогашеноПроцентов = (Выборка.СуммаПроцентовГрафик + Выборка.СуммаКомиссииГрафик) - (Выборка.ПроцентыВалПогашено + Выборка.КомиссияВалПогашено);
		ПогашеноПроцентов = ?(ПогашеноПроцентов < 0, 0, ПогашеноПроцентов);
		
		Если НужноПогаситьЗайма > 0 Или НачисленоПроцентов > 0 Тогда
			НоваяСтрока = Объект.ПогашениеЗаймов.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
			НоваяСтрока.ПогашеноЗайма = НужноПогаситьЗайма;
			НоваяСтрока.НачисленоПроцентов = НачисленоПроцентов;
			НоваяСтрока.ПогашеноПроцентов = ПогашеноПроцентов;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры


#КонецОбласти
