#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

// Инициализирует таблицы значений, содержащие данные табличных частей документа.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура ИнициализироватьДанныеДокумента(ДокументСсылкаПриемНаРаботу, СтруктураДополнительныеСвойства) Экспорт

	Запрос = Новый Запрос(
	"ВЫБРАТЬ 
	|	&Организация КАК Организация,
	|	ПриемНаРаботуСотрудники.НомерСтроки,
	|	ПриемНаРаботуСотрудники.Сотрудник,
	|	ПриемНаРаботуСотрудники.СтруктурнаяЕдиница,
	|	ПриемНаРаботуСотрудники.Должность,
	|	ПриемНаРаботуСотрудники.ГрафикРаботы,
	|	ПриемНаРаботуСотрудники.ЗанимаемыхСтавок,
	|	ПриемНаРаботуСотрудники.Период
	|ПОМЕСТИТЬ ТаблицаСотрудники
	|ИЗ
	|	Документ.ПриемНаРаботу.Сотрудники КАК ПриемНаРаботуСотрудники
	|ГДЕ
	|	ПриемНаРаботуСотрудники.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ 
	|	&Организация КАК Организация,
	|	ПриемНаРаботуСотрудники.НомерСтроки,
	|	ПриемНаРаботуСотрудники.Сотрудник,
	|	ПриемНаРаботуСотрудники.Период,
	|	ПриемНаРаботуНачисленияУдержания.ВидНачисленияУдержания,
	|	ПриемНаРаботуНачисленияУдержания.Валюта,
	|	ПриемНаРаботуНачисленияУдержания.Сумма,
	|	ПриемНаРаботуНачисленияУдержания.СчетЗатрат
	|ПОМЕСТИТЬ ТаблицаНачисленияУдержания
	|ИЗ
	|	Документ.ПриемНаРаботу.Сотрудники КАК ПриемНаРаботуСотрудники
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПриемНаРаботу.НачисленияУдержания КАК ПриемНаРаботуНачисленияУдержания
	|		ПО ПриемНаРаботуСотрудники.КлючСвязи = ПриемНаРаботуНачисленияУдержания.КлючСвязи
	|			И ПриемНаРаботуСотрудники.Ссылка = ПриемНаРаботуНачисленияУдержания.Ссылка
	|ГДЕ
	|	ПриемНаРаботуСотрудники.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Организация,
	|	ПриемНаРаботуСотрудники.НомерСтроки,
	|	ПриемНаРаботуСотрудники.Сотрудник,
	|	ПриемНаРаботуСотрудники.Период,
	|	ПриемНаРаботуНалогиНаДоходы.ВидНачисленияУдержания,
	|	ПриемНаРаботуНалогиНаДоходы.Валюта,
	|	0,
	|	НЕОПРЕДЕЛЕНО
	|ИЗ
	|	Документ.ПриемНаРаботу.Сотрудники КАК ПриемНаРаботуСотрудники
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПриемНаРаботу.НалогиНаДоходы КАК ПриемНаРаботуНалогиНаДоходы
	|		ПО ПриемНаРаботуСотрудники.КлючСвязи = ПриемНаРаботуНалогиНаДоходы.КлючСвязи
	|			И ПриемНаРаботуСотрудники.Ссылка = ПриемНаРаботуНалогиНаДоходы.Ссылка
	|ГДЕ
	|	ПриемНаРаботуСотрудники.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ 
	|	ТаблицаСотрудники.Организация,
	|	ТаблицаСотрудники.НомерСтроки,
	|	ТаблицаСотрудники.Сотрудник,
	|	ТаблицаСотрудники.СтруктурнаяЕдиница,
	|	ТаблицаСотрудники.Должность,
	|	ТаблицаСотрудники.ГрафикРаботы,
	|	ТаблицаСотрудники.ЗанимаемыхСтавок,
	|	ТаблицаСотрудники.Период
	|ИЗ
	|	ТаблицаСотрудники КАК ТаблицаСотрудники
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ 
	|	ТаблицаНачисленияУдержания.Организация,
	|	ТаблицаНачисленияУдержания.НомерСтроки,
	|	ТаблицаНачисленияУдержания.Сотрудник,
	|	ТаблицаНачисленияУдержания.Период,
	|	ТаблицаНачисленияУдержания.ВидНачисленияУдержания,
	|	ТаблицаНачисленияУдержания.Валюта,
	|	ТаблицаНачисленияУдержания.Сумма,
	|	ТаблицаНачисленияУдержания.СчетЗатрат,
	|	ИСТИНА КАК Актуальность
	|ИЗ
	|	ТаблицаНачисленияУдержания КАК ТаблицаНачисленияУдержания");
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаПриемНаРаботу);
	Запрос.УстановитьПараметр("Организация", СтруктураДополнительныеСвойства.ДляПроведения.Организация);
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаСотрудники", 				МассивРезультатов[2].Выгрузить());
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаПлановыеНачисленияИУдержания", МассивРезультатов[3].Выгрузить());
	
	СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц = Запрос.МенеджерВременныхТаблиц;
	
КонецПроцедуры // ИнициализироватьДанныеДокумента()

Процедура ЗарегистрироватьЛьготыСотрудника(ДокументСсылкаПриемНаРаботу) Экспорт

	Если ДокументСсылкаПриемНаРаботу.Проведен Тогда
		УдалитьЛьготыСотрудника(ДокументСсылкаПриемНаРаботу);
	КонецЕсли;
	
	Запрос = Новый Запрос;
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПриемНаРаботуСотрудники.Ссылка КАК ДокументРегистратор,
	|	ПриемНаРаботуСотрудники.Сотрудник,
	|	ПриемНаРаботуСотрудники.Период,
	|	ПриемНаРаботуЛьготыСотрудников.Инвалид,
	|	ПриемНаРаботуЛьготыСотрудников.Пенсионер,
	|	ПриемНаРаботуЛьготыСотрудников.ОсновнаяЛьгота,
	|	ПриемНаРаботуЛьготыСотрудников.СобственнаяЛьгота,
	|	ПриемНаРаботуЛьготыСотрудников.КоличествоДетей
	|ИЗ
	|	Документ.ПриемНаРаботу.Сотрудники КАК ПриемНаРаботуСотрудники
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПриемНаРаботу.ЛьготыСотрудников КАК ПриемНаРаботуЛьготыСотрудников
	|		ПО ПриемНаРаботуСотрудники.КлючСвязи = ПриемНаРаботуЛьготыСотрудников.КлючСвязи
	|			И ПриемНаРаботуСотрудники.Ссылка = ПриемНаРаботуЛьготыСотрудников.Ссылка
	|ГДЕ
	|	ПриемНаРаботуСотрудники.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаПриемНаРаботу);
	
	РезультатЗапроса = Запрос.Выполнить();
	ТаблицаЗаписей = РезультатЗапроса.Выгрузить();
	
	// Запись набора РС
	Для каждого СтрокаТаблицы Из ТаблицаЗаписей Цикл
		НаборЗаписей = РегистрыСведений.НДФЛЛьготыСотрудников.СоздатьНаборЗаписей();
		
		НаборЗаписей.Отбор.Период.Установить(СтрокаТаблицы.Период);
		НаборЗаписей.Отбор.Сотрудник.Установить(СтрокаТаблицы.Сотрудник);
		
		НоваяЗапись = НаборЗаписей.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяЗапись, СтрокаТаблицы);
		
		НаборЗаписей.Записать();
	КонецЦикла; 
	
КонецПроцедуры // ЗарегистрироватьЦеныПоставщика()

Процедура УдалитьЛьготыСотрудника(ДокументСсылкаПриемНаРаботу) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	НДФЛЛьготыСотрудников.Период,
	|	НДФЛЛьготыСотрудников.Сотрудник
	|ИЗ
	|	РегистрСведений.НДФЛЛьготыСотрудников КАК НДФЛЛьготыСотрудников
	|ГДЕ
	|	НДФЛЛьготыСотрудников.ДокументРегистратор = &ДокументРегистратор";
	
	Запрос.УстановитьПараметр("ДокументРегистратор", ДокументСсылкаПриемНаРаботу);
	
	РезультатЗапроса = Запрос.Выполнить();
	ТаблицаЗаписей = РезультатЗапроса.Выгрузить();	
	
	Для каждого СтрокаТаблицы Из ТаблицаЗаписей Цикл
		НаборЗаписей = РегистрыСведений.НДФЛЛьготыСотрудников.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Период.Установить(СтрокаТаблицы.Период);
		НаборЗаписей.Отбор.Сотрудник.Установить(СтрокаТаблицы.Сотрудник);
		НаборЗаписей.Записать();
	КонецЦикла;	

КонецПроцедуры // УдалитьЦеныПоставщика()

#Область ИнтерфейсПечати

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли