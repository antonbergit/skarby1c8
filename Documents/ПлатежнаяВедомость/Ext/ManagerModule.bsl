#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ИнтерфейсПечати

Функция ПечатнаяФорма(МассивОбъектов, ОбъектыПечати, ИмяМакета = "ПлатежнаяВедомость")
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.КлючПараметровПечати = "ПараметрыПечати_ПлатежнаяВедомость";
	
	ПервыйДокумент = Истина;
	
	Для Каждого ТекущийДокумент Из МассивОбъектов Цикл
		
		Если Не ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		ПервыйДокумент = Ложь;
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("ТекущийДокумент",   ТекущийДокумент);
		Запрос.УстановитьПараметр("ПериодРегистрации", КонецМесяца(ТекущийДокумент.ПериодРегистрации));
		Запрос.УстановитьПараметр("Осн", НСтр("ru='Осн.';uk='Під.'"));
		Запрос.УстановитьПараметр("Совм", НСтр("ru='Совм.';uk='Сум.'"));
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ПлатежнаяВедомостьСотрудники.Сотрудник.Код КАК ТабельныйНомер,
		|	ВЫБОР
		|		КОГДА ПлатежнаяВедомостьСотрудники.Сотрудник.ТипЗанятости = ЗНАЧЕНИЕ(Перечисление.ТипыЗанятости.ОсновноеМестоРаботы)
		|			ТОГДА &Осн
		|		ИНАЧЕ &Совм
		|	КОНЕЦ КАК ХарактерРаботы,
		|	СУММА(ПлатежнаяВедомостьСотрудники.СуммаПлатежа) КАК Сумма,
		|	ФИОФизЛицСрезПоследних.Фамилия,
		|	ФИОФизЛицСрезПоследних.Имя,
		|	ФИОФизЛицСрезПоследних.Отчество,
		|	ФИОФизЛицСрезПоследних.Период,
		|	ПлатежнаяВедомостьСотрудники.Сотрудник КАК Физлицо,
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(ФИОФизЛицСрезПоследних.Фамилия, """") <> """"
		|			ТОГДА ФИОФизЛицСрезПоследних.Фамилия + "" "" + ФИОФизЛицСрезПоследних.Имя + "" "" + ФИОФизЛицСрезПоследних.Отчество
		|		ИНАЧЕ ПлатежнаяВедомостьСотрудники.Сотрудник.Наименование
		|	КОНЕЦ КАК СотрудникПредставление,
		|	ПлатежнаяВедомостьСотрудники.Ссылка.Номер,
		|	ПлатежнаяВедомостьСотрудники.Ссылка.Организация.Префикс КАК Префикс
		|ИЗ
		|	Документ.ПлатежнаяВедомость.Сотрудники КАК ПлатежнаяВедомостьСотрудники
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФИОФизЛиц.СрезПоследних(&ПериодРегистрации, ) КАК ФИОФизЛицСрезПоследних
		|		ПО ПлатежнаяВедомостьСотрудники.Сотрудник.Физлицо = ФИОФизЛицСрезПоследних.ФизЛицо
		|ГДЕ
		|	ПлатежнаяВедомостьСотрудники.Ссылка = &ТекущийДокумент
		|
		|СГРУППИРОВАТЬ ПО
		|	ФИОФизЛицСрезПоследних.Имя,
		|	ФИОФизЛицСрезПоследних.Отчество,
		|	ВЫБОР
		|		КОГДА ПлатежнаяВедомостьСотрудники.Сотрудник.ТипЗанятости = ЗНАЧЕНИЕ(Перечисление.ТипыЗанятости.ОсновноеМестоРаботы)
		|			ТОГДА &Осн
		|		ИНАЧЕ &Совм
		|	КОНЕЦ,
		|	ФИОФизЛицСрезПоследних.Фамилия,
		|	ПлатежнаяВедомостьСотрудники.Сотрудник,
		|	ФИОФизЛицСрезПоследних.Период,
		|	ПлатежнаяВедомостьСотрудники.Сотрудник.Код,
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(ФИОФизЛицСрезПоследних.Фамилия, """") <> """"
		|			ТОГДА ФИОФизЛицСрезПоследних.Фамилия + "" "" + ФИОФизЛицСрезПоследних.Имя + "" "" + ФИОФизЛицСрезПоследних.Отчество
		|		ИНАЧЕ ПлатежнаяВедомостьСотрудники.Сотрудник.Наименование
		|	КОНЕЦ,
		|	ПлатежнаяВедомостьСотрудники.Ссылка.Номер,
		|	ПлатежнаяВедомостьСотрудники.Ссылка.Организация.Префикс
		|
		|УПОРЯДОЧИТЬ ПО
		|	СотрудникПредставление";
		
		Выборка = Запрос.Выполнить().Выбрать();

		ТабличныйДокумент.КлючПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ПлатежнаяВедомость_Макет";
		
		Если ТекущийДокумент.Дата < Дата('20110101') Тогда
			НомерДокумента = УправлениеНебольшойФирмойСервер.ПолучитьНомерНаПечать(ТекущийДокумент.Номер, ТекущийДокумент.Организация.Префикс);
		Иначе
			НомерДокумента = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ТекущийДокумент.Номер, Истина, Истина);
		КонецЕсли;
		
		Если ИмяМакета = "ПлатежнаяВедомость" Тогда
		
			Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ПлатежнаяВедомость.ПФ_MXL_Макет");
			
			КодЯзыкаПечать = Локализация.ПолучитьЯзыкФормированияПечатныхФорм();	
			Макет.КодЯзыкаМакета = КодЯзыкаПечать;
		
			ОбластьШапкаДокумента = Макет.ПолучитьОбласть("ШапкаДокумента");
			ОбластьШапка          = Макет.ПолучитьОбласть("Шапка");
			ОбластьДетали         = Макет.ПолучитьОбласть("Детали");
			ОбластьПодвал         = Макет.ПолучитьОбласть("Подвал");
			
			ОбластьШапкаДокумента.Параметры.НазваниеОрганизации = ТекущийДокумент.Организация.НаименованиеПолное;
			ОбластьШапкаДокумента.Параметры.Подразделение = ТекущийДокумент.СтруктурнаяЕдиница;
			ОбластьШапкаДокумента.Параметры.СуммаДокПрописью = РаботаСКурсамиВалют.СформироватьСуммуПрописью(ТекущийДокумент.Сотрудники.Итог("СуммаПлатежа"), ТекущийДокумент.ВалютаДокумента,,КодЯзыкаПечать);
			ОбластьШапкаДокумента.Параметры.СуммаДок = ТекущийДокумент.Сотрудники.Итог("СуммаПлатежа");
			ОбластьШапкаДокумента.Параметры.Валюта = ТекущийДокумент.ВалютаДокумента;
			ОбластьШапкаДокумента.Параметры.НомерДок = НомерДокумента;
			ОбластьШапкаДокумента.Параметры.ДатаДок = ТекущийДокумент.Дата;
			ОбластьШапкаДокумента.Параметры.ОтчетныйПериодС = ТекущийДокумент.ПериодРегистрации;
			ОбластьШапкаДокумента.Параметры.ОтчетныйПериодПо = КонецМесяца(ТекущийДокумент.ПериодРегистрации);
			
			Руководители = УправлениеНебольшойФирмойСервер.ОтветственныеЛицаОрганизационнойЕдиницы(ТекущийДокумент.Организация, ТекущийДокумент.Дата);
			ОбластьШапкаДокумента.Параметры.Заполнить(Руководители);
		
			ТабличныйДокумент.Вывести(ОбластьШапкаДокумента);
			ОбластьШапка.Параметры.НадписьСумма = НСтр("ru='Сумма, ';uk='Сума, '", КодЯзыкаПечать) + (ТекущийДокумент.ВалютаДокумента);
			ТабличныйДокумент.Вывести(ОбластьШапка);
			
			НПП = 0;
			Пока Выборка.Следующий() Цикл
				НПП = НПП + 1;
				ОбластьДетали.Параметры.НомерСтроки = НПП;
				ОбластьДетали.Параметры.Заполнить(Выборка);
				Если ЗначениеЗаполнено(Выборка.Фамилия) Тогда
					ФИО = УправлениеНебольшойФирмойСервер.ПолучитьФамилиюИмяОтчество(Выборка.Фамилия, Выборка.Имя, Выборка.Отчество, Истина);
					ОбластьДетали.Параметры.ФизЛицо = ?(ЗначениеЗаполнено(ФИО), ФИО, Выборка.Физлицо);
				КонецЕсли; 
				ТабличныйДокумент.Вывести(ОбластьДетали);
			КонецЦикла;
		
			ТабличныйДокумент.Вывести(ОбластьПодвал);
		Иначе //Ведомость 2009
			Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ПлатежнаяВедомость.ПФ_MXL_Ведомость2009");
			ОбластьШапкаДокумента 	= Макет.ПолучитьОбласть("ШапкаДокумента");
			ОбластьШапка        	= Макет.ПолучитьОбласть("Шапка");
			ОбластьСтрока       	= Макет.ПолучитьОбласть("Строка");
			ОбластьИтогПоЛисту		= Макет.ПолучитьОбласть("ИтогПоЛисту");
			ОбластьПодвал       	= Макет.ПолучитьОбласть("Подвал");
			ОбластьШапкаДокумента.Параметры.НазваниеОрганизации = ТекущийДокумент.Организация.НаименованиеПолное;
			ОбластьШапкаДокумента.Параметры.НазваниеПодразделенияОрганизации = ТекущийДокумент.СтруктурнаяЕдиница;
			ОбластьШапкаДокумента.Параметры.СуммаДок = Строка(ТекущийДокумент.Сотрудники.Итог("СуммаПлатежа"))+ " " + ТекущийДокумент.ВалютаДокумента;
			ОбластьШапкаДокумента.Параметры.НомерВедомости = НомерДокумента;
			ОбластьШапкаДокумента.Параметры.ДатаДок = ТекущийДокумент.Дата;
			ОбластьШапкаДокумента.Параметры.ДатаВыплаты = ТекущийДокумент.Дата;
			ОбластьШапкаДокумента.Параметры.КодПоЕДРПОУ = ТекущийДокумент.Организация.КодПоЕДРПОУ;
			ОбластьШапкаДокумента.Параметры.Счет = "";
			Руководители = УправлениеНебольшойФирмойСервер.ОтветственныеЛицаОрганизационнойЕдиницы(ТекущийДокумент.Организация, ТекущийДокумент.Дата);
			ОбластьШапкаДокумента.Параметры.ФИОРуководителя       = Руководители.ФИОРуководителя;
			ОбластьШапкаДокумента.Параметры.ФИОГлавБухгалтера     = Руководители.ФИОГлавногоБухгалтера;
			ОбластьШапкаДокумента.Параметры.КоличествоЛистов = 1;
			ОбластьШапкаДокумента.Параметры.СуммаВып = "";
			ОбластьШапкаДокумента.Параметры.СуммаЗадеп = "";
			ОбластьШапка.Параметры.НомерВедомости = ТекущийДокумент.Номер;
			ТабличныйДокумент.Вывести(ОбластьШапкаДокумента);
			ТабличныйДокумент.Вывести(ОбластьШапка); 			
			
			НПП = 0;
			ИтогоНаСтранице = 0;
			Пока Выборка.Следующий() Цикл
				НПП = НПП + 1;
				ОбластьСтрока.Параметры.НомерСтроки = НПП;
				ОбластьСтрока.Параметры.Заполнить(Выборка);
				ИтогоНаСтранице = ИтогоНаСтранице + Выборка.Сумма;
				ОбластьСтрока.Параметры.ЗаписьОДепонировании = "";
				Если ЗначениеЗаполнено(Выборка.Фамилия) Тогда
					ФИО = УправлениеНебольшойФирмойСервер.ПолучитьФамилиюИмяОтчество(Выборка.Фамилия, Выборка.Имя, Выборка.Отчество, Истина);
					ОбластьСтрока.Параметры.ФизЛицо = ?(ЗначениеЗаполнено(ФИО), ФИО, Выборка.Физлицо);
				КонецЕсли; 
				ТабличныйДокумент.Вывести(ОбластьСтрока);
			КонецЦикла;
			ОбластьИтогПоЛисту.Параметры.ИтогоНаСтранице = ИтогоНаСтранице;
			
			ТабличныйДокумент.Вывести(ОбластьИтогПоЛисту);
			
			ТабличныйДокумент.Вывести(ОбластьПодвал);			
		КонецЕсли;
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, ТекущийДокумент);
	
	КонецЦикла;
	
	ТабличныйДокумент.АвтоМасштаб = Истина;
	
	Возврат ТабличныйДокумент;
	
КонецФункции // ПечатнаяФорма()

// Сформировать печатные формы объектов
//
// ВХОДЯЩИЕ:
//   ИменаМакетов    - Строка    - Имена макетов, перечисленные через запятую
//   МассивОбъектов  - Массив    - Массив ссылок на объекты которые нужно распечатать
//   ПараметрыПечати - Структура - Структура дополнительных параметров печати
//
// ИСХОДЯЩИЕ:
//   КоллекцияПечатныхФорм - Таблица значений - Сформированные табличные документы
//   ПараметрыВывода       - Структура        - Параметры сформированных табличных документов
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПлатежнаяВедомость") Тогда
		Если МассивОбъектов[0].Дата >= '20090101' Тогда
			УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "ПлатежнаяВедомость", "Платежная ведомость", ПечатнаяФорма(МассивОбъектов, ОбъектыПечати, "ПлатежнаяВедомость2009"));
		Иначе
		
			УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "ПлатежнаяВедомость", "Платежная ведомость", ПечатнаяФорма(МассивОбъектов, ОбъектыПечати));
		КонецЕсли;
		
	КонецЕсли;
	
	// параметры отправки печатных форм по электронной почте
	УправлениеНебольшойФирмойСервер.ЗаполнитьПараметрыОтправки(ПараметрыВывода.ПараметрыОтправки, МассивОбъектов, КоллекцияПечатныхФорм);
	
КонецПроцедуры // Печать()

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ПлатежнаяВедомость";
	КомандаПечати.Представление = НСтр("ru='Платежная ведомость';uk='Платіжна відомість'");
	КомандаПечати.СписокФорм = "ФормаДокумента,ФормаСписка";
	КомандаПечати.ПроверкаПроведенияПередПечатью = Ложь;
	КомандаПечати.Порядок = 1;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли