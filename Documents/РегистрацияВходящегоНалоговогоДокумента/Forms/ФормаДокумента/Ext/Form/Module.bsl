////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ

&НаКлиенте
Перем мСписокВыбораСпецРежимНалогообложения;

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

&НаСервере
// Процедура вызывает обработку заполнения документа по основанию.
//
Процедура ЗаполнитьПоДокументу()
	  
	Документ = РеквизитФормыВЗначение("Объект");
	Документ.ОбработкаЗаполнения(Объект.ДокументОснование, );
	ЗначениеВРеквизитФормы(Документ, "Объект");
	Модифицированность = Истина;	
	
КонецПроцедуры // ЗаполнитьПоДокументу()

&НаСервереБезКонтекста
// Получает набор данных с сервера для процедуры ДатаПриИзменении.
//
Функция ПолучитьДанныеДатаПриИзменении(ДокументСсылка, ДатаНовая, ДатаПередИзменением)
	
	СтруктураДанные = Новый Структура();
	СтруктураДанные.Вставить("РазностьДат", УправлениеНебольшойФирмойСервер.ПроверитьНомерДокумента(ДокументСсылка, ДатаНовая, ДатаПередИзменением));
	
	Возврат СтруктураДанные;
	
КонецФункции // ПолучитьДанныеДатаПриИзменении()

&НаСервереБезКонтекста
// Получает набор данных с сервера.
//
Функция ПолучитьДанныеОрганизацияПриИзменении(Организация)
	
	СтруктураДанные = Новый Структура();
	СтруктураДанные.Вставить("Компания", УправлениеНебольшойФирмойСервер.ПолучитьОрганизацию(Организация));
	
	Возврат СтруктураДанные;
	
КонецФункции // ПолучитьДанныеОрганизацияПриИзменении()

&НаКлиенте
// Рассчитывается сумма НДС в строке табличной части.
//
Процедура РассчитатьСуммуНДС(СтрокаТабличнойЧасти)
	
	СтавкаНДС = УправлениеНебольшойФирмойПовтИсп.ПолучитьЗначениеСтавкиНДС(СтрокаТабличнойЧасти.СтавкаНДС);
	
	СтрокаТабличнойЧасти.СуммаНДС = СтрокаТабличнойЧасти.Сумма * СтавкаНДС / 100;	
	
КонецПроцедуры // ПересчитатьСуммыДокумента() 

&НаКлиенте
// Процедура рассчитывает сумму в строке табличной части.
//
Процедура РассчитатьСуммуВСтрокеТабличнойЧасти(СтрокаТабличнойЧасти = Неопределено)
	
	Если СтрокаТабличнойЧасти = Неопределено Тогда
		СтрокаТабличнойЧасти = Элементы.Запасы.ТекущиеДанные;
	КонецЕсли;
		
	// Сумма.
	СтрокаТабличнойЧасти.Сумма = СтрокаТабличнойЧасти.Количество * СтрокаТабличнойЧасти.Цена;
		
	// Сумма НДС.
	РассчитатьСуммуНДС(СтрокаТабличнойЧасти);
	
	// Всего.
	СтрокаТабличнойЧасти.СуммаВзаиморасчетов = СтрокаТабличнойЧасти.Сумма + СтрокаТабличнойЧасти.СуммаНДС;
	СтрокаТабличнойЧасти.Всего               = СтрокаТабличнойЧасти.СуммаВзаиморасчетов;
	
КонецПроцедуры // РассчитатьСуммуВСтрокеТабличнойЧасти()	

&НаСервереБезКонтекста
// Получает набор данных с сервера для процедуры КонтрагентПриИзменении.
//
Функция ПолучитьДанныеКонтрагентПриИзменении(Контрагент, Организация)
	
	ДоговорПоУмолчанию = Контрагент.ДоговорПоУмолчанию;
		
	Возврат ДоговорПоУмолчанию;
	
КонецФункции // ПолучитьДанныеКонтрагентПриИзменении()

&НаСервере
Функция ПолучитьЗначениеКорректировки()
	Корректировка = ((Объект.ВидОперации = Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.РасчетКорректировкиВозврат) 
				 ИЛИ (Объект.ВидОперации = Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.РасчетКорректировкиКорректировка));
				 
	Возврат Корректировка;
	
КонецФункции // ПолучитьЗначениеКорректировки()

&НаСервере
Функция ПолучитьЗначениеВключаетсяВУточняющийРасчет()
	
	Возврат Объект.ВидОперации = Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.ИсправлениеОшибки;
	
КонецФункции // ПолучитьЗначениеВключаетсяВУточняющийРасчет()

&НаСервере
Функция ПолучитьЗначениеЧека()
	
	Чек = (Объект.ВидОперации = Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.ТоварныйЧек);
	Возврат Чек;
	
КонецФункции // ПолучитьЗначениеЧека()

&НаСервере
Функция ПолучитьЗначениеИсправлениеОшибок()
	
	Возврат Объект.ВидОперации = Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.ИсправлениеОшибки;
	
КонецФункции // ПолучитьЗначениеЧека()

&НаСервере
Функция ПолучитьЗначениеИмпорт()

	мИмпорт = Истина;
	Если 	Объект.ВидОперации = Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.НалоговаяНакладная
		ИЛИ Объект.ВидОперации = Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.ТоварныйЧек
		ИЛИ Объект.ВидОперации = Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.РасчетКорректировкиВозврат
		ИЛИ Объект.ВидОперации = Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.РасчетКорректировкиКорректировка	Тогда
		
		мИмпорт = Ложь;
		
	КонецЕсли;

	Возврат мИмпорт;
	
КонецФункции
 
&НаКлиенте
Процедура ОбновитьПоясняющиеНадписи()

	Элементы.РасшифровкаСпецРежимНалогообложения.Заголовок = "";
	ТекСпецРежим = мСписокВыбораСпецРежимНалогообложения.НайтиПоЗначению(Объект.СпецРежимНалогообложения);
	Если НЕ ТекСпецРежим = Неопределено Тогда
		Элементы.РасшифровкаСпецРежимНалогообложения.Заголовок = ТекСпецРежим.Представление;		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
// Процедура устанавливает видимость элементов формы 
//
Процедура УстановитьВидимость()

    мИмпорт = ПолучитьЗначениеИмпорт();
	
	Элементы.ФормаОплаты.Видимость 	   		 		   = НЕ мИмпорт;
	
	Элементы.ДатаВыдачиВекселя.Видимость			   = мИмпорт;
	Элементы.ДатаПогашенияВекселя.Видимость 		   = мИмпорт;
	Элементы.ФактическаяДатаПогашенияВекселя.Видимость = мИмпорт;

	Чек = ПолучитьЗначениеЧека(); 
	ИсправлениеОшибок = ПолучитьЗначениеИсправлениеОшибок();
	Элементы.ВидДокумента.Видимость			    = Чек ИЛИ ИсправлениеОшибок;
	Элементы.ВидДокументаРасшифровка.Видимость	= Чек ИЛИ ИсправлениеОшибок;
	
	Элементы.ВключаетсяВУточняющийРасчет.Доступность = НЕ ПолучитьЗначениеВключаетсяВУточняющийРасчет();
	
	Элементы.УточняемыйПериод.Видимость = Объект.ВключаетсяВУточняющийРасчет;
	
	Корректировка = ПолучитьЗначениеКорректировки();
	Элементы.НомерКорректируемогоВходящегоДокумента.Видимость = Корректировка;
	Элементы.ДатаКорректируемогоВходящегоДокумента.Видимость  = Корректировка;
	Элементы.ОснованиеКорректировки.Видимость 		          = Корректировка; 
	
КонецПроцедуры // УстановитьВидимость()

&НаСервере
// Перезаполняет Статьи книги приобретения в строках ТЧ Запасы
Процедура ПерезаполнитьСтатьиКнигиПриобретения()

	Документ = РеквизитФормыВЗначение("Объект");
	Для каждого СтокаТЧ из Объект.Запасы Цикл
		СтокаТЧ.СтатьяКнигиПриобретения = Документ.ОпределитьСтатьюНалоговойДекларации(СтокаТЧ);
	КонецЦикла;
	
КонецПроцедуры

/////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ ДЛЯ РАБОТЫ С ПОДБОРОМ

&НаКлиенте
// Процедура - обработчик события Действие команды Подбор
//
Процедура Подбор(Команда)
	
	ИмяТабличнойЧасти 	= "Запасы";
	ЕстьХарактеристики 	= Истина;
	ЕстьПартии 			= Истина;
	
	ПараметрыПодбора = Новый Структура;
	
	ПараметрыПодбора.Вставить("Период", 				Объект.Дата);
	ПараметрыПодбора.Вставить("Организация", 			Компания);
	
	ПараметрыПодбора.Вставить("СуммаВключаетНДС", 		Ложь);
	
	Если Объект[ИмяТабличнойЧасти].Количество() > 0 Тогда
		АдресЗапасовВХранилище = ПоместитьЗапасыВХранилище(ИмяТабличнойЧасти, ЕстьХарактеристики, ЕстьПартии);
		ПараметрыПодбора.Вставить("АдресЗапасовДокумента", АдресЗапасовВХранилище);
	КонецЕсли; 
	
	ТипНоменклатуры = Новый СписокЗначений;
	Для каждого ЭлементМассива Из Элементы[ИмяТабличнойЧасти + "НаименованиеТовара"].ПараметрыВыбора Цикл
		Если ЭлементМассива.Имя = "Отбор.ТипНоменклатуры" Тогда
			Если ТипЗнч(ЭлементМассива.Значение) = Тип("ФиксированныйМассив") Тогда
				Для каждого ЭлементФиксМассива Из ЭлементМассива.Значение Цикл
					ТипНоменклатуры.Добавить(ЭлементФиксМассива);
				КонецЦикла; 
			Иначе
				ТипНоменклатуры.Добавить(ЭлементМассива.Значение);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	ПараметрыПодбора.Вставить("ТипНоменклатуры",	   ТипНоменклатуры);
		
	//СтатусПартии = Новый СписокЗначений;
	//Для каждого ЭлементМассива Из Элементы[ИмяТабличнойЧасти + "Партия"].ПараметрыВыбора Цикл
	//	Если ЭлементМассива.Имя = "Отбор.Статус" Тогда			
	//		Если ТипЗнч(ЭлементМассива.Значение) = Тип("ФиксированныйМассив") Тогда
	//			Для каждого ЭлементФиксМассива Из ЭлементМассива.Значение Цикл
	//				СтатусПартии.Добавить(ЭлементФиксМассива);
	//			КонецЦикла; 
	//		Иначе
	//			СтатусПартии.Добавить(ЭлементМассива.Значение);
	//		КонецЕсли;			
	//	КонецЕсли;
	//КонецЦикла;
	//ПараметрыПодбора.Вставить("СтатусПартии",	   		СтатусПартии);
		
	ФормаПодбора = ПолучитьФорму("ОбщаяФорма.ФормаПодбора", ПараметрыПодбора);
	
	АдресЗапасовВХранилище = ФормаПодбора.ОткрытьМодально();
	Если ЗначениеЗаполнено(АдресЗапасовВХранилище) Тогда
		ПолучитьЗапасыИзХранилища(АдресЗапасовВХранилище, ИмяТабличнойЧасти, ЕстьХарактеристики, ЕстьПартии);
	КонецЕсли;

КонецПроцедуры // ПодборВыполнить()

&НаСервере
// Функция помещает список запасов во временное хранилище и возвращает адрес 
//
Функция ПоместитьЗапасыВХранилище(ИмяТабличнойЧасти, ЕстьХарактеристики, ЕстьПартии) 
	
	СписокКолонок = "НаименованиеТовара, Количество, ЕдиницаИзмерения";
	Если ЕстьХарактеристики Тогда
		СписокКолонок = СписокКолонок + ", Характеристика";
	КонецЕсли;
	//Если ЕстьПартии Тогда
	//	СписокКолонок = СписокКолонок + ", Партия";
	//КонецЕсли;
	
	Возврат ПоместитьВоВременноеХранилище(Объект[ИмяТабличнойЧасти].Выгрузить(, СписокКолонок), УникальныйИдентификатор);
	
КонецФункции // ПоместитьЗапасыВХранилище()

&НаСервере
// Функция получает список товаров из временного хранилища
//
Процедура ПолучитьЗапасыИзХранилища(АдресЗапасовВХранилище, ИмяТабличнойЧасти, ЕстьХарактеристики, ЕстьПартии)
	
	ТаблицаДляЗагрузки = ПолучитьИзВременногоХранилища(АдресЗапасовВХранилище);
	
	Для каждого СтрокаЗагрузки Из ТаблицаДляЗагрузки Цикл
		
		СтруктураОтбора = Новый Структура;
		СтруктураОтбора.Вставить("НаименованиеТовара", 		СтрокаЗагрузки.Номенклатура.Наименование);
		//Если ЕстьХарактеристики Тогда
		//	СтруктураОтбора.Вставить("Характеристика", 	СтрокаЗагрузки.Характеристика);
		//КонецЕсли;
		//Если ЕстьПартии Тогда
		//	СтруктураОтбора.Вставить("Партия", 			СтрокаЗагрузки.Партия);
		//КонецЕсли;
		
		Строки = Объект[ИмяТабличнойЧасти].НайтиСтроки(СтруктураОтбора);
		Если Строки.Количество() > 0 Тогда
			
			СтрокаТабличнойЧасти = Строки[0];
			СтрокаТабличнойЧасти.Количество = СтрокаЗагрузки.Количество;
				
			// Сумма.
			СтрокаТабличнойЧасти.Сумма = СтрокаТабличнойЧасти.Количество * СтрокаТабличнойЧасти.Цена;
				
			// Сумма НДС.
			СтавкаНДС = ?(ЗначениеЗаполнено(СтрокаТабличнойЧасти.СтавкаНДС), СтрокаТабличнойЧасти.СтавкаНДС.Ставка, 0);	
			СтрокаТабличнойЧасти.СуммаНДС = СтрокаТабличнойЧасти.Сумма * СтавкаНДС / 100;
			
			// Всего.
			СтрокаТабличнойЧасти.СуммаВзаиморасчетов = СтрокаТабличнойЧасти.Сумма + СтрокаТабличнойЧасти.СуммаНДС;			
			СтрокаТабличнойЧасти.Всего               = СтрокаТабличнойЧасти.СуммаВзаиморасчетов;
	
        Иначе 
			НоваяСтрока = Объект[ИмяТабличнойЧасти].Добавить();			
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаЗагрузки);						
		КонецЕсли;	
		
	КонецЦикла;
	
КонецПроцедуры // ПолучитьЗапасыИзХранилища()


////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ДЛЯ УПРАВЛЕНИЯ ВНЕШНИМ ВИДОМ ФОРМЫ

&НаКлиенте
// Процедура устанавливает доступные типы документов-оснований.
//
// Параметры:
//  Нет.
//
Процедура ПриИзмененииВидаОперации()
		
	ВключаетсяВУточняющийРасчет = ПолучитьЗначениеВключаетсяВУточняющийРасчет();	
	
	ПерезаполнитьСтатьиКнигиПриобретения();
	
	УстановитьВидимость();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
// Процедура - обработчик события ПриСозданииНаСервере.
//
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Установка реквизитов формы.
	ДатаДокумента = Объект.Дата;
	Если НЕ ЗначениеЗаполнено(ДатаДокумента) Тогда
		ДатаДокумента = ТекущаяДата();
	КонецЕсли;
	Компания = УправлениеНебольшойФирмойСервер.ПолучитьОрганизацию(Объект.Организация);	
	Контрагент = Объект.Контрагент;
			
	
КонецПроцедуры // ПриСозданииНаСервере()

&НаКлиенте
// Процедура - обработчик события ПослеЗаписи.
//
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	УправлениеНебольшойФирмойКлиент.ОбновитьСостояниеДокумента(Объект, СостояниеДокумента, КартинкаСостоянияДокумента, РазрешеноПроведение);
	
КонецПроцедуры // ПослеЗаписи()

&НаКлиенте
// Процедура - обработчик события ПриОткрытии.
//
Процедура ПриОткрытии(Отказ)
	
	Если НеОткрыватьФорму Тогда
		Отказ = Истина;
	Иначе	
		
		ПриИзмененииВидаОперации();

		ОбновитьПоясняющиеНадписи();
		
	КонецЕсли;
	
КонецПроцедуры // ПриОткрытии()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ДЕЙСТВИЯ КОМАНДНЫХ ПАНЕЛЕЙ ФОРМЫ
      
&НаКлиенте
// Процедура - обработчик нажатия на кнопку Заполнить.
//
Процедура Заполнить(Команда)
	
	ПоказатьВопрос(Новый ОписаниеОповещения("ЗаполнитьЗавершение", ЭтотОбъект), НСтр("ru='Документ будет полностью перезаполнен по ""Основанию""! Продолжить выполнение операции?';uk='Документ буде повністю перезаповнений по ""Підставі""! Продовжити виконання операції?'"), РежимДиалогаВопрос.ДаНет, 0);

КонецПроцедуры  // ЗаполнитьВыполнить()

&НаКлиенте
Процедура ЗаполнитьЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт

	Ответ = РезультатВопроса;
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		ЗаполнитьПоДокументу();		
		ПриИзмененииВидаОперации();
	КонецЕсли;	

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ РЕКВИЗИТОВ ШАПКИ

&НаКлиенте
// Процедура - обработчик события НачалоВыбораИзСписка поля ввода ВидДокумента.
//
Процедура ВидДокументаНачалоВыбораИзСписка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	СписокВидовДокумента = Новый СписокЗначений;
	СписокВидовДокумента.Добавить("",    "(ЧК) Товарний чек");
	СписокВидовДокумента.Добавить("ЧК",  "(ЧК) Касовий чек");
	СписокВидовДокумента.Добавить("ТК",  "(ТК) Транспортний квиток");
	СписокВидовДокумента.Добавить("ГР",  "(ГР) Готельний рахунок");
	СписокВидовДокумента.Добавить("ПЗ",  "(ПЗ) Рахунок за послуги зв'язку");
	СписокВидовДокумента.Добавить("ПО",  "(ПО) Послуги, вартість яких по даним приладів обліку");
	СписокВидовДокумента.Добавить("ЗП",  "(ЗП) Заява платника відповідно до пункту 7.2.6 Закону");
	
	Обработчик = Новый ОписаниеОповещения("ВидДокументаПослеВыбораИзСписка", ЭтотОбъект, Новый Структура("СписокВидовДокумента", СписокВидовДокумента));
	ПоказатьВыборИзСписка(Обработчик, СписокВидовДокумента, Элемент, СписокВидовДокумента.НайтиПоЗначению(Объект.ВидДокумента));
	
КонецПроцедуры // ВидДокументаНачалоВыбораИзСписка()

&НаКлиенте
Процедура ВидДокументаПослеВыбораИзСписка(ВыбранныйЭлемент, ДополнительныеПараметры) Экспорт
	
	Результат = ВыбранныйЭлемент;
	СписокВидовДокумента = ДополнительныеПараметры.СписокВидовДокумента;
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;

	//Элемент.Значение = Результат.Значение;
	Объект.ВидДокумента = Результат.Значение;
	
	Элементы.ВидДокументаРасшифровка.Заголовок = "";
	ТекВидДокумента = СписокВидовДокумента.НайтиПоЗначению(Объект.ВидДокумента);
	Если НЕ ТекВидДокумента = Неопределено Тогда
		Элементы.ВидДокументаРасшифровка.Заголовок  = ТекВидДокумента.Представление;		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
// Процедура - обработчик события ПриИзменении поля ввода Дата.
// В процедуре определяется ситуация, когда при изменении своей даты документ 
// оказывается в другом периоде нумерации документов, и в этом случае
// присваивает документу новый уникальный номер.
// Переопределяет соответствующий параметр формы.
//
Процедура ДатаПриИзменении(Элемент)
	
	// Обработка события изменения даты.
	ДатаПередИзменением = ДатаДокумента;
	ДатаДокумента = Объект.Дата;
	Если Объект.Дата <> ДатаПередИзменением Тогда
		СтруктураДанные = ПолучитьДанныеДатаПриИзменении(Объект.Ссылка, Объект.Дата, ДатаПередИзменением);
		Если СтруктураДанные.РазностьДат <> 0 Тогда
			Объект.Номер = "";
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // ДатаПриИзменении()

&НаКлиенте
// Процедура - обработчик события ПриИзменении поля ввода Организация.
// В процедуре осуществляется очистка номера документа,
// а также производится установка параметров функциональных опций формы.
// Переопределяет соответствующий параметр формы.
//
Процедура ОрганизацияПриИзменении(Элемент)

	// Обработка события изменения организации.
	Объект.Номер = "";
	СтруктураДанные = ПолучитьДанныеОрганизацияПриИзменении(Объект.Организация);
	Компания = СтруктураДанные.Компания;
	
	УстановитьВидимость();
	
КонецПроцедуры // ОрганизацияПриИзменении()

&НаКлиенте
// Процедура - обработчик события ПриИзменении поля ввода ВидОперации.
//
Процедура ВидОперацииПриИзменении(Элемент)
	
	ПриИзмененииВидаОперации();
	
КонецПроцедуры // ВидОперацииПриИзменении()

&НаКлиенте
Процедура КонтрагентПриИзменении(Элемент)
	
	КонтрагентПередИзменением = Контрагент;
	Контрагент = Объект.Контрагент;
	
	Если КонтрагентПередИзменением <> Объект.Контрагент Тогда
		Объект.Договор = ПолучитьДанныеКонтрагентПриИзменении(Объект.Контрагент, Объект.Организация);
	КонецЕсли;
	
	УстановитьВидимость();
	
КонецПроцедуры

&НаКлиенте
Процедура ДоговорПриИзменении(Элемент)
	
	УстановитьВидимость();
	
КонецПроцедуры

&НаКлиенте
Процедура ВключаетсяВУточняющийРасчетПриИзменении(Элемент)
	УстановитьВидимость();
КонецПроцедуры

&НаКлиенте
Процедура СпецРежимНалогообложенияПриИзменении(Элемент)
	ОбновитьПоясняющиеНадписи();
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ РЕКВИЗИТОВ ТАБЛИЧНОЙ ЧАСТИ ЗАПАСЫ

&НаКлиенте
// Процедура - обработчик события ПриИзменении поля ввода Количество.
//
Процедура ЗапасыКоличествоПриИзменении(Элемент)
	
	РассчитатьСуммуВСтрокеТабличнойЧасти();
	
КонецПроцедуры // ЗапасыКоличествоПриИзменении()

&НаКлиенте
// Процедура - обработчик события ПриИзменении поля ввода Цена.
//
Процедура ЗапасыЦенаПриИзменении(Элемент)
	
	РассчитатьСуммуВСтрокеТабличнойЧасти();
	
КонецПроцедуры // ЗапасыЦенаПриИзменении()

&НаКлиенте
// Процедура - обработчик события ПриИзменении поля ввода Сумма.
//
Процедура ЗапасыСуммаПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.Запасы.ТекущиеДанные;
	
	// Цена.
	Если СтрокаТабличнойЧасти.Количество <> 0 Тогда
		СтрокаТабличнойЧасти.Цена = СтрокаТабличнойЧасти.Сумма / СтрокаТабличнойЧасти.Количество;
	КонецЕсли;
	
	// Сумма НДС.
	РассчитатьСуммуНДС(СтрокаТабличнойЧасти);
	
	// Всего.	
	СтрокаТабличнойЧасти.СуммаВзаиморасчетов = СтрокаТабличнойЧасти.Сумма + СтрокаТабличнойЧасти.СуммаНДС;
	СтрокаТабличнойЧасти.Всего               = СтрокаТабличнойЧасти.СуммаВзаиморасчетов;
	
КонецПроцедуры // ЗапасыСуммаПриИзменении()
 
&НаКлиенте
// Процедура - обработчик события ПриИзменении поля ввода СтавкаНДС.
//
Процедура ЗапасыСтавкаНДСПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.Запасы.ТекущиеДанные;
	
	// Сумма НДС.
	РассчитатьСуммуНДС(СтрокаТабличнойЧасти);
	
	// Всего.	
	СтрокаТабличнойЧасти.СуммаВзаиморасчетов = СтрокаТабличнойЧасти.Сумма + СтрокаТабличнойЧасти.СуммаНДС;
	СтрокаТабличнойЧасти.Всего               = СтрокаТабличнойЧасти.СуммаВзаиморасчетов;
	
КонецПроцедуры  // ЗапасыСтавкаНДСПриИзменении()

&НаКлиенте
// Процедура - обработчик события ПриИзменении поля ввода СтавкаНДС.
//
Процедура ЗапасыСуммаНДСПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.Запасы.ТекущиеДанные;
	
	// Всего.	
	СтрокаТабличнойЧасти.СуммаВзаиморасчетов = СтрокаТабличнойЧасти.Сумма + СтрокаТабличнойЧасти.СуммаНДС;	
	СтрокаТабличнойЧасти.Всего               = СтрокаТабличнойЧасти.СуммаВзаиморасчетов;
	
КонецПроцедуры // ЗапасыСуммаНДСПриИзменении()

&НаКлиенте
// Процедура - обработчик события ПриИзменении поля ввода СуммаВзаиморасчетов.
//
Процедура ЗапасыСуммаВзаиморасчетовПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.Запасы.ТекущиеДанные;
	
	// Сумма
	СтавкаНДС = УправлениеНебольшойФирмойПовтИсп.ПолучитьЗначениеСтавкиНДС(СтрокаТабличнойЧасти.СтавкаНДС);	
	СтрокаТабличнойЧасти.СуммаНДС = СтрокаТабличнойЧасти.СуммаВзаиморасчетов * СтавкаНДС / (100 + СтавкаНДС);		
	СтрокаТабличнойЧасти.Сумма = СтрокаТабличнойЧасти.СуммаВзаиморасчетов * 100 / (100 + СтавкаНДС);
	
	// Всего.	
	СтрокаТабличнойЧасти.Всего = СтрокаТабличнойЧасти.СуммаВзаиморасчетов;
	
	// Цена.
	Если СтрокаТабличнойЧасти.Количество <> 0 Тогда
		СтрокаТабличнойЧасти.Цена = СтрокаТабличнойЧасти.Сумма / СтрокаТабличнойЧасти.Количество;
	КонецЕсли;
	
КонецПроцедуры


мСписокВыбораСпецРежимНалогообложения = Новый СписокЗначений();
мСписокВыбораСпецРежимНалогообложения.Добавить(0, "- спец. режим не застосовується");
мСписокВыбораСпецРежимНалогообложения.Добавить(2, "2-сільськогосподарські підприємства, які застосовують 
													|спеціальний режим оподаткування діяльності у сфері сільського та 
													|лісового господарства, а також рибальства відповідно до статті 209 Кодексу");
мСписокВыбораСпецРежимНалогообложения.Добавить(3, "3-сільськогосподарські підприємства усіх форм власності, 
													|які відповідають критеріям, визначеним статтею 209 Кодексу ,
													|але які не обрали спеціальний режим оподаткування 
													|діяльності у сфері сільського, лісового господарства та рибальства 
													|та реалізують молоко, худобу, птицю, вовну власного виробництва, а 
													|також молочні продукти, молочну сировину та м'ясопродукти, 
													|вироблені у власних переробних цехах");
мСписокВыбораСпецРежимНалогообложения.Добавить(4, "4 - переробні підприємства усіх форм власності, які згідно з 
													|пунктом 1 підрозділу 2 розділу XX Кодексу за 
													|реалізовані ними молоко, молочну сировину та молочні продукти, 
													|м'ясо та м'ясопродукти, іншу продукцію переробних тварин, 
													|закуплених у живій вазі (шкури, субпродукти, м'ясо-кісткове 
													|борошно), у повному обсязі спрямовують суми податку на додану 
													|вартість до спеціального фонду державного бюджету");
