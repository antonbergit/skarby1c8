
///////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ ЗАПОЛНЕНИЯ ДОКУМЕНТА

// Процедура заполняет вид операции 
// Параметры: 
//  ДанныеЗаполнения - Структура - Данные заполнения документа
//
Процедура ЗаполнитьВидОперации(ДанныеЗаполнения)
	
	мВалютаРегламентированногоУчета = Константы.НациональнаяВалюта.Получить();
	
	Если  ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.РасходнаяНакладная") 
	  ИЛИ ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ПоступлениеВКассу")
	  ИЛИ ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ПоступлениеНаСчет") Тогда
		// возврат
		ВидОперации = Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.РасчетКорректировкиВозврат;
		
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.РасходИзКассы")
		  ИЛИ ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.РасходСоСчета") Тогда
		  
		ВидОперации = Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.НалоговаяНакладная;
		
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ПриходнаяНакладная") тогда 
		 Если НЕ (ДанныеЗаполнения.ВалютаДокумента = мВалютаРегламентированногоУчета) Тогда	
		
			ВидОперации = Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.РаботыОтНерезидентаПрошлогоПериода;
		Иначе
			
			ВидОперации = Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.НалоговаяНакладная;
			
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ОтчетКомиссионера")  
		 И НЕ (ДанныеЗаполнения.ВалютаДокумента = мВалютаРегламентированногоУчета) Тогда	
		
		ВидОперации = Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.РаботыОтНерезидентаПрошлогоПериода;
			
	Иначе
		
		Если НЕ (ДанныеЗаполнения.Ссылка.Метаданные().Реквизиты.Найти("УчитыватьНДС") = Неопределено) 
			И    ДанныеЗаполнения.УчитыватьНДС Тогда
			ВидОперации = Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.НалоговаяНакладная;
		ИначеЕсли НЕ (ДанныеЗаполнения.Ссылка.Метаданные().ТабличныеЧасти.Найти("РасшифровкаПлатежа") = Неопределено) тогда 
			ВидОперации = Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.НалоговаяНакладная;
		Иначе
			//покупка у неплательщика НДС. Основание включения в книгу - товарний чек (п. 10.2 Порядка)
			ВидОперации = Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.ТоварныйЧек;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры // ЗаполнитьВидОперации()

// Функция определеяет рекомендуемое значение статьи налоговой декларации (СтатьяКнигиПриобретения)
// в зависимости от вида операции документа и значений параметров учета, указанных в строке ТЧ
//
// Параметры:
// СтрокаТЧ - строка ТЧ Товары (возвратная тара в книгу покупок не попадает)
//
Функция ОпределитьСтатьюНалоговойДекларации(СтрокаТЧ) Экспорт
	
	СтатьиДеклараций = Справочники.СтатьиНалоговыхДеклараций;

	Если  ВидОперации = Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.ПогашениеНалоговогоВекселя Тогда			
		
		Если Дата >= '20110301' Тогда
			Возврат  СтатьиДеклараций.ПустаяСсылка();
		Иначе	
			Возврат  СтатьиДеклараций.НДС_БюджКорректировкаВексельПогашенный;
		КонецЕсли;
		
	ИначеЕсли  ВидОперации = Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.РаботыОтНерезидента Тогда		
		
		// 7 Декларации,
		// 9 Книги приобретений
		Возврат  СтатьиДеклараций.НДС_НОНерезидентУслуги
		
	ИначеЕсли  ВидОперации = Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.ИмпортПрочее Тогда		
		// импорт не товаров
		Если 	СтрокаТЧ.СтавкаНДС.Ставка = 0 
			ИЛИ СтрокаТЧ.СтавкаНДС =  Справочники.СтавкиНДС.ОсновнаяСтавкаНДС ИЛИ СтрокаТЧ.СтавкаНДС.Ставка = 7 Тогда
			// ст 12.1 Декларации
			Возврат СтатьиДеклараций.НДС_НКИмпортВРОблагНДСТамож;
		Иначе
			// ст 12.3 Декларации
			Возврат СтатьиДеклараций.НДС_НКИмпортВРОблагБезНДС;
		КонецЕсли;
		
	ИначеЕсли  ВидОперации = Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.ИмпортВексель Тогда
		// импорт по векселю
		Если 	СтрокаТЧ.СтавкаНДС.Ставка = 0 
			ИЛИ СтрокаТЧ.СтавкаНДС =  Справочники.СтавкиНДС.ОсновнаяСтавкаНДС ИЛИ СтрокаТЧ.СтавкаНДС.Ставка = 7 Тогда
			// ст 6 Декларации
			Возврат СтатьиДеклараций.НДС_НОИмпортВексель;
		КонецЕсли; 

	Иначе
		// НалоговаяНакладная 
		// ТоварныйЧек 
		// РасчетКорректировки
		// РаботыОтНерезидентаПрошлогоПериода 
		// BeксeльПpoшлoгoПepиoдa 
		
		// Считаем, что есть право на налоговый кредит	
			
		Если    СтрокаТЧ.СтавкаНДС.Ставка = 0 
			ИЛИ СтрокаТЧ.СтавкаНДС =  Справочники.СтавкиНДС.ОсновнаяСтавкаНДС ИЛИ СтрокаТЧ.СтавкаНДС.Ставка = 7 Тогда
			// приобретение с уплатой НДС
			
			Если    ВидОперации = Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.НалоговаяНакладная
				ИЛИ ВидОперации = Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.ИсправлениеОшибки
				ИЛИ ВидОперации = Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.ТоварныйЧек Тогда
				
				// 10.1 Декларации,

					Если (Дата >= '20080101' И Дата < '20110301') И Договор.ВидДоговора = Перечисления.ВидыДоговоров.СКомитентом Тогда	
						
						Возврат  СтатьиДеклараций.НДС_НКПокупкаСНДСвВРОблагКомиссия;
						
					Иначе
						
							Возврат  СтатьиДеклараций.НДС_НКПокупкаСНДСвВРОблаг;
						
					КонецЕсли;
				
			ИначеЕсли   ВидОперации = Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.РасчетКорректировкиВозврат
				ИЛИ ВидОперации = Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.РасчетКорректировкиКорректировка	Тогда
								
				Возврат СтатьиДеклараций.НДС_НКИзменениеСтоимости	
				
			ИначеЕсли ВидОперации = Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.РаботыОтНерезидентаПрошлогоПериода Тогда
								
				Возврат  СтатьиДеклараций.НДС_НКИмпортВРОблагУслугиНерезидента;
				
			ИначеЕсли ВидОперации = Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.BeксeльПpoшлoгoПepиoдa Тогда
								
				Возврат  СтатьиДеклараций.НДС_НКИмпортВРОблагВексель;
				
			ИначеЕсли  ВидОперации = Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.ТаможенноеОформлениеТоваровКомитента Тогда
				
				
					Возврат  СтатьиДеклараций.НДС_НКИмпортВРОблагНДСТамож;
					
				
			КонецЕсли;
			
		Иначе
			
			// приобретение без НДС
			Если    ВидОперации = Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.НалоговаяНакладная
				ИЛИ ВидОперации = Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.ИсправлениеОшибки
				ИЛИ ВидОперации = Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.ТоварныйЧек Тогда
				
					// 11.1 Декларации,
					Возврат  СтатьиДеклараций.НДС_НКПокупкаБезНДСвВРОблаг;
					
				ИначеЕсли ВидОперации = Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.РасчетКорректировкиВозврат
					  ИЛИ ВидОперации = Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.РасчетКорректировкиКорректировка	Тогда
					  
					Если Дата < '20110301' Тогда
						 Возврат  СтатьиДеклараций.НДС_НКПокупкаБезНДСвВРОблаг;
					Иначе	
						 Возврат  СтатьиДеклараций.НДС_НККорректировкаБезНК;
					КонецЕсли; 
				
			Иначе
				// Импорт
				
				Если ВидОперации = Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.РаботыОтНерезидентаПрошлогоПериода Тогда
					
					// странно, услуги от нерезидента успользуемые на территории Украины облагаются по ставке 20%
					Возврат  СтатьиДеклараций.ПустаяСсылка();
					
				ИначеЕсли ВидОперации = Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.BeксeльПpoшлoгoПepиoдa Тогда
					
					// странно, был оформлен  вексель по ГТД не предусматривающей уплаты НДС
					Возврат  СтатьиДеклараций.ПустаяСсылка();
					
				ИначеЕсли  ВидОперации = Перечисления.ВидыОперацийРегистрацияВходящегоНалоговогоДокумента.ТаможенноеОформлениеТоваровКомитента Тогда
					
					
					Возврат  СтатьиДеклараций.НДС_НКИмпортВРОблагБезНДС;
						
					
				КонецЕсли;
			КонецЕсли;
			
		КонецЕсли;	
	
	КонецЕсли;			
	
КонецФункции // ОпределитьСтатьюНалоговойДекларации()

// Процедура заполнения документа на основании отчета комитенту
//
// Параметры:
//	ДокументОснование - ДокументСсылка.ОтчетКомиссионера - отчет комитенту
//	ДанныеЗаполнения - Структура - Данные заполнения документа
//
Процедура ЗаполнитьПоОтчетКомитенту(ДанныеЗаполнения)
	
	ДокументОснование = ДанныеЗаполнения;
	
	Контрагент = ДанныеЗаполнения.Контрагент;	
	Организация = ДанныеЗаполнения.Организация;	
	
	ДоговорСделка 	   = "";
	Договор = ДанныеЗаполнения.Договор;
			
	Комментарий = ДанныеЗаполнения.Комментарий;
	              	
	Запасы.Очистить();
	
	Для Каждого ТекСтрока Из ДанныеЗаполнения.Запасы Цикл
		
		СтрокаЗапасов = Запасы.Добавить();
		СтрокаЗапасов.НаименованиеТовара 	= ТекСтрока.Номенклатура.Наименование;
		СтрокаЗапасов.СтавкаНДС 			= ТекСтрока.СтавкаНДС;
		СтрокаЗапасов.СуммаНДС 				= ТекСтрока.СуммаНДС;
		СтрокаЗапасов.Сумма 				= ТекСтрока.Всего - ТекСтрока.СуммаНДС;
		СтрокаЗапасов.СуммаВзаиморасчетов	= ТекСтрока.Всего;		
		СтрокаЗапасов.СтатьяКнигиПриобретения	= ОпределитьСтатьюНалоговойДекларации(СтрокаЗапасов);
		СтрокаЗапасов.Всего = СтрокаЗапасов.СуммаВзаиморасчетов;
		
	КонецЦикла;
	
КонецПроцедуры // ЗаполнитьПоОтчетКомитенту()

// Процедура заполнения документа на основании приходной накладной
//
// Параметры:
//	ДокументОснование - ДокументСсылка.ПриходнаяНакладная - приходная накладная
//	ДанныеЗаполнения - Структура - Данные заполнения документа
//
Процедура ЗаполнитьПоПриходнаяНакладная(ДанныеЗаполнения)
	
	ДокументОснование = ДанныеЗаполнения;
	
	Контрагент = ДанныеЗаполнения.Контрагент;	
	Организация = ДанныеЗаполнения.Организация;	
	
	ДоговорСделка 	   = "";
	Договор = ДанныеЗаполнения.Договор;
			
	Комментарий = ДанныеЗаполнения.Комментарий;
	                         	
	Запасы.Очистить();
	
	ЗапасыПоДокументуОснованию = ДокументОснование.Запасы.Выгрузить();
	РасходыПоДокументуОснованию = ДокументОснование.Расходы.Выгрузить();
	
	ЗапасыПоДокументуОснованию.Свернуть("СтавкаНДС", "Сумма, СуммаНДС");
	РасходыПоДокументуОснованию.Свернуть("СтавкаНДС", "Сумма, СуммаНДС");
	
	Для Каждого ТекСтрока Из ЗапасыПоДокументуОснованию Цикл
		
		СтрокаЗапасов = Запасы.Добавить();
		СтрокаЗапасов.СтавкаНДС 			= ТекСтрока.СтавкаНДС;
		СтрокаЗапасов.СуммаНДС 				= ТекСтрока.СуммаНДС;
		Если ДокументОснование.СуммаВключаетНДС Тогда
			СтрокаЗапасов.Сумма 				= ТекСтрока.Сумма - ТекСтрока.СуммаНДС;
			СтрокаЗапасов.СуммаВзаиморасчетов	= ТекСтрока.Сумма;
			СтрокаЗапасов.Всего                 = СтрокаЗапасов.СуммаВзаиморасчетов;
		Иначе	
			СтрокаЗапасов.Сумма 				= ТекСтрока.Сумма;
			СтрокаЗапасов.СуммаВзаиморасчетов	= ТекСтрока.Сумма + ТекСтрока.СуммаНДС;
			СтрокаЗапасов.Всего                 = СтрокаЗапасов.СуммаВзаиморасчетов;
		КонецЕсли; 
		СтрокаЗапасов.СтатьяКнигиПриобретения	= ОпределитьСтатьюНалоговойДекларации(СтрокаЗапасов);
		
	КонецЦикла;
	
	Для Каждого ТекСтрока Из РасходыПоДокументуОснованию Цикл
		
		СтрокаЗапасов = Запасы.Добавить();
		СтрокаЗапасов.СтавкаНДС 			= ТекСтрока.СтавкаНДС;
		СтрокаЗапасов.СуммаНДС 				= ТекСтрока.СуммаНДС;
		Если ДокументОснование.СуммаВключаетНДС Тогда
			СтрокаЗапасов.Сумма 				= ТекСтрока.Сумма - ТекСтрока.СуммаНДС;
			СтрокаЗапасов.СуммаВзаиморасчетов	= ТекСтрока.Сумма;
			СтрокаЗапасов.Всего                 = СтрокаЗапасов.СуммаВзаиморасчетов;
		Иначе
			СтрокаЗапасов.Сумма 				= ТекСтрока.Сумма;
			СтрокаЗапасов.СуммаВзаиморасчетов	= ТекСтрока.Сумма + ТекСтрока.СуммаНДС;
			СтрокаЗапасов.Всего                 = СтрокаЗапасов.СуммаВзаиморасчетов;
		КонецЕсли; 
		СтрокаЗапасов.СтатьяКнигиПриобретения	= ОпределитьСтатьюНалоговойДекларации(СтрокаЗапасов);
		
	КонецЦикла;
	
КонецПроцедуры // ЗаполнитьПоПриходнаяНакладная()

// Процедура заполнения документа на основании отчета комиссионера
//
// Параметры:
//	ДокументОснование - ДокументСсылка.ОтчетКомиссионера - отчет комиссионера
//	ДанныеЗаполнения - Структура - Данные заполнения документа
//
Процедура ЗаполнитьПоОтчетКомиссионера(ДанныеЗаполнения)
	
	ДокументОснование = ДанныеЗаполнения;
	
	Контрагент = ДанныеЗаполнения.Контрагент;	
	Организация = ДанныеЗаполнения.Организация;	
	
	ДоговорСделка 	   = "";
	Договор = ДанныеЗаполнения.Договор;
			
	Комментарий = ДанныеЗаполнения.Комментарий;
	                         	
	Запасы.Очистить();
	
	Для Каждого ТекСтрока Из ДанныеЗаполнения.Запасы Цикл
		
		СтрокаЗапасов = Запасы.Добавить();
		СтрокаЗапасов.НаименованиеТовара 	= ТекСтрока.Номенклатура.Наименование;
		СтрокаЗапасов.СтавкаНДС 			= ТекСтрока.СтавкаНДС;
		СтрокаЗапасов.СуммаНДС 				= ТекСтрока.СуммаНДС;
		СтрокаЗапасов.Сумма 				= ТекСтрока.Сумма;
		СтрокаЗапасов.Цена 					= ТекСтрока.Цена;
		СтрокаЗапасов.Количество 			= ТекСтрока.Количество;
		СтрокаЗапасов.СуммаВзаиморасчетов	= ТекСтрока.Сумма + ТекСтрока.СуммаНДС;		
		СтрокаЗапасов.СтатьяКнигиПриобретения	= ОпределитьСтатьюНалоговойДекларации(СтрокаЗапасов);
		СтрокаЗапасов.Всего = СтрокаЗапасов.СуммаВзаиморасчетов;
		
	КонецЦикла;
	
КонецПроцедуры // ЗаполнитьПоОтчетКомиссионера()

// Процедура заполнения документа на основании счета на оплату
//
// Параметры:
//	ДокументОснование - ДокументСсылка.СчетНаОплату - счет на оплату
//	ДанныеЗаполнения - Структура - Данные заполнения документа
//
Процедура ЗаполнитьПоСчетНаОплату(ДанныеЗаполнения)
	
	ДокументОснование = ДанныеЗаполнения;
	
	Контрагент = ДанныеЗаполнения.Контрагент;	
	Организация = ДанныеЗаполнения.Организация;	
	
	ДоговорСделка 	   = "";
	Договор = ДанныеЗаполнения.Договор;
			
	Комментарий = ДанныеЗаполнения.Комментарий;
	                         	
	Запасы.Очистить();
	
	Для Каждого ТекСтрока Из ДанныеЗаполнения.Запасы Цикл
		
		СтрокаЗапасов = Запасы.Добавить();
		СтрокаЗапасов.НаименованиеТовара 	= ТекСтрока.Номенклатура.Наименование;
		СтрокаЗапасов.СтавкаНДС 			= ТекСтрока.СтавкаНДС;
		СтрокаЗапасов.СуммаНДС 				= ТекСтрока.СуммаНДС;
		СтрокаЗапасов.Цена 					= ТекСтрока.Цена;
		СтрокаЗапасов.Количество 			= ТекСтрока.Количество;
		СтрокаЗапасов.Сумма 				= ТекСтрока.Всего - ТекСтрока.СуммаНДС;
		СтрокаЗапасов.СуммаВзаиморасчетов	= ТекСтрока.Всего;		
		СтрокаЗапасов.СтатьяКнигиПриобретения	= ОпределитьСтатьюНалоговойДекларации(СтрокаЗапасов);
		СтрокаЗапасов.Всего = СтрокаЗапасов.СуммаВзаиморасчетов;
		
	КонецЦикла;

КонецПроцедуры // ЗаполнитьПоСчетНаОплату()

// Процедура заполнения документа на основании расходной накладной
//
// Параметры:
//	ДокументОснование - ДокументСсылка.РасходнаяНакладная - расходная накладная
//	ДанныеЗаполнения - Структура - Данные заполнения документа
//
Процедура ЗаполнитьПоРасходнаяНакладная(ДанныеЗаполнения)
	
	Если ДанныеЗаполнения.ВидОперации <> Перечисления.ВидыОперацийРасходнаяНакладная.ВозвратПоставщику Тогда
		
		ТекстИсключения = НСтр("ru='На основании документа ""Расходная накладная"" можно вводить ""Регистрация входящего налогового документа"" только при возврате поставщику!';uk='На підставі документа ""Видаткова накладна"" можна вводити ""Реєстрація вхідного податкового документа"" тільки при поверненні постачальникові!'");				
		ВызватьИсключение ТекстИсключения;
		
	КонецЕсли;	
	
	ДокументОснование = ДанныеЗаполнения;
	
	Контрагент = ДанныеЗаполнения.Контрагент;	
	Организация = ДанныеЗаполнения.Организация;	
	
	ДоговорСделка 	   = "";
	Договор = ДанныеЗаполнения.Договор;
			
	Комментарий = ДанныеЗаполнения.Комментарий;
	                         	
	Запасы.Очистить();
	
	Для Каждого ТекСтрока Из ДанныеЗаполнения.Запасы Цикл
		
		СтрокаЗапасов = Запасы.Добавить();
		СтрокаЗапасов.НаименованиеТовара 	= ТекСтрока.Номенклатура.Наименование;
		СтрокаЗапасов.СтавкаНДС 			= ТекСтрока.СтавкаНДС;
		СтрокаЗапасов.СуммаНДС 				= ТекСтрока.СуммаНДС;
		СтрокаЗапасов.Цена 					= ТекСтрока.Цена;
		СтрокаЗапасов.Количество 			= ТекСтрока.Количество;
		СтрокаЗапасов.Сумма 				= ТекСтрока.Всего - ТекСтрока.СуммаНДС;
		СтрокаЗапасов.СуммаВзаиморасчетов	= ТекСтрока.Всего;		
		СтрокаЗапасов.СтатьяКнигиПриобретения	= ОпределитьСтатьюНалоговойДекларации(СтрокаЗапасов);
		СтрокаЗапасов.Всего = СтрокаЗапасов.СуммаВзаиморасчетов;
		
	КонецЦикла;
	       	
КонецПроцедуры // ЗаполнитьПоРасходнаяНакладная()

// Процедура заполнения документа на основании отчета о переработке
//
// Параметры:
//	ДокументОснование - ДокументСсылка.ОтчетОПереработке - отчет о переработке
//	ДанныеЗаполнения - Структура - Данные заполнения документа
//
Процедура ЗаполнитьПоОтчетОПереработке(ДанныеЗаполнения)
	
	ДокументОснование = ДанныеЗаполнения;
	
	Контрагент = ДанныеЗаполнения.Контрагент;	
	Организация = ДанныеЗаполнения.Организация;	
	
	ДоговорСделка 	   = "";
	Договор = ДанныеЗаполнения.Договор;
			
	Комментарий = ДанныеЗаполнения.Комментарий;
	                         	
	Запасы.Очистить();
	
	Для Каждого ТекСтрока Из ДанныеЗаполнения.Продукция Цикл
		
		СтрокаЗапасов = Запасы.Добавить();
		СтрокаЗапасов.НаименованиеТовара 	= ТекСтрока.Номенклатура.Наименование;
		СтрокаЗапасов.СтавкаНДС 			= ТекСтрока.СтавкаНДС;
		СтрокаЗапасов.СуммаНДС 				= ТекСтрока.СуммаНДС;
		СтрокаЗапасов.Сумма 				= ТекСтрока.Сумма;
		СтрокаЗапасов.Цена 					= ТекСтрока.Цена;
		СтрокаЗапасов.Количество 			= ТекСтрока.Количество;
		СтрокаЗапасов.СуммаВзаиморасчетов	= ТекСтрока.Сумма + ТекСтрока.СуммаНДС;		
		СтрокаЗапасов.СтатьяКнигиПриобретения	= ОпределитьСтатьюНалоговойДекларации(СтрокаЗапасов);
		СтрокаЗапасов.Всего = СтрокаЗапасов.СуммаВзаиморасчетов;
		
	КонецЦикла;
	
КонецПроцедуры // ЗаполнитьПоОтчетОПереработке()

// Процедура заполнения документа на основании поступления в кассу
//
// Параметры:
//	ДокументОснование - ДокументСсылка.ПоступлениеВКассу - поступление в кассу
//	ДанныеЗаполнения - Структура - Данные заполнения документа
//
Процедура ЗаполнитьПоПоступлениеВКассу(ДанныеЗаполнения)
	
	ДокументОснование = ДанныеЗаполнения;
	
	Контрагент = ДанныеЗаполнения.Контрагент;	
	Организация = ДанныеЗаполнения.Организация;	
	
	ДоговорСделка 	   = "";
				
	Комментарий = ДанныеЗаполнения.Комментарий;
	                         	
	Запасы.Очистить();
	
	Для Каждого ТекСтрока Из ДанныеЗаполнения.РасшифровкаПлатежа Цикл
		
		СтрокаЗапасов = Запасы.Добавить();
		Договор = ТекСтрока.Договор;		
		СтрокаЗапасов.СтавкаНДС 			= ТекСтрока.СтавкаНДС;
		СтрокаЗапасов.СуммаНДС 				= ТекСтрока.СуммаНДС;
		СтрокаЗапасов.Сумма 				= ТекСтрока.СуммаПлатежа;				
		СтрокаЗапасов.СтатьяКнигиПриобретения	= ОпределитьСтатьюНалоговойДекларации(СтрокаЗапасов);
		СтрокаЗапасов.Всего = СтрокаЗапасов.Сумма;
		
	КонецЦикла;
	
КонецПроцедуры // ЗаполнитьПоПоступлениеВКассу()

// Процедура заполнения документа на основании поступления на счет
//
// Параметры:
//	ДокументОснование - ДокументСсылка.ПоступлениеНаСчет - поступление на счет
//	ДанныеЗаполнения - Структура - Данные заполнения документа
//
Процедура ЗаполнитьПоПоступлениеНаСчет(ДанныеЗаполнения)
	
	ДокументОснование = ДанныеЗаполнения;
	
	Контрагент = ДанныеЗаполнения.Контрагент;	
	Организация = ДанныеЗаполнения.Организация;	
	
	ДоговорСделка 	   = "";
				
	Комментарий = ДанныеЗаполнения.Комментарий;
	                         	
	Запасы.Очистить();
	
	Для Каждого ТекСтрока Из ДанныеЗаполнения.РасшифровкаПлатежа Цикл
		
		СтрокаЗапасов = Запасы.Добавить();
		Договор = ТекСтрока.Договор;		
		СтрокаЗапасов.СтавкаНДС 			= ТекСтрока.СтавкаНДС;
		СтрокаЗапасов.СуммаНДС 				= ТекСтрока.СуммаНДС;
		СтрокаЗапасов.Сумма 				= ТекСтрока.СуммаПлатежа;				
		СтрокаЗапасов.СтатьяКнигиПриобретения	= ОпределитьСтатьюНалоговойДекларации(СтрокаЗапасов);
		СтрокаЗапасов.Всего = СтрокаЗапасов.Сумма;
		
	КонецЦикла;
		
КонецПроцедуры // ЗаполнитьПоПоступлениеНаСчет()

// Процедура заполнения документа на основании авансового отчета
//
// Параметры:
//	ДокументОснование - ДокументСсылка.АвансовыйОтчет - авансовый отчет
//	ДанныеЗаполнения - Структура - Данные заполнения документа
//
Процедура ЗаполнитьПоАвансовыйОтчет(ДанныеЗаполнения)
	
	ДокументОснование = ДанныеЗаполнения;
	 
	Организация = ДанныеЗаполнения.Организация;	
	
	ДоговорСделка 	   = "";
				
	Комментарий = ДанныеЗаполнения.Комментарий;
	                         	
	Запасы.Очистить();
	
	Для Каждого ТекСтрока Из ДанныеЗаполнения.Запасы Цикл
		
		СтрокаЗапасов = Запасы.Добавить();		
		СтрокаЗапасов.НаименованиеТовара 	= ТекСтрока.Номенклатура.Наименование;
		СтрокаЗапасов.СтавкаНДС 			= ТекСтрока.СтавкаНДС;
		СтрокаЗапасов.СуммаНДС 				= ТекСтрока.СуммаНДС;
		СтрокаЗапасов.Сумма 				= ТекСтрока.Всего - ТекСтрока.СуммаНДС;
		СтрокаЗапасов.СуммаВзаиморасчетов	= ТекСтрока.Всего;		
		СтрокаЗапасов.СтатьяКнигиПриобретения	= ОпределитьСтатьюНалоговойДекларации(СтрокаЗапасов);
		СтрокаЗапасов.Всего = СтрокаЗапасов.СуммаВзаиморасчетов;
		
	КонецЦикла;		
	
КонецПроцедуры // ЗаполнитьПоАвансовыйОтчет()

// Процедура заполнения документа на основании расхода из кассы
//
// Параметры:
//	ДокументОснование - ДокументСсылка.РасходИзКассы - расход из кассы
//	ДанныеЗаполнения - Структура - Данные заполнения документа
//
Процедура ЗаполнитьПоРасходИзКассы(ДанныеЗаполнения)
	
	ДокументОснование = ДанныеЗаполнения;
	
	Контрагент = ДанныеЗаполнения.Контрагент;	
	Организация = ДанныеЗаполнения.Организация;	
	
	ДоговорСделка 	   = "";
				
	Комментарий = ДанныеЗаполнения.Комментарий;
	                         	
	Запасы.Очистить();
	
	Для Каждого ТекСтрока Из ДанныеЗаполнения.РасшифровкаПлатежа Цикл
		
		СтрокаЗапасов = Запасы.Добавить();
		Договор = ТекСтрока.Договор;		
		СтрокаЗапасов.СтавкаНДС 			= ТекСтрока.СтавкаНДС;
		СтрокаЗапасов.СуммаНДС 				= ТекСтрока.СуммаНДС;
		СтрокаЗапасов.Сумма 				= ТекСтрока.СуммаПлатежа;				
		СтрокаЗапасов.СтатьяКнигиПриобретения	= ОпределитьСтатьюНалоговойДекларации(СтрокаЗапасов);
		СтрокаЗапасов.Всего = СтрокаЗапасов.Сумма;
		
	КонецЦикла;
	
КонецПроцедуры // ЗаполнитьПоРасходИзКассы()

// Процедура заполнения документа на основании расхода со счета
//
// Параметры:
//	ДокументОснование - ДокументСсылка.РасходСоСчета - расход со счета
//	ДанныеЗаполнения - Структура - Данные заполнения документа
//
Процедура ЗаполнитьПоРасходСоСчета(ДанныеЗаполнения)
	
	ДокументОснование = ДанныеЗаполнения;
	
	Контрагент = ДанныеЗаполнения.Контрагент;	
	Организация = ДанныеЗаполнения.Организация;	
	
	ДоговорСделка 	   = "";
				
	Комментарий = ДанныеЗаполнения.Комментарий;
	                         	
	Запасы.Очистить();
	
	Для Каждого ТекСтрока Из ДанныеЗаполнения.РасшифровкаПлатежа Цикл
		
		СтрокаЗапасов = Запасы.Добавить();
		Договор = ТекСтрока.Договор;		
		СтрокаЗапасов.СтавкаНДС 			= ТекСтрока.СтавкаНДС;
		СтрокаЗапасов.СуммаНДС 				= ТекСтрока.СуммаНДС;
		СтрокаЗапасов.Сумма 				= ТекСтрока.СуммаПлатежа;				
		СтрокаЗапасов.СтатьяКнигиПриобретения	= ОпределитьСтатьюНалоговойДекларации(СтрокаЗапасов);
		СтрокаЗапасов.Всего = СтрокаЗапасов.Сумма;
		
	КонецЦикла;
		
КонецПроцедуры // ЗаполнитьПоРасходСоСчета()

////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

// Процедура - обработчик события ОбработкаЗаполнения.
//
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка) Экспорт
	  	
	Если Не Документы.ТипВсеСсылки().СодержитТип(ТипЗнч(ДанныеЗаполнения)) ИЛИ ДанныеЗаполнения = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьВидОперации(ДанныеЗаполнения);
	
	мВалютаРегламентированногоУчета = Константы.НациональнаяВалюта.Получить();
	
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ОтчетКомитенту") Тогда
		ЗаполнитьПоОтчетКомитенту(ДанныеЗаполнения);
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ПриходнаяНакладная") Тогда 
		 Если ДанныеЗаполнения.ВалютаДокумента = мВалютаРегламентированногоУчета Тогда			
			ЗаполнитьПоПриходнаяНакладная(ДанныеЗаполнения);
		КонецЕсли;			
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ОтчетКомиссионера") Тогда
		ЗаполнитьПоОтчетКомиссионера(ДанныеЗаполнения);
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.СчетНаОплату") 
		Или ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.СчетНаОплатуПоставщика") Тогда		
		ЗаполнитьПоСчетНаОплату(ДанныеЗаполнения);
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.РасходнаяНакладная") Тогда
		ЗаполнитьПоРасходнаяНакладная(ДанныеЗаполнения);
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ОтчетОПереработке") Тогда
		ЗаполнитьПоОтчетОПереработке(ДанныеЗаполнения);
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ПоступлениеВКассу") Тогда
		ЗаполнитьПоПоступлениеВКассу(ДанныеЗаполнения);
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ПоступлениеНаСчет") Тогда
		ЗаполнитьПоПоступлениеНаСчет(ДанныеЗаполнения);
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.РасходИзКассы") Тогда
		ЗаполнитьПоРасходИзКассы(ДанныеЗаполнения);
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.РасходСоСчета") Тогда
		ЗаполнитьПоРасходСоСчета(ДанныеЗаполнения);
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.АвансовыйОтчет") Тогда
		ЗаполнитьПоАвансовыйОтчет(ДанныеЗаполнения);		
	КонецЕсли;
			
КонецПроцедуры // ОбработкаЗаполнения()

// Процедура - обработчик события ПередЗаписью объекта.
//
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	СуммаДокумента = Запасы.Итог("Всего");
	
КонецПроцедуры // ПередЗаписью()

