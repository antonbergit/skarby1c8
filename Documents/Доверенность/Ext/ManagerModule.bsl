#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ИнтерфейсПечати

// Процедура формирует печатную форму документа по указанному макету.
//
Функция ПечатнаяФорма(МассивОбъектов, ОбъектыПечати, ТипДокумента)
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.КлючПараметровПечати = "ПараметрыПечати_Доверенность";
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Доверенность.Ссылка КАК Ссылка,
	|	Доверенность.Номер КАК Номер,
	|	Доверенность.Дата КАК ДатаДокумента,
	|	Доверенность.Организация КАК Руководители,
	|	Доверенность.Организация,
	|	Доверенность.Организация.Префикс КАК Префикс,
	|	Доверенность.ФизЛицо,
	|	Доверенность.ФизЛицо.Наименование КАК ФамилияИмяОтчествоДоверенного,
	|	Доверенность.БанковскийСчет КАК БанковскийСчет,
	|	Доверенность.Контрагент КАК Поставщик,
	|	Доверенность.НаПолучениеОт КАК ПоставщикПредставление,
	|	Доверенность.ДатаДействия КАК ДатаДействия,
	|	Доверенность.ПоДокументу КАК РеквизитыДокументаНаПолучение,
	|	Доверенность.Запасы.(
	|		НомерСтроки КАК Номер,
	|		НаименованиеТовара КАК Ценности,
	|		НаименованиеТовара КАК ЦенностиПредставление,
	|		ЕдиницаИзмерения КАК ЕдиницаИзмеренияПредставление,
	|		Количество
	|	),
	|	Доверенность.Организация.КодПоЕДРПОУ КАК КодПоЕДРПОУ,
	|	Доверенность.ПоДокументу КАК ПоДокументу
	|ИЗ
	|	Документ.Доверенность КАК Доверенность
	|ГДЕ
	|	Доверенность.Ссылка В(&МассивОбъектов)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка,
	|	Номер";
	
	Шапка = Запрос.Выполнить().Выбрать();
	
	КодЯзыкаПечать = "uk"; // всегда на украинском
	
	ПервыйДокумент = Истина;
	
	Пока Шапка.Следующий() Цикл
		
		Если Не ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		ПервыйДокумент = Ложь;
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
	
		ВыборкаСтрокТовары = Шапка.Запасы.Выбрать();
		
		ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_Доверенность_ПФ_MXL_Доверенность";
		
		Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.Доверенность.ПФ_MXL_" + ТипДокумента); //Доверенность");
		
		ДанныеОФизЛице = УправлениеНебольшойФирмойСервер.ДанныеФизЛица(Шапка.Организация, Шапка.ФизЛицо, Шапка.ДатаДокумента);
		
		ФамилияИмяОтчествоДоверенного = СокрЛП(ДанныеОФизЛице.Фамилия) + " " + СокрЛП(ДанныеОФизЛице.Имя) + " " + СокрЛП(ДанныеОФизЛице.Отчество);
		Должность                    = СокрЛП(ДанныеОФизЛице.Должность);
		
		ОбластьМакета = Макет.ПолучитьОбласть("ЛицеваяСторона");
		
		ОбластьМакета.Параметры.Заполнить(Шапка);
		
		Если Шапка.ДатаДокумента < Дата('20110101') Тогда
			НомерДокумента = УправлениеНебольшойФирмойСервер.ПолучитьНомерНаПечать(Шапка.Номер, Шапка.Префикс);
		Иначе
			НомерДокумента = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Шапка.Номер, Истина, Истина);
		КонецЕсли;		
		СформированныйНомер = НомерДокумента;
		Если ТипДокумента = "Доверенность" Тогда			
			ОбластьМакета.Параметры.Номер = СформированныйНомер;			
		Иначе                                                   			
			ОбластьМакета.Параметры.Номер =  "/" + СформированныйНомер;
		КонецЕсли;
		
		СведенияОбОрганизации = УправлениеНебольшойФирмойСервер.СведенияОЮрФизЛице(Шапка.Организация, Шапка.ДатаДокумента,, Шапка.БанковскийСчет, КодЯзыкаПечать);
		
		КомуВыдана = ?(ЗначениеЗаполнено(Должность),Должность + " ","");
		ОбластьМакета.Параметры.КомуВыдана = КомуВыдана + ФамилияИмяОтчествоДоверенного;
		ОбластьМакета.Параметры.НазваниеОрганизации		 		 = УправлениеНебольшойФирмойСервер.ОписаниеОрганизации(СведенияОбОрганизации, "ПолноеНаименование,");
		ОбластьМакета.Параметры.ЕДРПОУОрганизации	     		 = СведенияОбОрганизации.КодПоЕДРПОУ;
		ОбластьМакета.Параметры.ЮридическийАдрес         		 = УправлениеНебольшойФирмойСервер.ОписаниеОрганизации(СведенияОбОрганизации, "ЮридическийАдрес,");
		
		Если НЕ ЗначениеЗаполнено(ОбластьМакета.Параметры.НомерРасчетногоСчетаОрганизации) Тогда
			ОбластьМакета.Параметры.НомерРасчетногоСчетаОрганизации = УправлениеНебольшойФирмойСервер.ОписаниеОрганизации(СведенияОбОрганизации, "НомерСчета,");
		КонецЕсли;
		Если НЕ ЗначениеЗаполнено(ОбластьМакета.Параметры.МФОБанкаОрганизации) Тогда
			ОбластьМакета.Параметры.МФОБанкаОрганизации	= УправлениеНебольшойФирмойСервер.ОписаниеОрганизации(СведенияОбОрганизации, "МФО,");
		КонецЕСли;
		Если НЕ ЗначениеЗаполнено(ОбластьМакета.Параметры.БанкОрганизации) Тогда
			ОбластьМакета.Параметры.БанкОрганизации = УправлениеНебольшойФирмойСервер.ОписаниеОрганизации(СведенияОбОрганизации, "Банк,");
		КонецЕсли;
		
		ОбластьМакета.Параметры.ПодтверждающийДокументВид     	 = "Паспорт"; // ПаспортФизЛица.Вид;
		ОбластьМакета.Параметры.ПодтверждающийДокументСерия      = ДанныеОФизЛице.ДокументСерия;
		ОбластьМакета.Параметры.ПодтверждающийДокументНомер      = ДанныеОФизЛице.ДокументНомер;
		ОбластьМакета.Параметры.ПодтверждающийДокументКемВыдан   = ДанныеОФизЛице.ДокументКемВыдан;
		ОбластьМакета.Параметры.ПодтверждающийДокументДатаВыдачи = ДанныеОФизЛице.ДокументДатаВыдачи;
		ТабличныйДокумент.Вывести(ОбластьМакета);
		
		ОбластьМакета = Макет.ПолучитьОбласть("ОбратнаяСторонаШапка");
		ТабличныйДокумент.Вывести(ОбластьМакета);
		
		Если (ТипДокумента = "ДоверенностьБланк") Тогда
			Если (ВыборкаСтрокТовары.Количество() > 6) Тогда
		
				ОбластьМакета = Макет.ПолучитьОбласть("Строка");
				ТекстСообщения = НСтр("ru='Количество строк в табличной части больше шести.Типовая форма М-2 ""Доверенность"" рассчитана на шесть строк!';uk='Кількість рядків у табличній частині більше шести.Типова форма М-2 ""Довіреність"" розрахована на шість рядків!'");
				УправлениеНебольшойФирмойСервер.СообщитьОбОшибке(
					Шапка.Ссылка ,
					ТекстСообщения,
					Неопределено,
					Неопределено,
					"",
					Истина
				);
			Иначе
				ОбластьМакета = Макет.ПолучитьОбласть("СтрокаВысокая");
			КонецЕсли;
		Иначе 
			ОбластьМакета = Макет.ПолучитьОбласть("СтрокаСПереносом");
		КонецЕсли;	
		
		Пока ВыборкаСтрокТовары.Следующий() Цикл
			ОбластьМакета.Параметры.Заполнить(ВыборкаСтрокТовары);
			ОбластьМакета.Параметры.КоличествоПрописью = УправлениеНебольшойФирмойСервер.КоличествоПрописью(ВыборкаСтрокТовары.Количество, КодЯзыкаПечать);
			ТабличныйДокумент.Вывести(ОбластьМакета);
		КонецЦикла;
		
		Если ТипДокумента = "Доверенность" Тогда
			ОбластьМакета = Макет.ПолучитьОбласть("ОбратнаяСторонаПодвал");
		
			ТабличныйДокумент.Вывести(ОбластьМакета);
		КонецЕсли;
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, Шапка.Ссылка);
		
	КонецЦикла;
	
	ТабличныйДокумент.АвтоМасштаб = Истина;
	
	Возврат ТабличныйДокумент;
	
КонецФункции // ПечатнаяФорма()

// Сформировать печатные формы объектов
//
// ВХОДЯЩИЕ:
//   ИменаМакетов    - Строка    - Имена макетов, перечисленные через запятую
//   МассивОбъектов  - Массив    - Массив ссылок на объекты которые нужно распечатать
//   ПараметрыПечати - Структура - Структура дополнительных параметров печати
//
// ИСХОДЯЩИЕ:
//   КоллекцияПечатныхФорм - Таблица значений - Сформированные табличные документы
//   ПараметрыВывода       - Структура        - Параметры сформированных табличных документов
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "Доверенность") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "Доверенность", "Типовая межотраслевая форма", ПечатнаяФорма(МассивОбъектов, ОбъектыПечати, "Доверенность"));
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ДоверенностьБланк") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "ДоверенностьБланк", "Типовая межотраслевая форма", ПечатнаяФорма(МассивОбъектов, ОбъектыПечати, "ДоверенностьБланк"));
	КонецЕсли;
	
	// параметры отправки печатных форм по электронной почте
	УправлениеНебольшойФирмойСервер.ЗаполнитьПараметрыОтправки(ПараметрыВывода.ПараметрыОтправки, МассивОбъектов, КоллекцияПечатныхФорм);
	
КонецПроцедуры

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "Доверенность";
	КомандаПечати.Представление = НСтр("ru='Доверенность';uk='Довіреність'");
	КомандаПечати.СписокФорм = "ФормаДокумента,ФормаСписка";
	КомандаПечати.ПроверкаПроведенияПередПечатью = Ложь;
	КомандаПечати.Порядок = 1;
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ДоверенностьБланк";
	КомандаПечати.Представление = НСтр("ru='Доверенность бланк';uk='Доручення бланк'");
	КомандаПечати.СписокФорм = "ФормаДокумента,ФормаСписка";
	КомандаПечати.ПроверкаПроведенияПередПечатью = Ложь;
	КомандаПечати.Порядок = 4;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли