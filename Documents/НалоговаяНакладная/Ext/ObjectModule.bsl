#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

///////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ ЗАПОЛНЕНИЯ ДОКУМЕНТА
	
// Процедура заполнения документа на основании приходной накладной.
//
// Параметры:
//	ДокументОснование - ДокументСсылка.ПриходнаяНакладная - приходная накладная
//	ДанныеЗаполнения - Структура - Данные заполнения документа
//		
Процедура ЗаполнитьПоПриходнаяНакладная(ДанныеЗаполнения)
	
	Если ДанныеЗаполнения.ВалютаДокумента = Константы.НациональнаяВалюта.Получить() Тогда
		
		ТекстИсключения = НСтр("ru='На основании документа ""Приходная накладная"" можно вводить ""Налоговую накладную"" только при поставке от нерезидента!';uk='На підставі документа ""Прибуткова накладна"" можна вводити ""Податкову накладну"" тільки при поставці від нерезидента!'");				
		ВызватьИсключение ТекстИсключения;
		
	КонецЕсли;	
	                                	
	ВидОперации = Перечисления.ВидыОперацийНалоговаяНакладная.РаботыОтНерезидента;
	
	// Заполнение табличной части документа.	
	Запасы.Очистить();
	Для каждого СтрокаТабличнойЧасти Из ДанныеЗаполнения.Запасы Цикл
		
		Если СтрокаТабличнойЧасти.Номенклатура.ТипНоменклатуры = Перечисления.ТипыНоменклатуры.Услуга Тогда
			
			НоваяСтрока = Запасы.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТабличнойЧасти);
			
			Если НЕ ЗначениеЗаполнено(НоваяСтрока.Содержание) Тогда
				НоваяСтрока.Содержание = УправлениеНебольшойФирмойСервер.ПолучитьПредставлениеНоменклатурыДляПечати(
				?(ЗначениеЗаполнено(СтрокаТабличнойЧасти.Номенклатура.НаименованиеПолное), 
				СтрокаТабличнойЧасти.Номенклатура.НаименованиеПолное, СтрокаТабличнойЧасти.Номенклатура.Наименование), 
				СтрокаТабличнойЧасти.Характеристика, СтрокаТабличнойЧасти.Номенклатура.Артикул);
			КонецЕсли; 
			
			НоваяСтрока.КодУКТВЭД = УправлениеНебольшойФирмойСервер.ПолучитьУКТВЭДВСтрокеТоваров(СтрокаТабличнойЧасти.Номенклатура, СтрокаТабличнойЧасти.Характеристика);
			НоваяСтрока.НомерГТД = УправлениеНебольшойФирмойСервер.ПолучитьНомерГТДСтрокеТоваров(СтрокаТабличнойЧасти.Номенклатура, СтрокаТабличнойЧасти.Характеристика);
			
		КонецЕсли;	
		
	КонецЦикла;
	
КонецПроцедуры // ЗаполнитьПоПриходнаяНакладная()

// Процедура заполнения документа на основании акт об оказании производственных услуг.
//
// Параметры:
//	ДокументОснование - ДокументСсылка.АктОбОказанииПроизводственныхУслуг - акт об оказании производственных услуг
//	ДанныеЗаполнения - Структура - Данные заполнения документа
//
Процедура ЗаполнитьПоАктОбОказанииПроизводственныхУслуг(ДанныеЗаполнения)
	
	ВидОперации = Перечисления.ВидыОперацийНалоговаяНакладная.ОблагаемыеОперации;
	ЗаполнитьУсловиеПродажи(ДанныеЗаполнения);
	СуммаВключаетНДС = ДанныеЗаполнения.СуммаВключаетНДС;
	АвторасчетНДС = ДанныеЗаполнения.АвторасчетНДС;
	                            	
	// Заполнение табличной части документа.	
	Запасы.Очистить();
	Для каждого СтрокаТабличнойЧасти Из ДанныеЗаполнения.РаботыИУслуги Цикл
		
		НоваяСтрока = Запасы.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТабличнойЧасти);
		
		НоваяСтрока.ДатаОтгрузкиОплаты = ДанныеЗаполнения.Дата;
		
		ЗаполнитьСтатьюДекларацииНДСНалоговыеОбязательства(НоваяСтрока, "Запасы");
		
		НоваяСтрока.КодУКТВЭД = УправлениеНебольшойФирмойСервер.ПолучитьУКТВЭДВСтрокеТоваров(СтрокаТабличнойЧасти.Номенклатура, СтрокаТабличнойЧасти.Характеристика);
		НоваяСтрока.НомерГТД = УправлениеНебольшойФирмойСервер.ПолучитьНомерГТДСтрокеТоваров(СтрокаТабличнойЧасти.Номенклатура, СтрокаТабличнойЧасти.Характеристика);
		
	КонецЦикла;
	
	                    	
КонецПроцедуры // ЗаполнитьПоАктОбОказанииПроизводственныхУслуг()

// Процедура заполнения документа на основании расходной накладной
//
// Параметры:
//	ДокументОснование - ДокументСсылка.РасходнаяНакладная - расходная накладная
//	ДанныеЗаполнения - Структура - Данные заполнения документа
//
Процедура ЗаполнитьПоРасходнаяНакладная(ДанныеЗаполнения)
	
	Если ДанныеЗаполнения.ВидОперации <> Перечисления.ВидыОперацийРасходнаяНакладная.ПродажаПокупателю Тогда
		
		ТекстИсключения = НСтр("ru='На основании документа ""Расходная накладная"" можно вводить ""Налоговую накладную"" только при продаже покупателю!';uk='На підставі документа ""Видаткова накладна"" можна вводити ""Податкову накладну"" тільки при продажі покупцеві!'");				
		ВызватьИсключение ТекстИсключения;
		
	КонецЕсли;	
	
	ЗаполнитьУсловиеПродажи(ДанныеЗаполнения);
	ВидОперации 		= Перечисления.ВидыОперацийНалоговаяНакладная.ОблагаемыеОперации;
	СуммаВключаетНДС 	= ДанныеЗаполнения.СуммаВключаетНДС;
	АвторасчетНДС 		= ДанныеЗаполнения.АвторасчетНДС;
	ВидЦен 		        = ДанныеЗаполнения.ВидЦен;
	
	// Заполнение табличной части документа.	
	Запасы.Очистить();
	Для каждого СтрокаТабличнойЧасти Из ДанныеЗаполнения.Запасы Цикл
			
		НоваяСтрока = Запасы.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТабличнойЧасти);
		
		НоваяСтрока.ДатаОтгрузкиОплаты = ДанныеЗаполнения.Дата;
		
		ЗаполнитьСтатьюДекларацииНДСНалоговыеОбязательства(НоваяСтрока, "Запасы");
		
		НоваяСтрока.КодУКТВЭД = УправлениеНебольшойФирмойСервер.ПолучитьУКТВЭДВСтрокеТоваров(СтрокаТабличнойЧасти.Номенклатура, СтрокаТабличнойЧасти.Характеристика);
		НоваяСтрока.НомерГТД = УправлениеНебольшойФирмойСервер.ПолучитьНомерГТДСтрокеТоваров(СтрокаТабличнойЧасти.Номенклатура, СтрокаТабличнойЧасти.Характеристика);
		
	КонецЦикла;
	
КонецПроцедуры // ЗаполнитьПоРасходнаяНакладная()

// Процедура заполнения документа на основании отчета о переработке
//
// Параметры:
//	ДокументОснование - ДокументСсылка.ОтчетОПереработке - отчет о переработке
//	ДанныеЗаполнения - Структура - Данные заполнения документа
//
Процедура ЗаполнитьПоОтчетОПереработке(ДанныеЗаполнения)
	
	ЗаполнитьУсловиеПродажи(ДанныеЗаполнения);

	ВидОперации = Перечисления.ВидыОперацийНалоговаяНакладная.ОблагаемыеОперации;
	
	// Заполнение табличной части документа.	
	Запасы.Очистить();
	Для каждого СтрокаТабличнойЧасти Из ДанныеЗаполнения.Запасы Цикл
		
		НоваяСтрока = Запасы.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТабличнойЧасти);
		НоваяСтрока.ДатаОтгрузкиОплаты = ДанныеЗаполнения.Дата;
		
		ЗаполнитьСтатьюДекларацииНДСНалоговыеОбязательства(НоваяСтрока, "Запасы");
		
		НоваяСтрока.КодУКТВЭД = УправлениеНебольшойФирмойСервер.ПолучитьУКТВЭДВСтрокеТоваров(СтрокаТабличнойЧасти.Номенклатура, СтрокаТабличнойЧасти.Характеристика);
		НоваяСтрока.НомерГТД = УправлениеНебольшойФирмойСервер.ПолучитьНомерГТДСтрокеТоваров(СтрокаТабличнойЧасти.Номенклатура, СтрокаТабличнойЧасти.Характеристика);
		
	КонецЦикла;   	
	
КонецПроцедуры // ЗаполнитьПоОтчетОПереработке()

// Процедура заполнения документа на основании отчета комиссионера
//
// Параметры:
//	ДокументОснование - ДокументСсылка.ОтчетКомиссионера - отчет комиссионера
//	ДанныеЗаполнения - Структура - Данные заполнения документа
//
Процедура ЗаполнитьПоОтчетКомиссионера(ДанныеЗаполнения)
	
	ЗаполнитьУсловиеПродажи(ДанныеЗаполнения);
	ВидОперации = Перечисления.ВидыОперацийНалоговаяНакладная.ОблагаемыеОперации;
	СуммаВключаетНДС = ДанныеЗаполнения.СуммаВключаетНДС;
	
	// Заполнение табличной части документа.	
	Запасы.Очистить();
	Для каждого СтрокаТабличнойЧасти Из ДанныеЗаполнения.Запасы Цикл
			
		НоваяСтрока = Запасы.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТабличнойЧасти);
		НоваяСтрока.ДатаОтгрузкиОплаты = ДанныеЗаполнения.Дата;
		ЗаполнитьСтатьюДекларацииНДСНалоговыеОбязательства(НоваяСтрока, "Запасы");
		
		НоваяСтрока.КодУКТВЭД = УправлениеНебольшойФирмойСервер.ПолучитьУКТВЭДВСтрокеТоваров(СтрокаТабличнойЧасти.Номенклатура, СтрокаТабличнойЧасти.Характеристика);
		НоваяСтрока.НомерГТД = УправлениеНебольшойФирмойСервер.ПолучитьНомерГТДСтрокеТоваров(СтрокаТабличнойЧасти.Номенклатура, СтрокаТабличнойЧасти.Характеристика);
		
	КонецЦикла;
	
КонецПроцедуры // ЗаполнитьПоОтчетКомиссионера()

// Процедура заполнения документа на основании отчета комитенту
//
// Параметры:
//	ДокументОснование - ДокументСсылка.ОтчетКомитенту - отчет комитенту
//	ДанныеЗаполнения - Структура - Данные заполнения документа
//
Процедура ЗаполнитьПоОтчетКомитенту(ДанныеЗаполнения)
	
	ЗаполнитьУсловиеПродажи(ДанныеЗаполнения);
	ВидОперации = Перечисления.ВидыОперацийНалоговаяНакладная.ОблагаемыеОперации;
	
	СуммаВключаетНДС = ДанныеЗаполнения.СуммаВключаетНДС;
	
	НаименованиеДоговора = "";
	
	Если ЗначениеЗаполнено(Договор.НомерДоговора) Тогда
		
		НаименованиеДоговора = "№ " + Договор.НомерДоговора;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Договор.ДатаДоговора) Тогда
		
		НаименованиеДоговора = НаименованиеДоговора +" від " + Формат(Договор.ДатаДоговора, "ДФ='дд.ММ.гггг'");
		
	КонецЕсли;
		
	
	Если НЕ ЗначениеЗаполнено(НаименованиеДоговора) Тогда
		УсловиеПродажи = "Договiр " + СокрЛП(НаименованиеДоговора);
	КонецЕсли;

	Запасы.Очистить();
	
	НоваяСтрока = Запасы.Добавить();
	НоваяСтрока.Номенклатура = ДанныеЗаполнения.УслугаПоВознаграждению;
	НоваяСтрока.Содержание = УправлениеНебольшойФирмойСервер.ПолучитьПредставлениеНоменклатурыДляПечати(
				?(ЗначениеЗаполнено(НоваяСтрока.Номенклатура.НаименованиеПолное), 
				НоваяСтрока.Номенклатура.НаименованиеПолное, НоваяСтрока.Номенклатура.Наименование), 
				, НоваяСтрока.Номенклатура.Артикул);
	НоваяСтрока.Количество = 1;
	НоваяСтрока.СтавкаНДС = ДанныеЗаполнения.УслугаПоВознаграждению.СтавкаНДС;
	Если ДанныеЗаполнения.Запасы.Итог("Цена") <> 0 Тогда
		НоваяСтрока.Цена = ДанныеЗаполнения.Запасы.Итог("Цена") / ДанныеЗаполнения.Запасы.Количество();
	КонецЕсли;
	
	НоваяСтрока.Сумма = ДанныеЗаполнения.Запасы.Итог("Сумма");
	НоваяСтрока.СуммаНДС = ДанныеЗаполнения.Запасы.Итог("СуммаНДС");
	ЗаполнитьСтатьюДекларацииНДСНалоговыеОбязательства(НоваяСтрока, "Запасы");
	НоваяСтрока.Всего = ДанныеЗаполнения.Запасы.Итог("Всего");
	
КонецПроцедуры // ЗаполнитьПоОтчетКомитенту()

// Процедура заполнения документа на основании поступления на счет
//
// Параметры:
//	ДокументОснование - ДокументСсылка.ПоступлениеНаСчет - поступление на счет
//	ДанныеЗаполнения - Структура - Данные заполнения документа
//
Процедура ЗаполнитьПоПоступлениеНаСчет(ДанныеЗаполнения)
	
	ЗаполнитьШапкуПоОснованию(ДанныеЗаполнения);
	ЗаполнитьУсловиеПродажи(ДанныеЗаполнения);
    	
	ВидОперации = Перечисления.ВидыОперацийНалоговаяНакладная.ОблагаемыеОперации;
		
КонецПроцедуры // ЗаполнитьПоПоступлениеНаСчет()

// Процедура заполнения документа на основании поступление в кассу
//
// Параметры:
//	ДокументОснование - ДокументСсылка.ПоступлениеВКассу - поступление в кассу
//	ДанныеЗаполнения - Структура - Данные заполнения документа
//
Процедура ЗаполнитьПоПоступлениеВКассу(ДанныеЗаполнения)		
	
	ЗаполнитьШапкуПоОснованию(ДанныеЗаполнения);
	ЗаполнитьУсловиеПродажи(ДанныеЗаполнения);
    	
	ВидОперации = Перечисления.ВидыОперацийНалоговаяНакладная.ОблагаемыеОперации;
		
КонецПроцедуры // ЗаполнитьПоПоступлениеВКассу()

// Процедура заполнения документа на основании счета на оплату
//
// Параметры:
//	ДокументОснование - ДокументСсылка.СчетНаОплату - счет на оплату
//	ДанныеЗаполнения - Структура - Данные заполнения документа
//
Процедура ЗаполнитьПоСчетНаОплату(ДанныеЗаполнения)
	
	ЗаполнитьУсловиеПродажи(ДанныеЗаполнения);
	ВидОперации = Перечисления.ВидыОперацийНалоговаяНакладная.ОблагаемыеОперации;
	
	СуммаВключаетНДС = ДанныеЗаполнения.СуммаВключаетНДС;
	АвторасчетНДС = ДанныеЗаполнения.АвторасчетНДС;
	
	// Заполнение табличной части документа.	
	Запасы.Очистить();
	Для каждого СтрокаТабличнойЧасти Из ДанныеЗаполнения.Запасы Цикл
		
		НоваяСтрока = Запасы.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТабличнойЧасти);
		НоваяСтрока.ДатаОтгрузкиОплаты = ДанныеЗаполнения.Дата;
		ЗаполнитьСтатьюДекларацииНДСНалоговыеОбязательства(НоваяСтрока, "Запасы");
		
		НоваяСтрока.КодУКТВЭД = УправлениеНебольшойФирмойСервер.ПолучитьУКТВЭДВСтрокеТоваров(СтрокаТабличнойЧасти.Номенклатура, СтрокаТабличнойЧасти.Характеристика);
		НоваяСтрока.НомерГТД = УправлениеНебольшойФирмойСервер.ПолучитьНомерГТДСтрокеТоваров(СтрокаТабличнойЧасти.Номенклатура, СтрокаТабличнойЧасти.Характеристика);
		
	КонецЦикла;
	
КонецПроцедуры // ЗаполнитьПоСчетНаОплату()

// Процедура заполнения документа на основании внеоборотных активов
//
// Параметры:
//	ДокументОснование - ДокументСсылка.ПередачаВА 
//	ДанныеЗаполнения - Структура - Данные заполнения документа
//
Процедура ЗаполнитьПоПередачаВА(ДанныеЗаполнения)
	
	ЗаполнитьУсловиеПродажи(ДанныеЗаполнения);
	
	ВидОперации = Перечисления.ВидыОперацийНалоговаяНакладная.ОблагаемыеОперации;
	
	
	// Заполнение табличной части документа.	
	ОС.Очистить();
	Для каждого СтрокаТабличнойЧасти Из ДанныеЗаполнения.ВнеоборотныеАктивы Цикл
		
		Если СтрокаТабличнойЧасти.ВнеоборотныйАктив.Тип = Перечисления.ТипыВнеоборотныхАктивов.ОсновноеСредство Тогда
			
			НоваяСтрока = ОС.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТабличнойЧасти);
			НоваяСтрока.ОсновноеСредство = СтрокаТабличнойЧасти.ВнеоборотныйАктив;						
			НоваяСтрока.Всего = НоваяСтрока.Сумма;
			НоваяСтрока.ДатаОтгрузкиОплаты = ДанныеЗаполнения.Дата;
			ЗаполнитьСтатьюДекларацииНДСНалоговыеОбязательства(НоваяСтрока, "ОС");
			
		КонецЕсли;	
		
	КонецЦикла;
	
КонецПроцедуры // ЗаполнитьПоСписаниеВА()

// Процедура заполнения документа на основании отчета о розничных продажах
//
// Параметры:
//	ДокументОснование - ДокументСсылка.ОтчетОРозничныхПродажах - отчет о розничных продажах
//	ДанныеЗаполнения - Структура - Данные заполнения документа
//
Процедура ЗаполнитьПоОтчетОРозничныхПродажах(ДанныеЗаполнения)
	
	ЗаполнитьУсловиеПродажи(ДанныеЗаполнения);
	ВидОперации = Перечисления.ВидыОперацийНалоговаяНакладная.ИтоговаяРозницаОблагаемыеОперации;
	
	СуммаВключаетНДС = ДанныеЗаполнения.СуммаВключаетНДС;
	АвторасчетНДС = ДанныеЗаполнения.АвторасчетНДС;
	
	// Заполнение табличной части документа.	
	Запасы.Очистить();
	Для каждого СтрокаТабличнойЧасти Из ДанныеЗаполнения.Запасы Цикл
		
		НоваяСтрока = Запасы.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТабличнойЧасти);
		НоваяСтрока.ДатаОтгрузкиОплаты = ДанныеЗаполнения.Дата;
		
		ЗаполнитьСтатьюДекларацииНДСНалоговыеОбязательства(НоваяСтрока, "Запасы");
		
		НоваяСтрока.КодУКТВЭД = УправлениеНебольшойФирмойСервер.ПолучитьУКТВЭДВСтрокеТоваров(СтрокаТабличнойЧасти.Номенклатура, СтрокаТабличнойЧасти.Характеристика);
		НоваяСтрока.НомерГТД = УправлениеНебольшойФирмойСервер.ПолучитьНомерГТДСтрокеТоваров(СтрокаТабличнойЧасти.Номенклатура, СтрокаТабличнойЧасти.Характеристика);
		
	КонецЦикла;

КонецПроцедуры // ЗаполнитьПоОтчетОРозничныхПродажах()

// Процедура заполнения документа на основании заказа покупателя
//
// Параметры:
//	ДокументОснование - ДокументСсылка.ЗаказПокупателя - заказ покупателя
//	ДанныеЗаполнения - Структура - Данные заполнения документа
//
Процедура ЗаполнитьПоЗаказПокупателя(ДанныеЗаполнения)
	
	//Если ДанныеЗаполнения.ВидОперации <> Перечисления.ВидыОперацийЗаказПокупателя.ЗаказНаПродажу Тогда
	//	
	//	ТекстИсключения = НСтр("ru='На основании документа ""Заказ покупателя"" можно вводить ""Налоговую накладную"" только при продаже покупателю!';uk='На підставі документа ""Замовлення покупця"" можна вводити ""Податкову накладну"" тільки при продажі покупцеві!'");				
	//	ВызватьИсключение ТекстИсключения;
	//	
	//КонецЕсли;	
	
	ЗаполнитьУсловиеПродажи(ДанныеЗаполнения);
	ВидОперации = Перечисления.ВидыОперацийНалоговаяНакладная.ОблагаемыеОперации;
	СуммаВключаетНДС = ДанныеЗаполнения.СуммаВключаетНДС;
	АвторасчетНДС = ДанныеЗаполнения.АвторасчетНДС;
	
	// Заполнение табличной части документа.	
	Запасы.Очистить();
	Для каждого СтрокаТабличнойЧасти Из ДанныеЗаполнения.Запасы Цикл
			
		НоваяСтрока = Запасы.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТабличнойЧасти);
		НоваяСтрока.ДатаОтгрузкиОплаты = ДанныеЗаполнения.Дата;
		ЗаполнитьСтатьюДекларацииНДСНалоговыеОбязательства(НоваяСтрока, "Запасы");
		
		НоваяСтрока.КодУКТВЭД = УправлениеНебольшойФирмойСервер.ПолучитьУКТВЭДВСтрокеТоваров(СтрокаТабличнойЧасти.Номенклатура, СтрокаТабличнойЧасти.Характеристика);
		НоваяСтрока.НомерГТД = УправлениеНебольшойФирмойСервер.ПолучитьНомерГТДСтрокеТоваров(СтрокаТабличнойЧасти.Номенклатура, СтрокаТабличнойЧасти.Характеристика);
		
	КонецЦикла;
	Для каждого СтрокаТабличнойЧасти Из ДанныеЗаполнения.Работы Цикл
			
		НоваяСтрока = Запасы.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТабличнойЧасти);
		НоваяСтрока.ЕдиницаИзмерения = СтрокаТабличнойЧасти.Номенклатура.ЕдиницаИзмерения;
		НоваяСтрока.ДатаОтгрузкиОплаты = ДанныеЗаполнения.Дата;
		ЗаполнитьСтатьюДекларацииНДСНалоговыеОбязательства(НоваяСтрока, "Запасы");
		
		НоваяСтрока.КодУКТВЭД = УправлениеНебольшойФирмойСервер.ПолучитьУКТВЭДВСтрокеТоваров(СтрокаТабличнойЧасти.Номенклатура, СтрокаТабличнойЧасти.Характеристика);
		НоваяСтрока.НомерГТД = УправлениеНебольшойФирмойСервер.ПолучитьНомерГТДСтрокеТоваров(СтрокаТабличнойЧасти.Номенклатура, СтрокаТабличнойЧасти.Характеристика);
		
	КонецЦикла;
	
КонецПроцедуры // ЗаполнитьПоЗаказПокупателя()

// Процедура заполняет статью налоговой деклации в табличной части документа 
// Параметры: 
//  СтрокаТЧ 	- строка табличной части документа
//  ИмяТабЧасти - имя табличной части документа
//
Процедура ЗаполнитьСтатьюДекларацииНДСНалоговыеОбязательства(СтрокаТЧ, ИмяТабЧасти) Экспорт
	
	Если  ВидОперации = Перечисления.ВидыОперацийНалоговаяНакладная.УсловнаяПродажа 
	  ИЛИ ВидОперации = Перечисления.ВидыОперацийНалоговаяНакладная.УсловнаяПродажаСписаниеОС Тогда
	  
	  СтрокаТЧ.СтатьяДекларацииНДСНалоговыеОбязательства = Справочники.СтатьиНалоговыхДеклараций.НДС_НОКорректировкаПрочее;
	  
  	ИначеЕсли ВидОперации = Перечисления.ВидыОперацийНалоговаяНакладная.РаботыОтНерезидента Тогда
		
		СтрокаТЧ.СтатьяДекларацииНДСНалоговыеОбязательства = Справочники.СтатьиНалоговыхДеклараций.НДС_НОНерезидентУслуги;	
		
	ИначеЕсли СтрокаТЧ.СтавкаНДС = Справочники.СтавкиНДС.ОсновнаяСтавкаНДС Тогда
				
		Если ИмяТабЧасти = "ОС" Тогда
		
			СтрокаТЧ.СтатьяДекларацииНДСНалоговыеОбязательства = Справочники.СтатьиНалоговыхДеклараций.НДС_НОПоСтавке20ПоставкаОФ;	
		
		Иначе		
			
			СтрокаТЧ.СтатьяДекларацииНДСНалоговыеОбязательства = Справочники.СтатьиНалоговыхДеклараций.НДС_НОПоСтавке20;
			
		КонецЕсли;
		
	ИначеЕсли СтрокаТЧ.СтавкаНДС.Ставка = 7 Тогда
				
		СтрокаТЧ.СтатьяДекларацииНДСНалоговыеОбязательства = Справочники.СтатьиНалоговыхДеклараций.НДС_НОПоСтавке20;
		
	ИначеЕсли СтрокаТЧ.СтавкаНДС.Ставка = 0 Тогда
		
		// 0%
		Если ЗначениеЗаполнено(Договор) 
			   И Договор.ВалютаРасчетов <> Константы.НациональнаяВалюта.Получить() Тогда
			
			// договор - внешнеэкономический
			СтрокаТЧ.СтатьяДекларацииНДСНалоговыеОбязательства = Справочники.СтатьиНалоговыхДеклараций.НДС_НОПоСтавке0Экспорт;	
			
		Иначе
			
			// на терриории Украины
			СтрокаТЧ.СтатьяДекларацииНДСНалоговыеОбязательства = Справочники.СтатьиНалоговыхДеклараций.НДС_НОПоСтавке0Другие;
			
		КонецЕсли;
		
	ИначеЕсли СтрокаТЧ.СтавкаНДС.Ставка = 0 И СтрокаТЧ.СтавкаНДС.НеОблагается Тогда
		
		// освобожденные операции (п.5 Закона)
		СтрокаТЧ.СтатьяДекларацииНДСНалоговыеОбязательства = Справочники.СтатьиНалоговыхДеклараций.НДС_НООсвобожден;
		
	Иначе
		
		//в строке ТЧ не указаны ставка НДС
		СтрокаТЧ.СтатьяДекларацииНДСНалоговыеОбязательства = Справочники.СтатьиНалоговыхДеклараций.ПустаяСсылка();
		
	КонецЕсли;
	
КонецПроцедуры // ЗаполнитьСтатьюДекларацииНДСНалоговыеОбязательства()

// Процедура заполняет условие продажи
// Параметры: 
//  ДанныеЗаполнения - Структура - Данные заполнения документа
//
Процедура ЗаполнитьУсловиеПродажи(ДанныеЗаполнения = Неопределено) Экспорт 
	
	Если ДанныеЗаполнения = Неопределено Тогда
		ДанныеЗаполнения = ДокументОснование;
	КонецЕсли; 
	
	УсловиеПродажи = Договор.Наименование;
	    
	Если    ВидОперации = Перечисления.ВидыОперацийНалоговаяНакладная.ОблагаемыеОперации
		ИЛИ ВидОперации = Перечисления.ВидыОперацийНалоговаяНакладная.ОсвобожденныеОперации 
		ИЛИ ВидОперации = Перечисления.ВидыОперацийНалоговаяНакладная.НеНДСОперации	Тогда
		
		НаименованиеДоговора = "";
		
		Если НЕ ЗначениеЗаполнено(НаименованиеДоговора) Тогда
			
			Если ЗначениеЗаполнено(Договор.НомерДоговора) Тогда
			
				НаименованиеДоговора = "№ " + Договор.НомерДоговора;
			
			КонецЕсли;
			
			Если ЗначениеЗаполнено(Договор.ДатаДоговора) Тогда
			
				НаименованиеДоговора = НаименованиеДоговора +" від " + Формат(Договор.ДатаДоговора, "ДФ='дд.ММ.гггг'");
			
			КонецЕсли;
		
		КонецЕсли; 
		
		Если ЗначениеЗаполнено(НаименованиеДоговора) Тогда
			УсловиеПродажи = "Договiр " + СокрЛП(НаименованиеДоговора);
		КонецЕсли;
		
	КонецЕсли; 
	
	// заполним текстовые реквизиты по-украински
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.АктВыполненныхРабот") Тогда
		
		УсловиеПродажи = ?(ЗначениеЗаполнено(УсловиеПродажи),УсловиеПродажи + ", ","") + "акт № " + УправлениеНебольшойФирмойСервер.ПолучитьНомерНаПечать(ДанныеЗаполнения.Ссылка, ДанныеЗаполнения.Ссылка.Организация.Префикс) + " вiд " + Формат(ДанныеЗаполнения.Дата, "ДФ='дд.ММ.гггг'");
		
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.СчетНаОплату") Тогда
		
		УсловиеПродажи = ?(ЗначениеЗаполнено(УсловиеПродажи),УсловиеПродажи + ", ","") + "рахунок № " + УправлениеНебольшойФирмойСервер.ПолучитьНомерНаПечать(ДанныеЗаполнения.Ссылка, ДанныеЗаполнения.Ссылка.Организация.Префикс) + " вiд " + Формат(ДанныеЗаполнения.Дата, "ДФ='дд.ММ.гггг'");
		
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.РасходнаяНакладная") 
		Или ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ОтчетОПереработке") Тогда 
		
		УсловиеПродажи = ?(ЗначениеЗаполнено(УсловиеПродажи),УсловиеПродажи + ", ","") + "накладна № " + УправлениеНебольшойФирмойСервер.ПолучитьНомерНаПечать(ДанныеЗаполнения.Ссылка, ДанныеЗаполнения.Ссылка.Организация.Префикс) + " вiд " + Формат(ДанныеЗаполнения.Дата, "ДФ='дд.ММ.гггг'");
		
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ОтчетКомиссионера") Тогда
		
		УсловиеПродажи = ?(ЗначениеЗаполнено(УсловиеПродажи),УсловиеПродажи + ", ","") + "звіт комісіонера № " + УправлениеНебольшойФирмойСервер.ПолучитьНомерНаПечать(ДанныеЗаполнения.Ссылка, ДанныеЗаполнения.Ссылка.Организация.Префикс) + " вiд " + Формат(ДанныеЗаполнения.Дата, "ДФ='дд.ММ.гггг'");
		
	КонецЕсли;
	
КонецПроцедуры // ЗаполнитьУсловиеПродажи

// Процедура заполняет реквизиты шапки  документа 
// Параметры: 
//  ДанныеЗаполнения - Структура - Данные заполнения документа
//
Процедура ЗаполнитьШапкуПоОснованию(ДанныеЗаполнения)
	
	ЭтотОбъект.ДокументОснование = ДанныеЗаполнения.Ссылка;
	Организация = ДанныеЗаполнения.Организация;
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ПоступлениеНаСчет") Тогда
		Если ДанныеЗаполнения.РасшифровкаПлатежа.Количество() <> 0 Тогда
			СтрокаРасшифровки = ДанныеЗаполнения.РасшифровкаПлатежа[0];
			
			Контрагент = ДанныеЗаполнения.Контрагент;
			Договор = СтрокаРасшифровки.Договор;	
			ВалютаДокумента = ДанныеЗаполнения.ВалютаДенежныхСредств;
			Курс = СтрокаРасшифровки.Курс;
			Кратность = СтрокаРасшифровки.Кратность;
		КонецЕсли;
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ПоступлениеВКассу") Тогда
		Если ДанныеЗаполнения.РасшифровкаПлатежа.Количество() <> 0 Тогда
			СтрокаРасшифровки = ДанныеЗаполнения.РасшифровкаПлатежа[0];
			
			Контрагент = ДанныеЗаполнения.Контрагент;
			Договор = СтрокаРасшифровки.Договор;	
			ВалютаДокумента = ДанныеЗаполнения.ВалютаДенежныхСредств;
			Курс = СтрокаРасшифровки.Курс;
			Кратность = СтрокаРасшифровки.Кратность;
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ОтчетОРозничныхПродажах") Тогда
		
		ВалютаДокумента = Константы.НациональнаяВалюта.Получить();
		Курс = 1;
		Кратность = 1;
		СуммаВключаетНДС = ДанныеЗаполнения. СуммаВключаетНДС;
	Иначе
		Контрагент = ДанныеЗаполнения.Контрагент;
		Договор = ДанныеЗаполнения.Договор;	
		ВалютаДокумента = ДанныеЗаполнения.ВалютаДокумента;
		Курс = ДанныеЗаполнения.Курс;
		Кратность = ДанныеЗаполнения.Кратность;
	КонецЕсли;
	
	ВидДоговора = Договор.ВидДоговораПоГК;
	Если Дата >= '2014-03-01' И Дата < '2015-01-01' Тогда
		Если ФормаРасчетов = "Оплата з поточного рахунка" Тогда
			ФормаРасчетов = "Оплата з поточного рахунку";
		КонецЕсли;
	ИначеЕсли Дата >= '2015-01-01' Тогда
		Если ФормаРасчетов = "Оплата з поточного рахунку" Тогда
			ФормаРасчетов = "Оплата з поточного рахунка";
		КонецЕсли;
	КонецЕсли;
		
КонецПроцедуры // ЗаполнитьШапкуПоОснованию()

Процедура УстановитьТипПричиныНевыдачиПокупателюПоУмолчанию() Экспорт

	ДатаВступленияВСилуПриказа1379 = Константы.ДатаВступленияВСилуПриказа1379.Получить();
	
	Если ВидОперации = Перечисления.ВидыОперацийНалоговаяНакладная.РаботыОтНерезидента Тогда
		
		Если ДатаВступленияВСилуПриказа1379 > Дата Тогда
			ТипПричиныНевыдачиПокупателю = 2;
		Иначе	
			ТипПричиныНевыдачиПокупателю = 14;
		КонецЕсли;
		
	ИначеЕсли ВидОперации = Перечисления.ВидыОперацийНалоговаяНакладная.СводнаяНаПревышениеБазыНадЦенойПоставки Тогда
		
		ТипПричиныНевыдачиПокупателю = 15;	
		
	ИначеЕсли  НЕ ВалютаДокумента = Константы.ВалютаУчета.Получить() 
		И НЕ ВидОперации = Перечисления.ВидыОперацийНалоговаяНакладная.РаботыОтНерезидента Тогда
		
		ТипПричиныНевыдачиПокупателю = 7;
		
	ИначеЕсли ВидОперации = Перечисления.ВидыОперацийНалоговаяНакладная.УсловнаяПродажа Тогда
		
		Если ДатаВступленияВСилуПриказа1379 > Дата  Тогда
			ТипПричиныНевыдачиПокупателю = 4;
		Иначе	
			ТипПричиныНевыдачиПокупателю = 13;
		КонецЕсли;
		
	ИначеЕсли ВидОперации = Перечисления.ВидыОперацийНалоговаяНакладная.УсловнаяПродажаСписаниеОС Тогда
		
	    ТипПричиныНевыдачиПокупателю = 5;
		
	ИначеЕсли ВидОперации = Перечисления.ВидыОперацийНалоговаяНакладная.ИтоговаяРозницаОблагаемыеОперации 
		  ИЛИ ВидОперации = Перечисления.ВидыОперацийНалоговаяНакладная.ИтоговаяРозницаОсвобожденныеОперации Тогда
		
	    ТипПричиныНевыдачиПокупателю = 11;
		
	ИначеЕсли НЕ ЗначениеЗаполнено(Контрагент.ИНН)
   		    ИЛИ Найти("0123456789",Лев(СокрЛ(Контрагент.ИНН),1)) = 0 Тогда		
		
		ТипПричиныНевыдачиПокупателю = 2;
		
	ИначеЕсли ПродажаНижеОбычнойЦены Тогда
		
		ТипПричиныНевыдачиПокупателю = 1;
		
	Иначе	
		
		ТипПричиныНевыдачиПокупателю = 0;
		
	КонецЕсли;
	
	Если ТипПричиныНевыдачиПокупателю > 0 Тогда
		Если Дата < '2015-01-01' Тогда
			ТребуетВключенияВЕдиныйРеестрНалоговыхНакладных = Ложь;	
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

// Процедура - обработчик события ОбработкаЗаполнения.
//
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка) Экспорт
	
	Если НЕ ЗначениеЗаполнено(ДанныеЗаполнения) Тогда
		Возврат;
	КонецЕсли;

	ИнфоПервоеСобытие = Новый Структура();
	ИнфоПервоеСобытие.Вставить("ПоДокументу", Ложь);
	ИнфоПервоеСобытие.Вставить("ПоНоменклатурномуСоставу", Ложь);
	
	ДокументОснование = ДанныеЗаполнения.Ссылка;		
	
	Пользователь = Пользователи.ТекущийПользователь();
	КтоВыписалНалоговуюНакладную = УправлениеНебольшойФирмойПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(Пользователь, "КтоВыписалНалоговуюНакладную");
	НалогообложениеНДС = Перечисления.ТипыНалогообложенияНДС.ОблагаетсяНДС;
	Авторасчет = Истина;
	ЗаполнитьШапкуПоОснованию(ДанныеЗаполнения);
	
	УстановитьТипПричиныНевыдачиПокупателюПоУмолчанию();

	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.АктВыполненныхРабот") Тогда
		ЗаполнитьПоАктОбОказанииПроизводственныхУслуг(ДанныеЗаполнения);		
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ОтчетКомитенту") Тогда
		ЗаполнитьПоОтчетКомитенту(ДанныеЗаполнения);		
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ОтчетКомиссионера") Тогда
		ЗаполнитьПоОтчетКомиссионера(ДанныеЗаполнения);		
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.СчетНаОплату") Тогда
		ЗаполнитьПоСчетНаОплату(ДанныеЗаполнения);		
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.РасходнаяНакладная") Тогда
		ЗаполнитьПоРасходнаяНакладная(ДанныеЗаполнения);
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ОтчетОПереработке") Тогда
		ЗаполнитьПоОтчетОПереработке(ДанныеЗаполнения);
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ПоступлениеВКассу") Тогда
		ЗаполнитьПоПоступлениеВКассу(ДанныеЗаполнения);		
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ПриходнаяНакладная") Тогда
		ЗаполнитьПоПриходнаяНакладная(ДанныеЗаполнения);
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ПоступлениеНаСчет") Тогда
		ЗаполнитьПоПоступлениеНаСчет(ДанныеЗаполнения);			
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ОтчетОРозничныхПродажах") Тогда
		ЗаполнитьПоОтчетОРозничныхПродажах(ДанныеЗаполнения);			
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ПередачаВА") Тогда
		ЗаполнитьПоПередачаВА(ДанныеЗаполнения);		
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ЗаказПокупателя") Тогда
		ЗаполнитьПоЗаказПокупателя(ДанныеЗаполнения);
	КонецЕсли;
	
КонецПроцедуры // ОбработкаЗаполнения()

// Процедура - обработчик события ПередЗаписью объекта.
//
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если АвторасчетНДС Тогда
		// соответствие для хранения погрешностей округлений
		ПогрешностиОкругления = Новый Соответствие();
		// пересчет сумм НДС с учетом ошибок округления
		УчетНДССервер.ПересчитатьНДСсУчетомПогрешностиОкругления(Запасы, ЭтотОбъект, ПогрешностиОкругления, "Запасы");
		УчетНДССервер.ПересчитатьНДСсУчетомПогрешностиОкругления(ОС, ЭтотОбъект, ПогрешностиОкругления, "Основные средства");
	КонецЕсли;
	
	СуммаДокумента = Запасы.Итог("Всего") + ОС.Итог("Всего");
	СуммаНДСДокумента = Запасы.Итог("СуммаНДС") + ОС.Итог("СуммаНДС");

	Если ТипПричиныНевыдачиПокупателю > 1
		И Дата >= '2014-03-01' Тогда
		
		ЭлектронныйДокумент = Ложь;
	    ТребуетВключенияВЕдиныйРеестрНалоговыхНакладных = Ложь;
		
	КонецЕсли;
	
	Если Дата >= '2015-01-01' Тогда
		ТребуетВключенияВЕдиныйРеестрНалоговыхНакладных = Истина;
		ЭлектронныйДокумент = Истина;
	ИначеЕсли ТипПричиныНевыдачиПокупателю > 0 Тогда
		// поставим флаг "Требует регистрация в реестре"
		// мы не управляем флажком ТребуетВключенияВЕдиныйРеестрНалоговыхНакладных
		// для налоговых накладных, которые не выдаются покупателю
		// согласно разъяснению ЕБНЗ такие налоговые не должны регистрироваться в Едином реестре
		
	ИначеЕсли ЭлектронныйДокумент = Истина Тогда
		
		ТребуетВключенияВЕдиныйРеестрНалоговыхНакладных = истина;	
		
	Иначе	
		
		ТребуетВключенияВЕдиныйРеестрНалоговыхНакладных = Ложь;
		
		ВалютаУчета = Константы.ВалютаУчета.Получить();
		СтруктураКурсовВалют = УправлениеНебольшойФирмойСервер.ПолучитьКурсыВалют(ВалютаДокумента, ВалютаУчета, Дата);
		
		Если ВалютаДокумента = ВалютаУчета Тогда
			НДСРегл = СуммаНДСДокумента;	
		Иначе 
			НДСРегл = УправлениеНебольшойФирмойСервер.ПересчитатьИзВалютыВВалюту(СуммаНДСДокумента,
		                                                           СтруктураКурсовВалют.КурсНач,
		                                                           СтруктураКурсовВалют.Курс,
		                                                           СтруктураКурсовВалют.КратностьНач,
		                                                           СтруктураКурсовВалют.Кратность);
		КонецЕсли;
		Если Дата >= '20120101' Тогда
			// или сумма НДС в документе больше 10 000 грн
			Если НДСРегл > 10000 Тогда
				ТребуетВключенияВЕдиныйРеестрНалоговыхНакладных = Истина;
				
			// или имеются подакцизные/импортированные товары
			// этот факт определим так - если в строке указан код УКТЗЭД - считаем что условие выполняется.
			ИначеЕсли   Запасы.НайтиСтроки(Новый Структура("КодУКТВЭД", Справочники.КлассификаторУКТВЭД.ПустаяСсылка())).Количество() 	<> Запасы.Количество()
					ИЛИ ОС.НайтиСтроки(Новый Структура("КодУКТВЭД", Справочники.КлассификаторУКТВЭД.ПустаяСсылка())).Количество() 	<> ОС.Количество() Тогда
				ТребуетВключенияВЕдиныйРеестрНалоговыхНакладных = Истина;
			КонецЕсли;
		ИначеЕсли Дата >= '20110701' Тогда
			// сумма НДС в документе больше 100 000 грн
			Если НДСРегл > 100000 Тогда
				ТребуетВключенияВЕдиныйРеестрНалоговыхНакладных = Истина;
			КонецЕсли;
		ИначеЕсли Дата >= '20110401' Тогда
			// сумма НДС в документе больше 500 000 грн
			Если НДСРегл > 500000 Тогда
				ТребуетВключенияВЕдиныйРеестрНалоговыхНакладных = Истина;
			КонецЕсли;
		ИначеЕсли Дата >= '20110101' Тогда
			// сумма НДС в документе больше 1 000 000 грн
			Если НДСРегл > 1000000 Тогда
				ТребуетВключенияВЕдиныйРеестрНалоговыхНакладных = Истина;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	// еще не приняли новую форму, для НН с НДС 7% нужно в спец. режиме указывать особое занчение (7)
	Если УчетНДССервер.ВДокументеЕстьСтавкаНДС7(ЭтотОбъект) Тогда
		СпецРежимНалогообложения = 7;
	Иначе	
		Если СпецРежимНалогообложения = 7 Тогда
			СпецРежимНалогообложения = 0;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // ПередЗаписью()

// Процедура - обработчик события ОбработкаПроведения объекта.
//
Процедура ОбработкаПроведения(Отказ, РежимПроведения)

	// Инициализация дополнительных свойств для проведения документа.
	УправлениеНебольшойФирмойСервер.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства);
	
	// Инициализация данных документа.
	Документы.НалоговаяНакладная.ИнициализироватьДанныеДокумента(Ссылка, ДополнительныеСвойства);
	
	// Подготовка наборов записей.
	УправлениеНебольшойФирмойСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	// Отражение в разделах учета.
	УправлениеНебольшойФирмойСервер.ОтразитьНДСПродаж(ДополнительныеСвойства, Движения, Отказ);

	// Запись наборов записей.
	УправлениеНебольшойФирмойСервер.ЗаписатьНаборыЗаписей(ЭтотОбъект);
	
	ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц.Закрыть();
	
КонецПроцедуры // ОбработкаПроведения()

// Процедура - обработчик события ОбработкаУдаленияПроведения объекта.
//
Процедура ОбработкаУдаленияПроведения(Отказ)
	
	// Инициализация дополнительных свойств для проведения документа
	УправлениеНебольшойФирмойСервер.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства);
	
	// Подготовка наборов записей
	УправлениеНебольшойФирмойСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	// Запись наборов записей
	УправлениеНебольшойФирмойСервер.ЗаписатьНаборыЗаписей(ЭтотОбъект);
	
КонецПроцедуры // ОбработкаУдаленияПроведения()

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если УчетНДССервер.ВДокументеЕстьСтавкаНДС7ИДругиеСтавки(ЭтотОбъект) Тогда
		Отказ = Истина;
	КонецЕсли;
		
КонецПроцедуры

#КонецЕсли

