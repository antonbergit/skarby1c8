////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ

&НаКлиенте
Перем мСписокВыбораТипПричиныНевыдачиПокупателю;
&НаКлиенте
Перем мСписокВыбораСпецРежимНалогообложения;
&НаКлиенте
Перем мСписокВыбораТипПричиныНевыдачиПокупателюПриПродажеНижеОбычнойЦены;
&НаКлиенте
Перем мСписокВыбораТекстДополненияКНаименованиюПриПродажеНижеОбычнойЦены;
&НаКлиенте
Перем ВыполняетсяОткрытие;
&НаКлиенте
Перем ЗаполнилиУспешно;

//////////////////////////////////////////////////////////////////////////////////
//// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

&НаСервере
Процедура ОпределитьПервоеСобытие(Основание)
	
	ТипОснования = ТипЗнч(Основание);
	
 	ОбработкаПервоеСобытие = Обработки.ЗаполнениеНоменклатурнымСоставом.Создать();
	
	ОбработкаПервоеСобытие.РасчетыВозврат = Перечисления.РасчетыВозврат.Расчеты;
	
	ОбработкаПервоеСобытие.Дата = Основание.Дата;
	
	Если НЕ ЗначениеЗаполнено(Основание.Организация) Тогда
		ТекстИсключения = "В документе не указана организация! Заполнение невозможно.";
		ВызватьИсключение ТекстИсключения;
	Иначе
		ОбработкаПервоеСобытие.Организация = Основание.Организация;
	КонецЕсли;
	
	ОснованиеДляПервогоСобытия = Истина;
	Для каждого ДопустимыйТип Из ОбработкаПервоеСобытие.НеДопустимыеТипыДокументов Цикл
		Если ТипОснования = ДопустимыйТип Тогда
			ОснованиеДляПервогоСобытия = Ложь;
			Прервать;
		КонецЕсли;
	КонецЦикла; 
	
	Если НЕ ОснованиеДляПервогоСобытия тогда
		ПризнакПервогоСобытия=0;
		Возврат;	
	КонецЕсли;
	
	// Основание может содержать номенклатуру для заполнения налоговой накладной
	ОснованиеСодержитНоменклатуру = Ложь;
	Для каждого ДопустимыйТип Из ОбработкаПервоеСобытие.ДопустимыеТипыДокументов Цикл
		Если ТипОснования = ДопустимыйТип Тогда
			ОснованиеСодержитНоменклатуру = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла; 

	ОтборДоговоров = ОбработкаПервоеСобытие.ОтборДоговоровКонтрагентов;
	Если ОснованиеСодержитНоменклатуру Тогда
		Если НЕ ЗначениеЗаполнено(Основание.Договор) Тогда
			ТекстИсключения = "В документе не указан договор контрагента! Заполнение невозможно.";
			ВызватьИсключение ТекстИсключения;
		Иначе	
			
			СтрокаОтбора = ОтборДоговоров.Добавить();
			СтрокаОтбора.ДоговорКонтрагента = Основание.Договор;
			СтрокаОтбора.Контрагент 		= Основание.Договор.Владелец;
			
		КонецЕсли;
	Иначе
		
	   Если НЕ Основание.Метаданные().ТабличныеЧасти.Найти("РасшифровкаПлатежа") = Неопределено Тогда
				
			// платежные документы
			Для каждого СтрокаТЧ Из Основание.РасшифровкаПлатежа  Цикл
					СтрокаОтбора = ОтборДоговоров.Добавить();
					СтрокаОтбора.ДоговорКонтрагента = СтрокаТЧ.Договор;
					СтрокаОтбора.Контрагент	 		= СтрокаТЧ.Договор.Владелец;		
			КонецЦикла;
			ОтборДоговоров.Свернуть("Контрагент, ДоговорКонтрагента","");
			
		Иначе
			
			ТекстИсключения = "В документе не указан договор контрагента! Заполнение невозможно.";
			ВызватьИсключение ТекстИсключения;
				
		КонецЕсли;		
		
	КонецЕсли;

	// получим данные регистра НДСПродаж на текущий момент
	ОбработкаПервоеСобытие.ОбновитьДоговораНаДату();
	
	Если ОснованиеСодержитНоменклатуру Тогда	
		
		СтрокаИсточниковНоменклатуры = ОбработкаПервоеСобытие.ИсточникиНоменклатуры.Добавить();
		СтрокаИсточниковНоменклатуры.Документ 			= Основание;
		СтрокаИсточниковНоменклатуры.ДоговорКонтрагента = Основание.Договор;
		СтрокаИсточниковНоменклатуры.РасчетыВозврат 	= ОбработкаПервоеСобытие.РасчетыВозврат;
		
		СтруктураОтбора = Новый Структура();
		СтруктураОтбора.Вставить("Документ", 			Основание);
		СтруктураОтбора.Вставить("ДоговорКонтрагента", 	Основание.Договор);
		СтруктураОтбора.Вставить("РасчетыВозврат", 		ОбработкаПервоеСобытие.РасчетыВозврат);
		СтруктураОтбора.Вставить("Дата", 				Основание.Дата);
	
		ОбработкаПервоеСобытие.ЗаполнитьТабличныеЧасти(СтруктураОтбора);
		
		ТаблицаЧастичнойОтгрузки = Неопределено;
		МожноФормироватьНалоговыеНакладные = ОбработкаПервоеСобытие.МожноФормироватьНалоговыеНакладные(СтруктураОтбора, ТаблицаЧастичнойОтгрузки);
		Если МожноФормироватьНалоговыеНакладные Тогда
			
			ПризнакПервогоСобытия=0;
			Возврат;	
			
		ИначеЕсли НЕ ОбработкаПервоеСобытие.БудутСформированыНалоговыеНакладные(ТаблицаЧастичнойОтгрузки) Тогда
			ПризнакПервогоСобытия=1;
		Иначе
			ПризнакПервогоСобытия=2;
		КонецЕсли;
	Иначе
		ПризнакПервогоСобытия=3;
	КонецЕсли;
	
КонецПроцедуры // ОпределитьПервоеСобытие()

&НаСервере
Функция ПолучитьДанныеДокументаОснования(СтруктураОснования)
	
	Основание = СтруктураОснования.Документ;
	
	СтруктураОснования.Вставить("Дата",Основание.Дата);
	СтруктураОснования.Вставить("Организация",Основание.Организация);
	
	СписокДоговоров = Новый СписокЗначений;
	
	Если НЕ (Основание.Метаданные().Реквизиты.Найти("Договор") = Неопределено) тогда
		СписокДоговоров.Добавить(Основание.Договор);
	Иначе
	   Если НЕ Основание.Метаданные().ТабличныеЧасти.Найти("РасшифровкаПлатежа") = Неопределено Тогда
				
			// платежные документы
			Для каждого СтрокаТЧ Из Основание.РасшифровкаПлатежа  Цикл
				СписокДоговоров.Добавить(СтрокаТЧ.Договор);
			КонецЦикла;
				
		КонецЕсли;		
		
	КонецЕсли;
	
	СтруктураОснования.Вставить("Договор",СписокДоговоров);
	
	Если НЕ (Основание.Метаданные().Реквизиты.Найти("ВидЦен") = Неопределено) тогда
		СтруктураОснования.Вставить("ВидЦен",Основание.ВидЦен);
	КонецЕсли;
	Если НЕ (Основание.Метаданные().Реквизиты.Найти("СуммаВключаетНДС") = Неопределено) тогда
		СтруктураОснования.Вставить("СуммаВключаетНДС",Основание.СуммаВключаетНДС);
	Иначе
		СтруктураОснования.Вставить("СуммаВключаетНДС",Истина);
	КонецЕсли;
	
	Возврат  СтруктураОснования;
	
КонецФункции

&НаКлиенте
Процедура ЗаполнитьНоменклатурнымСоставом(Основание)
	
	СтруктураОснования = Новый Структура;
	СтруктураОснования.Вставить("Документ",Основание);
	
	СтруктураОснования = ПолучитьДанныеДокументаОснования(СтруктураОснования);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ЗаполнитьНоменклатурнымСоставомЗавершение", ЭтотОбъект);
	ОткрытьФорму("Обработка.ЗаполнениеНоменклатурнымСоставом.Форма", СтруктураОснования,,,,, ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьНоменклатурнымСоставомЗавершение(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	АдресЗапасовВХранилище = РезультатЗакрытия;
	
	Если ЗначениеЗаполнено(АдресЗапасовВХранилище) Тогда
		ПолучитьЗапасыИзХранилищаПервогоСобытия(АдресЗапасовВХранилище, "Запасы");
		ЗаполнилиУспешно = Истина;
	Иначе
		ЗаполнилиУспешно = Ложь;
	КонецЕсли;

	Если НЕ ЗаполнилиУспешно Тогда
		ВыполняетсяОткрытие = Истина;
		Открыть();
	Иначе
		УстановитьВидимость();
		ОбновитьПоясняющиеНадписи();
		ОбновитьИтоговыеСуммы();
		
		ВыполняетсяОткрытие = Истина;
		Открыть();
	КонецЕсли;
	
КонецПроцедуры // ОткрытьФормуЦеныИВалютаЗавершение()

&НаСервере
// Процедура вызывает обработку заполнения документа по основанию.
//
Процедура ЗаполнитьПоДокументу()
	  
	Документ = РеквизитФормыВЗначение("Объект");
	Документ.ОбработкаЗаполнения(Объект.ДокументОснование, );
	ЗначениеВРеквизитФормы(Документ, "Объект");
	Модифицированность = Истина;	
		
КонецПроцедуры // ЗаполнитьПоДокументу()

&НаСервере
// Процедура пересчитывает сумму в табличной части при изменении Авторасчета НДС.
// 
Процедура ПересчитаемСуммуТабличнойЧастиПоФлагуАвторасчетНДС()
	
	ПогрешностиОкругления = Новый Соответствие();
	УчетНДССервер.ПересчитатьНДСсУчетомПогрешностиОкругления(Объект.Запасы, Объект, ПогрешностиОкругления, "Запасы");
	УчетНДССервер.ПересчитатьНДСсУчетомПогрешностиОкругления(Объект.ОС, Объект, ПогрешностиОкругления, "Основные средства");
	
КонецПроцедуры // ПересчитаемСуммуТабличнойЧастиПоФлагуАвторасчетНДС()	

&НаСервереБезКонтекста
// Получает набор данных с сервера для процедуры ДатаПриИзменении.
//
Функция ПолучитьДанныеДатаПриИзменении(ДокументСсылка, ДатаНовая, ДатаПередИзменением)
	
	СтруктураДанные = Новый Структура();
	СтруктураДанные.Вставить("РазностьДат", УправлениеНебольшойФирмойСервер.ПроверитьНомерДокумента(ДокументСсылка, ДатаНовая, ДатаПередИзменением));
	
	Возврат СтруктураДанные;
	
КонецФункции // ПолучитьДанныеДатаПриИзменении()

&НаСервереБезКонтекста
// Получает набор данных с сервера.
//
Функция ПолучитьДанныеОрганизацияПриИзменении(Организация)
	
	СтруктураДанные = Новый Структура();
	СтруктураДанные.Вставить("Компания", УправлениеНебольшойФирмойСервер.ПолучитьОрганизацию(Организация));
	
	Возврат СтруктураДанные;
	
КонецФункции // ПолучитьДанныеОрганизацияПриИзменении()

&НаСервереБезКонтекста
// Получает набор данных с сервера для процедуры НоменклатураПриИзменении.
//
Функция ПолучитьДанныеНоменклатураПриИзменении(СтруктураДанные)
	
	СтруктураДанные.Вставить("Содержание", УправлениеНебольшойФирмойСервер.ПолучитьПредставлениеНоменклатурыДляПечати(
																?(ЗначениеЗаполнено(СтруктураДанные.Номенклатура.НаименованиеПолное), 
																СтруктураДанные.Номенклатура.НаименованиеПолное, СтруктураДанные.Номенклатура.Наименование), 
																, СтруктураДанные.Номенклатура.Артикул));
	
	СтруктураДанные.Вставить("ЕдиницаИзмерения", СтруктураДанные.Номенклатура.ЕдиницаИзмерения);
	
	Если ЗначениеЗаполнено(СтруктураДанные.Номенклатура.СтавкаНДС) Тогда
		СтруктураДанные.Вставить("СтавкаНДС", СтруктураДанные.Номенклатура.СтавкаНДС);
	Иначе
		СтруктураДанные.Вставить("СтавкаНДС", СтруктураДанные.Организация.СтавкаНДСПоУмолчанию);
	КонецЕсли;
	СтруктураДанные.Вставить("КодУКТВЭД", УправлениеНебольшойФирмойСервер.ПолучитьУКТВЭДВСтрокеТоваров(СтруктураДанные.Номенклатура, СтруктураДанные.Характеристика));
	СтруктураДанные.Вставить("НомерГТД", УправлениеНебольшойФирмойСервер.ПолучитьНомерГТДСтрокеТоваров(СтруктураДанные.Номенклатура, СтруктураДанные.Характеристика));
	
	СтруктураДанные.Вставить("Цена", 0);		
	СтруктураДанные.Вставить("ПроцентСкидкиНаценки", 0);
	
	Возврат СтруктураДанные;
	
КонецФункции // ПолучитьДанныеНоменклатураПриИзменении()

&НаСервереБезКонтекста
// Получает набор данных с сервера для процедуры ХарактеристикаПриИзменении.
//
Функция ПолучитьДанныеХарактеристикаПриИзменении(СтруктураДанные)
	
	СтруктураДанные.Вставить("Содержание", УправлениеНебольшойФирмойСервер.ПолучитьПредставлениеНоменклатурыДляПечати(
																?(ЗначениеЗаполнено(СтруктураДанные.Номенклатура.НаименованиеПолное), 
																СтруктураДанные.Номенклатура.НаименованиеПолное, СтруктураДанные.Номенклатура.Наименование), 
																СтруктураДанные.Характеристика, СтруктураДанные.Номенклатура.Артикул));
	
	Если СтруктураДанные.Свойство("ВидЦен") Тогда
		
		Если ТипЗнч(СтруктураДанные.ЕдиницаИзмерения) = Тип("СправочникСсылка.КлассификаторЕдиницИзмерения") Тогда
			СтруктураДанные.Вставить("Коэффициент", 1);
		Иначе
			СтруктураДанные.Вставить("Коэффициент", СтруктураДанные.ЕдиницаИзмерения.Коэффициент);
		КонецЕсли;
		
		Цена = УправлениеНебольшойФирмойСервер.ПолучитьЦенуНоменклатурыПоВидуЦен(СтруктураДанные);
		СтруктураДанные.Вставить("Цена", Цена);
		
	Иначе
		
		СтруктураДанные.Вставить("Цена", 0);
		
	КонецЕсли;
	СтруктураДанные.Вставить("КодУКТВЭД", УправлениеНебольшойФирмойСервер.ПолучитьУКТВЭДВСтрокеТоваров(СтруктураДанные.Номенклатура, СтруктураДанные.Характеристика));
	СтруктураДанные.Вставить("НомерГТД", УправлениеНебольшойФирмойСервер.ПолучитьНомерГТДСтрокеТоваров(СтруктураДанные.Номенклатура, СтруктураДанные.Характеристика));
	
	Возврат СтруктураДанные;
	
КонецФункции // ПолучитьДанныеХарактеристикаПриИзменении()

&НаСервереБезКонтекста
// Получает набор данных с сервера для процедуры ЕдиницаИзмеренияПриИзменении.
//
Функция ПолучитьДанныеЕдиницаИзмеренияПриИзменении(ТекущаяЕдиницаИзмерения = Неопределено, ЕдиницаИзмерения = Неопределено)
	
	СтруктураДанные = Новый Структура();
	
	Если ТекущаяЕдиницаИзмерения = Неопределено Тогда
		СтруктураДанные.Вставить("ТекущийКоэффициент", 1);
	Иначе
		СтруктураДанные.Вставить("ТекущийКоэффициент", ТекущаяЕдиницаИзмерения.Коэффициент);
	КонецЕсли;	
		
	Если ЕдиницаИзмерения = Неопределено Тогда
		СтруктураДанные.Вставить("Коэффициент", 1);
	Иначе	
		СтруктураДанные.Вставить("Коэффициент", ЕдиницаИзмерения.Коэффициент);
	КонецЕсли;	
	
	Возврат СтруктураДанные;
	
КонецФункции // ПолучитьДанныеЕдиницаИзмеренияПриИзменении()

&НаКлиенте
// Рассчитывается сумма НДС в строке табличной части.
//
Процедура РассчитатьСуммуНДС(СтрокаТабличнойЧасти)
	
	СтавкаНДС = УправлениеНебольшойФирмойПовтИсп.ПолучитьЗначениеСтавкиНДС(СтрокаТабличнойЧасти.СтавкаНДС);
	
	СтрокаТабличнойЧасти.СуммаНДС = ?(Объект.СуммаВключаетНДС, 
									  СтрокаТабличнойЧасти.Сумма - (СтрокаТабличнойЧасти.Сумма) / ((СтавкаНДС + 100) / 100),
									  СтрокаТабличнойЧасти.Сумма * СтавкаНДС / 100);
	
КонецПроцедуры // РассчитатьСуммуНДС() 

&НаКлиенте
// Процедура рассчитывает сумму в строке табличной части.
//
Процедура РассчитатьСуммуВСтрокеТабличнойЧасти(СтрокаТабличнойЧасти = Неопределено)
	
	Если СтрокаТабличнойЧасти = Неопределено Тогда
		СтрокаТабличнойЧасти = Элементы.Запасы.ТекущиеДанные;
	КонецЕсли;
	
	СтрокаТабличнойЧасти.Сумма = СтрокаТабличнойЧасти.Количество * СтрокаТабличнойЧасти.Цена;
	
	Если СтрокаТабличнойЧасти.ПроцентСкидкиНаценки = 100 Тогда
		СтрокаТабличнойЧасти.Сумма = 0;
	ИначеЕсли СтрокаТабличнойЧасти.ПроцентСкидкиНаценки <> 0
		    И СтрокаТабличнойЧасти.Количество <> 0 Тогда
		СтрокаТабличнойЧасти.Сумма = СтрокаТабличнойЧасти.Сумма * (1 - СтрокаТабличнойЧасти.ПроцентСкидкиНаценки / 100);
	КонецЕсли;
	
	РассчитатьСуммуНДС(СтрокаТабличнойЧасти);
	СтрокаТабличнойЧасти.Всего = СтрокаТабличнойЧасти.Сумма + ?(Объект.СуммаВключаетНДС, 0, СтрокаТабличнойЧасти.СуммаНДС);
	
	ОбновитьИтоговыеСуммы();
	
КонецПроцедуры // РассчитатьСуммуВСтрокеТабличнойЧасти()	

&НаКлиенте
// Процедура выполняет пересчет в табличной части документа после изменений 
// в форме "Цены и валюта".Выполняется пересчет колонок: цена, скидка, сумма,
// сумма НДС, всего.
//
Процедура ОбработатьИзмененияПоКнопкеЦеныИВалюты()
	
	// 1. Формируем структуру параметров для заполнения формы "Цены и Валюта".
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("ВалютаДокумента",		  Объект.ВалютаДокумента);
	СтруктураПараметров.Вставить("Курс",				  Объект.Курс);
	СтруктураПараметров.Вставить("Кратность",			  Объект.Кратность);
	СтруктураПараметров.Вставить("СуммаВключаетНДС",	  Объект.СуммаВключаетНДС);
	СтруктураПараметров.Вставить("Контрагент",			  Объект.Контрагент);
	СтруктураПараметров.Вставить("Договор",				  Объект.Договор);
	СтруктураПараметров.Вставить("Организация",			  Компания);
	СтруктураПараметров.Вставить("ДатаДокумента",		  Объект.Дата);
	СтруктураПараметров.Вставить("ПерезаполнитьЦены",	  Ложь);
	СтруктураПараметров.Вставить("ПересчитатьЦены",		  Ложь);
	СтруктураПараметров.Вставить("БылиВнесеныИзменения",  Ложь);
	СтруктураПараметров.Вставить("АвторасчетНДС",		 Объект.АвторасчетНДС);
	
	СтруктураПараметров.Вставить("ВидЦен", Объект.ВидЦен);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ОбработатьИзмененияПоКнопкеЦеныИВалютыЗавершение", ЭтотОбъект);
	ОткрытьФорму("ОбщаяФорма.ФормаЦеныИВалюта", СтруктураПараметров,,,,, ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры // ОбработатьИзмененияПоКнопкеЦеныИВалюты()	

&НаКлиенте
// Процедура-обработчик результата открытия формы "Цены и валюты"
//
Процедура ОбработатьИзмененияПоКнопкеЦеныИВалютыЗавершение(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	СтруктураЦеныИВалюта = РезультатЗакрытия;
	
	// 3. Перезаполняем табличную часть "Затраты" если были внесены изменения в форме "Цены и Валюта".
	Если ТипЗнч(СтруктураЦеныИВалюта) = Тип("Структура") И СтруктураЦеныИВалюта.БылиВнесеныИзменения Тогда
		
		Объект.ВидЦен = СтруктураЦеныИВалюта.ВидЦен;
		Объект.ВалютаДокумента = СтруктураЦеныИВалюта.ВалютаДокумента;
		Объект.Курс = СтруктураЦеныИВалюта.КурсРасчетов;
		Объект.Кратность = СтруктураЦеныИВалюта.КратностьРасчетов;
		Объект.СуммаВключаетНДС = СтруктураЦеныИВалюта.СуммаВключаетНДС;
		Объект.АвторасчетНДС = СтруктураЦеныИВалюта.АвторасчетНДС;
		
		// Пересчитываем цены по виду цен.
		Если СтруктураЦеныИВалюта.ПерезаполнитьЦены Тогда
			УправлениеНебольшойФирмойКлиент.ПерезаполнитьЦеныТабличнойЧастиПоВидуЦен(ЭтаФорма, "Запасы", Ложь);
			Если СтруктураЦеныИВалюта.АвторасчетНДС Тогда
				ПересчитаемСуммуТабличнойЧастиПоФлагуАвторасчетНДС();
			КонецЕсли;
		КонецЕсли;
		
		// Пересчитываем цены по валюте.
		Если НЕ СтруктураЦеныИВалюта.ПерезаполнитьЦены
			  И СтруктураЦеныИВалюта.ПересчитатьЦены Тогда	
			УправлениеНебольшойФирмойКлиент.ПересчитатьЦеныТабличнойЧастиПоВалюте(ЭтаФорма, СтруктураЦеныИВалюта.ВалютаДокумента, "Запасы");
			Если СтруктураЦеныИВалюта.АвторасчетНДС Тогда
				ПересчитаемСуммуТабличнойЧастиПоФлагуАвторасчетНДС();
			КонецЕсли;
		КонецЕсли;
		
		// Пересчитываем сумму если изменился признак "Сумма включает НДС".
		Если СтруктураЦеныИВалюта.СуммаВключаетНДС <> СтруктураЦеныИВалюта.ПредСуммаВключаетНДС Тогда
			УправлениеНебольшойФирмойКлиент.ПересчитаемСуммуТабличнойЧастиПоФлагуСуммаВключаетНДС(ЭтаФорма, "Запасы");	
			
			Для каждого СтрокаТабличнойЧастиОС Из Объект.ОС Цикл
				
				СтавкаНДС = УправлениеНебольшойФирмойПовтИсп.ПолучитьЗначениеСтавкиНДС(СтрокаТабличнойЧастиОС.СтавкаНДС);
				
				Если Объект.СуммаВключаетНДС Тогда
					СтрокаТабличнойЧастиОС.Сумма = (СтрокаТабличнойЧастиОС.Сумма * (100 + СтавкаНДС)) / 100;
				Иначе
					СтрокаТабличнойЧастиОС.Сумма = (СтрокаТабличнойЧастиОС.Сумма * 100) / (100 + СтавкаНДС);
				КонецЕсли;
				
				СтрокаТабличнойЧастиОС.Всего = СтрокаТабличнойЧастиОС.Сумма + ?(Объект.СуммаВключаетНДС, 0, СтрокаТабличнойЧастиОС.СуммаНДС);
				
			КонецЦикла;
			
			Если СтруктураЦеныИВалюта.АвторасчетНДС Тогда
				ПересчитаемСуммуТабличнойЧастиПоФлагуАвторасчетНДС();
			КонецЕсли;
		КонецЕсли;
		
		// Пересчитываем сумму если изменился признак Авторасчет НДС.
		Если СтруктураЦеныИВалюта.АвторасчетНДС <> СтруктураЦеныИВалюта.ПредАвторасчетНДС И СтруктураЦеныИВалюта.АвторасчетНДС Тогда
			ПересчитаемСуммуТабличнойЧастиПоФлагуАвторасчетНДС();	
		КонецЕсли;
		Элементы.ЗапасыСуммаНДС.ТолькоПросмотр = Объект.АвторасчетНДС;
		Элементы.ОССуммаНДС.ТолькоПросмотр = Объект.АвторасчетНДС;
		
	КонецЕсли;
	
	// Сформируем надпись цены и валюты.
	СтруктураНадписи = Новый Структура("ВидЦен, ВалютаДокумента, Курс, СуммаВключаетНДС, УчетВалютныхОпераций", Объект.ВидЦен, Объект.ВалютаДокумента, Объект.Курс, Объект.СуммаВключаетНДС, УчетВалютныхОпераций);
	ЦеныИВалюта = СформироватьНадписьЦеныИВалюта(СтруктураНадписи);
	
	ОбновитьИтоговыеСуммы();
	
КонецПроцедуры // ОткрытьФормуЦеныИВалютаЗавершение()

&НаСервереБезКонтекста
// Получает набор данных с сервера для процедуры КонтрагентПриИзменении.
//
Функция ПолучитьДанныеКонтрагентПриИзменении(Контрагент, Организация)
	
	ДоговорПоУмолчанию = Контрагент.ДоговорПоУмолчанию;
		
	Возврат ДоговорПоУмолчанию;
	
КонецФункции // ПолучитьДанныеКонтрагентПриИзменении()

&НаСервере
// Получает набор данных с сервера для процедуры ДоговорПриИзменении.
//
Функция ПолучитьДанныеДоговорПриИзменении(Договор)
	
	Запись = Новый Структура();
	
	Если Договор.ВидДоговора = Перечисления.ВидыДоговоров.Бартерный Тогда
		Запись.Вставить("ФормаРасчета", "Бартер");
	Иначе
		Если Объект.Дата >= '2014-03-01' И Объект.Дата < '2015-01-01' Тогда
			Запись.Вставить("ФормаРасчета", "Оплата з поточного рахунку");
		ИначеЕсли Объект.Дата >= '2015-01-01' Тогда
			Запись.Вставить("ФормаРасчета", "Оплата з поточного рахунка");
		КонецЕсли;
	КонецЕсли;
	
	Запись.Вставить("ВидДоговора", Договор.ВидДоговораПоГК);
	
	Возврат Запись;
	
КонецФункции // ПолучитьДанныеДоговорПриИзменении()

// Процедура формирует соответствие видов операций.
//
&НаСервере
Процедура ПолучитьСтруктуруВидовОпераций()
	
	Структура = Новый Структура;
	
	Структура.Вставить("ОблагаемыеОперации", Перечисления.ВидыОперацийНалоговаяНакладная.ОблагаемыеОперации);
	Структура.Вставить("ОсвобожденныеОперации", Перечисления.ВидыОперацийНалоговаяНакладная.ОсвобожденныеОперации);
	Структура.Вставить("НеНДСОперации", Перечисления.ВидыОперацийНалоговаяНакладная.НеНДСОперации);
	Структура.Вставить("ИтоговаяРозницаОблагаемыеОперации", Перечисления.ВидыОперацийНалоговаяНакладная.ИтоговаяРозницаОблагаемыеОперации);
	Структура.Вставить("ИтоговаяРозницаОсвобожденныеОперации", Перечисления.ВидыОперацийНалоговаяНакладная.ИтоговаяРозницаОсвобожденныеОперации);
	Структура.Вставить("РозницаКонрагентуОблагаемыеОперации", Перечисления.ВидыОперацийНалоговаяНакладная.РозницаКонрагентуОблагаемыеОперации);
	Структура.Вставить("РозницаКонрагентуОсвобожденныеОперации", Перечисления.ВидыОперацийНалоговаяНакладная.РозницаКонрагентуОсвобожденныеОперации);
	Структура.Вставить("УсловнаяПродажа", Перечисления.ВидыОперацийНалоговаяНакладная.УсловнаяПродажа);
	Структура.Вставить("УсловнаяПродажаСписаниеОС", Перечисления.ВидыОперацийНалоговаяНакладная.УсловнаяПродажаСписаниеОС);
	Структура.Вставить("РаботыОтНерезидента", Перечисления.ВидыОперацийНалоговаяНакладная.РаботыОтНерезидента);
	Структура.Вставить("СводнаяНаПревышениеБазыНадЦенойПоставки", Перечисления.ВидыОперацийНалоговаяНакладная.СводнаяНаПревышениеБазыНадЦенойПоставки);
	
	ВидыОпераций = Структура;
	
КонецПроцедуры // ПолучитьСтруктуруВидовОпераций()

&НаКлиенте
Процедура ОбновитьПоясняющиеНадписи()

	Элементы.РасшифровкаСпецРежимНалогообложения.Заголовок = "";
	ТекСпецРежим = мСписокВыбораСпецРежимНалогообложения.НайтиПоЗначению(Объект.СпецРежимНалогообложения);
	Если НЕ ТекСпецРежим = Неопределено Тогда
		Элементы.РасшифровкаСпецРежимНалогообложения.Заголовок = ТекСпецРежим.Представление;		
	КонецЕсли;
	
	Элементы.РасшифровкаТипПричиныНевыдачиПокупателю.Заголовок = "";
	ТекТипПричины = мСписокВыбораТипПричиныНевыдачиПокупателю.НайтиПоЗначению(Объект.ТипПричиныНевыдачиПокупателю);
	Если НЕ ТекТипПричины = Неопределено Тогда
		Элементы.РасшифровкаТипПричиныНевыдачиПокупателю.Заголовок = ТекТипПричины.Представление;		
	КонецЕсли;

	Элементы.РасшифровкаТипПричиныНевыдачиПокупателюПриПродажеНижеОбычнойЦены.Заголовок = "";
	ТекТипПричины = мСписокВыбораТипПричиныНевыдачиПокупателюПриПродажеНижеОбычнойЦены.НайтиПоЗначению(Объект.ТипПричиныНевыдачиПокупателюПриПродажеНижеОбычнойЦены);
	Если НЕ ТекТипПричины = Неопределено Тогда
		Элементы.РасшифровкаТипПричиныНевыдачиПокупателюПриПродажеНижеОбычнойЦены.Заголовок = ТекТипПричины.Представление;		
	Иначе	
		Элементы.РасшифровкаТипПричиныНевыдачиПокупателюПриПродажеНижеОбычнойЦены.Заголовок = "Оберіть тип причини!";
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьИтоговыеСуммы()
	
	ИтогВсего = Объект.Запасы.Итог("Всего") + Объект.ОС.Итог("Всего");
	ИтогСуммаНДС = Объект.Запасы.Итог("СуммаНДС") + Объект.ОС.Итог("СуммаНДС");
		
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьВидДоговора(ВидДоговора)

	Объект.ВидДоговора = ВидДоговора;

КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииДоговора()
	
	стФормаРасчетаВидДоговора = ПолучитьДанныеДоговорПриИзменении(Объект.Договор);
	
	Объект.ФормаРасчетов = стФормаРасчетаВидДоговора.ФормаРасчета;  	
	
	ЗаполнитьУсловиеПродажи(); 
	
	ЗаполнитьВидДоговора(стФормаРасчетаВидДоговора.ВидДоговора);
	
	УстановитьВидимость();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииВидаОперации()
	
	ВидимостьЭлементов = ПолучитьЗначениеОтображенияЭлементов(Объект.ВидОперации);
	
	Элементы.Контрагент.Видимость = ВидимостьЭлементов;
	Элементы.Договор.Видимость = ВидимостьЭлементов;
	
	УстановитьТипПричиныНевыдачиПокупателюПоУмолчанию();
	
	УстановитьВидимость();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьУсловиеПродажи()

	Документ = РеквизитФормыВЗначение("Объект");
	Документ.ЗаполнитьУсловиеПродажи();
	ЗначениеВРеквизитФормы(Документ, "Объект");

КонецПроцедуры

&НаСервере
Процедура УстановитьТипПричиныНевыдачиПокупателюПоУмолчанию()

	Документ = РеквизитФормыВЗначение("Объект");
	Документ.УстановитьТипПричиныНевыдачиПокупателюПоУмолчанию();
	ЗначениеВРеквизитФормы(Документ, "Объект");

КонецПроцедуры

&НаКлиенте
Процедура НачалоВыбораНоменклатурыГТД(Элемент, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;
	
	ТекущаяСтрокаТоваров = Элементы.Запасы.ТекущиеДанные;
	Если ТекущаяСтрокаТоваров = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ВариантыВыбора = Новый СписокЗначений();
	ВариантыВыбора.Добавить(Ложь,   НСтр("ru='Выбрать по данным номенклатуры';uk='Вибрати по даним номенклатури'"));
	ВариантыВыбора.Добавить(Истина, НСтр("ru='Произвольный выбор';uk='Довільний вибір'"));
	
	ПоказатьВыборИзСписка(Новый ОписаниеОповещения("НачалоВыбораНоменклатурыГТДЗавершение", ЭтотОбъект, Новый Структура("Элемент", Элемент)), ВариантыВыбора, Элемент, 0);

КонецПроцедуры

&НаКлиенте
Процедура НачалоВыбораНоменклатурыГТДЗавершение(ВыбранныйЭлемент, ДополнительныеПараметры) Экспорт
    
    Элемент = ДополнительныеПараметры.Элемент;
	
    РезультатВыбора = ВыбранныйЭлемент;
    
    Если РезультатВыбора = Неопределено Тогда
        Возврат;
	КонецЕсли; 
	
	ТекущаяСтрокаТоваров = Элементы.Запасы.ТекущиеДанные;
	
	Если РезультатВыбора.Значение = Истина Тогда
		ПараметрыВыбора1 = Новый Структура("ТекущаяСтрока");
		Если ТекущаяСтрокаТоваров <> Неопределено И ЗначениеЗаполнено(ТекущаяСтрокаТоваров.КодУКТВЭД) Тогда
			ПараметрыВыбора1.Вставить("ТекущаяСтрока", ТекущаяСтрокаТоваров.КодУКТВЭД);	
		КонецЕсли;  
		ОткрытьФорму("Справочник.КлассификаторУКТВЭД.Форма.ФормаВыбора", ПараметрыВыбора1, Элемент);
	Иначе
	    ДанныеСтроки = Новый Структура("КодУКТВЭД, НомерГТД, Номенклатура");
	    ЗаполнитьЗначенияСвойств(ДанныеСтроки, ТекущаяСтрокаТоваров); 
	    ПараметрыВыбора1 = ЗаполнитьПараметрыВыбораНоменклатурыГТД(ДанныеСтроки);
		
		ОткрытьФорму("Справочник.НоменклатураГТД.Форма.ФормаВыбора", ПараметрыВыбора1, Элемент);
	КонецЕсли;

КонецПроцедуры

&НаСервереБезКонтекста
Функция ЗаполнитьПараметрыВыбораНоменклатурыГТД(ТекущаяСтрокаТоваров)
	
	Параметры = Новый Структура("Отбор, ТекущаяСтрока");
	
	Параметры.Отбор = Новый Структура("Владелец", ТекущаяСтрокаТоваров.Номенклатура); 
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("КодУКТВЭД", ТекущаяСтрокаТоваров.КодУКТВЭД);
	Запрос.УстановитьПараметр("НомерГТД",  ТекущаяСтрокаТоваров.НомерГТД);
	Запрос.УстановитьПараметр("Владелец",  ТекущаяСтрокаТоваров.Номенклатура);
	
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	               |	НоменклатураГТД.Ссылка
	               |ИЗ
	               |	Справочник.НоменклатураГТД КАК НоменклатураГТД
	               |ГДЕ
	               |	НоменклатураГТД.КодУКТВЭД  = &КодУКТВЭД
	               |	И НоменклатураГТД.НомерГТД = &НомерГТД
	               |	И НоменклатураГТД.Владелец = &Владелец";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Параметры.ТекущаяСтрока = Выборка.Ссылка;	
	КонецЕсли;
	
	Возврат Параметры; 
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьРеквизитНоменклатурыГТДНаСервере(Ссылка, ИмяРекзвизита)

	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, ИмяРекзвизита);	

КонецФункции 

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ ДЛЯ РАБОТЫ С ПОДБОРОМ

&НаКлиенте
// Процедура - обработчик события Действие команды Подбор
//
Процедура Подбор(Команда)
	
	ИмяТабличнойЧасти  = "Запасы";
	
	Если НЕ ПустаяСтрока(ПараметрыОткрытияПодбора[ИмяТабличнойЧасти]) Тогда
		
		ПодборНоменклатурыВДокументахКлиент.ОткрытьПодбор(ЭтаФорма, ИмяТабличнойЧасти, ПараметрыОткрытияПодбора[ИмяТабличнойЧасти]);
		Возврат;
		
	КонецЕсли;
	
	ПараметрыПодбора = Новый Структура;
	
	ПараметрыПодбора.Вставить("Период",					Объект.Дата);
	ПараметрыПодбора.Вставить("Организация",			Компания);
	ПараметрыПодбора.Вставить("ВидЦен",					Объект.ВидЦен);
	ПараметрыПодбора.Вставить("Валюта",					Объект.ВалютаДокумента);
	ПараметрыПодбора.Вставить("НалогообложениеНДС",		Объект.НалогообложениеНДС);
	ПараметрыПодбора.Вставить("СуммаВключаетНДС",		Объект.СуммаВключаетНДС);
	ПараметрыПодбора.Вставить("ОрганизацияДокумента",	Объект.Организация);
	
	ТипНоменклатуры = Новый СписокЗначений;
	Для каждого ЭлементМассива Из Элементы[ИмяТабличнойЧасти + "Номенклатура"].ПараметрыВыбора Цикл
		Если ЭлементМассива.Имя = "Отбор.ТипНоменклатуры" Тогда
			Если ТипЗнч(ЭлементМассива.Значение) = Тип("ФиксированныйМассив") Тогда
				Для каждого ЭлементФиксМассива Из ЭлементМассива.Значение Цикл
					ТипНоменклатуры.Добавить(ЭлементФиксМассива);
				КонецЦикла; 
			Иначе
				ТипНоменклатуры.Добавить(ЭлементМассива.Значение);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	ПараметрыПодбора.Вставить("ТипНоменклатуры", ТипНоменклатуры);
	
	ПараметрыПодбора.Вставить("УникальныйИдентификаторФормыВладельца", УникальныйИдентификатор);
	
	#Если ВебКлиент Тогда
		//Обход ошибки платформы передачи данных формы в веб-клиенте при изменении состава элементов формы
		ОткрытьФорму("ОбщаяФорма.ФормаПодбораОстаткиРезервыЦены", ПараметрыПодбора, ЭтаФорма);
		
	#Иначе
		
		ОткрытьФорму("ОбщаяФорма.ФормаПодбора", ПараметрыПодбора, ЭтаФорма);
		
	#КонецЕсли
	
КонецПроцедуры // ПодборВыполнить()

&НаСервере
// Функция получает список товаров из временного хранилища
//
Процедура ПолучитьЗапасыИзХранилища(АдресЗапасовВХранилище, ИмяТабличнойЧасти, ЕстьХарактеристики)
	
	ТаблицаДляЗагрузки = ПолучитьИзВременногоХранилища(АдресЗапасовВХранилище);
	
	Для каждого СтрокаЗагрузки Из ТаблицаДляЗагрузки Цикл
		
		СтруктураОтбора = Новый Структура;
		СтруктураОтбора.Вставить("Номенклатура", 		СтрокаЗагрузки.Номенклатура);
		Если ЕстьХарактеристики Тогда
			СтруктураОтбора.Вставить("Характеристика", 	СтрокаЗагрузки.Характеристика);
		КонецЕсли;
		
		Строки = Объект[ИмяТабличнойЧасти].НайтиСтроки(СтруктураОтбора);
		Если Строки.Количество() > 0 Тогда
			
			СтрокаТабличнойЧасти = Строки[0];
			СтрокаТабличнойЧасти.Количество = СтрокаЗагрузки.Количество;
				
			// Сумма.
			СтрокаТабличнойЧасти.Сумма = СтрокаТабличнойЧасти.Количество * СтрокаТабличнойЧасти.Цена;
				
			// Сумма НДС.
			СтавкаНДС = ?(ЗначениеЗаполнено(СтрокаТабличнойЧасти.СтавкаНДС), СтрокаТабличнойЧасти.СтавкаНДС.Ставка, 0);	
			СтрокаТабличнойЧасти.СуммаНДС = СтрокаТабличнойЧасти.Сумма * СтавкаНДС / 100;
			
			// Всего.
			СтрокаТабличнойЧасти.Всего = СтрокаТабличнойЧасти.Сумма + ?(Объект.СуммаВключаетНДС, 0, СтрокаТабличнойЧасти.СуммаНДС);
			
		Иначе 
			НоваяСтрока = Объект[ИмяТабличнойЧасти].Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаЗагрузки);
			
			СтруктураДанные = Новый Структура;
			СтруктураДанные.Вставить("СтавкаНДС", НоваяСтрока.СтавкаНДС);
			СтруктураДанные.Вставить("ИмяТабЧасти", ИмяТабличнойЧасти);
			
			СтруктураДанные = ЗаполнитьСтатьюДекларацииНДСНалоговыеОбязательства(СтруктураДанные);
			НоваяСтрока.СтатьяДекларацииНДСНалоговыеОбязательства = СтруктураДанные.СтатьяДекларацииНДСНалоговыеОбязательства;
			
		КонецЕсли;	
		
	КонецЦикла;
	
КонецПроцедуры // ПолучитьЗапасыИзХранилища()

&НаСервере
// Функция получает список товаров из временного хранилища
//
Процедура ПолучитьЗапасыИзХранилищаПервогоСобытия(АдресЗапасовВХранилище, ИмяТабличнойЧасти)
	
	ТаблицаДляЗагрузки = ПолучитьИзВременногоХранилища(АдресЗапасовВХранилище);
	Объект[ИмяТабличнойЧасти].Очистить();
	 
	Для каждого СтрокаЗагрузки Из ТаблицаДляЗагрузки Цикл
		Если СтрокаЗагрузки.КоличествоВДокумент = 0 Тогда
			Продолжить;	
		КонецЕсли; 
		НоваяСтрока = Объект[ИмяТабличнойЧасти].Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаЗагрузки);
		
		НоваяСтрока.Количество = СтрокаЗагрузки.КоличествоВДокумент;
		НоваяСтрока.Всего = СтрокаЗагрузки.ВсегоВДокумент;
		НоваяСтрока.СуммаНДС = СтрокаЗагрузки.СуммаНДСВДокумент;
		
		СтруктураДанные = Новый Структура;
		СтруктураДанные.Вставить("СтавкаНДС", НоваяСтрока.СтавкаНДС);
		СтруктураДанные.Вставить("ИмяТабЧасти", ИмяТабличнойЧасти);
		
		СтруктураДанные = ЗаполнитьСтатьюДекларацииНДСНалоговыеОбязательства(СтруктураДанные);
		НоваяСтрока.СтатьяДекларацииНДСНалоговыеОбязательства = СтруктураДанные.СтатьяДекларацииНДСНалоговыеОбязательства;
		
		НоваяСтрока.Сумма = НоваяСтрока.Всего - ?(Объект.СуммаВключаетНДС, 0, НоваяСтрока.СуммаНДС);
		
	КонецЦикла;
	
	Объект.СуммаДокумента = Объект.Запасы.Итог("Всего");
	
КонецПроцедуры // ПолучитьЗапасыИзХранилища()

///////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ДЛЯ УПРАВЛЕНИЯ ВНЕШНИМ ВИДОМ ФОРМЫ

&НаСервереБезКонтекста
Функция ПолучитьЗначениеОтображенияЭлементов(ВидОперации)
	
	Возврат Не (ВидОперации = Перечисления.ВидыОперацийНалоговаяНакладная.УсловнаяПродажаСписаниеОС 
	Или ВидОперации = Перечисления.ВидыОперацийНалоговаяНакладная.ИтоговаяРозницаОблагаемыеОперации
	Или ВидОперации = Перечисления.ВидыОперацийНалоговаяНакладная.УсловнаяПродажа);
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
// Функция возвращает текст надписи "Цены и валюта".
//
Функция СформироватьНадписьЦеныИВалюта(СтруктураНадписи)
	
	ТекстНадписи = "";
	
	// Валюта.
	Если СтруктураНадписи.УчетВалютныхОпераций Тогда
		Если ЗначениеЗаполнено(СтруктураНадписи.ВалютаДокумента) Тогда
			ТекстНадписи = НСтр("ru='Валюта: %Валюта%, курс: %Курс%';uk='Валюта: %Валюта%, курс: %Курс%'");
			ТекстНадписи = СтрЗаменить(ТекстНадписи, "%Валюта%", СокрЛП(Строка(СтруктураНадписи.ВалютаДокумента)));
			ТекстНадписи = СтрЗаменить(ТекстНадписи, "%Курс%", СокрЛП(Строка(СтруктураНадписи.Курс)));
		Иначе
			ТекстНадписи = НСтр("ru='Валюта: <нет>';uk='Валюта: <немає>'");
		КонецЕсли;
	КонецЕсли;	
	
	// Вид цен.
	Если ЗначениеЗаполнено(СтруктураНадписи.ВидЦен) Тогда
		Если ПустаяСтрока(ТекстНадписи) Тогда
			ТекстНадписи = ТекстНадписи + НСтр("ru='Вид цен: %ВидЦен%';uk='Вид цін: %ВидЦен%'");
		Иначе	
			ТекстНадписи = ТекстНадписи + НСтр("ru='; Вид цен: %ВидЦен%';uk='; Вид цін: %ВидЦен%'");
		КонецЕсли;	
		ТекстНадписи = СтрЗаменить(ТекстНадписи, "%ВидЦен%", СокрЛП(Строка(СтруктураНадписи.ВидЦен)));
	КонецЕсли;
	
	// Флаг сумма включает НДС.
	Если ПустаяСтрока(ТекстНадписи) Тогда	
		Если СтруктураНадписи.СуммаВключаетНДС Тогда	
			ТекстНадписи = НСтр("ru='Сумма включает НДС';uk='Сума включає ПДВ'");
		Иначе		
			ТекстНадписи = НСтр("ru='Сумма не включает НДС';uk='Сума не включає ПДВ'");
		КонецЕсли;	
	КонецЕсли;	
	
	Возврат ТекстНадписи;
	
КонецФункции // СформироватьНадписьЦеныИВалюта()	

&НаКлиенте
// Процедура устанавливает видимость элементов формы 
//
Процедура УстановитьВидимость()

	ДействуетНалоговыйКодекс = (Объект.Дата >= '2011-01-01');
	УтвержденыНовыеПечатныеФормы = (Объект.Дата >= '2011-01-10');
	
	Элементы.ВключенаВЕдиныйРеестрНалоговыхНакладных.Видимость	= ДействуетНалоговыйКодекс;
	
	Элементы.ГруппаДополнительнойИнформацииПраво3.Видимость = ДействуетНалоговыйКодекс И НЕ УтвержденыНовыеПечатныеФормы;
	
	Элементы.ВидДоговора.Видимость		= ДействуетНалоговыйКодекс;
	Элементы.Группа4.Видимость 			= ДействуетНалоговыйКодекс;
	Элементы.Группа5.Видимость 			= ДействуетНалоговыйКодекс;
	
	Элементы.НомерГТД.Доступность = Объект.ПодтверждаетсяГТД;
	
	Элементы.УточняемыйПериод.Видимость = Объект.ВключаетсяВУточняющийРасчет;
	
	Элементы.ТребуетВключенияВЕдиныйРеестрНалоговыхНакладных.Доступность = НЕ (Объект.ТипПричиныНевыдачиПокупателю = 0) И (Объект.Дата < '2014-03-01');
	
	Если Объект.ВидОперации = ВидыОпераций.ОблагаемыеОперации или 
		 Объект.ВидОперации = ВидыОпераций.ИтоговаяРозницаОблагаемыеОперации или 
		 Объект.ВидОперации = ВидыОпераций.РозницаКонрагентуОблагаемыеОперации или
		 Объект.ВидОперации = ВидыОпераций.УсловнаяПродажа или
		 Объект.ВидОперации = ВидыОпераций.РаботыОтНерезидента Тогда
		НовыйПараметр = Новый ПараметрВыбора("Отбор.НеОблагается", Ложь);
		НовыйМассив = Новый Массив();
		НовыйМассив.Добавить(НовыйПараметр);
		НовыеПараметры = Новый ФиксированныйМассив(НовыйМассив);
		Элементы.Запасы.ПодчиненныеЭлементы.ЗапасыСтавкаНДС.ПараметрыВыбора = НовыеПараметры;
	Иначе
		НовыйПараметр = Новый ПараметрВыбора("Отбор.НеОблагается", Истина);
		НовыйМассив = Новый Массив();
		НовыйМассив.Добавить(НовыйПараметр);
		НовыеПараметры = Новый ФиксированныйМассив(НовыйМассив);
		Элементы.Запасы.ПодчиненныеЭлементы.ЗапасыСтавкаНДС.ПараметрыВыбора = НовыеПараметры;
	КонецЕсли;
	
	Если Объект.ВидОперации = ВидыОпераций.СводнаяНаПревышениеБазыНадЦенойПоставки Тогда
		Элементы.ЗапасыОсновнаяНН.Видимость = Истина;
	КонецЕсли;
	
	// Реквизиты, которые касаются обычной цены
	Элементы.ТекстДополненияКНаименованиюПриПродажеНижеОбычнойЦены.Видимость 			= Объект.ПродажаНижеОбычнойЦены;
	Элементы.ТипПричиныНевыдачиПокупателюПриПродажеНижеОбычнойЦены.Видимость 			= Объект.ПродажаНижеОбычнойЦены И Объект.Дата >= '2015-01-01';
	Элементы.РасшифровкаТипПричиныНевыдачиПокупателюПриПродажеНижеОбычнойЦены.Видимость = Объект.ПродажаНижеОбычнойЦены И Объект.Дата >= '2015-01-01';
	
	Элементы.ПоставкаДипПредставительству.Видимость = (Объект.Дата >= '2014-12-01') И (Объект.ТипПричиныНевыдачиПокупателю <= 2) И (Объект.Дата < УчетНДССервер.ДатаВступленияВСилуПриказа1307());
	
	Элементы.ЭлектронныйДокумент.Доступность = (Объект.Дата < '2015-01-01');
	Если Объект.Дата >= '2015-01-01' Тогда
		Элементы.ТребуетВключенияВЕдиныйРеестрНалоговыхНакладных.Доступность = Ложь;
	КонецЕсли;
	
	ВидимостьСводная = (Объект.Дата >= УчетНДССервер.ДатаВступленияВСилуПриказа1307())
		И (Объект.ВидОперации = ВидыОпераций.ОблагаемыеОперации 
		ИЛИ Объект.ВидОперации = ВидыОпераций.ОсвобожденныеОперации 
		ИЛИ Объект.ВидОперации = ВидыОпераций.УсловнаяПродажа 
	);
	Элементы.Сводная.Видимость = ВидимостьСводная;
	
КонецПроцедуры // УстановитьВидимость()

&НаСервере
Процедура УстановитьСписокВыбораФормРасчетов(ДатаДокумента)

	Если ДатаДокумента >= '20150101' Тогда
		Элементы.ФормаРасчетов.СписокВыбора.Добавить("Оплата з поточного рахунка");
	Иначе
	    Элементы.ФормаРасчетов.СписокВыбора.Добавить("Оплата з поточного рахунку");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПроверитьСоответствиеДоговоров()
	
	Если  Объект.ДокументОснование <> Неопределено
		И УправлениеНебольшойФирмойСервер.ЕстьРеквизитДокумента("Договор", Объект.ДокументОснование.Метаданные()) Тогда
		
		Если Объект.ДокументОснование.Договор <> Объект.Договор Тогда
			Объект.ДокументОснование = Неопределено;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
// Процедура - обработчик события ПриСозданииНаСервере.
//
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Установка реквизитов формы.
	ДатаДокумента = Объект.Дата;
	Если НЕ ЗначениеЗаполнено(ДатаДокумента) Тогда
		ДатаДокумента = ТекущаяДата();
	КонецЕсли;
	
	Компания = УправлениеНебольшойФирмойСервер.ПолучитьОрганизацию(Объект.Организация);	
	Контрагент = Объект.Контрагент;
	
	Если Объект.ПродажаНижеОбычнойЦены тогда
		Элементы.ТекстДополненияКНаименованиюПриПродажеНижеОбычнойЦены.Видимость = Истина;	
		Элементы.ЗапасыСуммаПревышения.Видимость = Истина;
		Элементы.ЗапасыСуммаНДСПревышения.Видимость = Истина;
		Элементы.ЗапасыЦенаОбычная.Видимость = Истина;
		
		Элементы.ОССуммаПревышения.Видимость = Истина;
		Элементы.ОССуммаНДСПревышения.Видимость = Истина;
		Элементы.ОСЦенаОбычная.Видимость = Истина;
	Иначе
		Элементы.ТекстДополненияКНаименованиюПриПродажеНижеОбычнойЦены.Видимость = Ложь;
		Элементы.ЗапасыСуммаПревышения.Видимость = Ложь;
		Элементы.ЗапасыСуммаНДСПревышения.Видимость = Ложь;
		Элементы.ЗапасыЦенаОбычная.Видимость = Ложь;
		
		Элементы.ОССуммаПревышения.Видимость = Ложь;
		Элементы.ОССуммаНДСПревышения.Видимость = Ложь;
		Элементы.ОСЦенаОбычная.Видимость = Ложь;
	КонецЕсли;
	
	Если Объект.ПодтверждаетсяГТД тогда
		Элементы.НомерГТД.Видимость = Истина;	
	Иначе
		Элементы.НомерГТД.Видимость = Ложь;	
	КонецЕсли;
	
	Элементы.ЗапасыОсновнаяНН.Видимость = Ложь;
	
	// Авторасчет НДС
	Элементы.ЗапасыСуммаНДС.ТолькоПросмотр = Объект.АвторасчетНДС;
	Элементы.ОССуммаНДС.ТолькоПросмотр = Объект.АвторасчетНДС;
	
	Элементы.ОС.Видимость = Константы.ФункциональнаяОпцияУчетВнеоборотныхАктивов.Получить();
	
	// Сформируем надпись цены и валюты.
	УчетВалютныхОпераций = Константы.ФункциональнаяУчетВалютныхОпераций.Получить();
	СтруктураНадписи = Новый Структура("ВидЦен, ВалютаДокумента, Курс, СуммаВключаетНДС, УчетВалютныхОпераций", Объект.ВидЦен, Объект.ВалютаДокумента, Объект.Курс, Объект.СуммаВключаетНДС, УчетВалютныхОпераций);
	ЦеныИВалюта = СформироватьНадписьЦеныИВалюта(СтруктураНадписи);
	
	ИтогВсего = Объект.Запасы.Итог("Всего") + Объект.ОС.Итог("Всего");
	ИтогСуммаНДС = Объект.Запасы.Итог("СуммаНДС") + Объект.ОС.Итог("СуммаНДС");
	
	ДокументОбъект = РеквизитФормыВЗначение("Объект");
	Если ДокументОбъект.ЭтоНовый() Тогда
		УстановитьТипПричиныНевыдачиПокупателюПоУмолчанию();
		Объект.КтоВыписалНалоговуюНакладную = УправлениеНебольшойФирмойПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(ПараметрыСеанса.ТекущийПользователь, "КтоВыписалНалоговуюНакладную");
		
		Если ДатаДокумента > '2015-01-01' Тогда
			ЭлектронныйДокумент = Истина;
			ТребуетВключенияВЕдиныйРеестрНалоговыхНакладных = Истина;
		КонецЕсли;
	КонецЕсли; 
	
	УстановитьСписокВыбораФормРасчетов(ДатаДокумента);
	СформироватьСписокВыбораТекстДополненияКНаименованиюПриПродажеНижеОбычнойЦены(ДатаДокумента);
	ПолучитьСтруктуруВидовОпераций();
	
	Если ДокументОбъект.ЭтоНовый() и ЗначениеЗаполнено(Объект.ДокументОснование) Тогда
		ОпределитьПервоеСобытие(Объект.ДокументОснование);
	Иначе
		ПризнакПервогоСобытия = 0;
	КонецЕсли;
	
	// ПодборНоменклатурыВДокументах
	ПодборНоменклатурыВДокументах.НазначитьФормуПодбора(ПараметрыОткрытияПодбора, Объект.Ссылка.Метаданные().Имя, "Запасы");
	// Конец ПодборНоменклатурыВДокументах
	
	// СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
	ДополнительныеОтчетыИОбработки.ПриСозданииНаСервере(ЭтаФорма);
	// Конец СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
	
	// СтандартныеПодсисте.Печать
	УправлениеПечатью.ПриСозданииНаСервере(ЭтаФорма);
	// Конец СтандартныеПодсистемы.Печать
	
КонецПроцедуры // ПриСозданииНаСервере()

&НаКлиенте
// Процедура - обработчик события ПослеЗаписи.
//
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	УправлениеНебольшойФирмойКлиент.ОбновитьСостояниеДокумента(Объект, СостояниеДокумента, КартинкаСостоянияДокумента, РазрешеноПроведение);
	
КонецПроцедуры // ПослеЗаписи()

&НаКлиенте
// Процедура - обработчик события ПриОткрытии.
//
Процедура ПриОткрытии(Отказ)
	
	ТекстПредупреждения = НСтр("ru='Формирование налоговой накладной отменено!';uk='Формування податкової накладної скасовано!'"); 
	
	Если Не ВыполняетсяОткрытие Тогда
		
		Если ПризнакПервогоСобытия=1 тогда
			
		    Отказ = Истина;
			ТекстВопроса = НСтр("ru='По данному документу не возникают налоговые обязательства. Открыть форму обработки заполнения налоговой накладной номенклатурным составом?';uk='За даним документом не виникають податкові зобов`язання. Відкрити форму обробки заповнення податкової накладної номенклатурним складом?'");
			
			ОбработатьПризнакПервогоСобытия(ТекстВопроса, ТекстПредупреждения);
			
		ИначеЕсли ПризнакПервогоСобытия = 2 тогда
			
		    Отказ = Истина;
			ТекстВопроса = НСтр("ru='На текущий момент сумма неподтвержденных налоговых обязательств по одной или нескольким ставкам НДС меньше соответствующей суммы документа. Заполнить налоговую накладную номенклатурным составом невозможно. Открыть форму обработки заполнения налоговой накладной номенклатурным составом?';uk='На поточний момент сума непідтверджених податкових зобов`язань по одній або декільком ставками ПДВ менше відповідної суми документа. Заповнити податкову накладну номенклатурним складом неможливо. Відкрити форму обробки заповнення податкової накладної номенклатурним складом?'");
			
			ОбработатьПризнакПервогоСобытия(ТекстВопроса, ТекстПредупреждения);
			
		ИначеЕсли ПризнакПервогоСобытия = 3 тогда
			
		    Отказ = Истина;
			ЗаполнитьНоменклатурнымСоставом(Объект.ДокументОснование);
			
		Иначе
			
			Отказ = Ложь;
			
			УстановитьВидимость();
			ОбновитьПоясняющиеНадписи();
			
		КонецЕсли;
		
	Иначе
		Если Не ЗаполнилиУспешно Тогда
			ПоказатьПредупреждение(, ТекстПредупреждения);
			Отказ = Истина;		
		Иначе	
			Отказ = Ложь;
		КонецЕсли; 
	КонецЕсли;
	
КонецПроцедуры

// Процедура - обработчик события ОбработкаОповещения формы.
//
&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ПодборПроизведен" 
		И ЗначениеЗаполнено(Параметр) 
		//Проверка на владельца формы
		И Источник <> Новый УникальныйИдентификатор("00000000-0000-0000-0000-000000000000")
		И Источник = УникальныйИдентификатор
		Тогда
		
		АдресЗапасовВХранилище	= Параметр;
		
		ПолучитьЗапасыИзХранилища(АдресЗапасовВХранилище, "Запасы", Истина);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьПризнакПервогоСобытия(Вопрос, Предупреждение)

	Контекст = Новый Структура("Предупреждение", Предупреждение);
	ПоказатьВопрос(Новый ОписаниеОповещения("ОбработатьПризнакПервогоСобытияЗавершение", ЭтотОбъект, Контекст), Вопрос, РежимДиалогаВопрос.ДаНет);

КонецПроцедуры
 
&НаКлиенте
Процедура ОбработатьПризнакПервогоСобытияЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт

	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		ЗаполнитьНоменклатурнымСоставом(Объект.ДокументОснование);
	Иначе	
		ВыполняетсяОткрытие = Истина;
		Открыть();
	КонецЕсли; 

КонецПроцедуры

//////////////////////////////////////////////////////////////////////////////////
//// ПРОЦЕДУРЫ - ДЕЙСТВИЯ КОМАНДНЫХ ПАНЕЛЕЙ ФОРМЫ

&НаКлиенте
// Процедура вызывается при нажатии кнопки "ЦеныВалюта" командной панели
// табличного поля.
//
Процедура РедактироватьЦеныИВалюту(Команда)
	
	ОбработатьИзмененияПоКнопкеЦеныИВалюты();
	
КонецПроцедуры // РедактироватьЦеныИВалюту()

&НаКлиенте
// Процедура - обработчик нажатия на кнопку Заполнить.
//
Процедура Заполнить(Команда)
	
	ПоказатьВопрос(Новый ОписаниеОповещения("ЗаполнитьЗавершение", ЭтотОбъект), НСтр("ru='Документ будет полностью перезаполнен по ""Основанию""! Продолжить выполнение операции?';uk='Документ буде повністю перезаповнений по ""Підставі""! Продовжити виконання операції?'"), РежимДиалогаВопрос.ДаНет, 0);
	
КонецПроцедуры  // ЗаполнитьВыполнить()

&НаКлиенте
Процедура ЗаполнитьЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт

	Ответ = РезультатВопроса;
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		ЗаполнитьПоДокументу();
		ОбновитьИтоговыеСуммы();
		СтруктураНадписи = Новый Структура("ВидЦен, ВалютаДокумента, Курс, СуммаВключаетНДС, УчетВалютныхОпераций", Объект.ВидЦен, Объект.ВалютаДокумента, Объект.Курс, Объект.СуммаВключаетНДС, УчетВалютныхОпераций);
		ЦеныИВалюта = СформироватьНадписьЦеныИВалюта(СтруктураНадписи);
	КонецЕсли;

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ РЕКВИЗИТОВ ШАПКИ

&НаКлиенте
// Процедура - обработчик события ПриИзменении поля ввода Дата.
// В процедуре определяется ситуация, когда при изменении своей даты документ 
// оказывается в другом периоде нумерации документов, и в этом случае
// присваивает документу новый уникальный номер.
// Переопределяет соответствующий параметр формы.
//
Процедура ДатаПриИзменении(Элемент)
	
	// Обработка события изменения даты.
	ДатаПередИзменением = ДатаДокумента;
	ДатаДокумента = Объект.Дата;
	Если Объект.Дата <> ДатаПередИзменением Тогда
		СтруктураДанные = ПолучитьДанныеДатаПриИзменении(Объект.Ссылка, Объект.Дата, ДатаПередИзменением);
		Если СтруктураДанные.РазностьДат <> 0 Тогда
			Объект.Номер = "";
		КонецЕсли;
	КонецЕсли;
	
	УстановитьВидимость();
	
КонецПроцедуры // ДатаПриИзменении()

&НаКлиенте
// Процедура - обработчик события ПриИзменении поля ввода Организация.
// В процедуре осуществляется очистка номера документа,
// а также производится установка параметров функциональных опций формы.
// Переопределяет соответствующий параметр формы.
//
Процедура ОрганизацияПриИзменении(Элемент)

	// Обработка события изменения организации.
	Объект.Номер = "";
	СтруктураДанные = ПолучитьДанныеОрганизацияПриИзменении(Объект.Организация);
	Компания = СтруктураДанные.Компания;
	
КонецПроцедуры // ОрганизацияПриИзменении()

&НаКлиенте
// Процедура - обработчик события ПриИзменении поля ввода ВидОперации.
//
Процедура ВидОперацииПриИзменении(Элемент)
	
	ПриИзмененииВидаОперации();
	
КонецПроцедуры // ВидОперацииПриИзменении()

&НаКлиенте
Процедура КонтрагентПриИзменении(Элемент)
	
	КонтрагентПередИзменением = Контрагент;
	Контрагент = Объект.Контрагент;
	
	Если КонтрагентПередИзменением <> Объект.Контрагент Тогда
		Объект.Договор = ПолучитьДанныеКонтрагентПриИзменении(Объект.Контрагент, Объект.Организация);
	КонецЕсли;
	
	ПриИзмененииДоговора();
	
	УстановитьТипПричиныНевыдачиПокупателюПоУмолчанию();
	
КонецПроцедуры

&НаКлиенте
Процедура ДоговорПриИзменении(Элемент)
	
	ПриИзмененииДоговора();
	
	ПроверитьСоответствиеДоговоров();
	
КонецПроцедуры

&НаКлиенте
Процедура ДокументОснованиеПриИзменении(Элемент)
	
	ЗаполнитьУсловиеПродажи();
	
КонецПроцедуры

&НаКлиенте
Процедура ПодтверждаетсяГТДПриИзменении(Элемент)
	Если Объект.ПодтверждаетсяГТД тогда
		Элементы.НомерГТД.Видимость = Истина;	
	Иначе
		Элементы.НомерГТД.Видимость = Ложь;	
	КонецЕсли;
	
	УстановитьВидимость();
КонецПроцедуры

&НаКлиенте
Процедура ПродажаНижеОбычнойЦеныПриИзменении(Элемент)
	Если Объект.ПродажаНижеОбычнойЦены Тогда
		Элементы.ТекстДополненияКНаименованиюПриПродажеНижеОбычнойЦены.Видимость = Истина;	
		Элементы.ЗапасыСуммаПревышения.Видимость = Истина;
		Элементы.ЗапасыСуммаНДСПревышения.Видимость = Истина;
		Элементы.ЗапасыЦенаОбычная.Видимость = Истина;
		
		Элементы.ОССуммаПревышения.Видимость = Истина;
		Элементы.ОССуммаНДСПревышения.Видимость = Истина;
		Элементы.ОСЦенаОбычная.Видимость = Истина;
	Иначе
		Элементы.ТекстДополненияКНаименованиюПриПродажеНижеОбычнойЦены.Видимость = Ложь;
		Элементы.ЗапасыСуммаПревышения.Видимость = Ложь;
		Элементы.ЗапасыСуммаНДСПревышения.Видимость = Ложь;
		Элементы.ЗапасыЦенаОбычная.Видимость = Ложь;
		
		Элементы.ОССуммаПревышения.Видимость = Ложь;
		Элементы.ОССуммаНДСПревышения.Видимость = Ложь;
		Элементы.ОСЦенаОбычная.Видимость = Ложь;
	КонецЕсли;
	
	УстановитьТипПричиныНевыдачиПокупателюПоУмолчанию();

	УстановитьВидимость(); 
	
	Если Объект.Дата >= '2015-01-01' Тогда
		
		ТипПричиныНевыдачиПокупателюПриПродажеНижеОбычнойЦены = 15;
		
		Если Объект.Запасы.Итог("Всего") + Объект.ОС.Итог("Всего") = 0 Тогда
		
			ТипПричиныНевыдачиПокупателюПриПродажеНижеОбычнойЦены = 12;	
			
		ИначеЕсли Объект.ОС.Количество() > 0  Тогда
			
			ТипПричиныНевыдачиПокупателюПриПродажеНижеОбычнойЦены = 16;		
			
		КонецЕсли;
		
		УстановитьТекстДополнения();
		
	КонецЕсли;
	
	ОбновитьПоясняющиеНадписи();	
	
КонецПроцедуры

&НаКлиенте
Процедура СпецРежимНалогообложенияПриИзменении(Элемент)
	ОбновитьПоясняющиеНадписи();
КонецПроцедуры

&НаКлиенте
Процедура ТипПричиныНевыдачиПокупателюПриИзменении(Элемент)
	ОбновитьПоясняющиеНадписи();
	УстановитьВидимость();
КонецПроцедуры

&НаКлиенте
Процедура ТипПричиныНевыдачиПокупателюПриПродажеНижеОбычнойЦеныПриИзменении(Элемент)
	ОбновитьПоясняющиеНадписи();
	УстановитьТекстДополнения();
КонецПроцедуры

&НаСервере
Процедура СформироватьСписокВыбораТекстДополненияКНаименованиюПриПродажеНижеОбычнойЦены(ДатаДокумента)
	
	Элементы.ТекстДополненияКНаименованиюПриПродажеНижеОбычнойЦены.СписокВыбора.Очистить();
	
	Если ДатаДокумента < '2015-01-01' Тогда
	    НомерДокумента = ПрефиксацияОбъектовКлиентСервер.ПолучитьНомерНаПечать(Объект.Номер, Истина, Истина);
		
		// добавим если нужно постфикс
		КодСпецРежима = ?(ЗначениеЗаполнено(Объект.СпецРежимНалогообложения), "" + Объект.СпецРежимНалогообложения, " "); //или пробел или одна цифра
		НомерФилиала  = ""; 
		Если ЗначениеЗаполнено(КодСпецРежима + НомерФилиала) Тогда
			НомерДокумента	= НомерДокумента + "/" + КодСпецРежима + НомерФилиала; 
		КонецЕсли;
		
		Элементы.ТекстДополненияКНаименованиюПриПродажеНижеОбычнойЦены.СписокВыбора.Добавить("(перевищення звичайної ціни над фактичною за товарами,  послугами, указаними  в  податковій  накладній №"+ НомерДокумента +")");
		Элементы.ТекстДополненияКНаименованиюПриПродажеНижеОбычнойЦены.СписокВыбора.Добавить("(перевищення звичайної ціни над фактичною за товарами,  послугами, указаними  в  податковій  накладній №"+ НомерДокумента +"; сума процентів нарахованих або таких, що мають бути нараховані на суму номіналу процентного векселя)");
		Элементы.ТекстДополненияКНаименованиюПриПродажеНижеОбычнойЦены.СписокВыбора.Добавить("(звичайна ціна за товарами, послугами, указаними  в  податковій  накладній №"+ НомерДокумента +")");
	Иначе
		Элементы.ТекстДополненияКНаименованиюПриПродажеНижеОбычнойЦены.СписокВыбора.Добавить("(перевищення звичайної ціни над фактичною за товарами, послугами, указаними  в  податковій  накладній № <визначається автоматично>)");
		Элементы.ТекстДополненияКНаименованиюПриПродажеНижеОбычнойЦены.СписокВыбора.Добавить("(перевищення звичайної ціни над фактичною за товарами, послугами, указаними  в  податковій  накладній № <визначається автоматично>; сума процентів нарахованих або таких, що мають бути нараховані на суму номіналу процентного векселя)");
		Элементы.ТекстДополненияКНаименованиюПриПродажеНижеОбычнойЦены.СписокВыбора.Добавить("(звичайна ціна за товарами, послугами, указаними  в  податковій  накладній № <визначається автоматично>)");
		Элементы.ТекстДополненияКНаименованиюПриПродажеНижеОбычнойЦены.СписокВыбора.Добавить("(перевищення ціни придбання над фактичною ціною постачання товарів/послуг, указаних в податковій накладній № <визначається автоматично>)");
		Элементы.ТекстДополненияКНаименованиюПриПродажеНижеОбычнойЦены.СписокВыбора.Добавить("(перевищення балансової (залишкової) вартості над фактичною ціною постачання необоротних активів, указаних в податковій накладні № <визначається автоматично>)");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВключаетсяВУточняющийРасчетПриИзменении(Элемент)
	УстановитьВидимость(); 
КонецПроцедуры

&НаКлиенте
Процедура ВидДоговораНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ОткрытьФорму("Справочник.ВидыДоговоровПоГК.Форма.ФормаВыбора", , Элемент);
КонецПроцедуры

&НаКлиенте
Процедура УстановитьТекстДополнения();

	ПодходящееЗначение = мСписокВыбораТекстДополненияКНаименованиюПриПродажеНижеОбычнойЦены.НайтиПоЗначению(Объект.ТипПричиныНевыдачиПокупателюПриПродажеНижеОбычнойЦены);	
	Если НЕ ПодходящееЗначение = Неопределено Тогда
		ТекстДополненияКНаименованиюПриПродажеНижеОбычнойЦены = ПодходящееЗначение.Представление;	
	КонецЕсли;

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ РЕКВИЗИТОВ ТАБЛИЧНОЙ ЧАСТИ ЗАПАСЫ

&НаСервере                                                                    	
Функция ЗаполнитьСтатьюДекларацииНДСНалоговыеОбязательства(СтруктураДанные) 
	
	Структура = Новый Структура;
	Структура.Вставить("СтатьяДекларацииНДСНалоговыеОбязательства","");	
	
	МетаданныеДокумента = Объект.Ссылка.Метаданные();
	
	//Если НЕ ОбщегоНазначения.ЕстьРеквизитТабЧастиДокумента("СтавкаНДС", МетаданныеДокумента, ИмяТабЧасти) Тогда
	//		Возврат
	//КонецЕсли;

	Если  Объект.ВидОперации = Перечисления.ВидыОперацийНалоговаяНакладная.УсловнаяПродажа 
	  ИЛИ Объект.ВидОперации = Перечисления.ВидыОперацийНалоговаяНакладная.УсловнаяПродажаСписаниеОС Тогда
	  
	  Структура.СтатьяДекларацииНДСНалоговыеОбязательства = Справочники.СтатьиНалоговыхДеклараций.НДС_НОКорректировкаПрочее;
	  
  	ИначеЕсли Объект.ВидОперации = Перечисления.ВидыОперацийНалоговаяНакладная.РаботыОтНерезидента Тогда
		
		Структура.СтатьяДекларацииНДСНалоговыеОбязательства = Справочники.СтатьиНалоговыхДеклараций.НДС_НОНерезидентУслуги;	
		
	ИначеЕсли СтруктураДанные.СтавкаНДС = Справочники.СтавкиНДС.ОсновнаяСтавкаНДС Тогда
		
		// 20%
		Если СтруктураДанные.ИмяТабЧасти = "ОС" Тогда
		
			Структура.СтатьяДекларацииНДСНалоговыеОбязательства = Справочники.СтатьиНалоговыхДеклараций.НДС_НОПоСтавке20ПоставкаОФ;	
		
		Иначе		
			
			Структура.СтатьяДекларацииНДСНалоговыеОбязательства = Справочники.СтатьиНалоговыхДеклараций.НДС_НОПоСтавке20;
			
		КонецЕсли;
		
	ИначеЕсли СтруктураДанные.СтавкаНДС.Ставка = 0 Тогда
		
		// 0%
		Если ЗначениеЗаполнено(Объект.Договор) 
			   И Объект.Договор.ВалютаРасчетов <> Константы.НациональнаяВалюта.Получить() Тогда
			
			// договор - внешнеэкономический
			Структура.СтатьяДекларацииНДСНалоговыеОбязательства = Справочники.СтатьиНалоговыхДеклараций.НДС_НОПоСтавке0Экспорт;	
			
		Иначе
			
			// на терриории Украины
			Структура.СтатьяДекларацииНДСНалоговыеОбязательства = Справочники.СтатьиНалоговыхДеклараций.НДС_НОПоСтавке0Другие;
			
		КонецЕсли;
		
	ИначеЕсли СтруктураДанные.СтавкаНДС.Ставка = 0 И СтруктураДанные.СтавкаНДС.НеОблагается Тогда
		
		// освобожденные операции (п.5 Закона)
		Структура.СтатьяДекларацииНДСНалоговыеОбязательства = Справочники.СтатьиНалоговыхДеклараций.НДС_НООсвобожден;
		
	Иначе
		
		//в строке ТЧ не указаны ставка НДС
		Структура.СтатьяДекларацииНДСНалоговыеОбязательства = Справочники.СтатьиНалоговыхДеклараций.ПустаяСсылка();
		
	КонецЕсли;
	
	Возврат Структура;
	
КонецФункции
             
&НаКлиенте
// Процедура - обработчик события ПриИзменении поля ввода Номенклатура.
//
Процедура ЗапасыНоменклатураПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.Запасы.ТекущиеДанные;
	
	СтруктураДанные = Новый Структура;
	СтруктураДанные.Вставить("Организация", Компания);
	СтруктураДанные.Вставить("Номенклатура", СтрокаТабличнойЧасти.Номенклатура);
	СтруктураДанные.Вставить("Характеристика",	 СтрокаТабличнойЧасти.Характеристика);
	
	СтруктураДанные = ПолучитьДанныеНоменклатураПриИзменении(СтруктураДанные);
	
	СтрокаТабличнойЧасти.ЕдиницаИзмерения = СтруктураДанные.ЕдиницаИзмерения;
	
	СтрокаТабличнойЧасти.Количество = 1;
	СтрокаТабличнойЧасти.Цена = СтруктураДанные.Цена;
	СтрокаТабличнойЧасти.СтавкаНДС = СтруктураДанные.СтавкаНДС;
	СтрокаТабличнойЧасти.Содержание = СтруктураДанные.Содержание;
	СтрокаТабличнойЧасти.КодУКТВЭД = СтруктураДанные.КодУКТВЭД;
	СтрокаТабличнойЧасти.НомерГТД = СтруктураДанные.НомерГТД;
	
	СтруктураДанные = Новый Структура;
	СтруктураДанные.Вставить("СтавкаНДС", СтрокаТабличнойЧасти.СтавкаНДС);
	СтруктураДанные.Вставить("ИмяТабЧасти", "Запасы");
	
	СтруктураДанные = ЗаполнитьСтатьюДекларацииНДСНалоговыеОбязательства(СтруктураДанные);
	СтрокаТабличнойЧасти.СтатьяДекларацииНДСНалоговыеОбязательства = СтруктураДанные.СтатьяДекларацииНДСНалоговыеОбязательства;
	
	РассчитатьСуммуВСтрокеТабличнойЧасти();
	
КонецПроцедуры // ЗапасыНоменклатураПриИзменении()

// Процедура - обработчик события ПриИзменении поля ввода Характеристика.
//
&НаКлиенте
Процедура ЗапасыХарактеристикаПриИзменении(Элемент)
	СтрокаТабличнойЧасти = Элементы.Запасы.ТекущиеДанные;
		
	СтруктураДанные = Новый Структура;
	СтруктураДанные.Вставить("Номенклатура",	 СтрокаТабличнойЧасти.Номенклатура);
	СтруктураДанные.Вставить("Характеристика",	 СтрокаТабличнойЧасти.Характеристика);
		
	Если ЗначениеЗаполнено(Объект.ВидЦен) Тогда
		
		СтруктураДанные.Вставить("ДатаОбработки",	 	Объект.Дата);
		СтруктураДанные.Вставить("ВалютаДокумента",	 	Объект.ВалютаДокумента);
		СтруктураДанные.Вставить("СуммаВключаетНДС", 	Объект.СуммаВключаетНДС);
		
		СтруктураДанные.Вставить("СтавкаНДС", 			СтрокаТабличнойЧасти.СтавкаНДС);
		СтруктураДанные.Вставить("Цена",			 	СтрокаТабличнойЧасти.Цена);
		
		СтруктураДанные.Вставить("ВидЦен", Объект.ВидЦен);
		СтруктураДанные.Вставить("ЕдиницаИзмерения", СтрокаТабличнойЧасти.ЕдиницаИзмерения);
		
	КонецЕсли;
				
	СтруктураДанные = ПолучитьДанныеХарактеристикаПриИзменении(СтруктураДанные);
	
	СтрокаТабличнойЧасти.Цена = СтруктураДанные.Цена;
	СтрокаТабличнойЧасти.Содержание = СтруктураДанные.Содержание;
	СтрокаТабличнойЧасти.КодУКТВЭД = СтруктураДанные.КодУКТВЭД;
	СтрокаТабличнойЧасти.НомерГТД = СтруктураДанные.НомерГТД;
	
	РассчитатьСуммуВСтрокеТабличнойЧасти();
КонецПроцедуры // ЗапасыХарактеристикаПриИзменении()

&НаКлиенте
// Процедура - обработчик события ПриИзменении поля ввода Количество.
//
Процедура ЗапасыКоличествоПриИзменении(Элемент)
	
	РассчитатьСуммуВСтрокеТабличнойЧасти();
	
КонецПроцедуры // ЗапасыКоличествоПриИзменении()

// Процедура - обработчик события ОбработкаВыбора поля ввода ЕдиницаИзмерения.
//
&НаКлиенте
Процедура ЗапасыЕдиницаИзмеренияОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтрокаТабличнойЧасти = Элементы.Запасы.ТекущиеДанные;
	
	Если СтрокаТабличнойЧасти.ЕдиницаИзмерения = ВыбранноеЗначение 
		ИЛИ СтрокаТабличнойЧасти.Цена = 0 Тогда
		Возврат;
	КонецЕсли;	
	
	ТекущийКоэффициент = 0;
	Если ТипЗнч(СтрокаТабличнойЧасти.ЕдиницаИзмерения) = Тип("СправочникСсылка.КлассификаторЕдиницИзмерения") Тогда
		ТекущийКоэффициент = 1;
	КонецЕсли;	
	
	Коэффициент = 0;
	Если ТипЗнч(ВыбранноеЗначение) = Тип("СправочникСсылка.КлассификаторЕдиницИзмерения") Тогда
		Коэффициент = 1;
	КонецЕсли;
	
	Если ТекущийКоэффициент = 0 И Коэффициент = 0 Тогда
		СтруктураДанные = ПолучитьДанныеЕдиницаИзмеренияПриИзменении(СтрокаТабличнойЧасти.ЕдиницаИзмерения, ВыбранноеЗначение);
	ИначеЕсли ТекущийКоэффициент = 0 Тогда
		СтруктураДанные = ПолучитьДанныеЕдиницаИзмеренияПриИзменении(СтрокаТабличнойЧасти.ЕдиницаИзмерения);
	ИначеЕсли Коэффициент = 0 Тогда
		СтруктураДанные = ПолучитьДанныеЕдиницаИзмеренияПриИзменении(,ВыбранноеЗначение);
	ИначеЕсли ТекущийКоэффициент = 1 И Коэффициент = 1 Тогда
		СтруктураДанные = Новый Структура("ТекущийКоэффициент, Коэффициент", 1, 1);
	КонецЕсли;	
	
	// Цена.
	Если СтруктураДанные.ТекущийКоэффициент <> 0 Тогда
		СтрокаТабличнойЧасти.Цена = СтрокаТабличнойЧасти.Цена * СтруктураДанные.Коэффициент / СтруктураДанные.ТекущийКоэффициент;
	КонецЕсли; 		
	
	РассчитатьСуммуВСтрокеТабличнойЧасти();
	
КонецПроцедуры // ЗапасыЕдиницаИзмеренияОбработкаВыбора()

&НаКлиенте
// Процедура - обработчик события ПриИзменении поля ввода Цена.
//
Процедура ЗапасыЦенаПриИзменении(Элемент)
	
	РассчитатьСуммуВСтрокеТабличнойЧасти();
	
КонецПроцедуры // ЗапасыЦенаПриИзменении()

&НаКлиенте
// Процедура - обработчик события ПриИзменении поля ввода Сумма.
//
Процедура ЗапасыСуммаПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.Запасы.ТекущиеДанные;
	
	// Цена.
	Если СтрокаТабличнойЧасти.Количество <> 0 Тогда
		СтрокаТабличнойЧасти.Цена = СтрокаТабличнойЧасти.Сумма / СтрокаТабличнойЧасти.Количество;
	КонецЕсли;
	
	// Скидка.
	Если СтрокаТабличнойЧасти.ПроцентСкидкиНаценки = 100 Тогда
		СтрокаТабличнойЧасти.Цена = 0;
	ИначеЕсли СтрокаТабличнойЧасти.ПроцентСкидкиНаценки <> 0 И СтрокаТабличнойЧасти.Количество <> 0 Тогда
		СтрокаТабличнойЧасти.Цена = СтрокаТабличнойЧасти.Сумма / ((1 - СтрокаТабличнойЧасти.ПроцентСкидкиНаценки / 100) * СтрокаТабличнойЧасти.Количество);
	КонецЕсли;
	
	РассчитатьСуммуНДС(СтрокаТабличнойЧасти);
	СтрокаТабличнойЧасти.Всего = СтрокаТабличнойЧасти.Сумма + ?(Объект.СуммаВключаетНДС, 0, СтрокаТабличнойЧасти.СуммаНДС);
	
КонецПроцедуры // ЗапасыСуммаПриИзменении()
 
&НаКлиенте
// Процедура - обработчик события ПриИзменении поля ввода СтавкаНДС.
//
Процедура ЗапасыСтавкаНДСПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.Запасы.ТекущиеДанные;
	
	РассчитатьСуммуНДС(СтрокаТабличнойЧасти);
	СтрокаТабличнойЧасти.Всего = СтрокаТабличнойЧасти.Сумма + ?(Объект.СуммаВключаетНДС, 0, СтрокаТабличнойЧасти.СуммаНДС);
	
КонецПроцедуры  // ЗапасыСтавкаНДСПриИзменении()

&НаКлиенте
// Процедура - обработчик события ПриИзменении поля ввода СуммаНДС.
//
Процедура ЗапасыСуммаНДСПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.Запасы.ТекущиеДанные;
	
	// Всего.	
	СтрокаТабличнойЧасти.Всего = СтрокаТабличнойЧасти.Сумма + ?(Объект.СуммаВключаетНДС, 0, СтрокаТабличнойЧасти.СуммаНДС);
	
КонецПроцедуры // ЗапасыСуммаНДСПриИзменении()

&НаКлиенте
// Процедура - обработчик события ПриИзменении поля ввода ПроцентСкидкиНаценки.
//
Процедура ЗапасыПроцентСкидкиНаценкиПриИзменении(Элемент)
	
	РассчитатьСуммуВСтрокеТабличнойЧасти();
	
КонецПроцедуры // ЗапасыПроцентСкидкиНаценкиПриИзменении()

// Процедура - обработчик события ПослеУдаления строки списка Запасы.
//
&НаКлиенте
Процедура ЗапасыПослеУдаления(Элемент)
	ОбновитьИтоговыеСуммы();
КонецПроцедуры

&НаКлиенте
Процедура ЗапасыКодУКТВЭДНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	НачалоВыбораНоменклатурыГТД(Элемент, СтандартнаяОбработка);

КонецПроцедуры

&НаКлиенте
Процедура ЗапасыНомерГТДНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	НачалоВыбораНоменклатурыГТД(Элемент, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапасыКодУКТВЭДОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("СправочникСсылка.НоменклатураГТД") Тогда
	
		ТекущаяСтрокаТоваров = Элементы.Запасы.ТекущиеДанные;
		Если НЕ ТекущаяСтрокаТоваров = Неопределено Тогда
			ТекущаяСтрокаТоваров.НомерГТД = ПолучитьРеквизитНоменклатурыГТДНаСервере(ВыбранноеЗначение, "НомерГТД");
		КонецЕсли;

		ВыбранноеЗначение = ПолучитьРеквизитНоменклатурыГТДНаСервере(ВыбранноеЗначение, "КодУКТВЭД");	
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапасыНомерГТДОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("СправочникСсылка.НоменклатураГТД") Тогда
	
		ТекущаяСтрокаТоваров = Элементы.Запасы.ТекущиеДанные;
		Если НЕ ТекущаяСтрокаТоваров = Неопределено Тогда
			ТекущаяСтрокаТоваров.КодУКТВЭД = ПолучитьРеквизитНоменклатурыГТДНаСервере(ВыбранноеЗначение, "КодУКТВЭД");
		КонецЕсли;

		ВыбранноеЗначение = ПолучитьРеквизитНоменклатурыГТДНаСервере(ВыбранноеЗначение, "НомерГТД");
		
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ РЕКВИЗИТОВ ТАБЛИЧНОЙ ЧАСТИ ОС

&НаКлиенте
// Процедура - обработчик события ПриИзменении поля ввода Сумма.
//
Процедура ОССуммаПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.ОС.ТекущиеДанные;
		
	// Сумма НДС.
	РассчитатьСуммуНДС(СтрокаТабличнойЧасти);
	
	// Всего.	
	СтрокаТабличнойЧасти.Всего = СтрокаТабличнойЧасти.Сумма + ?(Объект.СуммаВключаетНДС, 0, СтрокаТабличнойЧасти.СуммаНДС);
	
	ОбновитьИтоговыеСуммы();
	
КонецПроцедуры // ОССуммаПриИзменении()

&НаКлиенте
// Процедура - обработчик события ПриИзменении поля ввода СтавкаНДС.
//
Процедура ОССтавкаНДСПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.ОС.ТекущиеДанные;
	
	// Сумма НДС.
	РассчитатьСуммуНДС(СтрокаТабличнойЧасти);
	
	// Всего.	
	СтрокаТабличнойЧасти.Всего = СтрокаТабличнойЧасти.Сумма + ?(Объект.СуммаВключаетНДС, 0, СтрокаТабличнойЧасти.СуммаНДС);
	                        	
	ОбновитьИтоговыеСуммы();
	
КонецПроцедуры // ОССтавкаНДСПриИзменении()

&НаКлиенте
// Процедура - обработчик события ПриИзменении поля ввода СуммаНДС.
//
Процедура ОССуммаНДСПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.ОС.ТекущиеДанные;
	
	// Всего.	
	СтрокаТабличнойЧасти.Всего = СтрокаТабличнойЧасти.Сумма + ?(Объект.СуммаВключаетНДС, 0, СтрокаТабличнойЧасти.СуммаНДС);
	
	ОбновитьИтоговыеСуммы();
	
КонецПроцедуры // ОССуммаНДСПриИзменении()

&НаСервереБезКонтекста
// Получает набор данных с сервера для процедуры НоменклатураПриИзменении.
//
Функция ПолучитьДанныеОСПриИзменении(СтруктураДанные)
	
	СтруктураДанные.Вставить("СтавкаНДС", СтруктураДанные.Организация.СтавкаНДСПоУмолчанию);	
				
	Возврат СтруктураДанные;
	
КонецФункции // ПолучитьДанныеНоменклатураПриИзменении()

&НаКлиенте
// Процедура - обработчик события ПриИзменении поля ввода ОсновноеСредство.
//
Процедура ОСОсновноеСредствоПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.ОС.ТекущиеДанные;
	
	СтруктураДанные = Новый Структура;
	СтруктураДанные.Вставить("Организация", Компания);
	СтруктураДанные.Вставить("Номенклатура", СтрокаТабличнойЧасти.ОсновноеСредство);
		
	СтруктураДанные = ПолучитьДанныеОСПриИзменении(СтруктураДанные);
			
	СтрокаТабличнойЧасти.СтавкаНДС = СтруктураДанные.СтавкаНДС;
		
	СтруктураДанные = Новый Структура;
	СтруктураДанные.Вставить("СтавкаНДС", СтрокаТабличнойЧасти.СтавкаНДС);
	СтруктураДанные.Вставить("ИмяТабЧасти", "ОС");
	
	СтруктураДанные = ЗаполнитьСтатьюДекларацииНДСНалоговыеОбязательства(СтруктураДанные);
	СтрокаТабличнойЧасти.СтатьяДекларацииНДСНалоговыеОбязательства = СтруктураДанные.СтатьяДекларацииНДСНалоговыеОбязательства;
			
КонецПроцедуры // ОСОсновноеСредствоПриИзменении()

// Процедура - обработчик события ПослеУдаления строки списка ОС.
//
&НаКлиенте
Процедура ОСПослеУдаления(Элемент)
	ОбновитьИтоговыеСуммы();
КонецПроцедуры

#Область ОбработчикиБиблиотек

// СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки

&НаКлиенте
Процедура Подключаемый_ВыполнитьНазначаемуюКоманду(Команда)
	Если НЕ ДополнительныеОтчетыИОбработкиКлиент.ВыполнитьНазначаемуюКомандуНаКлиенте(ЭтаФорма, Команда.Имя) Тогда
		РезультатВыполнения = Неопределено;
		ДополнительныеОтчетыИОбработкиВыполнитьНазначаемуюКомандуНаСервере(Команда.Имя, РезультатВыполнения);
		ДополнительныеОтчетыИОбработкиКлиент.ПоказатьРезультатВыполненияКоманды(ЭтаФорма, РезультатВыполнения);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ДополнительныеОтчетыИОбработкиВыполнитьНазначаемуюКомандуНаСервере(ИмяЭлемента, РезультатВыполнения)
	ДополнительныеОтчетыИОбработки.ВыполнитьНазначаемуюКомандуНаСервере(ЭтаФорма, ИмяЭлемента, РезультатВыполнения);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки

// СтандартныеПодсистемы.Печать
&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуПечати(Команда)
	УправлениеПечатьюКлиент.ВыполнитьПодключаемуюКомандуПечати(Команда, ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.Печать

#КонецОбласти

ВыполняетсяОткрытие = Ложь;
ЗаполнилиУспешно = Ложь;

мСписокВыбораТипПричиныНевыдачиПокупателю = Новый СписокЗначений();
мСписокВыбораТипПричиныНевыдачиПокупателю.Добавить(0, " - Податкова накладна видається покупцю");
мСписокВыбораТипПричиныНевыдачиПокупателю.Добавить(1, "01-Виписана на суму перевищення звичайної ціни над фактичною");
мСписокВыбораТипПричиныНевыдачиПокупателю.Добавить(2, "02-Постачання неплатнику податку");
мСписокВыбораТипПричиныНевыдачиПокупателю.Добавить(3, "03-Натуральна виплата в рахунок оплати праці фізичним особам");
мСписокВыбораТипПричиныНевыдачиПокупателю.Добавить(4, "04-Постачання у межах балансу для невиробничого використання");
мСписокВыбораТипПричиныНевыдачиПокупателю.Добавить(5, "05-Ліквідація основних фондів за самостійним рішенням платника податку");
мСписокВыбораТипПричиныНевыдачиПокупателю.Добавить(6, "06-Переведення основних фондів до складу невиробничих");
мСписокВыбораТипПричиныНевыдачиПокупателю.Добавить(7, "07-Експортні постачання");
мСписокВыбораТипПричиныНевыдачиПокупателю.Добавить(8, "08-Постачання для операцій, які не є об'єктом оподаткування податком 
                                                       |на додану вартість");
мСписокВыбораТипПричиныНевыдачиПокупателю.Добавить(9, "09-Постачання для операцій, які звільнені від оподаткування податком 
                                                       |на додану вартість");
мСписокВыбораТипПричиныНевыдачиПокупателю.Добавить(10, "10-Визнання умовного постачання товарних залишків та/або основних 
                                                        |фондів, що перебувають в обліку платника податку на день 
														|анулювання його реєстрації як платника податку на додану вартість,
														|щодо яких був нарахований податковий кредит у минулих або поточному 
														|податкових періодах при анулюванні реєстрації платника податку 
														|на додану вартість");
мСписокВыбораТипПричиныНевыдачиПокупателю.Добавить(11, "11-Виписана за щоденними підсумками операцій"); 

мСписокВыбораСпецРежимНалогообложения = Новый СписокЗначений();
мСписокВыбораСпецРежимНалогообложения.Добавить(0, "0   спец. режим не застосовується");
мСписокВыбораСпецРежимНалогообложения.Добавить(2, "2   сільськогосподарські підприємства, які застосовують 
													|спеціальний режим оподаткування діяльності у сфері сільського та 
													|лісового господарства, а також рибальства відповідно до статті 209 Кодексу");
мСписокВыбораСпецРежимНалогообложения.Добавить(3, "3   сільськогосподарські підприємства усіх форм власності, 
													|які відповідають критеріям, визначеним статтею 209 Кодексу ,
													|але які не обрали спеціальний режим оподаткування 
													|діяльності у сфері сільського, лісового господарства та рибальства 
													|та реалізують молоко, худобу, птицю, вовну власного виробництва, а 
													|також молочні продукти, молочну сировину та м'ясопродукти, 
													|вироблені у власних переробних цехах");
мСписокВыбораСпецРежимНалогообложения.Добавить(4, "4 (до 01.01.2015) переробні підприємства усіх форм власності, які згідно з 
													|пунктом 1 підрозділу 2 розділу XX Кодексу за 
													|реалізовані ними молоко, молочну сировину та молочні продукти, 
													|м'ясо та м'ясопродукти, іншу продукцію переробних тварин, 
													|закуплених у живій вазі (шкури, субпродукти, м'ясо-кісткове 
													|борошно), у повному обсязі спрямовують суми податку на додану 
													|вартість до спеціального фонду державного бюджету");
													
мСписокВыбораТипПричиныНевыдачиПокупателюПриПродажеНижеОбычнойЦены = Новый СписокЗначений();
мСписокВыбораТипПричиныНевыдачиПокупателюПриПродажеНижеОбычнойЦены.Добавить(1,  "01-Виписана на суму перевищення звичайної ціни над фактичною");
мСписокВыбораТипПричиныНевыдачиПокупателюПриПродажеНижеОбычнойЦены.Добавить(12, "12-Виписана на вартість безоплатно поставлених товарів/послуг, 
                                                                                   |обчислену виходячи з рівня звичайних цін"); 
мСписокВыбораТипПричиныНевыдачиПокупателюПриПродажеНижеОбычнойЦены.Добавить(15, "15-Складена на суму перевищення ціни придбання товарів/послуг
                                                                                   |над фактичною ціною їх постачання"); 
мСписокВыбораТипПричиныНевыдачиПокупателюПриПродажеНижеОбычнойЦены.Добавить(16, "16-Складена на суму перевищення балансової (залишкової) 
                                                                                   |вартості необоротних активів над фактичною ціною їх постачання"); 

мСписокВыбораТекстДополненияКНаименованиюПриПродажеНижеОбычнойЦены = Новый СписокЗначений();
мСписокВыбораТекстДополненияКНаименованиюПриПродажеНижеОбычнойЦены.Добавить(1,   "(перевищення звичайної ціни над фактичною за товарами, послугами, указаними  в  податковій  накладній № <визначається автоматично>)");
мСписокВыбораТекстДополненияКНаименованиюПриПродажеНижеОбычнойЦены.Добавить("1_1","(перевищення звичайної ціни над фактичною за товарами, послугами, указаними  в  податковій  накладній № <визначається автоматично>; сума процентів нарахованих або таких, що мають бути нараховані на суму номіналу процентного векселя)");
мСписокВыбораТекстДополненияКНаименованиюПриПродажеНижеОбычнойЦены.Добавить(12,  "(звичайна ціна за товарами, послугами, указаними  в  податковій  накладній № <визначається автоматично>)");
мСписокВыбораТекстДополненияКНаименованиюПриПродажеНижеОбычнойЦены.Добавить(15,  "(перевищення ціни придбання над фактичною ціною постачання товарів/послуг, указаних в податковій накладній № <визначається автоматично>)");
мСписокВыбораТекстДополненияКНаименованиюПриПродажеНижеОбычнойЦены.Добавить(16,  "(перевищення балансової (залишкової) вартості над фактичною ціною постачання необоротних активів, указаних в податковій накладні № <визначається автоматично>)");
