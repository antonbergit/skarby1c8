
///////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ ЗАПОЛНЕНИЯ ДОКУМЕНТА

// Процедура заполняет реквизиты шапки  документа 
// Параметры: 
//  ДанныеЗаполнения - Структура - Данные заполнения документа
//
Процедура ЗаполнитьШапкуДокументаПоОснованию(ДанныеЗаполнения)
	
	Автор = ДанныеЗаполнения.Автор;
	ВалютаДокумента = ДанныеЗаполнения.ВалютаДокумента;
	Договор = ДанныеЗаполнения.Договор;
	Комментарий = ДанныеЗаполнения.Комментарий;
	Контрагент = ДанныеЗаполнения.Контрагент;
	Кратность = ДанныеЗаполнения.Кратность;
	КтоВыписалНалоговуюНакладную = ДанныеЗаполнения.КтоВыписалНалоговуюНакладную;
	Курс = ДанныеЗаполнения.Курс;
	Организация = ДанныеЗаполнения.Организация;
	НалоговаяНакладная = ДанныеЗаполнения.Ссылка;
	СуммаВключаетНДС = ДанныеЗаполнения.СуммаВключаетНДС;
	СуммаДокумента = ДанныеЗаполнения.СуммаДокумента;
	СуммаНДСДокумента = ДанныеЗаполнения.СуммаНДСДокумента;
	ВидЦен = ДанныеЗаполнения.ВидЦен;
	НалогообложениеНДС = ДанныеЗаполнения.НалогообложениеНДС;
	АвторасчетНДС = ДанныеЗаполнения.АвторасчетНДС;
	
КонецПроцедуры // ЗаполнитьШапкуДокументаПоОснованию()
	
////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

// Процедура - обработчик события ОбработкаЗаполнения.
//
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка) Экспорт
	           	
	Если НЕ ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.НалоговаяНакладная") Тогда
		Возврат;
	КонецЕсли;	
    // предусмотрен только ввод на основании НалоговойНакладной
	
	// Заполним реквизиты из стандартного набора по документу основанию.
	ЗаполнитьШапкуДокументаПоОснованию(ДанныеЗаполнения);
	
	НалоговаяНакладная = ДанныеЗаполнения.Ссылка;
	ФормаРасчетов = ДанныеЗаполнения.ФормаРасчетов;
	
	// определим вид операции
	Если ДанныеЗаполнения.ВидОперации = Перечисления.ВидыОперацийНалоговаяНакладная.ОблагаемыеОперации Тогда
		ВидОперации = Перечисления.ВидыОперацийПриложение1КНалоговойНакладной.ОблагаемыеОперации;
	ИначеЕсли ДанныеЗаполнения.ВидОперации = Перечисления.ВидыОперацийНалоговаяНакладная.ОсвобожденныеОперации Тогда
		ВидОперации = Перечисления.ВидыОперацийПриложение1КНалоговойНакладной.ОсвобожденныеОперации;
	ИначеЕсли ДанныеЗаполнения.ВидОперации = Перечисления.ВидыОперацийНалоговаяНакладная.НеНДСОперации Тогда
		ВидОперации = Перечисления.ВидыОперацийПриложение1КНалоговойНакладной.НеНДСОперации;
	Иначе
		
		ТекстСообщения = "Не предусмотрен ввод Приложения 1 к Налоговой накладной с видом операции:" + ДанныеЗаполнения.ВидОперации;
		УправлениеНебольшойФирмойСервер.СообщитьОбОшибке(ЭтотОбъект, ТекстСообщения, , , , Истина);
		
		ВидОперации = Перечисления.ВидыОперацийПриложение1КНалоговойНакладной.ОблагаемыеОперации;
		Возврат;
	КонецЕсли;
	
	Запасы.Очистить();
	ОС.Очистить();
	Для Каждого ТекСтрокаЗапасы Из ДанныеЗаполнения.Запасы Цикл
		НоваяСтрока = Запасы.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекСтрокаЗапасы);
		НоваяСтрока.КоличествоОсталось = ТекСтрокаЗапасы.Количество;
	КонецЦикла;
	
	Для Каждого ТекСтрокаОС Из ДанныеЗаполнения.ОС Цикл
		НоваяСтрока = ОС.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекСтрокаОС);
		НоваяСтрока.КоличествоОсталось = ТекСтрокаЗапасы.Количество;
	КонецЦикла;
		      	
КонецПроцедуры // ОбработкаЗаполнения()

// Процедура - обработчик события ПередЗаписью объекта.
//
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	СуммаДокумента = Запасы.Итог("Всего") + ОС.Итог("Всего");
	
КонецПроцедуры // ПередЗаписью()

