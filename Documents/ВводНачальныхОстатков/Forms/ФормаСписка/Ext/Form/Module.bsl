
#Область СобытияФормы

&НаСервере
// Процедура - обработчик события ПриСозданииНаСервере.
//
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Список.Параметры.УстановитьЗначениеПараметра("РасчетыСПоставщикамиИПокупателями", НСтр("ru='Расчеты с поставщиками и покупателями';uk='Розрахунки з постачальниками й покупцями'"));
	Список.Параметры.УстановитьЗначениеПараметра("Запасы", НСтр("ru='Запасы';uk='Запаси'"));
	Список.Параметры.УстановитьЗначениеПараметра("ДенежныеCредства", НСтр("ru='Денежные средства';uk='Кошти'"));
	Список.Параметры.УстановитьЗначениеПараметра("РасчетыПоНалогам", НСтр("ru='Расчеты по налогам';uk='Розрахунки за податками'"));
	Список.Параметры.УстановитьЗначениеПараметра("РасчетыСПодотчетниками", НСтр("ru='Расчеты с подотчетниками';uk='Розрахунки з підзвітниками'"));
	Список.Параметры.УстановитьЗначениеПараметра("РасчетыСПерсоналом", НСтр("ru='Расчеты с персоналом';uk='Розрахунки з персоналом'"));
	Список.Параметры.УстановитьЗначениеПараметра("Имущество", НСтр("ru='Имущество';uk='Майно'"));
	Список.Параметры.УстановитьЗначениеПараметра("ПрочиеРазделы", НСтр("ru='Прочие разделы';uk='Інші розділи'"));
	Список.Параметры.УстановитьЗначениеПараметра("Эквайринг", НСтр("ru='Эквайринг';uk='Еквайринг'"));
	
	УправлениеНебольшойФирмойСервер.УстановитьОформлениеКолонкиДата(Список);
	
	// СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
	ДополнительныеОтчетыИОбработки.ПриСозданииНаСервере(ЭтаФорма);
	// Конец СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
	
	// СтандартныеПодсистемы.Печать
	УправлениеПечатью.ПриСозданииНаСервере(ЭтаФорма, Элементы.ГруппаВажныеКоманды);
	// Конец СтандартныеПодсистемы.Печать
	
	УстановитьВидимостьИДоступность();
	
КонецПроцедуры // ПриСозданииНаСервере()

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия="ВводОстатков" Тогда
		Элементы.Список.Обновить();
		УстановитьВидимостьИДоступность();
	КонецЕсли; 	
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	ОткрытьФорму("Обработка.ПомощникВводаНачальныхОстатков.Форма",, ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиБиблиотек

// СтандартныеПодсистемы.Печать
&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуПечати(Команда)
	УправлениеПечатьюКлиент.ВыполнитьПодключаемуюКомандуПечати(Команда, ЭтотОбъект, Элементы.Список);
КонецПроцедуры

// Конец СтандартныеПодсистемы.Печать

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьВидимостьИДоступность()
	
	Если Документы.ВводНачальныхОстатков.ОстаткиВведены() И НЕ Элементы.Найти("ФормаСоздать")=Неопределено Тогда
		Элементы.ФормаСоздать.Заголовок = НСтр("ru='Продолжить ввод остатков';uk='Продолжить ввод остатков'");
	КонецЕсли; 
	
КонецПроцедуры
 
#КонецОбласти 