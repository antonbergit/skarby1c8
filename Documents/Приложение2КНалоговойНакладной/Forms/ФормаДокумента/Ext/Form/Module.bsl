////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ

&НаКлиенте
Перем мСписокВыбораТипПричиныНевыдачиПокупателю;
&НаКлиенте
Перем мСписокВыбораСпецРежимНалогообложения;

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

&НаСервере
// Процедура вызывает обработку заполнения документа по основанию.
//
Процедура ЗаполнитьПоДокументу()
	  
	Документ = РеквизитФормыВЗначение("Объект");
	Документ.ОбработкаЗаполнения(Объект.НалоговаяНакладная, );
	ЗначениеВРеквизитФормы(Документ, "Объект");
	Модифицированность = Истина;	
	
КонецПроцедуры // ЗаполнитьПоДокументу()

// Процедура формирует соответствие видов операций.
//
&НаСервере
Процедура ПолучитьСтруктуруВидовОпераций()
	
	Структура = Новый Структура;
	
	Структура.Вставить("ОблагаемыеОперацииВозврат", Перечисления.ВидыОперацийПриложение2КНалоговойНакладной.ОблагаемыеОперацииВозврат);
	Структура.Вставить("ОблагаемыеОперацииКорректировка", Перечисления.ВидыОперацийПриложение2КНалоговойНакладной.ОблагаемыеОперацииКорректировка);
	Структура.Вставить("ОсвобожденныеОперацииВозврат", Перечисления.ВидыОперацийПриложение2КНалоговойНакладной.ОсвобожденныеОперацииВозврат);
	Структура.Вставить("ОсвобожденныеОперацииКорректировка", Перечисления.ВидыОперацийПриложение2КНалоговойНакладной.ОсвобожденныеОперацииКорректировка);
	Структура.Вставить("НеНДСОперацииВозврат", Перечисления.ВидыОперацийПриложение2КНалоговойНакладной.НеНДСОперацииВозврат);
	Структура.Вставить("НеНДСОперацииКорректировка", Перечисления.ВидыОперацийПриложение2КНалоговойНакладной.НеНДСОперацииКорректировка);
	Структура.Вставить("ИтоговаяРозницаОблагаемыеОперацииВозврат", Перечисления.ВидыОперацийПриложение2КНалоговойНакладной.ИтоговаяРозницаОблагаемыеОперацииВозврат);
	Структура.Вставить("ИтоговаяРозницаОсвобожденныеОперацииВозврат", Перечисления.ВидыОперацийПриложение2КНалоговойНакладной.ИтоговаяРозницаОсвобожденныеОперацииВозврат);
	Структура.Вставить("РозницаКонрагентуОблагаемыеОперацииВозврат", Перечисления.ВидыОперацийПриложение2КНалоговойНакладной.РозницаКонрагентуОблагаемыеОперацииВозврат);
	Структура.Вставить("РозницаКонрагентуОсвобожденныеОперацииВозврат", Перечисления.ВидыОперацийПриложение2КНалоговойНакладной.РозницаКонрагентуОсвобожденныеОперацииВозврат);
	Структура.Вставить("РаботыОтНерезидентаКорректировка", Перечисления.ВидыОперацийПриложение2КНалоговойНакладной.РаботыОтНерезидентаКорректировка);
	
	ВидыОпераций = Структура;
	
КонецПроцедуры // ПолучитьСтруктуруВидовОпераций()

&НаСервереБезКонтекста
// Получает набор данных с сервера для процедуры ДатаПриИзменении.
//
Функция ПолучитьДанныеДатаПриИзменении(ДокументСсылка, ДатаНовая, ДатаПередИзменением)
	
	СтруктураДанные = Новый Структура();
	СтруктураДанные.Вставить("РазностьДат", УправлениеНебольшойФирмойСервер.ПроверитьНомерДокумента(ДокументСсылка, ДатаНовая, ДатаПередИзменением));
	
	Возврат СтруктураДанные;
	
КонецФункции // ПолучитьДанныеДатаПриИзменении()

&НаСервереБезКонтекста
// Получает набор данных с сервера.
//
Функция ПолучитьДанныеОрганизацияПриИзменении(Организация)
	
	СтруктураДанные = Новый Структура();
	СтруктураДанные.Вставить("Компания", УправлениеНебольшойФирмойСервер.ПолучитьОрганизацию(Организация));
	
	Возврат СтруктураДанные;
	
КонецФункции // ПолучитьДанныеОрганизацияПриИзменении()

&НаСервереБезКонтекста
// Получает набор данных с сервера для процедуры НоменклатураПриИзменении.
//
Функция ПолучитьДанныеНоменклатураПриИзменении(СтруктураДанные)
	
	СтруктураДанные.Вставить("Содержание", УправлениеНебольшойФирмойСервер.ПолучитьПредставлениеНоменклатурыДляПечати(
																?(ЗначениеЗаполнено(СтруктураДанные.Номенклатура.НаименованиеПолное), 
																СтруктураДанные.Номенклатура.НаименованиеПолное, СтруктураДанные.Номенклатура.Наименование), 
																, СтруктураДанные.Номенклатура.Артикул));
	
	СтруктураДанные.Вставить("ЕдиницаИзмерения", СтруктураДанные.Номенклатура.ЕдиницаИзмерения);
	
	Если ЗначениеЗаполнено(СтруктураДанные.Номенклатура.СтавкаНДС) Тогда
		СтруктураДанные.Вставить("СтавкаНДС", СтруктураДанные.Номенклатура.СтавкаНДС);
	Иначе
		СтруктураДанные.Вставить("СтавкаНДС", СтруктураДанные.Организация.СтавкаНДСПоУмолчанию);
	КонецЕсли;
	СтруктураДанные.Вставить("КодУКТВЭД", УправлениеНебольшойФирмойСервер.ПолучитьУКТВЭДВСтрокеТоваров(СтруктураДанные.Номенклатура, СтруктураДанные.Характеристика));
	СтруктураДанные.Вставить("НомерГТД", УправлениеНебольшойФирмойСервер.ПолучитьНомерГТДСтрокеТоваров(СтруктураДанные.Номенклатура, СтруктураДанные.Характеристика));
	
	СтруктураДанные.Вставить("Цена", 0);		
	СтруктураДанные.Вставить("ПроцентСкидкиНаценки", 0);
		
	Возврат СтруктураДанные;
	
КонецФункции // ПолучитьДанныеНоменклатураПриИзменении()

&НаСервереБезКонтекста
// Получает набор данных с сервера для процедуры ХарактеристикаПриИзменении.
//
Функция ПолучитьДанныеХарактеристикаПриИзменении(СтруктураДанные)
	
	СтруктураДанные.Вставить("Содержание", УправлениеНебольшойФирмойСервер.ПолучитьПредставлениеНоменклатурыДляПечати(
																?(ЗначениеЗаполнено(СтруктураДанные.Номенклатура.НаименованиеПолное), 
																СтруктураДанные.Номенклатура.НаименованиеПолное, СтруктураДанные.Номенклатура.Наименование), 
																СтруктураДанные.Характеристика, СтруктураДанные.Номенклатура.Артикул));
	
	Если СтруктураДанные.Свойство("ВидЦен") Тогда
		
		Если ТипЗнч(СтруктураДанные.ЕдиницаИзмерения) = Тип("СправочникСсылка.КлассификаторЕдиницИзмерения") Тогда
			СтруктураДанные.Вставить("Коэффициент", 1);
		Иначе
			СтруктураДанные.Вставить("Коэффициент", СтруктураДанные.ЕдиницаИзмерения.Коэффициент);
		КонецЕсли;
		
		Цена = УправлениеНебольшойФирмойСервер.ПолучитьЦенуНоменклатурыПоВидуЦен(СтруктураДанные);
		СтруктураДанные.Вставить("Цена", Цена);
		
	Иначе
		
		СтруктураДанные.Вставить("Цена", 0);
		
	КонецЕсли;
	СтруктураДанные.Вставить("КодУКТВЭД", УправлениеНебольшойФирмойСервер.ПолучитьУКТВЭДВСтрокеТоваров(СтруктураДанные.Номенклатура, СтруктураДанные.Характеристика));
	СтруктураДанные.Вставить("НомерГТД", УправлениеНебольшойФирмойСервер.ПолучитьНомерГТДСтрокеТоваров(СтруктураДанные.Номенклатура, СтруктураДанные.Характеристика));
	
	Возврат СтруктураДанные;
	
КонецФункции // ПолучитьДанныеХарактеристикаПриИзменении()

&НаСервереБезКонтекста
// Получает набор данных с сервера для процедуры ЕдиницаИзмеренияНачалоВыбораИзСписка.
//
Функция ПолучитьДанныеЕдиницаИзмеренияНачалоВыбораИзСписка(Номенклатура)
	
	СписокЕдиницаИзмерения = Новый СписокЗначений;
	
	СтруктураДанные = Новый Структура();
	СтруктураДанные.Вставить("СписокЕдиницаИзмерения", СписокЕдиницаИзмерения);
		
	СписокЕдиницаИзмерения.Добавить(Номенклатура.ЕдиницаИзмерения, Номенклатура.ЕдиницаИзмерения.Наименование + " (ед. хранения)");
	
	МассивОтбора = Новый Массив;
	МассивОтбора.Добавить(Номенклатура);
	МассивОтбора.Добавить(Номенклатура.НоменклатурнаяГруппа);
	
	Родитель = Номенклатура.НоменклатурнаяГруппа.Родитель;
		
	Пока Истина Цикл
		
		Если НЕ ЗначениеЗаполнено(Родитель) Тогда
			Прервать;
		Иначе
			МассивОтбора.Добавить(Родитель);
		КонецЕсли;
		
		Родитель = Родитель.Родитель;
		
	КонецЦикла;	
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЕдиницыИзмерения.Ссылка КАК ЕдиницаИзмерения
	|ИЗ
	|	Справочник.ЕдиницыИзмерения КАК ЕдиницыИзмерения
	|ГДЕ
	|	ЕдиницыИзмерения.Владелец В(&Номенклатура)";	
	
	Запрос.УстановитьПараметр("Номенклатура", МассивОтбора);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		СписокЕдиницаИзмерения.Добавить(Выборка.ЕдиницаИзмерения);
	КонецЦикла;
		
	Возврат СтруктураДанные;
	
КонецФункции // ПолучитьДанныеЕдиницаИзмеренияНачалоВыбораИзСписка()	

&НаСервереБезКонтекста
// Получает набор данных с сервера для процедуры ЕдиницаИзмеренияПриИзменении.
//
Функция ПолучитьДанныеЕдиницаИзмеренияПриИзменении(ТекущаяЕдиницаИзмерения = Неопределено, ЕдиницаИзмерения = Неопределено)
	
	СтруктураДанные = Новый Структура();
	
	Если ТекущаяЕдиницаИзмерения = Неопределено Тогда
		СтруктураДанные.Вставить("ТекущийКоэффициент", 1);
	Иначе
		СтруктураДанные.Вставить("ТекущийКоэффициент", ТекущаяЕдиницаИзмерения.Коэффициент);
	КонецЕсли;	
		
	Если ЕдиницаИзмерения = Неопределено Тогда
		СтруктураДанные.Вставить("Коэффициент", 1);
	Иначе	
		СтруктураДанные.Вставить("Коэффициент", ЕдиницаИзмерения.Коэффициент);
	КонецЕсли;	
	
	Возврат СтруктураДанные;
	
КонецФункции // ПолучитьДанныеЕдиницаИзмеренияПриИзменении()

&НаКлиенте
// Рассчитывается сумма НДС в строке табличной части.
//
Процедура РассчитатьСуммуНДС(СтрокаТабличнойЧасти)
	
	СтавкаНДС = УправлениеНебольшойФирмойПовтИсп.ПолучитьЗначениеСтавкиНДС(СтрокаТабличнойЧасти.СтавкаНДС);
	
	СтрокаТабличнойЧасти.СуммаНДС = ?(Объект.СуммаВключаетНДС, 
									  СтрокаТабличнойЧасти.Сумма - (СтрокаТабличнойЧасти.Сумма) / ((СтавкаНДС + 100) / 100),
									  СтрокаТабличнойЧасти.Сумма * СтавкаНДС / 100);
	
КонецПроцедуры // ПересчитатьСуммыДокумента() 

&НаКлиенте
// Процедура рассчитывает сумму в строке табличной части.
//
Процедура РассчитатьСуммуВСтрокеТабличнойЧасти(СтрокаТабличнойЧасти = Неопределено, ИмяТаблицы = "Запасы")
	
	Если СтрокаТабличнойЧасти = Неопределено Тогда
		СтрокаТабличнойЧасти = Элементы.Запасы.ТекущиеДанные;
	КонецЕсли;
	
	СтрокаТабличнойЧасти.Сумма = СтрокаТабличнойЧасти.Количество * СтрокаТабличнойЧасти.Цена;
	
	Если СтрокаТабличнойЧасти.ПроцентСкидкиНаценки = 100 Тогда
		СтрокаТабличнойЧасти.Сумма = 0;
	ИначеЕсли СтрокаТабличнойЧасти.ПроцентСкидкиНаценки <> 0
		    И СтрокаТабличнойЧасти.Количество <> 0 Тогда
		СтрокаТабличнойЧасти.Сумма = СтрокаТабличнойЧасти.Сумма * (1 - СтрокаТабличнойЧасти.ПроцентСкидкиНаценки / 100);
	КонецЕсли;
	
	РассчитатьСуммуНДС(СтрокаТабличнойЧасти);
	СтрокаТабличнойЧасти.Всего = СтрокаТабличнойЧасти.Сумма + ?(Объект.СуммаВключаетНДС, 0, СтрокаТабличнойЧасти.СуммаНДС);
	
	ОбновитьИтоговыеСуммы();
	
КонецПроцедуры // РассчитатьСуммуВСтрокеТабличнойЧасти()	

&НаКлиенте
Процедура ОбновитьИтоговыеСуммы()
	
	ИтогВсего = Объект.Запасы.Итог("Всего") + Объект.ОС.Итог("Всего");
	ИтогСуммаНДС = Объект.Запасы.Итог("СуммаНДС") + Объект.ОС.Итог("СуммаНДС");
		
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьПоясняющиеНадписи()

	Элементы.РасшифровкаСпецРежимНалогообложения.Заголовок = "";
	ТекСпецРежим = мСписокВыбораСпецРежимНалогообложения.НайтиПоЗначению(Объект.СпецРежимНалогообложения);
	Если НЕ ТекСпецРежим = Неопределено Тогда
		Элементы.РасшифровкаСпецРежимНалогообложения.Заголовок = ТекСпецРежим.Представление;		
	КонецЕсли;
	
	Элементы.РасшифровкаТипПричиныНевыдачиПокупателю.Заголовок = "";
	ТекТипПричины = мСписокВыбораТипПричиныНевыдачиПокупателю.НайтиПоЗначению(Объект.ТипПричиныНевыдачиПокупателю);
	Если НЕ ТекТипПричины = Неопределено Тогда
		Элементы.РасшифровкаТипПричиныНевыдачиПокупателю.Заголовок = ТекТипПричины.Представление;		
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура УстановитьТипПричиныНевыдачиПокупателюПоУмолчанию()

	Документ = РеквизитФормыВЗначение("Объект");
	Документ.УстановитьТипПричиныНевыдачиПокупателюПоУмолчанию();
	ЗначениеВРеквизитФормы(Документ, "Объект");

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьУсловиеПродажи()

	Документ = РеквизитФормыВЗначение("Объект");
	Документ.ЗаполнитьУсловиеПродажи();
	ЗначениеВРеквизитФормы(Документ, "Объект");

КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииВидаОперации()
	
	УстановитьТипПричиныНевыдачиПокупателюПоУмолчанию();
	
	УстановитьВидимость();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьВидДоговора(ВидДоговора)

	Объект.ВидДоговора = ВидДоговора;

КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииДоговора()
	
	стФормаРасчетаВидДоговора = ПолучитьДанныеДоговорПриИзменении(Объект.Договор);
	
	Объект.ФормаРасчетов = стФормаРасчетаВидДоговора.ФормаРасчета;  	
	
	ЗаполнитьУсловиеПродажи(); 
	
	ЗаполнитьВидДоговора(стФормаРасчетаВидДоговора.ВидДоговора);
	
	УстановитьВидимость();
	
КонецПроцедуры

&НаКлиенте
// Процедура выполняет пересчет в табличной части документа после изменений 
// в форме "Цены и валюта".Выполняется пересчет колонок: цена, скидка, сумма,
// сумма НДС, всего.
//
Процедура ОбработатьИзмененияПоКнопкеЦеныИВалюты()
	
	// 1. Формируем структуру параметров для заполнения формы "Цены и Валюта".
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("ВалютаДокумента", Объект.ВалютаДокумента);
	СтруктураПараметров.Вставить("Курс", Объект.Курс);
	СтруктураПараметров.Вставить("Кратность", Объект.Кратность);
			
	СтруктураПараметров.Вставить("Организация",	Компания); 
    СтруктураПараметров.Вставить("ДатаДокумента", Объект.Дата);
    	
	СтруктураПараметров.Вставить("ПерезаполнитьЦены", Ложь);
	СтруктураПараметров.Вставить("ПересчитатьЦены", Ложь);
	СтруктураПараметров.Вставить("БылиВнесеныИзменения", Ложь);
	
	СтруктураПараметров.Вставить("СуммаВключаетНДС",	  Объект.СуммаВключаетНДС);

	ОписаниеОповещения = Новый ОписаниеОповещения("ОбработатьИзмененияПоКнопкеЦеныИВалютыЗавершение", ЭтотОбъект);
	ОткрытьФорму("ОбщаяФорма.ФормаЦеныИВалюта", СтруктураПараметров,,,,, ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры // ОбработатьИзмененияПоКнопкеЦеныИВалюты()	

&НаКлиенте
// Процедура-обработчик результата открытия формы "Цены и валюты"
//
Процедура ОбработатьИзмененияПоКнопкеЦеныИВалютыЗавершение(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	СтруктураЦеныИВалюта = РезультатЗакрытия;
	
	// 3. Перезаполняем табличную часть "Затраты" если были внесены изменения в форме "Цены и Валюта".
	Если ТипЗнч(СтруктураЦеныИВалюта) = Тип("Структура") И СтруктураЦеныИВалюта.БылиВнесеныИзменения Тогда
									
		Модифицированность = Истина;
		
		Объект.ВалютаДокумента = СтруктураЦеныИВалюта.ВалютаДокумента;
		Объект.Курс = СтруктураЦеныИВалюта.Курс;
		Объект.Кратность = СтруктураЦеныИВалюта.Кратность;
		Объект.СуммаВключаетНДС = СтруктураЦеныИВалюта.СуммаВключаетНДС;
		
		// Пересчитываем цены по валюте.
		Если НЕ СтруктураЦеныИВалюта.ПерезаполнитьЦены
			  И СтруктураЦеныИВалюта.ПересчитатьЦены Тогда	
				
			УправлениеНебольшойФирмойКлиент.ПересчитатьЦеныТабличнойЧастиПоВалюте(ЭтаФорма, СтруктураЦеныИВалюта.ПредВалютаДокумента, "Запасы");
			
		КонецЕсли;
		
		// Пересчитываем сумму если изменился признак "Сумма включает НДС".
		Если СтруктураЦеныИВалюта.СуммаВключаетНДС <> СтруктураЦеныИВалюта.ПредСуммаВключаетНДС Тогда
			УправлениеНебольшойФирмойКлиент.ПересчитаемСуммуТабличнойЧастиПоФлагуСуммаВключаетНДС(ЭтаФорма, "Запасы");	
			
			Для каждого СтрокаТабличнойЧастиЗапасы Из Объект.Запасы Цикл
				ЗапасыПриИзмененииЦеныКоличества(СтрокаТабличнойЧастиЗапасы);				
			КонецЦикла;
			
			Для каждого СтрокаТабличнойЧастиОС Из Объект.ОС Цикл
				СтавкаНДС = УправлениеНебольшойФирмойПовтИсп.ПолучитьЗначениеСтавкиНДС(СтрокаТабличнойЧастиОС.СтавкаНДС);
				Если Объект.СуммаВключаетНДС Тогда
					СтрокаТабличнойЧастиОС.Сумма = (СтрокаТабличнойЧастиОС.Сумма * (100 + СтавкаНДС)) / 100;
				Иначе
					СтрокаТабличнойЧастиОС.Сумма = (СтрокаТабличнойЧастиОС.Сумма * 100) / (100 + СтавкаНДС);
				КонецЕсли;
					
				СтрокаТабличнойЧастиОС.Всего = СтрокаТабличнойЧастиОС.Сумма + ?(Объект.СуммаВключаетНДС, 0, СтрокаТабличнойЧастиОС.СуммаНДС);
				
				ОСПриИзмененииЦеныКоличества(СтрокаТабличнойЧастиОС);				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;	
	
	СтруктураНадписи = Новый Структура("ВалютаДокумента, Курс, СуммаВключаетНДС, УчетВалютныхОпераций", Объект.ВалютаДокумента, Объект.Курс, Объект.СуммаВключаетНДС, УчетВалютныхОпераций);
	ЦеныИВалюта = СформироватьНадписьЦеныИВалюта(СтруктураНадписи);
	
КонецПроцедуры // ОткрытьФормуЦеныИВалютаЗавершение()

&НаСервереБезКонтекста
// Получает набор данных с сервера для процедуры КонтрагентПриИзменении.
//
Функция ПолучитьДанныеКонтрагентПриИзменении(Контрагент, Организация)
	
	ДоговорПоУмолчанию = Контрагент.ДоговорПоУмолчанию;
		
	Возврат ДоговорПоУмолчанию;
	
КонецФункции // ПолучитьДанныеКонтрагентПриИзменении()

&НаСервере
// Получает набор данных с сервера для процедуры ДоговорПриИзменении.
//
Функция ПолучитьДанныеДоговорПриИзменении(Договор)
	
	Запись = Новый Структура();
	
	Если Договор.ВидДоговора = Перечисления.ВидыДоговоров.Бартерный Тогда
		Запись.Вставить("ФормаРасчета", "Бартер");
	Иначе
		Если Объект.Дата >= '2014-03-01' И Объект.Дата < '2015-01-01' Тогда
			Запись.Вставить("ФормаРасчета", "Оплата з поточного рахунку");
		ИначеЕсли Объект.Дата >= '2015-01-01' Тогда
			Запись.Вставить("ФормаРасчета", "Оплата з поточного рахунка");
		КонецЕсли;
	КонецЕсли;
	
	Запись.Вставить("ВидДоговора", Договор.ВидДоговораПоГК);
	
	Возврат Запись;
	
КонецФункции // ПолучитьДанныеДоговорПриИзменении()

&НаСервереБезКонтекста
// Получает набор данных с сервера для процедуры НалоговаяНакладнаяПриИзменении.
//
Функция ПолучитьДанныеННПриИзменении(НН)
	
	Запись = Новый Структура();
	
	Запись.Вставить("СпецРежимНалогообложения", НН.СпецРежимНалогообложения);
	Запись.Вставить("ТипПричиныНевыдачиПокупателю", НН.ТипПричиныНевыдачиПокупателю);
	
	Возврат Запись;
	
КонецФункции // ПолучитьДанныеДоговорПриИзменении()

&НаКлиенте
Процедура НачалоВыбораНоменклатурыГТД(Элемент, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;
	
	СтрокаТабличнойЧасти = Элементы.Запасы.ТекущиеДанные;
	Если СтрокаТабличнойЧасти = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ВариантыВыбора = Новый СписокЗначений();
	ВариантыВыбора.Добавить(Ложь,   НСтр("ru='Выбрать по данным номенклатуры';uk='Вибрати по даним номенклатури'"));
	ВариантыВыбора.Добавить(Истина, НСтр("ru='Произвольный выбор';uk='Довільний вибір'"));
	
	РезультатВыбора = Неопределено;
	
	ПоказатьВыборИзСписка(Новый ОписаниеОповещения("НачалоВыбораНоменклатурыГТДЗавершение", ЭтотОбъект, Новый Структура("Элемент", Элемент)), ВариантыВыбора, Элемент, 0);

КонецПроцедуры

&НаКлиенте
Процедура НачалоВыбораНоменклатурыГТДЗавершение(ВыбранныйЭлемент, ДополнительныеПараметры) Экспорт
    
    Элемент = ДополнительныеПараметры.Элемент;
    
    РезультатВыбора = ВыбранныйЭлемент;
    
    Если РезультатВыбора = Неопределено Тогда
        Возврат;
    КонецЕсли;
        
	ТекущаяСтрокаТоваров = Элементы.Запасы.ТекущиеДанные;
	
	Если РезультатВыбора.Значение = Истина Тогда
		ПараметрыВыбора1 = Новый Структура("ТекущаяСтрока");
		Если ТекущаяСтрокаТоваров <> Неопределено И ЗначениеЗаполнено(ТекущаяСтрокаТоваров.КодУКТВЭД) Тогда
			ПараметрыВыбора1.Вставить("ТекущаяСтрока", ТекущаяСтрокаТоваров.КодУКТВЭД);	
		КонецЕсли;  
		ОткрытьФорму("Справочник.КлассификаторУКТВЭД.Форма.ФормаВыбора", ПараметрыВыбора1, Элемент);
	Иначе
	    ДанныеСтроки = Новый Структура("КодУКТВЭД, НомерГТД, Номенклатура");
	    ЗаполнитьЗначенияСвойств(ДанныеСтроки, ТекущаяСтрокаТоваров); 
	    ПараметрыВыбора1 = ЗаполнитьПараметрыВыбораНоменклатурыГТД(ДанныеСтроки);
		
		ОткрытьФорму("Справочник.НоменклатураГТД.ФормаВыбора", ПараметрыВыбора1, Элемент);
	КонецЕсли;

КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьРеквизитНоменклатурыГТДНаСервере(Ссылка, ИмяРекзвизита)

	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, ИмяРекзвизита);	

КонецФункции 

&НаСервереБезКонтекста
Функция ЗаполнитьПараметрыВыбораНоменклатурыГТД(ТекущаяСтрокаТоваров)
	
	Параметры = Новый Структура("Отбор, ТекущаяСтрока");
	
	Параметры.Отбор = Новый Структура("Владелец", ТекущаяСтрокаТоваров.Номенклатура); 
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("КодУКТВЭД", ТекущаяСтрокаТоваров.КодУКТВЭД);
	Запрос.УстановитьПараметр("НомерГТД",  ТекущаяСтрокаТоваров.НомерГТД);
	Запрос.УстановитьПараметр("Владелец",  ТекущаяСтрокаТоваров.Номенклатура);
	
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	               |	НоменклатураГТД.Ссылка
	               |ИЗ
	               |	Справочник.НоменклатураГТД КАК НоменклатураГТД
	               |ГДЕ
	               |	НоменклатураГТД.КодУКТВЭД  = &КодУКТВЭД
	               |	И НоменклатураГТД.НомерГТД = &НомерГТД
	               |	И НоменклатураГТД.Владелец = &Владелец";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Параметры.ТекущаяСтрока = Выборка.Ссылка;	
	КонецЕсли;
	
	Возврат Параметры; 
	
КонецФункции	

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ДЛЯ УПРАВЛЕНИЯ ВНЕШНИМ ВИДОМ ФОРМЫ

&НаКлиентеНаСервереБезКонтекста
// Функция возвращает текст надписи "Цены и валюта".
//
Функция СформироватьНадписьЦеныИВалюта(СтруктураНадписи)
	
	ТекстНадписи = "";
	
	// Валюта.
	Если СтруктураНадписи.УчетВалютныхОпераций Тогда
		Если ЗначениеЗаполнено(СтруктураНадписи.ВалютаДокумента) Тогда
			ТекстНадписи = НСтр("ru='Валюта: %Валюта%, курс: %Курс%';uk='Валюта: %Валюта%, курс: %Курс%'");
			ТекстНадписи = СтрЗаменить(ТекстНадписи, "%Валюта%", СокрЛП(Строка(СтруктураНадписи.ВалютаДокумента)));
			ТекстНадписи = СтрЗаменить(ТекстНадписи, "%Курс%", СокрЛП(Строка(СтруктураНадписи.Курс)));
		Иначе
			ТекстНадписи = НСтр("ru='Валюта: <нет>';uk='Валюта: <немає>'");
		КонецЕсли;
	КонецЕсли;	
	
	// Флаг сумма включает НДС.
	Если ПустаяСтрока(ТекстНадписи) Тогда	
		Если СтруктураНадписи.СуммаВключаетНДС Тогда	
			ТекстНадписи = НСтр("ru='Сумма включает НДС';uk='Сума включає ПДВ'");
		Иначе		
			ТекстНадписи = НСтр("ru='Сумма не включает НДС';uk='Сума не включає ПДВ'");
		КонецЕсли;	
	КонецЕсли;	
	
	Возврат ТекстНадписи;
	
КонецФункции // СформироватьНадписьЦеныИВалюта()	

&НаКлиенте
// Процедура устанавливает видимость элементов формы 
//
Процедура УстановитьВидимость()

	ДействуетНалоговыйКодекс = (Объект.Дата >= '20110101');
	УтвержденыНовыеПечатныеФормы = (Объект.Дата >= '20110110');
	
	Элементы.ВключенаВЕдиныйРеестрНалоговыхНакладных.Видимость	= ДействуетНалоговыйКодекс;
	
	Элементы.ВидДоговора.Видимость		= ДействуетНалоговыйКодекс;
	Элементы.ГруппаПравоЛево.Видимость 	= ДействуетНалоговыйКодекс;
	Элементы.ГруппаПравоПраво.Видимость = ДействуетНалоговыйКодекс;
	
	Элементы.УточняемыйПериод.Видимость = Объект.ВключаетсяВУточняющийРасчет;
	Элементы.НомерДляРеестра.Видимость = Объект.ВключаетсяВУточняющийРасчет;
	Элементы.ДатаДляРеестра.Видимость = Объект.ВключаетсяВУточняющийРасчет;
	
	Элементы.ДатаПолученияПокупателем.Видимость = Объект.ДатаПолученияПокупателемНеРавнаДатеДокумента;
	
	Элементы.ТребуетВключенияВЕдиныйРеестрНалоговыхНакладных.Доступность = НЕ (Объект.ТипПричиныНевыдачиПокупателю = 0) И (Объект.Дата < '2014-03-01');
	
	Элементы.ЭлектронныйДокумент.Доступность = (Объект.Дата < '20150101');
	Если Объект.Дата >= '20150101' Тогда
		Элементы.ТребуетВключенияВЕдиныйРеестрНалоговыхНакладных.Доступность = Ложь;
	КонецЕсли;
	
	Если Объект.ВидОперации = ВидыОпераций.ОблагаемыеОперацииВозврат или 
		 Объект.ВидОперации = ВидыОпераций.ОблагаемыеОперацииКорректировка или 
		 Объект.ВидОперации = ВидыОпераций.ИтоговаяРозницаОблагаемыеОперацииВозврат или
		 Объект.ВидОперации = ВидыОпераций.РозницаКонрагентуОблагаемыеОперацииВозврат или
		 Объект.ВидОперации = ВидыОпераций.РаботыОтНерезидентаКорректировка Тогда
		НовыйПараметр = Новый ПараметрВыбора("Отбор.НеОблагается", Ложь);
		НовыйМассив = Новый Массив();
		НовыйМассив.Добавить(НовыйПараметр);
		НовыеПараметры = Новый ФиксированныйМассив(НовыйМассив);
		Элементы.Запасы.ПодчиненныеЭлементы.ЗапасыСтавкаНДС.ПараметрыВыбора = НовыеПараметры;
	Иначе
		НовыйПараметр = Новый ПараметрВыбора("Отбор.НеОблагается", Истина);
		НовыйМассив = Новый Массив();
		НовыйМассив.Добавить(НовыйПараметр);
		НовыеПараметры = Новый ФиксированныйМассив(НовыйМассив);
		Элементы.Запасы.ПодчиненныеЭлементы.ЗапасыСтавкаНДС.ПараметрыВыбора = НовыеПараметры;
	КонецЕсли;
	
КонецПроцедуры // УстановитьВидимость()

&НаСервере
Процедура УстановитьСписокВыбораФормРасчетов(ДатаДокумента)

	Если ДатаДокумента >= '20150101' Тогда
		Элементы.ФормаРасчетов.СписокВыбора.Добавить("Оплата з поточного рахунка");
	Иначе
	    Элементы.ФормаРасчетов.СписокВыбора.Добавить("Оплата з поточного рахунку");
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ПроверитьСоответствиеДоговоров()
	
	Если  Объект.ДокументОснование <> Неопределено
		И УправлениеНебольшойФирмойСервер.ЕстьРеквизитДокумента("Договор", Объект.ДокументОснование.Метаданные()) Тогда
		
		Если Объект.ДокументОснование.Договор <> Объект.Договор Тогда
			Объект.ДокументОснование = Неопределено;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
// Процедура - обработчик события ПриСозданииНаСервере.
//
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Установка реквизитов формы.
	ДатаДокумента = Объект.Дата;
	Если НЕ ЗначениеЗаполнено(ДатаДокумента) Тогда
		ДатаДокумента = ТекущаяДата();
	КонецЕсли;
	Компания = УправлениеНебольшойФирмойСервер.ПолучитьОрганизацию(Объект.Организация);	
	Контрагент = Объект.Контрагент;
	
	Элементы.ОС.Видимость = Константы.ФункциональнаяОпцияУчетВнеоборотныхАктивов.Получить();
	
	ИтогВсего = Объект.Запасы.Итог("Всего") + Объект.ОС.Итог("Всего");
	ИтогСуммаНДС = Объект.Запасы.Итог("СуммаНДС") + Объект.ОС.Итог("СуммаНДС");
	
	// Сформируем надпись цены и валюты.
	УчетВалютныхОпераций = Константы.ФункциональнаяУчетВалютныхОпераций.Получить();
	СтруктураНадписи = Новый Структура("ВалютаДокумента, Курс, СуммаВключаетНДС, УчетВалютныхОпераций", Объект.ВалютаДокумента, Объект.Курс, Объект.СуммаВключаетНДС, УчетВалютныхОпераций);
	ЦеныИВалюта = СформироватьНадписьЦеныИВалюта(СтруктураНадписи);
	
	ДокументОбъект = РеквизитФормыВЗначение("Объект");
	Если ДокументОбъект.ЭтоНовый() Тогда
		УстановитьТипПричиныНевыдачиПокупателюПоУмолчанию();
	КонецЕсли;
	
	УстановитьСписокВыбораФормРасчетов(ДатаДокумента);
	ПолучитьСтруктуруВидовОпераций();
	
	// СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
	ДополнительныеОтчетыИОбработки.ПриСозданииНаСервере(ЭтаФорма);
	// Конец СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
	
	// СтандартныеПодсисте.Печать
	УправлениеПечатью.ПриСозданииНаСервере(ЭтаФорма);
	// Конец СтандартныеПодсистемы.Печать
	
КонецПроцедуры // ПриСозданииНаСервере()

&НаКлиенте
// Процедура - обработчик события ПослеЗаписи.
//
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	УправлениеНебольшойФирмойКлиент.ОбновитьСостояниеДокумента(Объект, СостояниеДокумента, КартинкаСостоянияДокумента, РазрешеноПроведение);
	
КонецПроцедуры // ПослеЗаписи()

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УстановитьВидимость();
	
	ОбновитьПоясняющиеНадписи();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ДЕЙСТВИЯ КОМАНДНЫХ ПАНЕЛЕЙ ФОРМЫ

&НаКлиенте
// Процедура вызывается при нажатии кнопки "ЦеныВалюта" командной панели
// табличного поля.
//
Процедура РедактироватьЦеныИВалюту(Команда)
	
	ОбработатьИзмененияПоКнопкеЦеныИВалюты();
	
КонецПроцедуры // РедактироватьЦеныИВалюту()

&НаКлиенте
// Процедура - обработчик нажатия на кнопку Заполнить.
//
Процедура Заполнить(Команда)
	
	ПоказатьВопрос(Новый ОписаниеОповещения("ЗаполнитьЗавершение", ЭтотОбъект), НСтр("ru='Документ будет полностью перезаполнен по ""Основанию""! Продолжить выполнение операции?';uk='Документ буде повністю перезаповнений по ""Підставі""! Продовжити виконання операції?'"), РежимДиалогаВопрос.ДаНет, 0);
	
КонецПроцедуры  // ЗаполнитьВыполнить()

&НаКлиенте
Процедура ЗаполнитьЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт

	Ответ = РезультатВопроса;
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		ЗаполнитьПоДокументу();
		СтруктураНадписи = Новый Структура("ВалютаДокумента, Курс, СуммаВключаетНДС, УчетВалютныхОпераций", Объект.ВалютаДокумента, Объект.Курс, Объект.СуммаВключаетНДС, УчетВалютныхОпераций);
		ЦеныИВалюта = СформироватьНадписьЦеныИВалюта(СтруктураНадписи);
	КонецЕсли;	

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ РЕКВИЗИТОВ ШАПКИ

&НаКлиенте
// Процедура - обработчик события ПриИзменении поля ввода Дата.
// В процедуре определяется ситуация, когда при изменении своей даты документ 
// оказывается в другом периоде нумерации документов, и в этом случае
// присваивает документу новый уникальный номер.
// Переопределяет соответствующий параметр формы.
//
Процедура ДатаПриИзменении(Элемент)
	
	// Обработка события изменения даты.
	ДатаПередИзменением = ДатаДокумента;
	ДатаДокумента = Объект.Дата;
	Если Объект.Дата <> ДатаПередИзменением Тогда
		СтруктураДанные = ПолучитьДанныеДатаПриИзменении(Объект.Ссылка, Объект.Дата, ДатаПередИзменением);
		Если СтруктураДанные.РазностьДат <> 0 Тогда
			Объект.Номер = "";
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // ДатаПриИзменении()

&НаКлиенте
// Процедура - обработчик события ПриИзменении поля ввода Организация.
// В процедуре осуществляется очистка номера документа,
// а также производится установка параметров функциональных опций формы.
// Переопределяет соответствующий параметр формы.
//
Процедура ОрганизацияПриИзменении(Элемент)

	// Обработка события изменения организации.
	Объект.Номер = "";
	СтруктураДанные = ПолучитьДанныеОрганизацияПриИзменении(Объект.Организация);
	Компания = СтруктураДанные.Компания;
	
КонецПроцедуры // ОрганизацияПриИзменении()

&НаКлиенте
Процедура КонтрагентПриИзменении(Элемент)
	
	КонтрагентПередИзменением = Контрагент;
	Контрагент = Объект.Контрагент;
	
	Если КонтрагентПередИзменением <> Объект.Контрагент Тогда
		Объект.Договор = ПолучитьДанныеКонтрагентПриИзменении(Объект.Контрагент, Объект.Организация);
	КонецЕсли;
	
	ПриИзмененииДоговора();
	
	УстановитьТипПричиныНевыдачиПокупателюПоУмолчанию();
	
КонецПроцедуры

&НаКлиенте
Процедура ДоговорПриИзменении(Элемент)
	
	ПриИзмененииДоговора();
	
	ПроверитьСоответствиеДоговоров();
	
КонецПроцедуры

&НаКлиенте
Процедура НалоговаяНакладнаяПриИзменении(Элемент)
	
	стДанныеНН = ПолучитьДанныеННПриИзменении(Объект.НалоговаяНакладная);
	
	Объект.СпецРежимНалогообложения     = стДанныеНН.СпецРежимНалогообложения;
	Объект.ТипПричиныНевыдачиПокупателю = стДанныеНН.ТипПричиныНевыдачиПокупателю;
	
	ОбновитьПоясняющиеНадписи();
	
КонецПроцедуры

&НаКлиенте
Процедура СпецРежимНалогообложенияПриИзменении(Элемент)
	ОбновитьПоясняющиеНадписи();
КонецПроцедуры

&НаКлиенте
Процедура ТипПричиныНевыдачиПокупателюПриИзменении(Элемент)
	ОбновитьПоясняющиеНадписи();
КонецПроцедуры

&НаКлиенте
// Процедура - обработчик события ПриИзменении поля ввода ВидОперации.
//
Процедура ВидОперацииПриИзменении(Элемент)
	
	ПриИзмененииВидаОперации();
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьИзменениеСуммыНДС(СтрокаТабличнойЧасти)
	
	СтавкаНДС = УправлениеНебольшойФирмойПовтИсп.ПолучитьЗначениеСтавкиНДС(СтрокаТабличнойЧасти.СтавкаНДС);
	
	СтрокаТабличнойЧасти.ИзменениеСуммыНДС = ?(Объект.СуммаВключаетНДС, 
									  СтрокаТабличнойЧасти.ИзменениеСуммы - (СтрокаТабличнойЧасти.ИзменениеСуммы) / ((СтавкаНДС + 100) / 100),
									  СтрокаТабличнойЧасти.ИзменениеСуммы * СтавкаНДС / 100);
	
КонецПроцедуры  

&НаКлиенте
Процедура ВидДоговораНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ОткрытьФорму("Справочник.ВидыДоговоровПоГК.Форма.ФормаВыбора", , Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ВключаетсяВУточняющийРасчетПриИзменении(Элемент)
	УстановитьВидимость(); 
КонецПроцедуры

&НаКлиенте
Процедура ДатаПолученияПокупателемНеРавнаДатеДокументаПриИзменении(Элемент)
	УстановитьВидимость(); 
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ РЕКВИЗИТОВ ТАБЛИЧНОЙ ЧАСТИ ЗАПАСЫ

&НаСервере                                                                    	
Функция ЗаполнитьСтатьюДекларацииНДСНалоговыеОбязательства(СтруктураДанные) 
	
	Структура = Новый Структура;
	Структура.Вставить("СтатьяДекларацииНДСНалоговыеОбязательства","");	
	
	МетаданныеДокумента = Объект.Ссылка.Метаданные();
	
	//Если НЕ ОбщегоНазначения.ЕстьРеквизитТабЧастиДокумента("СтавкаНДС", МетаданныеДокумента, ИмяТабЧасти) Тогда
	//		Возврат
	//КонецЕсли;

	Если  Объект.ВидОперации = Перечисления.ВидыОперацийНалоговаяНакладная.УсловнаяПродажа 
	  ИЛИ Объект.ВидОперации = Перечисления.ВидыОперацийНалоговаяНакладная.УсловнаяПродажаСписаниеОС Тогда
	  
	  Структура.СтатьяДекларацииНДСНалоговыеОбязательства = Справочники.СтатьиНалоговыхДеклараций.НДС_НОКорректировкаПрочее;
	  
  	ИначеЕсли Объект.ВидОперации = Перечисления.ВидыОперацийНалоговаяНакладная.РаботыОтНерезидента Тогда
		
		Структура.СтатьяДекларацииНДСНалоговыеОбязательства = Справочники.СтатьиНалоговыхДеклараций.НДС_НОНерезидентУслуги;	
		
	ИначеЕсли СтруктураДанные.СтавкаНДС = Справочники.СтавкиНДС.ОсновнаяСтавкаНДС Тогда
		
		// 20%
		Если СтруктураДанные.ИмяТабЧасти = "ОС" Тогда
		
			Структура.СтатьяДекларацииНДСНалоговыеОбязательства = Справочники.СтатьиНалоговыхДеклараций.НДС_НОПоСтавке20ПоставкаОФ;	
		
		Иначе		
			
			Структура.СтатьяДекларацииНДСНалоговыеОбязательства = Справочники.СтатьиНалоговыхДеклараций.НДС_НОПоСтавке20;
			
		КонецЕсли;
		
	ИначеЕсли СтруктураДанные.СтавкаНДС.Ставка = 0 Тогда
		
		// 0%
		Если ЗначениеЗаполнено(Объект.Договор) 
			   И Объект.Договор.ВалютаРасчетов <> Константы.НациональнаяВалюта.Получить() Тогда
			
			// договор - внешнеэкономический
			Структура.СтатьяДекларацииНДСНалоговыеОбязательства = Справочники.СтатьиНалоговыхДеклараций.НДС_НОПоСтавке0Экспорт;	
			
		Иначе
			
			// на терриории Украины
			Структура.СтатьяДекларацииНДСНалоговыеОбязательства = Справочники.СтатьиНалоговыхДеклараций.НДС_НОПоСтавке0Другие;
			
		КонецЕсли;
		
	ИначеЕсли СтруктураДанные.СтавкаНДС.Ставка = 0 И СтруктураДанные.СтавкаНДС.НеОблагается Тогда
		
		// освобожденные операции (п.5 Закона)
		Структура.СтатьяДекларацииНДСНалоговыеОбязательства = Справочники.СтатьиНалоговыхДеклараций.НДС_НООсвобожден;
		
	Иначе
		
		//в строке ТЧ не указаны ставка НДС
		Структура.СтатьяДекларацииНДСНалоговыеОбязательства = Справочники.СтатьиНалоговыхДеклараций.ПустаяСсылка();
		
	КонецЕсли;
	
	Возврат Структура;
	
КонецФункции

&НаКлиенте
// Процедура - обработчик события ПриИзменении поля ввода Номенклатура.
//
Процедура ЗапасыНоменклатураПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.Запасы.ТекущиеДанные;
	
	СтруктураДанные = Новый Структура;
	СтруктураДанные.Вставить("Организация", Компания);
	СтруктураДанные.Вставить("Номенклатура", СтрокаТабличнойЧасти.Номенклатура);
	СтруктураДанные.Вставить("Характеристика",	 СтрокаТабличнойЧасти.Характеристика);
	
	СтруктураДанные = ПолучитьДанныеНоменклатураПриИзменении(СтруктураДанные);
	
	СтрокаТабличнойЧасти.ЕдиницаИзмерения = СтруктураДанные.ЕдиницаИзмерения;	
	СтрокаТабличнойЧасти.Количество = 1;
	СтрокаТабличнойЧасти.Цена = СтруктураДанные.Цена;
	СтрокаТабличнойЧасти.СтавкаНДС = СтруктураДанные.СтавкаНДС;
	СтрокаТабличнойЧасти.КодУКТВЭД = СтруктураДанные.КодУКТВЭД;
	СтрокаТабличнойЧасти.НомерГТД = СтруктураДанные.НомерГТД;
	
	СтруктураДанные = Новый Структура;
	СтруктураДанные.Вставить("СтавкаНДС", СтрокаТабличнойЧасти.СтавкаНДС);
	СтруктураДанные.Вставить("ИмяТабЧасти", "Запасы");
	
	СтруктураДанные = ЗаполнитьСтатьюДекларацииНДСНалоговыеОбязательства(СтруктураДанные);
	СтрокаТабличнойЧасти.СтатьяДекларацииНДСНалоговыеОбязательства = СтруктураДанные.СтатьяДекларацииНДСНалоговыеОбязательства;
	
	РассчитатьСуммуВСтрокеТабличнойЧасти();
	
КонецПроцедуры // ЗапасыНоменклатураПриИзменении()

// Процедура - обработчик события ПриИзменении поля ввода Характеристика.
//
&НаКлиенте
Процедура ЗапасыХарактеристикаПриИзменении(Элемент)
	СтрокаТабличнойЧасти = Элементы.Запасы.ТекущиеДанные;
		
	СтруктураДанные = Новый Структура;
	СтруктураДанные.Вставить("Номенклатура",	 СтрокаТабличнойЧасти.Номенклатура);
	СтруктураДанные.Вставить("Характеристика",	 СтрокаТабличнойЧасти.Характеристика);
		
	Если ЗначениеЗаполнено(Объект.ВидЦен) Тогда
		
		СтруктураДанные.Вставить("ДатаОбработки",	 	Объект.Дата);
		СтруктураДанные.Вставить("ВалютаДокумента",	 	Объект.ВалютаДокумента);
		СтруктураДанные.Вставить("СуммаВключаетНДС", 	Объект.СуммаВключаетНДС);
		
		СтруктураДанные.Вставить("СтавкаНДС", 			СтрокаТабличнойЧасти.СтавкаНДС);
		СтруктураДанные.Вставить("Цена",			 	СтрокаТабличнойЧасти.Цена);
		
		СтруктураДанные.Вставить("ВидЦен", Объект.ВидЦен);
		СтруктураДанные.Вставить("ЕдиницаИзмерения", СтрокаТабличнойЧасти.ЕдиницаИзмерения);
		
	КонецЕсли;
				
	СтруктураДанные = ПолучитьДанныеХарактеристикаПриИзменении(СтруктураДанные);
	
	СтрокаТабличнойЧасти.Цена = СтруктураДанные.Цена;
	СтрокаТабличнойЧасти.КодУКТВЭД = СтруктураДанные.КодУКТВЭД;
	СтрокаТабличнойЧасти.НомерГТД = СтруктураДанные.НомерГТД;
	
	РассчитатьСуммуВСтрокеТабличнойЧасти();
КонецПроцедуры // ЗапасыХарактеристикаПриИзменении()

&НаКлиенте
// Процедура - обработчик события ПриИзменении поля ввода Количество.
//
Процедура ЗапасыКоличествоПриИзменении(Элемент)
	
	РассчитатьСуммуВСтрокеТабличнойЧасти();
	
КонецПроцедуры // ЗапасыКоличествоПриИзменении()

&НаКлиенте
// Процедура - обработчик события ОбработкаВыбора поля ввода ЕдиницаИзмерения.
//
Процедура ЗапасыЕдиницаИзмеренияОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтрокаТабличнойЧасти = Элементы.Запасы.ТекущиеДанные;
	
	Если СтрокаТабличнойЧасти.ЕдиницаИзмерения = ВыбранноеЗначение 
		ИЛИ СтрокаТабличнойЧасти.Цена = 0 Тогда
		Возврат;
	КонецЕсли;	
	
	ТекущийКоэффициент = 0;
	Если ТипЗнч(СтрокаТабличнойЧасти.ЕдиницаИзмерения) = Тип("СправочникСсылка.КлассификаторЕдиницИзмерения") Тогда
		ТекущийКоэффициент = 1;
	КонецЕсли;	
	
	Коэффициент = 0;
	Если ТипЗнч(ВыбранноеЗначение) = Тип("СправочникСсылка.КлассификаторЕдиницИзмерения") Тогда
		Коэффициент = 1;
	КонецЕсли;
	
	Если ТекущийКоэффициент = 0 И Коэффициент = 0 Тогда
		СтруктураДанные = ПолучитьДанныеЕдиницаИзмеренияПриИзменении(СтрокаТабличнойЧасти.ЕдиницаИзмерения, ВыбранноеЗначение);
	ИначеЕсли ТекущийКоэффициент = 0 Тогда
		СтруктураДанные = ПолучитьДанныеЕдиницаИзмеренияПриИзменении(СтрокаТабличнойЧасти.ЕдиницаИзмерения);
	ИначеЕсли Коэффициент = 0 Тогда
		СтруктураДанные = ПолучитьДанныеЕдиницаИзмеренияПриИзменении(,ВыбранноеЗначение);
	ИначеЕсли ТекущийКоэффициент = 1 И Коэффициент = 1 Тогда
		СтруктураДанные = Новый Структура("ТекущийКоэффициент, Коэффициент", 1, 1);
	КонецЕсли;	
	
	// Цена.
	Если СтруктураДанные.ТекущийКоэффициент <> 0 Тогда
		СтрокаТабличнойЧасти.Цена = СтрокаТабличнойЧасти.Цена * СтруктураДанные.Коэффициент / СтруктураДанные.ТекущийКоэффициент;
	КонецЕсли; 		
	
	РассчитатьСуммуВСтрокеТабличнойЧасти();
	
КонецПроцедуры // ЗапасыЕдиницаИзмеренияОбработкаВыбора()

&НаКлиенте
// Процедура - обработчик события ПриИзменении поля ввода Цена.
//
Процедура ЗапасыЦенаПриИзменении(Элемент)
	
	РассчитатьСуммуВСтрокеТабличнойЧасти();
	
КонецПроцедуры // ЗапасыЦенаПриИзменении()

&НаКлиенте
// Процедура - обработчик события ПриИзменении поля ввода Сумма.
//
Процедура ЗапасыСуммаПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.Запасы.ТекущиеДанные;
	
	// Цена.
	Если СтрокаТабличнойЧасти.Количество <> 0 Тогда
		СтрокаТабличнойЧасти.Цена = СтрокаТабличнойЧасти.Сумма / СтрокаТабличнойЧасти.Количество;
	КонецЕсли;
	
	// Сумма НДС.
	РассчитатьСуммуНДС(СтрокаТабличнойЧасти);
	
	// Всего.	
	СтрокаТабличнойЧасти.Всего = СтрокаТабличнойЧасти.Сумма + ?(Объект.СуммаВключаетНДС, 0, СтрокаТабличнойЧасти.СуммаНДС);
	
	ОбновитьИтоговыеСуммы();
	
КонецПроцедуры // ЗапасыСуммаПриИзменении()
 
&НаКлиенте
// Процедура - обработчик события ПриИзменении поля ввода СтавкаНДС.
//
Процедура ЗапасыСтавкаНДСПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.Запасы.ТекущиеДанные;
	
	// Сумма НДС.
	РассчитатьСуммуНДС(СтрокаТабличнойЧасти);
	
	// Всего.	
	СтрокаТабличнойЧасти.Всего = СтрокаТабличнойЧасти.Сумма + ?(Объект.СуммаВключаетНДС, 0, СтрокаТабличнойЧасти.СуммаНДС);
	
	ОбновитьИтоговыеСуммы();
	
КонецПроцедуры  // ЗапасыСтавкаНДСПриИзменении()

&НаКлиенте
// Процедура - обработчик события ПриИзменении поля ввода СтавкаНДС.
//
Процедура ЗапасыСуммаНДСПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.Запасы.ТекущиеДанные;
	
	// Всего.	
	СтрокаТабличнойЧасти.Всего = СтрокаТабличнойЧасти.Сумма + ?(Объект.СуммаВключаетНДС, 0, СтрокаТабличнойЧасти.СуммаНДС);
	
	ОбновитьИтоговыеСуммы();
	
КонецПроцедуры // ЗапасыСуммаНДСПриИзменении()

&НаКлиенте
// Процедура - обработчик события ПриИзменении поля ввода ПроцентСкидкиНаценки.
//
Процедура ЗапасыПроцентСкидкиНаценкиПриИзменении(Элемент)
	
	РассчитатьСуммуВСтрокеТабличнойЧасти();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапасыИзменениеЦеныПриИзменении(Элемент)
	ЗапасыПриИзмененииЦеныКоличества(Элементы.Запасы.ТекущиеДанные);
КонецПроцедуры

&НаКлиенте
Процедура ЗапасыИзменениеКоличестваПриИзменении(Элемент)
	ЗапасыПриИзмененииЦеныКоличества(Элементы.Запасы.ТекущиеДанные);
КонецПроцедуры

&НаКлиенте
Процедура ЗапасыПриИзмененииЦеныКоличества(ЗапасыТекущаяСтрока)
	ЗапасыТекущаяСтрока.ИзменениеСуммы = (ЗапасыТекущаяСтрока.Количество *  ЗапасыТекущаяСтрока.ИзменениеЦены ) + (ЗапасыТекущаяСтрока.ИзменениеКоличества * ЗапасыТекущаяСтрока.Цена * (1-(ЗапасыТекущаяСтрока.ПроцентСкидкиНаценки /100)));
	РассчитатьИзменениеСуммыНДС(ЗапасыТекущаяСтрока);
КонецПроцедуры

&НаКлиенте
// Процедура - обработчик события ПослеУдаления строки списка Запасы.
//
Процедура ЗапасыПослеУдаления(Элемент)
	ОбновитьИтоговыеСуммы()
КонецПроцедуры

&НаКлиенте
Процедура ЗапасыКодУКТВЭДНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	НачалоВыбораНоменклатурыГТД(Элемент, СтандартнаяОбработка);

КонецПроцедуры

&НаКлиенте
Процедура ЗапасыКодУКТВЭДОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("СправочникСсылка.НоменклатураГТД") Тогда
	
		ТекущаяСтрокаТоваров = Элементы.Запасы.ТекущиеДанные;
		Если НЕ ТекущаяСтрокаТоваров = Неопределено Тогда
			ТекущаяСтрокаТоваров.НомерГТД = ПолучитьРеквизитНоменклатурыГТДНаСервере(ВыбранноеЗначение, "НомерГТД");
		КонецЕсли;

		ВыбранноеЗначение = ПолучитьРеквизитНоменклатурыГТДНаСервере(ВыбранноеЗначение, "КодУКТВЭД");	
		
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ РЕКВИЗИТОВ ТАБЛИЧНОЙ ЧАСТИ ОС

&НаСервереБезКонтекста
// Получает набор данных с сервера для процедуры НоменклатураПриИзменении.
//
Функция ПолучитьДанныеОСПриИзменении(СтруктураДанные)
	
	СтруктураДанные.Вставить("СтавкаНДС", СтруктураДанные.Организация.СтавкаНДСПоУмолчанию);	
				
	Возврат СтруктураДанные;
	
КонецФункции // ПолучитьДанныеНоменклатураПриИзменении()

&НаКлиенте
// Процедура - обработчик события ПриИзменении поля ввода ОсновноеСредство.
//
Процедура ОСОсновноеСредствоПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.ОС.ТекущиеДанные;
	
	СтруктураДанные = Новый Структура;
	СтруктураДанные.Вставить("Организация", Компания);
	СтруктураДанные.Вставить("Номенклатура", СтрокаТабличнойЧасти.ОсновноеСредство);
		
	СтруктураДанные = ПолучитьДанныеОСПриИзменении(СтруктураДанные);
			
	СтрокаТабличнойЧасти.СтавкаНДС = СтруктураДанные.СтавкаНДС;
		
	СтруктураДанные = Новый Структура;
	СтруктураДанные.Вставить("СтавкаНДС", СтрокаТабличнойЧасти.СтавкаНДС);
	СтруктураДанные.Вставить("ИмяТабЧасти", "Запасы");
	
	СтруктураДанные = ЗаполнитьСтатьюДекларацииНДСНалоговыеОбязательства(СтруктураДанные);
	СтрокаТабличнойЧасти.СтатьяДекларацииНДСНалоговыеОбязательства = СтруктураДанные.СтатьяДекларацииНДСНалоговыеОбязательства;
	
КонецПроцедуры // ОСОсновноеСредствоПриИзменении()

&НаКлиенте
// Процедура - обработчик события ПриИзменении поля ввода Сумма.
//
Процедура ОССуммаПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.ОС.ТекущиеДанные;
		
	// Сумма НДС.
	РассчитатьСуммуНДС(СтрокаТабличнойЧасти);
	
	// Всего.	
	СтрокаТабличнойЧасти.Всего = СтрокаТабличнойЧасти.Сумма + ?(Объект.СуммаВключаетНДС, 0, СтрокаТабличнойЧасти.СуммаНДС);
	
	ОбновитьИтоговыеСуммы();
	
КонецПроцедуры // ОССуммаПриИзменении()

&НаКлиенте
// Процедура - обработчик события ПриИзменении поля ввода СтавкаНДС.
//
Процедура ОССтавкаНДСПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.ОС.ТекущиеДанные;
	
	// Сумма НДС.
	РассчитатьСуммуНДС(СтрокаТабличнойЧасти);
	
	// Всего.	
	СтрокаТабличнойЧасти.Всего = СтрокаТабличнойЧасти.Сумма + ?(Объект.СуммаВключаетНДС, 0, СтрокаТабличнойЧасти.СуммаНДС);
	                        	
	ОбновитьИтоговыеСуммы();
	
КонецПроцедуры // ОССтавкаНДСПриИзменении()

&НаКлиенте
// Процедура - обработчик события ПриИзменении поля ввода СуммаНДС.
//
Процедура ОССуммаНДСПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.ОС.ТекущиеДанные;
	
	// Всего.	
	СтрокаТабличнойЧасти.Всего = СтрокаТабличнойЧасти.Сумма + ?(Объект.СуммаВключаетНДС, 0, СтрокаТабличнойЧасти.СуммаНДС);
	
	ОбновитьИтоговыеСуммы();
	
КонецПроцедуры // ОССуммаНДСПриИзменении()

&НаКлиенте
Процедура ОСИзменениеЦеныПриИзменении(Элемент)
	ОСПриИзмененииЦеныКоличества(Элементы.ОС.ТекущиеДанные);
КонецПроцедуры

&НаКлиенте
Процедура ОСИзменениеКоличестваПриИзменении(Элемент)
	ОСПриИзмененииЦеныКоличества(Элементы.ОС.ТекущиеДанные);
КонецПроцедуры

&НаКлиенте
Процедура ОСПриИзмененииЦеныКоличества(ОСТекущаяСтрока)
	ОСТекущаяСтрока.ИзменениеСуммы = ОСТекущаяСтрока.ИзменениеЦены  + (ОСТекущаяСтрока.ИзменениеКоличества * ОСТекущаяСтрока.Сумма);
	РассчитатьИзменениеСуммыНДС(ОСТекущаяСтрока);
КонецПроцедуры

&НаКлиенте
// Процедура - обработчик события ПослеУдаления строки списка ОС.
//
Процедура ОСПослеУдаления(Элемент)
	ОбновитьИтоговыеСуммы()
КонецПроцедуры

#Область ОбработчикиБиблиотек

// СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки

&НаКлиенте
Процедура Подключаемый_ВыполнитьНазначаемуюКоманду(Команда)
	Если НЕ ДополнительныеОтчетыИОбработкиКлиент.ВыполнитьНазначаемуюКомандуНаКлиенте(ЭтаФорма, Команда.Имя) Тогда
		РезультатВыполнения = Неопределено;
		ДополнительныеОтчетыИОбработкиВыполнитьНазначаемуюКомандуНаСервере(Команда.Имя, РезультатВыполнения);
		ДополнительныеОтчетыИОбработкиКлиент.ПоказатьРезультатВыполненияКоманды(ЭтаФорма, РезультатВыполнения);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ДополнительныеОтчетыИОбработкиВыполнитьНазначаемуюКомандуНаСервере(ИмяЭлемента, РезультатВыполнения)
	ДополнительныеОтчетыИОбработки.ВыполнитьНазначаемуюКомандуНаСервере(ЭтаФорма, ИмяЭлемента, РезультатВыполнения);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки

// СтандартныеПодсистемы.Печать
&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуПечати(Команда)
	УправлениеПечатьюКлиент.ВыполнитьПодключаемуюКомандуПечати(Команда, ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.Печать

#КонецОбласти

мСписокВыбораТипПричиныНевыдачиПокупателю = Новый СписокЗначений();
мСписокВыбораТипПричиныНевыдачиПокупателю.Добавить(0, " - Податкова накладна видається покупцю");
мСписокВыбораТипПричиныНевыдачиПокупателю.Добавить(1, "01-Виписана на суму перевищення звичайної ціни над фактичною");
мСписокВыбораТипПричиныНевыдачиПокупателю.Добавить(2, "02-Постачання неплатнику податку");
мСписокВыбораТипПричиныНевыдачиПокупателю.Добавить(3, "03-Натуральна виплата в рахунок оплати праці фізичним особам");
мСписокВыбораТипПричиныНевыдачиПокупателю.Добавить(4, "04-Постачання у межах балансу для невиробничого використання");
мСписокВыбораТипПричиныНевыдачиПокупателю.Добавить(5, "05-Ліквідація основних фондів за самостійним рішенням платника податку");
мСписокВыбораТипПричиныНевыдачиПокупателю.Добавить(6, "06-Переведення основних фондів до складу невиробничих");
мСписокВыбораТипПричиныНевыдачиПокупателю.Добавить(7, "07-Експортні постачання");
мСписокВыбораТипПричиныНевыдачиПокупателю.Добавить(8, "08-Постачання для операцій, які не є об'єктом оподаткування податком 
                                                       |на додану вартість");
мСписокВыбораТипПричиныНевыдачиПокупателю.Добавить(9, "09-Постачання для операцій, які звільнені від оподаткування податком 
                                                       |на додану вартість");
мСписокВыбораТипПричиныНевыдачиПокупателю.Добавить(10, "10-Визнання умовного постачання товарних залишків та/або основних 
                                                        |фондів, що перебувають в обліку платника податку на день анулювання 
														|його реєстрації як платника податку на додану вартість, щодо яких був 
														|нарахований податковий кредит у минулих або поточному податкових періодах 
														|при анулюванні реєстрації платника податку на додану вартість");
мСписокВыбораТипПричиныНевыдачиПокупателю.Добавить(11, "11-Виписана за щоденними підсумками операцій"); 
мСписокВыбораТипПричиныНевыдачиПокупателю.Добавить(12, "12-Виписана на вартість безоплатно поставлених товарів/послуг, 
                                                        |обчислену виходячи з рівня звичайних цін"); 
мСписокВыбораТипПричиныНевыдачиПокупателю.Добавить(13, "13-Використання виробничих або невиробничих засобів, 
                                                        |інших товарів/послуг не у господарській діяльності"); 
мСписокВыбораТипПричиныНевыдачиПокупателю.Добавить(14, "14-Виписана покупцем (отримувачем) послуг від нерезидента"); 
мСписокВыбораТипПричиныНевыдачиПокупателю.Добавить(15, "15-Складена на суму перевищення ціни придбання товарів/послуг 
                                                        |над фактичною ціною їх постачання"); 
мСписокВыбораТипПричиныНевыдачиПокупателю.Добавить(16, "16-Складена на суму перевищення балансової (залишкової) вартості 
                                                        |необоротних активів над фактичною ціною їх постачання"); 

мСписокВыбораСпецРежимНалогообложения = Новый СписокЗначений();
мСписокВыбораСпецРежимНалогообложения.Добавить(0, "0   спец. режим не застосовується");
мСписокВыбораСпецРежимНалогообложения.Добавить(2, "2   сільськогосподарські підприємства, які застосовують 
													|спеціальний режим оподаткування діяльності у сфері сільського та 
													|лісового господарства, а також рибальства відповідно до статті 209 Кодексу");
мСписокВыбораСпецРежимНалогообложения.Добавить(3, "3   сільськогосподарські підприємства усіх форм власності, 
													|які відповідають критеріям, визначеним статтею 209 Кодексу ,
													|але які не обрали спеціальний режим оподаткування 
													|діяльності у сфері сільського, лісового господарства та рибальства 
													|та реалізують молоко, худобу, птицю, вовну власного виробництва, а 
													|також молочні продукти, молочну сировину та м'ясопродукти, 
													|вироблені у власних переробних цехах");
мСписокВыбораСпецРежимНалогообложения.Добавить(4, "4 (до 01.01.2015) переробні підприємства усіх форм власності, які згідно з 
													|пунктом 1 підрозділу 2 розділу XX Кодексу за 
													|реалізовані ними молоко, молочну сировину та молочні продукти, 
													|м'ясо та м'ясопродукти, іншу продукцію переробних тварин, 
													|закуплених у живій вазі (шкури, субпродукти, м'ясо-кісткове 
													|борошно), у повному обсязі спрямовують суми податку на додану 
													|вартість до спеціального фонду державного бюджету");