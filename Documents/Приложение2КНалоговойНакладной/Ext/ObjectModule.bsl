
///////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ ЗАПОЛНЕНИЯ ДОКУМЕНТА

// Процедура заполняет реквизиты шапки  документа 
// Параметры: 
//  ДанныеЗаполнения - Структура - Данные заполнения документа
//
Процедура ЗаполнитьШапкуДокументаПоОснованию(ДанныеЗаполнения)
	
	Автор = ДанныеЗаполнения.Автор;
	АвторасчетНДС = ДанныеЗаполнения.АвторасчетНДС;
	ВалютаДокумента = ДанныеЗаполнения.ВалютаДокумента;
	ВключаетсяВУточняющийРасчет = ДанныеЗаполнения.ВключаетсяВУточняющийРасчет;
	Договор = ДанныеЗаполнения.Договор;
	Комментарий = ДанныеЗаполнения.Комментарий;
	Контрагент = ДанныеЗаполнения.Контрагент;
	Кратность = ДанныеЗаполнения.Кратность;
	КтоВыписалНалоговуюНакладную = ДанныеЗаполнения.КтоВыписалНалоговуюНакладную;
	Курс = ДанныеЗаполнения.Курс;
	Организация = ДанныеЗаполнения.Организация;
	НалоговаяНакладная = ДанныеЗаполнения.Ссылка;
	СуммаВключаетНДС = ДанныеЗаполнения.СуммаВключаетНДС;
	СуммаДокумента = ДанныеЗаполнения.СуммаДокумента;
	СуммаНДСДокумента = ДанныеЗаполнения.СуммаНДСДокумента;
	ВидЦен = ДанныеЗаполнения.ВидЦен;
	НалогообложениеНДС = ДанныеЗаполнения.НалогообложениеНДС;
	УсловиеПродажи = ДанныеЗаполнения.УсловиеПродажи;
	ФормаРасчетов = ДанныеЗаполнения.ФормаРасчетов;
	АвторасчетНДС = ДанныеЗаполнения.АвторасчетНДС;
	ТребуетВключенияВЕдиныйРеестрНалоговыхНакладных = ДанныеЗаполнения.ТребуетВключенияВЕдиныйРеестрНалоговыхНакладных;
	ВидДоговора = ДанныеЗаполнения.ВидДоговора;
	
КонецПроцедуры // ЗаполнитьШапкуДокументаПоОснованию()

// Процедура заполняет вид операции 
// Параметры: 
//  ДанныеЗаполнения - Структура - Данные заполнения документа
//
Процедура ЗаполнитьВидОперации(ДанныеЗаполнения)
	
	// определим вид операции если обработка формирования не запускается
	Если ДанныеЗаполнения.ВидОперации = Перечисления.ВидыОперацийНалоговаяНакладная.ИтоговаяРозницаОблагаемыеОперации Тогда 
		
		ВидОперации = Перечисления.ВидыОперацийПриложение2КНалоговойНакладной.ИтоговаяРозницаОблагаемыеОперацииВозврат;
		ЗапускатьОбработку = Ложь;
		
	ИначеЕсли ДанныеЗаполнения.ВидОперации = Перечисления.ВидыОперацийНалоговаяНакладная.ИтоговаяРозницаОсвобожденныеОперации Тогда	
		
		ВидОперации = Перечисления.ВидыОперацийПриложение2КНалоговойНакладной.ИтоговаяРозницаОсвобожденныеОперацииВозврат;
		ЗапускатьОбработку = Ложь;
		
	ИначеЕсли ДанныеЗаполнения.ВидОперации = Перечисления.ВидыОперацийНалоговаяНакладная.РозницаКонрагентуОблагаемыеОперации Тогда	
		
		ВидОперации = Перечисления.ВидыОперацийПриложение2КНалоговойНакладной.РозницаКонрагентуОблагаемыеОперацииВозврат;
		ЗапускатьОбработку = Ложь;
		
	ИначеЕсли ДанныеЗаполнения.ВидОперации = Перечисления.ВидыОперацийНалоговаяНакладная.РозницаКонрагентуОсвобожденныеОперации Тогда	
		
		ВидОперации = Перечисления.ВидыОперацийПриложение2КНалоговойНакладной.РозницаКонрагентуОсвобожденныеОперацииВозврат;
		ЗапускатьОбработку = Ложь;
		
	ИначеЕсли ДанныеЗаполнения.ВидОперации = Перечисления.ВидыОперацийНалоговаяНакладная.УсловнаяПродажа Тогда
		
		ВидОперации = Перечисления.ВидыОперацийПриложение2КНалоговойНакладной.ОблагаемыеОперацииКорректировка;	
		ЗапускатьОбработку = Ложь;
		
	ИначеЕсли ДанныеЗаполнения.ВидОперации = Перечисления.ВидыОперацийНалоговаяНакладная.УсловнаяПродажаСписаниеОС Тогда
		
		ВидОперации = Перечисления.ВидыОперацийПриложение2КНалоговойНакладной.ОблагаемыеОперацииКорректировка;	
		ЗапускатьОбработку = Ложь;
		
	ИначеЕсли ДанныеЗаполнения.ВидОперации = Перечисления.ВидыОперацийНалоговаяНакладная.РаботыОтНерезидента Тогда
		
		ВидОперации = Перечисления.ВидыОперацийПриложение2КНалоговойНакладной.РаботыОтНерезидентаКорректировка;	
		ЗапускатьОбработку = Ложь;
		
	ИначеЕсли ДанныеЗаполнения.ВидОперации = Перечисления.ВидыОперацийНалоговаяНакладная.СводнаяНаПревышениеБазыНадЦенойПоставки Тогда
		
		ВидОперации = Перечисления.ВидыОперацийПриложение2КНалоговойНакладной.СводнаяНаПревышениеБазыНадЦенойПоставки;	
		ЗапускатьОбработку = Ложь;
		
	КонецЕсли;
		
КонецПроцедуры // ЗаполнитьВидОперации()

Процедура УстановитьТипПричиныНевыдачиПокупателюПоУмолчанию() Экспорт

	ДатаВступленияВСилуПриказа1379 = Константы.ДатаВступленияВСилуПриказа1379.Получить();
	
	Если ВидОперации = Перечисления.ВидыОперацийПриложение2КНалоговойНакладной.РаботыОтНерезидентаКорректировка Тогда
	
		Если Дата < ДатаВступленияВСилуПриказа1379 Тогда
		    ТипПричиныНевыдачиПокупателю = 2;
		Иначе	
			ТипПричиныНевыдачиПокупателю = 14;
		КонецЕсли;
		
	ИначеЕсли ВидОперации = Перечисления.ВидыОперацийПриложение2КНалоговойНакладной.СводнаяНаПревышениеБазыНадЦенойПоставки Тогда
		
		ТипПричиныНевыдачиПокупателю = 15;
		
	ИначеЕсли  НЕ ВалютаДокумента = Константы.ВалютаУчета.Получить() 
		И НЕ ВидОперации = Перечисления.ВидыОперацийПриложение2КНалоговойНакладной.РаботыОтНерезидентаКорректировка Тогда
		
		ТипПричиныНевыдачиПокупателю = 7;
		
	ИначеЕсли ВидОперации = Перечисления.ВидыОперацийПриложение2КНалоговойНакладной.ИтоговаяРозницаОблагаемыеОперацииВозврат 
		  ИЛИ ВидОперации = Перечисления.ВидыОперацийПриложение2КНалоговойНакладной.ИтоговаяРозницаОсвобожденныеОперацииВозврат Тогда
		
	    ТипПричиныНевыдачиПокупателю = 11;
		
	ИначеЕсли НЕ ЗначениеЗаполнено(Контрагент.ИНН)
			ИЛИ Найти("0123456789",Лев(СокрЛ(Контрагент.ИНН),1)) = 0 Тогда		
		
		ТипПричиныНевыдачиПокупателю = 2;
		
	Иначе	
		
		ТипПричиныНевыдачиПокупателю = 0;
		
	КонецЕсли;
	
	Если ТипПричиныНевыдачиПокупателю > 0 Тогда
		ТребуетВключенияВЕдиныйРеестрНалоговыхНакладных = Ложь;
	КонецЕсли;
	
КонецПроцедуры

// Процедура заполняет условие продажи
// Параметры: 
//  ДанныеЗаполнения - Структура - Данные заполнения документа
//
Процедура ЗаполнитьУсловиеПродажи() Экспорт 
	
	Если  ВидОперации <> Перечисления.ВидыОперацийПриложение2КНалоговойНакладной.ИтоговаяРозницаОблагаемыеОперацииВозврат
		И ВидОперации <> Перечисления.ВидыОперацийПриложение2КНалоговойНакладной.ИтоговаяРозницаОсвобожденныеОперацииВозврат
		И ВидОперации <> Перечисления.ВидыОперацийПриложение2КНалоговойНакладной.РозницаКонрагентуОблагаемыеОперацииВозврат
		И ВидОперации <> Перечисления.ВидыОперацийПриложение2КНалоговойНакладной.РозницаКонрагентуОсвобожденныеОперацииВозврат Тогда

		УсловиеПродажи 	= НалоговаяНакладная.УсловиеПродажи;
		ФормаРасчетов 	= НалоговаяНакладная.ФормаРасчетов;
		
	Иначе
		
		УсловиеПродажи =  НалоговаяНакладная.УсловиеПродажи;
		ФормаРасчетов  = "Касовий чек";
		
	КонецЕсли;
		
КонецПроцедуры // ЗаполнитьУсловиеПродажи

////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

// Процедура - обработчик события ОбработкаЗаполнения.
//
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка) Экспорт
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.НалоговаяНакладная") Тогда
		// Заполнение шапки
		
		ЗаполнитьШапкуДокументаПоОснованию(ДанныеЗаполнения);
		
		ЗаполнитьВидОперации(ДанныеЗаполнения);
			
		Запасы.Очистить();
		ОС.Очистить();
		Для Каждого ТекСтрокаЗапасы Из ДанныеЗаполнения.Запасы Цикл
			НоваяСтрока = Запасы.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекСтрокаЗапасы);
			НоваяСтрока.ДатаКорректировки = ТекущаяДата();
		КонецЦикла;
		
		Для Каждого ТекСтрокаОС Из ДанныеЗаполнения.ОС Цикл
			НоваяСтрока = ОС.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекСтрокаОС);
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры // ОбработкаЗаполнения()

// Процедура - обработчик события ПередЗаписью объекта.
//
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	СуммаДокумента = Запасы.Итог("Всего") + ОС.Итог("Всего");
	
	// поставим флаг "Требует регистрация в реестре"
	Если Дата >= '2015-01-01' Тогда
		ТребуетВключенияВЕдиныйРеестрНалоговыхНакладных = Истина;
		ЭлектронныйДокумент = Истина;
	ИначеЕсли НалоговаяНакладная.ТребуетВключенияВЕдиныйРеестрНалоговыхНакладных Тогда
		
		ТребуетВключенияВЕдиныйРеестрНалоговыхНакладных = Истина;
		
	ИначеЕсли ЭлектронныйДокумент Тогда
		
		ТребуетВключенияВЕдиныйРеестрНалоговыхНакладных = Истина;
	
	Иначе
		
		Если ТипПричиныНевыдачиПокупателю > 0 Тогда
			// мы не управляем флажком ТребуетВключенияВЕдиныйРеестрНалоговыхНакладных
			// для налоговых накладных, которые не выдаются покупателю
			// согласно разъяснению ЕБНЗ такие налоговые не должны регистрироваться в Едином реестре
			
		Иначе	
			
			ТребуетВключенияВЕдиныйРеестрНалоговыхНакладных = Ложь;
			
			ВалютаУчета = Константы.ВалютаУчета.Получить();
			СтруктураКурсовВалют = УправлениеНебольшойФирмойСервер.ПолучитьКурсыВалют(ВалютаДокумента, ВалютаУчета, Дата);
			
			Если ВалютаДокумента = ВалютаУчета Тогда
				НДСРегл = СуммаНДСДокумента;	
			Иначе 
				НДСРегл = УправлениеНебольшойФирмойСервер.ПересчитатьИзВалютыВВалюту(СуммаНДСДокумента,
			                                                           СтруктураКурсовВалют.КурсНач,
			                                                           СтруктураКурсовВалют.Курс,
			                                                           СтруктураКурсовВалют.КратностьНач,
			                                                           СтруктураКурсовВалют.Кратность);
			КонецЕсли;
			
			Если Дата >= '20120101' Тогда
				// или сумма НДС в документе больше 10 000 грн
				Если НДСРегл > 10000 Тогда
					ТребуетВключенияВЕдиныйРеестрНалоговыхНакладных = Истина;
					
				// или имеются подакцизные/импортированные товары
				// этот факт определим так - если в строке указан код УКТЗЭД - считаем что условие выполняется.
				ИначеЕсли   Запасы.НайтиСтроки(Новый Структура("КодУКТВЭД", Справочники.КлассификаторУКТВЭД.ПустаяСсылка())).Количество() 	<> Запасы.Количество()
						ИЛИ ОС.    НайтиСтроки(Новый Структура("КодУКТВЭД", Справочники.КлассификаторУКТВЭД.ПустаяСсылка())).Количество() 	<> ОС.Количество() Тогда
					ТребуетВключенияВЕдиныйРеестрНалоговыхНакладных = Истина;
				КонецЕсли;
			ИначеЕсли Дата >= '20110701' Тогда
				// сумма НДС в документе больше 100 000 грн
				Если НДСРегл > 100000 Тогда
					ТребуетВключенияВЕдиныйРеестрНалоговыхНакладных = Истина;
				КонецЕсли;
			ИначеЕсли Дата >= '20110401' Тогда
				// сумма НДС в документе больше 500 000 грн
				Если НДСРегл > 500000 Тогда
					ТребуетВключенияВЕдиныйРеестрНалоговыхНакладных = Истина;
				КонецЕсли;
			ИначеЕсли Дата >= '20110101' Тогда
				// сумма НДС в документе больше 1 000 000 грн
				Если НДСРегл > 1000000 Тогда
					ТребуетВключенияВЕдиныйРеестрНалоговыхНакладных = Истина;
				КонецЕсли;
			КонецЕсли;
			
			Если ТребуетВключенияВЕдиныйРеестрНалоговыхНакладных = Истина
			   И НалоговаяНакладная.ВключенаВЕдиныйРеестрНалоговыхНакладных = Ложь Тогда
			   
			   Сообщить(СтрШаблон(НСтр("ru = 'Перед выгрузкой документа: ¤1¤ в Единый реестр налоговых накладных
                                                    |необходимо также зарегистрировать исходную налоговую накладную: ¤2¤!'; uk = 'Перед вивантаженням документа: ¤1¤ до Єдиного реєстра податкових накладних
                                                    |необхідно також зареєструвати в ньому податкову накладну: ¤2¤!'"), Ссылка, НалоговаяНакладная), СтатусСообщения.Важное);
			   
			КонецЕсли;
			
			
		КонецЕсли;
		
	КонецЕсли;
	
	// еще не приняли новую форму, для НН с НДС 7% нужно в спец. режиме указывать особое занчение (7)
	Если УчетНДССервер.ВДокументеЕстьСтавкаНДС7(ЭтотОбъект) Тогда
		СпецРежимНалогообложения = 7;
	Иначе	
		Если СпецРежимНалогообложения = 7 Тогда
			СпецРежимНалогообложения = 0;
		КонецЕсли;
	КонецЕсли;
	
	Если Дата >= '2015-01-01' Тогда
		Если  НалоговаяНакладная.Дата >= '2015-01-01'
			И ТипПричиныНевыдачиПокупателю = 0
			И СуммаНДСДокумента < 0 Тогда
			РегистрируетсяВЕРННПокупателем = Истина;
		Иначе
			РегистрируетсяВЕРННПокупателем = Ложь;
		КонецЕсли;			
	Иначе
		РегистрируетсяВЕРННПокупателем = Ложь;
	КонецЕсли;
	
КонецПроцедуры // ПередЗаписью()

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если УчетНДССервер.ВДокументеЕстьСтавкаНДС7ИДругиеСтавки(ЭтотОбъект) Тогда
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

