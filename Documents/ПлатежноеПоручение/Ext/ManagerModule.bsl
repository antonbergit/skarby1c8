#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ИнтерфейсПечати

// Процедура формирует и выводит печатную форму документа по указанному макету.
//
Функция ПечатнаяФорма(МассивОбъектов, ОбъектыПечати, КоличествоЭкземпляров = 1) Экспорт
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.КлючПараметровПечати = "ПараметрыПечати_ПлатежноеПоручение";
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПлатежноеПоручение.Ссылка КАК Ссылка,
	|	ПлатежноеПоручение.Номер КАК Номер,
	|	ПлатежноеПоручение.Дата КАК ДатаДокумента,
	|	ПлатежноеПоручение.НомерПоручения КАК НомерПП,
	|	ПлатежноеПоручение.ВидПлатежа КАК ВидПлатежа,
	|	ПлатежноеПоручение.ИННПлательщика КАК ИННПлательщика,
	|	ПлатежноеПоручение.ИННПолучателя КАК ИННПолучателя,
	|	ПлатежноеПоручение.КодБК КАК КодБК,
	|	ПлатежноеПоручение.КодКОАТУУ КАК КодКОАТУУ,
	|	ПлатежноеПоручение.ЕДРПОУПлательщика КАК КодПоЕДРПОУ,
	|	ПлатежноеПоручение.ЕДРПОУПолучателя КАК ПолучательЕДРПОУ,
	|	ПлатежноеПоручение.НазначениеПлатежа КАК НазначениеПлатежа,
	|	ПлатежноеПоручение.ВидПеречисленияВБюджет КАК ВидПеречисленияВБюджет,
	|	ПлатежноеПоручение.ОчередностьПлатежа КАК Очередность,
	|	ПлатежноеПоручение.ПоказательДаты КАК ПоказательДаты,
	|	ПлатежноеПоручение.ПоказательНомера КАК ПоказательНомера,
	|	ПлатежноеПоручение.ПоказательОснования КАК ПоказательОснования,
	|	ПлатежноеПоручение.ПоказательПериода КАК ПоказательПериода,
	|	ПлатежноеПоручение.ПоказательТипа КАК ПоказательТипа,
	|	ПлатежноеПоручение.СуммаДокумента КАК СуммаДокумента,
	|	ПлатежноеПоручение.СчетКонтрагента.НомерСчета КАК НомерСчетаПолучателя,
	|	ПРЕДСТАВЛЕНИЕ(ПлатежноеПоручение.СчетКонтрагента.Банк) КАК БанкПолучателя,
	|	ПлатежноеПоручение.СчетКонтрагента.Банк.Код КАК КодБанкаПолучателя,
	|	ПРЕДСТАВЛЕНИЕ(ПлатежноеПоручение.СчетКонтрагента.БанкРасчетов) КАК НаименованиеБанкаРасчетовПолучателя,
	|	ПлатежноеПоручение.СчетКонтрагента.Банк.Город КАК ГородБанкаПолучателя,
	|	ПлатежноеПоручение.СчетКонтрагента.БанкРасчетов.Город КАК ГородБанкаРасчетовПолучателя,
	|	ПлатежноеПоручение.СчетКонтрагента.Банк.КоррСчет КАК СчетБанкаПолучателя,
	|	ПлатежноеПоручение.БанковскийСчет.СуммаБезКопеек КАК СуммаБезКопеек,
	|	ПлатежноеПоручение.БанковскийСчет.ВалютаДенежныхСредств КАК ВалютаДенежныхСредств,
	|	ПлатежноеПоручение.БанковскийСчет.НомерСчета КАК НомерСчетаПлательщика,
	|	ПРЕДСТАВЛЕНИЕ(ПлатежноеПоручение.БанковскийСчет.Банк) КАК БанкПлательщика,
	|	ПРЕДСТАВЛЕНИЕ(ПлатежноеПоручение.БанковскийСчет.БанкРасчетов) КАК НаименованиеБанкаРасчетовПлательщика,
	|	ПлатежноеПоручение.БанковскийСчет.Банк.Город КАК ГородБанкаПлательщика,
	|	ПлатежноеПоручение.БанковскийСчет.БанкРасчетов.Город КАК ГородБанкаРасчетовПлательщика,
	|	ПлатежноеПоручение.БанковскийСчет.Банк.Код КАК КодБанкаПлательщика,
	|	ПлатежноеПоручение.ТекстПлательщика КАК Плательщик,
	|	ПлатежноеПоручение.ТекстПолучателя КАК Получатель,
	|	ПлатежноеПоручение.БанковскийСчет.ВариантВыводаМесяца КАК ВариантВыводаМесяца,
	|	ПлатежноеПоручение.ВидОперации КАК ВидОперации,
	|	ПлатежноеПоручение.Организация.Префикс КАК Префикс
	|ИЗ
	|	Документ.ПлатежноеПоручение КАК ПлатежноеПоручение
	|ГДЕ
	|	ПлатежноеПоручение.Ссылка В(&МассивОбъектов)";
	
	ВыборкаДляПечати = Запрос.Выполнить().Выбрать();
	
	ПервыйДокумент = Истина;
	
	Пока ВыборкаДляПечати.Следующий() Цикл
		
		Если НЕ ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
	
		ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ПлатежноеПоручение_ПлатежноеПоручение";
		
		
		Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ПлатежноеПоручение.ПФ_MXL_ПлатежноеПоручение");
		
		КодЯзыкаПечать = "uk";	
		Макет.КодЯзыкаМакета = КодЯзыкаПечать;
		
		ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
		
		ОбластьМакета.Параметры.Заполнить(ВыборкаДляПечати);
				
		ЧислоДокумента                          = Формат(ВыборкаДляПечати.ДатаДокумента,"Л="+КодЯзыкаПечать+"; ДФ='дд'");
		ДатаДокумента                           = Формат(ВыборкаДляПечати.ДатаДокумента,"Л="+КодЯзыкаПечать+"; ДФ='дд ММММ ггггр.'");
		ДатаДокументаБезЧисла                   = Прав(ДатаДокумента, СтрДлина(ДатаДокумента) - 2);
		ОбластьМакета.Параметры.ДатаДокумента          	= """" + ЧислоДокумента + """" + ДатаДокументаБезЧисла;
		
		МесяцПрописью = ВыборкаДляПечати.ВариантВыводаМесяца = Перечисления.ВариантыВыводаМесяцаВДатеДокумента.Прописью;
		ФорматДаты    = "Л="+КодЯзыкаПечать+"; ДФ=" + ?(МесяцПрописью, "'дд ММММ гггг'", "'дд.ММ.гггг'");
        		
		ОбластьМакета.Параметры.СуммаПлатежа = УправлениеНебольшойФирмойСервер.ФорматироватьСуммуПлатежногоДокумента(
			ВыборкаДляПечати.СуммаДокумента,
			ВыборкаДляПечати.СуммаБезКопеек
		);
		
		ОбластьМакета.Параметры.СуммаПрописью = УправлениеНебольшойФирмойСервер.ФорматироватьСуммуПрописьюПлатежногоДокумента(
			ВыборкаДляПечати.СуммаДокумента,
			ВыборкаДляПечати.ВалютаДенежныхСредств,
			ВыборкаДляПечати.СуммаБезКопеек,
			КодЯзыкаПечать
		);
			
		
		Для НомерЭкземпляра = 1 по КоличествоЭкземпляров Цикл
			ОбластьМакета.Параметры.НомерЭкземпляра = НомерЭкземпляра;
		
			ТабличныйДокумент.Вывести(ОбластьМакета);
			Если  НЕ ТабличныйДокумент.ПроверитьВывод(ОбластьМакета)  Тогда
				ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			КонецЕсли;
		КонецЦикла;	
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, ВыборкаДляПечати.Ссылка);
		
	КонецЦикла; 
	
	ТабличныйДокумент.АвтоМасштаб = Истина;
	
	Возврат ТабличныйДокумент;

КонецФункции // ПечатнаяФорма()

// Процедура формирует и выводит печатную форму документа по указанному макету.
//
Функция ПечатнаяФормаВВалюте(МассивОбъектов, ОбъектыПечати, КоличествоЭкземпляров = 1) Экспорт
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.КлючПараметровПечати = "ПараметрыПечати_ПлатежноеПоручение";
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПлатежноеПоручение.Ссылка КАК Ссылка,
	|	ПлатежноеПоручение.Номер КАК Номер,
	|	ПлатежноеПоручение.НомерПоручения КАК НомерПП,
	|	ПлатежноеПоручение.ДатаПоручения КАК ДатаДокумента,
	|	ПлатежноеПоручение.ВидПлатежа КАК ВидПлатежа,
	|	ПлатежноеПоручение.ИННПлательщика КАК ИННПлательщика,
	|	ПлатежноеПоручение.ИННПолучателя КАК ИННПолучателя,
	|	ПлатежноеПоручение.КодКОАТУУ КАК КодКОАТУУ,
	|	ПлатежноеПоручение.ЕДРПОУПлательщика КАК ЕДРПОУПлательщика,
	|	ПлатежноеПоручение.ЕДРПОУПолучателя КАК ЕДРПОУПолучателя,
	|	ПлатежноеПоручение.НазначениеПлатежа КАК НазначениеПлатежа,
	|	ПлатежноеПоручение.ВидПеречисленияВБюджет КАК ВидПеречисленияВБюджет,
	|	ПлатежноеПоручение.ОчередностьПлатежа КАК Очередность,
	|	ПлатежноеПоручение.ПоказательДаты КАК ПоказательДаты,
	|	ПлатежноеПоручение.ПоказательНомера КАК ПоказательНомера,
	|	ПлатежноеПоручение.ПоказательОснования КАК ПоказательОснования,
	|	ПлатежноеПоручение.ПоказательПериода КАК ПоказательПериода,
	|	ПлатежноеПоручение.ПоказательТипа КАК ПоказательТипа,
	|	ПлатежноеПоручение.СуммаДокумента КАК СуммаДокумента,
	|	ПлатежноеПоручение.СчетКонтрагента.НомерСчета КАК НомерСчетаПолучателя,
	|	ПРЕДСТАВЛЕНИЕ(ПлатежноеПоручение.СчетКонтрагента.Банк) КАК БанкПолучателя,
	|	ПРЕДСТАВЛЕНИЕ(ПлатежноеПоручение.СчетКонтрагента.БанкРасчетов) КАК НаименованиеБанкаРасчетовПолучателя,
	|	ПлатежноеПоручение.СчетКонтрагента.Банк.Город КАК ГородБанкаПолучателя,
	|	ПлатежноеПоручение.СчетКонтрагента.БанкРасчетов.Город КАК ГородБанкаРасчетовПолучателя,
	|	ПлатежноеПоручение.СчетКонтрагента.Банк.Код КАК МФОБанкаПолучателя,
	|	ПлатежноеПоручение.СчетКонтрагента.Банк.КоррСчет КАК СчетБанкаПолучателя,
	|	ПлатежноеПоручение.БанковскийСчет.СуммаБезКопеек КАК СуммаБезКопеек,
	|	ПлатежноеПоручение.БанковскийСчет.ВалютаДенежныхСредств КАК ВалютаДенежныхСредств,
	|	ПлатежноеПоручение.БанковскийСчет.НомерСчета КАК НомерСчетаПлательщика,
	|	ПРЕДСТАВЛЕНИЕ(ПлатежноеПоручение.БанковскийСчет.Банк) КАК БанкПлательщика,
	|	ПРЕДСТАВЛЕНИЕ(ПлатежноеПоручение.БанковскийСчет.БанкРасчетов) КАК НаименованиеБанкаРасчетовПлательщика,
	|	ПлатежноеПоручение.БанковскийСчет.Банк.Город КАК ГородБанкаПлательщика,
	|	ПлатежноеПоручение.БанковскийСчет.БанкРасчетов.Город КАК ГородБанкаРасчетовПлательщика,
	|	ПлатежноеПоручение.БанковскийСчет.Банк.Код КАК МФОБанкаПлательщика,
	|	ПлатежноеПоручение.БанковскийСчет.Банк.КоррСчет КАК СчетБанкаПлательщика,
	|	ПлатежноеПоручение.ТекстПлательщика КАК ТекстПлательщика,
	|	ПлатежноеПоручение.ТекстПолучателя КАК ТекстПолучателя,
	|	ПлатежноеПоручение.БанковскийСчет.ВариантВыводаМесяца КАК ВариантВыводаМесяца,
	|	ПлатежноеПоручение.ВидОперации КАК ВидОперации,
	|	ПлатежноеПоручение.БанковскийСчет.Банк.Адрес КАК АдресБанкаПлательщика,
	|	ПлатежноеПоручение.СчетКонтрагента.Банк.Адрес КАК АдресБанкаПолучателя,
	|	ПлатежноеПоручение.Организация.НаименованиеПолное КАК Плательщик,
	|	ПлатежноеПоручение.Организация,
	|	ПлатежноеПоручение.БанковскийСчет.ВалютаДенежныхСредств.Код КАК КодВалюты,
	|	ПлатежноеПоручение.Контрагент.НеЯвляетсяРезидентом КАК НеЯвляетсяРезидентом,
	|	ПлатежноеПоручение.Контрагент.Представление КАК Получатель,
	|	ПлатежноеПоручение.Контрагент,
	|	ПлатежноеПоручение.СчетКонтрагента,
	|	ПлатежноеПоручение.БанковскийСчет КАК СчетПлательщика,
	|	ПлатежноеПоручение.Организация.Префикс КАК Префикс
	|ИЗ
	|	Документ.ПлатежноеПоручение КАК ПлатежноеПоручение
	|ГДЕ
	|	ПлатежноеПоручение.Ссылка В(&МассивОбъектов)";
	
	ВыборкаДляПечати = Запрос.Выполнить().Выбрать();
	
	ПервыйДокумент = Истина;
	
	Пока ВыборкаДляПечати.Следующий() Цикл
		
		Если НЕ ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
	
		ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ПлатежноеПоручение_ПлатежноеПоручениеВВалюте";
		
		Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ПлатежноеПоручение.ПФ_MXL_ПлатежноеПоручениеВВалюте");
		
		КодЯзыкаПечать = Локализация.ПолучитьЯзыкФормированияПечатныхФорм();	
		Макет.КодЯзыкаМакета = КодЯзыкаПечать;
					
		ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
		ОбластьМакета.Параметры.Заполнить(ВыборкаДляПечати);
		
		ОбластьМакета.Параметры.РезидентПлательщик      =	Истина;	
		
		СведенияОбОрганизации = УправлениеНебольшойФирмойСервер.СведенияОЮрФизЛице(ВыборкаДляПечати.Организация, ВыборкаДляПечати.ДатаДокумента, , ВыборкаДляПечати.СчетПлательщика, КодЯзыкаПечать);		
		
		ОбластьМакета.Параметры.Плательщик          = ?(ПустаяСтрока(ВыборкаДляПечати.ТекстПлательщика),ВыборкаДляПечати.Плательщик,СокрЛП(ВыборкаДляПечати.ТекстПлательщика));
		ОбластьМакета.Параметры.АдресПлательщика	= УправлениеНебольшойФирмойСервер.ОписаниеОрганизации(СведенияОбОрганизации, "ЮридическийАдрес,");
						
		ОбластьМакета.Параметры.СуммаПлатежа = УправлениеНебольшойФирмойСервер.ФорматироватьСуммуПлатежногоДокумента(
			ВыборкаДляПечати.СуммаДокумента,
			ВыборкаДляПечати.СуммаБезКопеек
		);
																										  
		ОбластьМакета.Параметры.СуммаПрописью = УправлениеНебольшойФирмойСервер.ФорматироватьСуммуПрописьюПлатежногоДокумента(
			ВыборкаДляПечати.СуммаДокумента,
			ВыборкаДляПечати.ВалютаДенежныхСредств,
			ВыборкаДляПечати.СуммаБезКопеек,
			КодЯзыкаПечать
		);
		
		ОбластьМакета.Параметры.РезидентПолучатель	= ВыборкаДляПечати.НеЯвляетсяРезидентом;
		
		СведенияОПолучателе = УправлениеНебольшойФирмойСервер.СведенияОЮрФизЛице(ВыборкаДляПечати.Контрагент, ВыборкаДляПечати.ДатаДокумента, , ВыборкаДляПечати.СчетКонтрагента, КодЯзыкаПечать);
		
		ОбластьМакета.Параметры.АдресПолучателя = СведенияОПолучателе.ЮридическийАдрес;
				
		ОбластьМакета.Параметры.НазначениеПлатежа       = ВыборкаДляПечати.НазначениеПлатежа;
		ОбластьМакета.Параметры.КодПоЕДРПОУ 			  = ВыборкаДляПечати.ЕДРПОУПлательщика;
						
		Для НомерЭкземпляра = 1 по КоличествоЭкземпляров Цикл
			ОбластьМакета.Параметры.НомерЭкземпляра = НомерЭкземпляра;
			ТабличныйДокумент.Вывести(ОбластьМакета);
			Если НЕ ТабличныйДокумент.ПроверитьВывод(ОбластьМакета) Тогда
				ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			КонецЕсли;
		КонецЦикла;	
				
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, ВыборкаДляПечати.Ссылка);
				
	КонецЦикла; 
	
	ТабличныйДокумент.АвтоМасштаб = Истина;
	
	Возврат ТабличныйДокумент;

КонецФункции // ПечатнаяФормаВВалюте()

// Сформировать печатные формы объектов
//
// ВХОДЯЩИЕ:
//   ИменаМакетов    - Строка    - Имена макетов, перечисленные через запятую
//   МассивОбъектов  - Массив    - Массив ссылок на объекты которые нужно распечатать
//   ПараметрыПечати - Структура - Структура дополнительных параметров печати
//
// ИСХОДЯЩИЕ:
//   КоллекцияПечатныхФорм - Таблица значений - Сформированные табличные документы
//   ПараметрыВывода       - Структура        - Параметры сформированных табличных документов
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода, КоличествоЭкземпляров = 2) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПлатежноеПоручение") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			"ПлатежноеПоручение",
			НСтр("ru='Платежное поручение';uk='Платіжне доручення'"),
			ПечатнаяФорма(МассивОбъектов, ОбъектыПечати, КоличествоЭкземпляров)
		);
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПлатежноеПоручениеВВалюте") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм, 
			"ПлатежноеПоручениеВВалюте", 
			НСтр("ru='Платежное поручение в валюте';uk='Платіжне доручення у валюті'"), 
			ПечатнаяФормаВВалюте(МассивОбъектов, ОбъектыПечати, КоличествоЭкземпляров)
		);
	КонецЕсли;
	
	// параметры отправки печатных форм по электронной почте
	УправлениеНебольшойФирмойСервер.ЗаполнитьПараметрыОтправки(ПараметрыВывода.ПараметрыОтправки, МассивОбъектов, КоллекцияПечатныхФорм);
	
КонецПроцедуры

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ПлатежноеПоручение";
	КомандаПечати.Представление = НСтр("ru='Платежное поручение';uk='Платіжне доручення'");
	КомандаПечати.СписокФорм = "ФормаДокумента,ФормаСписка";
	КомандаПечати.ПроверкаПроведенияПередПечатью = Ложь;
	КомандаПечати.Порядок = 1;
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ПлатежноеПоручениеВВалюте";
	КомандаПечати.Представление = НСтр("ru='Платежное поручение в валюте';uk='Платіжне доручення у валюті'");
	КомандаПечати.СписокФорм = "ФормаДокумента,ФормаСписка";
	КомандаПечати.ПроверкаПроведенияПередПечатью = Ложь;
	КомандаПечати.ПереопределитьПользовательскиеНастройкиКоличества = Истина;
	КомандаПечати.Порядок = 4;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
