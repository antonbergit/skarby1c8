&НаКлиенте
Перем СтрокаПоискаПоКБК;

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("СозданПоКомандеИзФормыСписка") Тогда
		СозданПоКомандеИзФормыСписка = Параметры.СозданПоКомандеИзФормыСписка;
	Иначе
		СозданПоКомандеИзФормыСписка = Ложь;
	КонецЕсли;
	
	ДатаДокумента = Объект.Дата;
	Если НЕ ЗначениеЗаполнено(ДатаДокумента) Тогда
		ДатаДокумента = ТекущаяДата();
	КонецЕсли;
	
	Компания = УправлениеНебольшойФирмойСервер.ПолучитьОрганизацию(Объект.Организация);
	ЭтоФизЛицо = Объект.Организация.ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо;
	БезНалогаНДС = Объект.СтавкаНДС.НеОблагается;
	
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка)
	   И НЕ ЗначениеЗаполнено(Параметры.ЗначениеКопирования) Тогда
		Если НЕ ЗначениеЗаполнено(Объект.ДатаПоручения) Тогда
			Объект.ДатаПоручения = ТекущаяДата();
			ЭтаФорма.ДатаПлатежа = Объект.ДатаПоручения;
		КонецЕсли;
		Если НЕ ЗначениеЗаполнено(Объект.БанковскийСчет)
			И ЗначениеЗаполнено(Объект.ДокументОснование)
			И Объект.ДокументОснование.ВалютаДокумента = Объект.Организация.БанковскийСчетПоУмолчанию.ВалютаДенежныхСредств Тогда
				Объект.БанковскийСчет = Объект.Организация.БанковскийСчетПоУмолчанию;
		КонецЕсли;
		Если НЕ ЗначениеЗаполнено(Объект.БанковскийСчет)
			И НЕ ЗначениеЗаполнено(Объект.ДокументОснование) Тогда
				Объект.БанковскийСчет = Объект.Организация.БанковскийСчетПоУмолчанию;
		КонецЕсли;
		Если НЕ ЗначениеЗаполнено(Объект.ИННПлательщика) Тогда
			Объект.ИННПлательщика = Объект.Организация.ИНН;
		КонецЕсли;
		Если НЕ ЗначениеЗаполнено(Объект.ЕДРПОУПлательщика) Тогда
			Объект.ЕДРПОУПлательщика = Объект.Организация.КодПоЕДРПОУ;
		КонецЕсли;
		Если НЕ ЗначениеЗаполнено(Объект.ТекстПлательщика) Тогда
			Если Объект.ВидОперации = Перечисления.ВидыОперацийПлатежноеПоручение.ПеречислениеНалога
			   И ЗначениеЗаполнено(Объект.Организация.НаименованиеПлательщикаПриПеречисленииНалогов) Тогда
				Объект.ТекстПлательщика = Объект.Организация.НаименованиеПлательщикаПриПеречисленииНалогов;
			Иначе
				Объект.ТекстПлательщика = ?(
				ЗначениеЗаполнено(Объект.БанковскийСчет.ТекстКорреспондента),
				Объект.БанковскийСчет.ТекстКорреспондента,
				Объект.Организация.НаименованиеПолное
				);
			КонецЕсли;
		КонецЕсли;
		Если ЗначениеЗаполнено(Объект.Контрагент) Тогда
			Если НЕ ЗначениеЗаполнено(Объект.СчетКонтрагента)
				И ЗначениеЗаполнено(Объект.ДокументОснование)
				И Объект.ДокументОснование.ВалютаДокумента = Объект.Контрагент.БанковскийСчетПоУмолчанию.ВалютаДенежныхСредств Тогда
					Объект.СчетКонтрагента = Объект.Контрагент.БанковскийСчетПоУмолчанию;
			КонецЕсли;
			Объект.ИННПолучателя = Объект.Контрагент.ИНН;
			Объект.ЕДРПОУПолучателя = Объект.Контрагент.КодПоЕДРПОУ;
			Объект.ТекстПолучателя = ?(
				ЗначениеЗаполнено(Объект.СчетКонтрагента.ТекстКорреспондента),
				Объект.СчетКонтрагента.ТекстКорреспондента,
				Объект.Контрагент.НаименованиеПолное);
			НазначениеПлатежа = Объект.СчетКонтрагента.ТекстНазначения;
		КонецЕсли; 
		Если Объект.ВидОперации = Перечисления.ВидыОперацийПлатежноеПоручение.ПеречислениеНалога Тогда
			Объект.КодКОАТУУ = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Организация, "КодПоОКТМО");
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.СчетКонтрагента) Тогда
		НазначениеПлатежа = Объект.СчетКонтрагента.ТекстНазначения;
	КонецЕсли;
	
	ВидОперации = Объект.ВидОперации;
	
	Если Объект.ПоказательПериода = "0"
	 ИЛИ ПустаяСтрока(Объект.ПоказательПериода)
	 ИЛИ ПустаяСтрока(СтрЗаменить(Объект.ПоказательПериода, ".", "")) Тогда
		ПериодичностьУплаты = "0";
	Иначе
		СтрокаВидаПериода = Лев(Объект.ПоказательПериода, 2);
		СтрокаПериода     = Сред(Объект.ПоказательПериода, 4, 2);
		СтрокаГода        = Сред(Объект.ПоказательПериода, 7);
		Если СтрокаВидаПериода = "ГД" Тогда
			ПериодичностьУплаты = "ГД";
		ИначеЕсли СтрокаВидаПериода = "ПЛ" Тогда
			ПериодичностьУплаты = "ПЛ";
		ИначеЕсли СтрокаВидаПериода = "КВ" Тогда
			ПериодичностьУплаты = "КВ";
		ИначеЕсли СтрокаВидаПериода = "МС" Тогда
			ПериодичностьУплаты = "МС";
		Иначе
			ПериодичностьУплаты = "-";
		КонецЕсли;
		Попытка
			ГодЧислом    = Число(СтрокаГода);
			ПериодЧислом = Число(СтрокаПериода);
			Если ПериодичностьУплаты = "-" Тогда
				ДеньЧислом  = Число(СтрокаВидаПериода);
				ДатаПлатежа = Дата(ГодЧислом, ПериодЧислом, ДеньЧислом);
			Иначе
				ГодПлатежа    = ГодЧислом;
				ПериодПлатежа = ПериодЧислом;
			КонецЕсли;
		Исключение
			ПоказательПериода   = "0";
			ПериодичностьУплаты = "0";
		КонецПопытки;
	КонецЕсли;

	Если ПериодичностьУплаты = "0" Тогда
		Элементы.ГодПериод.Видимость = Ложь;
		Элементы.ДатаПлатежа.Видимость = Ложь;
	Иначе
		Если ПериодичностьУплаты = "-" Тогда
			Элементы.ГодПериод.Видимость = Ложь;
			Элементы.ДатаПлатежа.Видимость = Истина;
		Иначе
			Элементы.ГодПериод.Видимость = Истина;
			Элементы.ДатаПлатежа.Видимость = Ложь;
			Элементы.ПериодПлатежа.Видимость = ПериодичностьУплаты <> "ГД";
			Если ПериодичностьУплаты = "МС" Тогда
				Элементы.ПериодПлатежа.МинимальноеЗначение = 1;
				Элементы.ПериодПлатежа.МаксимальноеЗначение = 12;
				Элементы.ПериодПлатежа.Заголовок = "Месяц";
			ИначеЕсли ПериодичностьУплаты = "КВ" Тогда
				Элементы.ПериодПлатежа.МинимальноеЗначение = 1;
				Элементы.ПериодПлатежа.МаксимальноеЗначение = 4;
				Элементы.ПериодПлатежа.Заголовок = "Квартал";
			ИначеЕсли ПериодичностьУплаты = "ПЛ" Тогда
				Элементы.ПериодПлатежа.МинимальноеЗначение = 1;
				Элементы.ПериодПлатежа.МаксимальноеЗначение = 2;
				Элементы.ПериодПлатежа.Заголовок = "Полугодие";
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если ТипЗнч(Объект.ДокументОснование) = Тип("ДокументСсылка.СчетНаОплатуПоставщика") Тогда
		СтруктураДанные = ПолучитьДанныеДокументОснованиеПриИзменении(Объект.ДокументОснование);
		НомерВходящегоДокумента = СтруктураДанные.НомерВходящегоДокумента;
		ДатаВходящегоДокумента = СтруктураДанные.ДатаВходящегоДокумента;
	КонецЕсли;

	Элементы.РеквизитыДляПеречисленияНалога.Видимость = Объект.ВидОперации = Перечисления.ВидыОперацийПлатежноеПоручение.ПеречислениеНалога;
	Элементы.Налог.Видимость     = Элементы.РеквизитыДляПеречисленияНалога.Видимость;
	Элементы.СтавкаНДС.Видимость = Объект.ВидОперации <> Перечисления.ВидыОперацийПлатежноеПоручение.ПеречислениеНалога;
	Элементы.СуммаНДС.Видимость = Объект.ВидОперации <> Перечисления.ВидыОперацийПлатежноеПоручение.ПеречислениеНалога;
	УстановитьФормуС2015Года();
	
	УстановитьВидимостьВЗависимостиОтВидаПеречисленияВБюджет(ЭтаФорма);
	
	// Документ основание.
	Элементы.ДокументОснованиеНадпись.Заголовок = РаботаСФормойДокументаКлиентСервер.СформироватьНадписьДокументОснование(Объект.ДокументОснование);
	
	НовыйМассив = Новый Массив();
	НовыеПараметры = Новый ФиксированныйМассив(НовыйМассив);
	ПараметрыВыбораДокументаОснования = НовыеПараметры;
	// Конец Документ основание.
	
	// Остатки ДС и взаиморасчетов на форме
	ОбновитьВидимостьИОстаткиВзаиморасчетов();
	ОбновитьВидимостьИОстаткиДС(Объект.БанковскийСчет);
	// Конец Остатки ДС и взаиморасчетов на форме
	
	ОбменСGoogle.ПодключитьОбработчикиСобытияАвтоподбор(ЭтотОбъект);
	
	ОтчетыУНФ.ПриСозданииНаСервереФормыСвязанногоОбъекта(ЭтотОбъект);
	
	// ЭДО
	ОбменСБанками.ПриСозданииНаСервере(ЭтотОбъект);
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		УстановитьТекстСостоянияЭДНаСервере();
	КонецЕсли;
	// Конец ЭДО
	
	// СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
	ДополнительныеОтчетыИОбработки.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
	
	// СтандартныеПодсистемы.Печать
	УправлениеПечатью.ПриСозданииНаСервере(ЭтотОбъект, Элементы.ПодменюПечать);
	// Конец СтандартныеПодсистемы.Печать
	УправлениеНебольшойФирмойСервер.УстановитьОтображаниеПодменюПечати(Элементы.ПодменюПечать);
	
	УстановитьЗаголовокГруппыБюджет(ЭтотОбъект);
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	УстановитьЗаголовокГруппыБюджет(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// Обсуждения
	ТекущийОбъект.ДополнительныеСвойства.Вставить("Модифицированность",Модифицированность);
	// Конец Обсуждения
	
КонецПроцедуры // ПередЗаписьюНаСервере()

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ОбменСGoogle.УвеличитьЗначениеСчетчикаПодсказок(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	УстановитьТекстСостоянияЭДНаСервере();
	
	ОбновитьВидимостьИОстаткиВзаиморасчетов();
	ОбновитьВидимостьИОстаткиДС(Объект.БанковскийСчет);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ОбновитьСостояниеЭД" Тогда
		УстановитьТекстСостоянияЭДНаСервере();
	КонецЕсли;
	
	// Обсуждения
	ОбсужденияКлиент.ОбработкаОповещения(ИмяСобытия, Параметр, Источник, ЭтотОбъект, Объект.Ссылка);
	// Конец Обсуждения

КонецПроцедуры // ОбработкаОповещения()

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Если СозданПоКомандеИзФормыСписка Тогда
		ПараметрыОповещения = Новый Структура("Ссылка", Объект.Ссылка);
		Оповестить("Запись_ПлатежноеПоручение", ПараметрыОповещения);
	КонецЕсли;
	
	УстановитьДоступностьПоляВводаНомерПоручения();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УстановитьДоступностьПоляВводаНомерПоручения();
	
КонецПроцедуры // ПриОткрытии()

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

// Процедура - обработчик события ПриИзменении реквизита ПериодичностьУплаты.
// В процедуре устанавливается максимальное значение реквизита ПериодПлатежа в
// зависимости от реквизита ПериодичностьУплаты.
//
&НаКлиенте
Процедура ПериодичностьУплатыПриИзменении(Элемент)
	
	Если НЕ ЗначениеЗаполнено(СокрЛП(ПериодичностьУплаты)) Тогда
		ПериодичностьУплаты= "0";
	КонецЕсли;

	Модифицированность = Истина;
	
	Если ПериодичностьУплаты <> "0"
	   И ПериодичностьУплаты <> "-" Тогда
		Если ГодПлатежа = 0 Тогда
			ГодПлатежа = Год(Объект.Дата);
		КонецЕсли;
		Если ПериодичностьУплаты = "ГД" Тогда
			ПериодПлатежа = 0;
		Иначе
			Если ПериодичностьУплаты = "МС" Тогда
				ПериодПлатежа = Мин(ПериодПлатежа, 12);
			ИначеЕсли ПериодичностьУплаты = "КВ" Тогда
				ПериодПлатежа = Мин(ПериодПлатежа, 4);
			ИначеЕсли ПериодичностьУплаты = "ПЛ" Тогда
				ПериодПлатежа = Мин(ПериодПлатежа, 2);
			КонецЕсли;
			ПериодПлатежа = Макс(1, ПериодПлатежа);
		КонецЕсли;
	Иначе
		ГодПлатежа = 0;
	КонецЕсли;
	
	УстановитьПоказательПериода();
	УстановитьДоступностьРеквизитовПеречисленияНалогов();
	
КонецПроцедуры // ПериодичностьУплатыПриИзменении()

// Процедура - обработчик события ПриИзменении реквизита ГодПлатежа.
// В процедуре устанавливается реквизит Показатель периода.
//
&НаКлиенте
Процедура ГодПлатежаПриИзменении(Элемент)
	
	Модифицированность = Истина;
	УстановитьПоказательПериода();
	
КонецПроцедуры // ГодПлатежаПриИзменении()

// Процедура - обработчик события ПриИзменении реквизита ПериодПлатежа.
// В процедуре устанавливается реквизит Показатель периода.
//
&НаКлиенте
Процедура ПериодПлатежаПриИзменении(Элемент)
	
	Модифицированность = Истина;
	УстановитьПоказательПериода();
	
КонецПроцедуры // ПериодПлатежаПриИзменении()

// Процедура - обработчик события ПриИзменении реквизита ДатаПлатежа.
// В процедуре устанавливается реквизит Показатель периода.
//
&НаКлиенте
Процедура ДатаПлатежаПриИзменении(Элемент)
	
	Модифицированность = Истина;
	УстановитьПоказательПериода();
	
КонецПроцедуры // ДатаПлатежаПриИзменении()

// Процедура - обработчик события ПриИзменении поля ввода Организация.
// В процедуре осуществляется очистка номера документа,
// а также производится установка параметров функциональных опций формы.
// Переопределяет соответствующий параметр формы.
//
&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	Объект.Номер 			 = "";
	СтруктураДанные 		 = ПолучитьДанныеОрганизацияПриИзменении(Объект.Организация, Объект.БанковскийСчет, Объект.Контрагент, Объект.СчетКонтрагента, Объект.ВидОперации, Объект.Дата);
	Компания 				 = СтруктураДанные.Компания;
	Объект.БанковскийСчет	 = СтруктураДанные.БанковскийСчет;
	Объект.ВалютаДокумента	 = СтруктураДанные.ВалютаДокумента;
	Объект.СчетКонтрагента	 = СтруктураДанные.СчетКонтрагента;
	Объект.ИННПлательщика	 = СтруктураДанные.ИННПлательщика;
	Объект.ЕДРПОУПлательщика = СтруктураДанные.ЕДРПОУПлательщика;
	
	ЗаполнитьТекстПлательщика(СтруктураДанные);
	
	ЭтоФизЛицо = СтруктураДанные.ЭтоФизЛицо;
	
	Если ЗначениеЗаполнено(Объект.Организация) Тогда
		Если Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийПлатежноеПоручение.ПеречислениеНалога") Тогда
			Объект.КодКОАТУУ = СтруктураДанные.КодКОАТУУ;
			Если ЭтоФизЛицо Тогда
				Объект.ЕДРПОУПлательщика = 0;
			КонецЕсли;
		Иначе
			Объект.КодКОАТУУ = "";
			Если ЭтоФизЛицо Тогда
				Объект.ЕДРПОУПлательщика = "";
			КонецЕсли;
		КонецЕсли;
	Иначе
		Объект.БанковскийСчет = "";
	КонецЕсли;
	
КонецПроцедуры

// Процедура - обработчик события ПриИзменении реквизита  СчетОрганизации.
//
&НаКлиенте
Процедура СчетОрганизацииПриИзменении(Элемент)
	
	СтруктураДанные = ПолучитьДанныеСчетОрганизацииПриИзменении(Объект.Организация, Объект.БанковскийСчет, Объект.ВалютаДокумента, Объект.Дата, Объект.ВидОперации);
	Объект.ЕДРПОУПлательщика = СтруктураДанные.ЕДРПОУПлательщика;
	
	Если НЕ Объект.ВалютаДокумента = СтруктураДанные.ВалютаДенежныхСредств Тогда
		
		Объект.СчетКонтрагента = Неопределено;
		Объект.ВалютаДокумента = СтруктураДанные.ВалютаДенежныхСредств;
		
		Если Объект.СуммаДокумента <> 0 Тогда
			ТекстСообщения = НСтр("ru='Изменилась валюта банковского счета. Пересчитать суммы документа?';uk='Змінилася валюта банківського рахунку. Перерахувати суми документа?'");
			
			Режим = РежимДиалогаВопрос.ДаНет;
			Ответ = Неопределено;

			ПоказатьВопрос(Новый ОписаниеОповещения("СчетОрганизацииПриИзмененииЗавершение", ЭтотОбъект, Новый Структура("СтруктураДанные", СтруктураДанные)), ТекстСообщения, Режим, 0);
            Возврат;
		КонецЕсли;
		
	КонецЕсли;
	
	СчетОрганизацииПриИзмененииФрагмент(СтруктураДанные);
КонецПроцедуры

&НаКлиенте
Процедура СчетОрганизацииПриИзмененииЗавершение(Результат, ДополнительныеПараметры) Экспорт
    
    СтруктураДанные = ДополнительныеПараметры.СтруктураДанные;
    
    
    Ответ = Результат;
    
    Если Ответ = КодВозвратаДиалога.Да Тогда
        Объект.СуммаДокумента = УправлениеНебольшойФирмойКлиент.ПересчитатьИзВалютыВВалюту(
        Объект.СуммаДокумента,
        СтруктураДанные.ВалютаКурсКратностьПередИзменением.Курс,
        СтруктураДанные.ВалютаКурсКратность.Курс,
        СтруктураДанные.ВалютаКурсКратностьПередИзменением.Кратность,
        СтруктураДанные.ВалютаКурсКратность.Кратность
        );
        Объект.СуммаНДС = УправлениеНебольшойФирмойКлиент.ПересчитатьИзВалютыВВалюту(
        Объект.СуммаНДС,
        СтруктураДанные.ВалютаКурсКратностьПередИзменением.Курс,
        СтруктураДанные.ВалютаКурсКратность.Курс,
        СтруктураДанные.ВалютаКурсКратностьПередИзменением.Кратность,
        СтруктураДанные.ВалютаКурсКратность.Кратность
        );
    КонецЕсли;
    
    СчетОрганизацииПриИзмененииФрагмент(СтруктураДанные);

КонецПроцедуры

&НаКлиенте
Процедура СчетОрганизацииПриИзмененииФрагмент(Знач СтруктураДанные)
    
    ЗаполнитьТекстПлательщика(СтруктураДанные);

КонецПроцедуры // СчетОрганизацииПриИзменении()

// Процедура - обработчик события ПриИзменении поля ввода Дата.
// В процедуре определяется ситуация, когда при изменении своей даты документ 
// оказывается в другом периоде нумерации документов, и в этом случае
// присваивает документу новый уникальный номер.
// Переопределяет соответствующий параметр формы.
//
&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	
	// Обработка события изменения даты.
	ДатаПередИзменением = ДатаДокумента;
	ДатаДокумента = Объект.Дата;
	Если Объект.Дата <> ДатаПередИзменением Тогда
		СтруктураДанные = ПолучитьДанныеДатаПриИзменении(Объект.Ссылка, Объект.Дата, ДатаПередИзменением);
		Если СтруктураДанные.РазностьДат <> 0 Тогда
			Объект.Номер = "";
		КонецЕсли;
	КонецЕсли;
	
	Если Объект.ДатаПоручения < Объект.Дата тогда
		Объект.ДатаПоручения = Объект.Дата;			
	КонецЕсли;
	
	Если Объект.ДатаПоручения < ТекущаяДата() Тогда
		Объект.ДатаПоручения = ТекущаяДата();	
	КонецЕсли;	
	
	СброситьУстановленныйНомерПоручения();
	
	УстановитьФормуС2015Года();
	
КонецПроцедуры // ДатаПриИзменении()

// Процедура - обработчик события ПриИзменении реквизита ВидОперации.
// В процедуре устанавливается доступность реквизитов формы в зависимости от
// вида операции.
//
&НаКлиенте
Процедура ВидОперацииПриИзменении(Элемент)
	
	Если Объект.ВидОперации = ВидОперации Тогда
		Возврат;
	КонецЕсли;
	ВидОперации = Объект.ВидОперации;
	
	УстановитьВидимостьДоступность();
	СтруктураДанные = ПолучитьДанныеОрганизацияПриИзменении(Объект.Организация, Объект.БанковскийСчет, Объект.Контрагент, Объект.СчетКонтрагента, Объект.ВидОперации, Объект.Дата);
	
	Если Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийПлатежноеПоручение.ПеречислениеНалога") Тогда
		Объект.КодБК = "";
		Объект.КодКОАТУУ = СтруктураДанные.КодКОАТУУ;
		Объект.ВидПеречисленияВБюджет = ПредопределенноеЗначение("Перечисление.ВидыПеречисленийВБюджет.НалоговыйПлатеж");
		Объект.СтавкаНДС = Неопределено;
		Объект.СуммаНДС = Неопределено;
		Если ЭтоФизЛицо Тогда
			Объект.ЕДРПОУПлательщика = 0;
		КонецЕсли;
	Иначе
		Объект.КодБК  = "";
		Объект.КодКОАТУУ = "";
		Объект.СтатусСоставителя = "";
		Объект.ПоказательОснования = "";
		Объект.ПоказательТипа = "";
		Объект.ПоказательПериода = "";
		Объект.ПоказательНомера = "";
		Объект.ПоказательДаты = "";
		ПериодичностьУплаты = "";
		ГодПлатежа = "";
		ПериодПлатежа = "";
		Объект.ВидПеречисленияВБюджет = Неопределено;
		Если ЭтоФизЛицо Тогда
			Объект.ЕДРПОУПлательщика = "";
		КонецЕсли;
	КонецЕсли;
	
	Если Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийПлатежноеПоручение.ПеречислениеНалога")
	   И ЗначениеЗаполнено(СтруктураДанные.НаименованиеПлательщикаПриПеречисленииНалогов) Тогда
		Объект.ТекстПлательщика = СтруктураДанные.НаименованиеПлательщикаПриПеречисленииНалогов;
	Иначе
		Объект.ТекстПлательщика = ?(
			ЗначениеЗаполнено(СтруктураДанные.ТекстКорреспондента),
			СтруктураДанные.ТекстКорреспондента,
			СтруктураДанные.НаименованиеПолное
		);
	КонецЕсли;
	
	СформироватьНазначениеПлатежа();
	
	УстановитьЗаголовокГруппыБюджет(ЭтотОбъект);
	
КонецПроцедуры // ВидОперацииПриИзменении()

// Процедура - обработчик события ПриИзменении реквизита Контрагент.
// В процедуре устанавливаются реквизиты формы, относящиеся к контрагенту.
//
&НаКлиенте
Процедура КонтрагентПриИзменении(Элемент)
	
	СтруктураДанные = ПолучитьДанныеКонтрагентПриИзменении(Объект.Контрагент, Объект.ВалютаДокумента, Объект.ВидОперации);
	
	Объект.СчетКонтрагента 	= СтруктураДанные.СчетКонтрагента;
	Объект.ИННПолучателя	= СтруктураДанные.ИННПолучателя;
	Объект.ЕДРПОУПолучателя = СтруктураДанные.ЕДРПОУПолучателя;
	ЗаполнитьТекстПолучателя(СтруктураДанные);
	
	НазначениеПлатежа 		= СтруктураДанные.НазначениеПлатежа;
	
	СформироватьНазначениеПлатежа();
	
КонецПроцедуры // КонтрагентПриИзменении()

// Процедура - обработчик события ПриИзменении реквизита СчетКонтрагента.
// В процедуре устанавливаются реквизиты формы, относящиеся к счету контрагента.
//
&НаКлиенте
Процедура СчетКонтрагентаПриИзменении(Элемент)
	
	СтруктураДанные = ПолучитьДанныеСчетКонтрагентаПриИзменении(Объект.Контрагент, Объект.СчетКонтрагента, Объект.ВидОперации);
	
	Объект.ЕДРПОУПолучателя = СтруктураДанные.ЕДРПОУПолучателя;
	ЗаполнитьТекстПолучателя(СтруктураДанные);
	
	НазначениеПлатежа = СтруктураДанные.НазначениеПлатежа;
	СформироватьНазначениеПлатежа();
	
КонецПроцедуры // СчетКонтрагентаПриИзменении()

// Процедура - обработчик события ПриИзменении реквизита ЕДРПОУПлательщика.
//
&НаКлиенте
Процедура ЕДРПОУПлательщикаПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Объект.Организация)
	И НЕ ЗначениеЗаполнено(Объект.ЕДРПОУПлательщика)
	И Строка(Объект.ВидОперации) = "Перечисление в бюджет"
	И ЭтоФизЛицо Тогда
		Объект.ЕДРПОУПлательщика = 0;
	КонецЕсли;

КонецПроцедуры // ЕДРПОУПлательщикаПриИзменении()

// Процедура - обработчик события ПриИзменении реквизита СуммаДокумента.
// В процедуре формируется реквизит НазначениеПлатежа.
//
&НаКлиенте
Процедура СуммаДокументаПриИзменении(Элемент)
	
	СтавкаНДС = УправлениеНебольшойФирмойПовтИсп.ПолучитьЗначениеСтавкиНДС(Объект.СтавкаНДС);
	
	Объект.СуммаНДС = Объект.СуммаДокумента - (Объект.СуммаДокумента) / ((СтавкаНДС + 100) / 100);
	
	СформироватьНазначениеПлатежа(Истина);
	
КонецПроцедуры // СуммаДокументаПриИзменении()

// Процедура - обработчик события ПриИзменении реквизита СтавкаНДС.
// В процедуре расчитывается сумма НДС и формируется реквизит НазначениеПлатежа.
//
&НаКлиенте
Процедура СтавкаНДСПриИзменении(Элемент)
	
	СтруктураДанные = ПолучитьДанныеСтавкаНДСПриИзменении(Объект.СтавкаНДС);
	
	СтавкаНДС = СтруктураДанные.Ставка;
	БезНалогаНДС = СтруктураДанные.НеОблагается;
	
	Объект.СуммаНДС = Объект.СуммаДокумента - (Объект.СуммаДокумента) / ((СтавкаНДС + 100) / 100);
	
	СформироватьНазначениеПлатежа(Истина);
	
КонецПроцедуры // СтавкаНДСПриИзменении()

// Процедура - обработчик события ПриИзменении реквизита СуммаНДС.
// В процедуре формируется реквизит НазначениеПлатежа.
//
&НаКлиенте
Процедура СуммаНДСПриИзменении(Элемент)
	
	СформироватьНазначениеПлатежа(Истина);
	
КонецПроцедуры // СуммаНДСПриИзменении()

// Процедура - обработчик события ПриИзменении реквизита ВидПеречисленияВБюджет.
//
&НаКлиенте
Процедура ВидПеречисленияВБюджетПриИзменении(Элемент)
	
	ЗаполнитьРеквизитыПлатежаВБюджетПоУмолчанию();
	УстановитьВидимостьВЗависимостиОтВидаПеречисленияВБюджет(ЭтаФорма);
	УстановитьЗаголовокГруппыБюджет(ЭтотОбъект);
	
КонецПроцедуры // ВидПеречисленияВБюджетПриИзменении()

// Процедура - обработчик события ПриИзменении реквизита СтатусСоставителя.
//
&НаКлиенте
Процедура СтатусСоставителяПриИзменении(Элемент)
	
	Если НЕ ЗначениеЗаполнено(СокрЛП(Объект.СтатусСоставителя)) Тогда
		Объект.СтатусСоставителя = "01";
	КонецЕсли;
		
КонецПроцедуры // СтатусСоставителяПриИзменении()

// Процедура - обработчик события ПриИзменении реквизита ПоказательОснования.
//
&НаКлиенте
Процедура ПоказательОснованияПриИзменении(Элемент)
	
	Если НЕ ЗначениеЗаполнено(СокрЛП(Объект.ПоказательОснования)) Тогда
		Объект.ПоказательОснования = "0";
	КонецЕсли;

КонецПроцедуры // ПоказательОснованияПриИзменении()

// Процедура - обработчик события ПриИзменении реквизита ПоказательТипа.
//
&НаКлиенте
Процедура ПоказательТипаПриИзменении(Элемент)
	
	Если НЕ ЗначениеЗаполнено(СокрЛП(Объект.ПоказательТипа)) Тогда
		Объект.ПоказательТипа = "0";
	КонецЕсли;
	
КонецПроцедуры // ПоказательТипаПриИзменении()

// Процедура - обработчик события ПриИзменении реквизита ПоказательПериодаТаможенныйПлатеж.
//
&НаКлиенте
Процедура ПоказательПериодаТаможенныйПлатежПриИзменении(Элемент)
	
	Если Объект.ВидПеречисленияВБюджет <> ПредопределенноеЗначение("Перечисление.ВидыПеречисленийВБюджет.ТаможенныйПлатеж")
	   И НЕ ЗначениеЗаполнено(СокрЛП(Объект.ПоказательПериода)) Тогда
		Объект.ПоказательПериода = "0";
	КонецЕсли;
	
КонецПроцедуры // ПоказательПериодаТаможенныйПлатежПриИзменении()

// Процедура - обработчик события ПриИзменении реквизита ПоказательПериодаИнойПлатеж.
//
&НаКлиенте
Процедура ПоказательПериодаИнойПлатежПриИзменении(Элемент)
	
	Если НЕ ЗначениеЗаполнено(СокрЛП(Объект.ПоказательПериода)) Тогда
		Объект.ПоказательПериода = "0";
	КонецЕсли;

КонецПроцедуры // ПоказательПериодаИнойПлатежПриИзменении()

&НаКлиенте
Процедура ОстатокВзаиморасчетовОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	РаботаСФормойДокументаКлиент.ОткрытьОтчетВзаиморасчетрыСКонтрагентом(Объект.Контрагент);
	
КонецПроцедуры

&НаКлиенте
Процедура ОстатокДенежныхСредствОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	РаботаСФормойДокументаКлиент.ОткрытьОтчетДенежныеСредства(Объект.БанковскийСчет);
	
КонецПроцедуры

&НаКлиенте
Процедура КодБКПриИзменении(Элемент)
	КодБК = ПлатежиВБюджетКлиентСервер.ОставитьВСтрокеТолькоЦифры(Объект.КодБК);
	Объект.КодБК = КодБК;
	УстановитьЗаголовокГруппыБюджет(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура КомментарийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(Элемент.ТекстРедактирования, ЭтотОбъект, "Объект.Комментарий");
	СтатистикаИспользованияФормКлиент.ПриИнтерактивномДействии(ЭтотОбъект, Элемент, "НачалоВыбора");
КонецПроцедуры

&НаКлиенте
Процедура ДатаПорученияПриИзменении(Элемент)
	
	ЭтаФорма.ДатаПлатежа = Объект.ДатаПоручения;
	
КонецПроцедуры

&НаКлиенте
Процедура НомерПорученияРегулирование(Элемент, Направление, СтандартнаяОбработка)
	
	Попытка
		НомерПорученияЧисло = Число(Объект.НомерПоручения) + Направление;
		Объект.НомерПоручения = Формат(НомерПорученияЧисло, "ЧГ=0");
	Исключение
		Объект.НомерПоручения = "1";
	КонецПопытки;	
	
КонецПроцедуры

&НаКлиенте
Процедура НомерПорученияОткрытие(Элемент, СтандартнаяОбработка)
	
	УстановитьНомерПоручения();
	УстановитьДоступностьПоляВводаНомерПоручения();
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура НомерПорученияПриИзменении(Элемент)
	
	УстановитьДоступностьПоляВводаНомерПоручения();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Формирует номер платежного поручения в пределах расчетного счета
//
&НаСервере
Процедура УстановитьНомерПоручения()
	
	Запрос = Новый Запрос;
	ОбъектДата = Объект.Дата;
	Если НЕ ЗначениеЗаполнено(ОбъектДата) Тогда
		ОбъектДата = ТекущаяДата();
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ДатаНачалаГода", НачалоГода(ОбъектДата));
	Запрос.УстановитьПараметр("ДатаКонцаГода", КонецГода(ОбъектДата));
	Запрос.УстановитьПараметр("Организация", Объект.Организация);
	Запрос.УстановитьПараметр("БанковскийСчет", Объект.БанковскийСчет);

	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПлатежноеПоручение.НомерПоручения КАК НомерПоручения
	|ИЗ
	|	Документ.ПлатежноеПоручение КАК ПлатежноеПоручение
	|ГДЕ
	|	ПлатежноеПоручение.Организация = &Организация
	|	И ПлатежноеПоручение.БанковскийСчет = &БанковскийСчет
	|	И ПлатежноеПоручение.Дата >= &ДатаНачалаГода
	|	И ПлатежноеПоручение.Дата <= &ДатаКонцаГода";

	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Запрос.Текст = Запрос.Текст + "	И ПлатежноеПоручение.Ссылка <> &Ссылка ";
		Запрос.УстановитьПараметр("Ссылка", Объект.Ссылка);
	КонецЕсли;
	
	РезультатЗапроса = Запрос.Выполнить();
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		СписокПлатежек = РезультатЗапроса.Выбрать();
		
		ТаблицаЗначений = Новый ТаблицаЗначений;
		
		ТаблицаЗначений.Колонки.Добавить("Номер"); 
		ТаблицаЗначений.Колонки.Добавить("Длина"); 
		
		Пока СписокПлатежек.Следующий() Цикл
			НоваяСтрока = ТаблицаЗначений.Добавить(); 
			НоваяСтрока.Номер = СписокПлатежек.НомерПоручения; 
			НоваяСтрока.Длина = СтрДлина(СписокПлатежек.НомерПоручения); 
		КонецЦикла;
		
		ТаблицаЗначений.Сортировать("Длина Убыв, Номер Убыв");
		
		Попытка
			НомерПорученияЧисло = Число(ТаблицаЗначений[0].Номер) + 1;
			Объект.НомерПоручения = Формат(НомерПорученияЧисло, "ЧГ=0");
		Исключение
			Объект.НомерПоручения = "1";
		КонецПопытки;	
	Иначе
		Объект.НомерПоручения = "1";
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьДатуПорученияПоТекущейДате(Команда)
	
	Объект.ДатаПоручения = ТекущаяДата();
	
КонецПроцедуры

//Процедура добавления текста в назначение платежа
// 1 - согласно счета, 2 - согласно договора, 3 - В том числе НДС, 4 - Без НДС, 5 - Код КоАТУУ Организации, Другое - Номер и дата документа основания.
&НаСервере
Процедура СформироватьКомментарий(РежимФормирования = 0)
	
	КодЯзыкаПечать = Локализация.ПолучитьЯзыкФормированияПечатныхФорм();
	
	Если РежимФормирования = 1 Тогда
		
		Объект.НазначениеПлатежа = Объект.НазначениеПлатежа + НСтр("ru='Согласно счета №';uk='Згідно рахунку №'", КодЯзыкаПечать) + Объект.ДокументОснование.НомерВходящегоДокумента + НСтр("ru=' от ';uk=' від '", КодЯзыкаПечать) + Формат(Объект.ДокументОснование.ДатаВходящегоДокумента, "ДФ=dd.MM.yy");
		
	ИначеЕсли РежимФормирования = 2 Тогда
		Если ЗначениеЗаполнено(Объект.ДокументОснование)
			И ЗначениеЗаполнено(Объект.ДокументОснование.Договор) Тогда
			
			Объект.НазначениеПлатежа = Объект.НазначениеПлатежа + НСтр("ru='Согласно договора №';uk='Згідно договору №'", КодЯзыкаПечать) + Объект.ДокументОснование.Договор.НомерДоговора + НСтр("ru=' от ';uk=' від '", КодЯзыкаПечать) + Формат(Объект.ДокументОснование.Договор.ДатаДоговора, "ДФ=dd.MM.yy");
		Иначе
			Объект.НазначениеПлатежа = Объект.НазначениеПлатежа + НСтр("ru='Согласно договора №';uk='Згідно договору №'", КодЯзыкаПечать) + " " + НСтр("ru=' от ';uk=' від '", КодЯзыкаПечать);
		КонецЕсли;
		
	ИначеЕсли РежимФормирования = 3 Тогда
		Объект.НазначениеПлатежа = Объект.НазначениеПлатежа + НСтр("ru='в т.ч. НДС ';uk='у т.ч. ПДВ '", КодЯзыкаПечать) + Формат(Объект.СуммаНДС, "ЧЦ=15; ЧДЦ=2") + " грн.";
		
	ИначеЕсли РежимФормирования = 4 Тогда
		Объект.НазначениеПлатежа = Объект.НазначениеПлатежа + НСтр("ru='Без НДС.';uk='Без ПДВ.'", КодЯзыкаПечать);
		
	ИначеЕсли РежимФормирования = 5 Тогда
		Объект.НазначениеПлатежа = Объект.НазначениеПлатежа + Объект.Организация.КодКОАТУУ;
		
	ИначеЕсли ЗначениеЗаполнено(Объект.ДокументОснование) Тогда
		Объект.НазначениеПлатежа = Объект.НазначениеПлатежа + Объект.ДокументОснование.Номер + НСтр("ru=' от ';uk=' від '", КодЯзыкаПечать) + Формат(Объект.ДокументОснование.Дата, "ДФ=dd.MM.yy");
		
	Иначе
		Объект.НазначениеПлатежа = Объект.НазначениеПлатежа + " " + НСтр("ru=' от ';uk=' від '", КодЯзыкаПечать);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьНазначение(Команда)
	
	Объект.НазначениеПлатежа = "";
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьВНазначениеТекстСогласноДоговора(Команда)
	
	Если ЗначениеЗаполнено(Объект.ДокументОснование) Тогда
		
		СформироватьКомментарий(2);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьВНазначениеТекстСогласноСчета(Команда)
	
	Если ЗначениеЗаполнено(Объект.ДокументОснование) Тогда
		
		СформироватьКомментарий(1);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьВНазначениеТекстВТомЧислеНДС(Команда)
	
	СформироватьКомментарий(3);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьВНазначениеТекстКодКОАТУУ(Команда)
	
	СформироватьКомментарий(5);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьВНазначениеТекстБезНДС(Команда)
	
	СформироватьКомментарий(4);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьВНазначениеНомерИДатуДокументаОснования(Команда)
	
	СформироватьКомментарий(6);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьДоступностьПоляВводаНомерПоручения()
	
	Если НЕ ЗначениеЗаполнено(Объект.НомерПоручения) Тогда
		Элементы.НомерПоручения.Подсказка =  НСтр("ru='Номер поручения будет присвоен при записи объекта.';uk='Номер доручення буде привласнений при записі об`єкта.'");
	Иначе
		Элементы.НомерПоручения.Подсказка =  НСтр("ru='Номер поручения';uk='Номер доручення'");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура СброситьУстановленныйНомерПоручения()
	
	НомерПоручения = "";
	УстановитьДоступностьПоляВводаНомерПоручения();
	
КонецПроцедуры	

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьЗаголовокГруппыБюджет(Форма)
	
	Объект = Форма.Объект;
	ДинамическиеПараметры = Новый Массив;
	
	Если Не ПустаяСтрока(Объект.ВидПеречисленияВБюджет) Тогда
		ДинамическиеПараметры.Добавить(НСтр("ru='Вид перечисления';uk='Вид перерахування'") + " " + Объект.ВидПеречисленияВБюджет);
	Иначе
		ДинамическиеПараметры.Добавить(НСтр("ru='Вид перечисления ...';uk='Вид перерахування ...'"));
	КонецЕсли;
	
	Если Не ПустаяСтрока(Объект.КодКОАТУУ) Тогда
		ДинамическиеПараметры.Добавить(НСтр("ru='ОКТМО';uk='ЗКТМУ'") + " " + Объект.КодКОАТУУ);
	Иначе
		ДинамическиеПараметры.Добавить(НСтр("ru='ОКТМО ...';uk='ОКТМО ...'"));
	КонецЕсли;
	
	Если Не ПустаяСтрока(Объект.КодБК) Тогда
		ДинамическиеПараметры.Добавить(НСтр("ru='КБК';uk='КБК'") + " " + Объект.КодБК);
	Иначе
		ДинамическиеПараметры.Добавить(НСтр("ru='КБК ...';uk='КБК ...'"));
	КонецЕсли;
	
	УстановитьЗаголовокСвернутогоОтображения(Форма, "РеквизитыДляПеречисленияНалога", ДинамическиеПараметры);
	
КонецПроцедуры

// Процедура устанавливает заголовок свернутого отображения для группы, по шаблону:
// <заголовок группы (как задан в конфигураторе)> : <динамический параметр 1>, <динамический параметр 2>
//
// Параметры:
//  Форма					 - Форма	 - текущая форма
//  НазваниеГруппы			 - Строка	 - имя группы формы, для которой устанавливается заголовок
//  ДинамическиеПараметры	 - Массив	 - массив частей заголовка.
//
&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьЗаголовокСвернутогоОтображения(Форма, НазваниеГруппы, ДинамическиеПараметры)
	
	ЗаголовокТекст = Форма.Элементы[НазваниеГруппы].Заголовок;
	Если ДинамическиеПараметры.Количество() > 0 Тогда
		ЗаголовокТекст = ЗаголовокТекст + ": ";
		Для Каждого Параметр Из ДинамическиеПараметры Цикл
			ЗаголовокТекст = ЗаголовокТекст + Параметр + ", ";
		КонецЦикла;
		СтроковыеФункцииКлиентСервер.УдалитьПоследнийСимволВСтроке(ЗаголовокТекст, 2);
	КонецЕсли;
	
	#Если ВебКлиент Тогда
		СистемнаяИнформация = Новый СистемнаяИнформация;
		ВерсияПлатформыБезНомераСборки = ОбщегоНазначенияКлиентСервер.ВерсияКонфигурацииБезНомераСборки(СистемнаяИнформация.ВерсияПриложения);
		Если ВерсияПлатформыБезНомераСборки = "8.3.5" Тогда
			Форма.Элементы[НазваниеГруппы].СвернутыйЗаголовок = ЗаголовокТекст;
		Иначе
			Форма.Элементы[НазваниеГруппы].ЗаголовокСвернутогоОтображения = ЗаголовокТекст;
		КонецЕсли;
	#Иначе
		Форма.Элементы[НазваниеГруппы].ЗаголовокСвернутогоОтображения = ЗаголовокТекст;
	#КонецЕсли
	
КонецПроцедуры


&НаКлиентеНаСервереБезКонтекста
Процедура НастроитьСписокВыбора(Элемент, СписокДанных, РедактированиеОграничено = Ложь, Предупреждение = "")
	
	Элемент.СписокВыбора.Очистить();
	
	Элемент.РежимВыбораИзСписка = Истина;
	Элемент.КнопкаОчистки       = Ложь;
	Для каждого ЭлементДанных Из СписокДанных Цикл
		Элемент.СписокВыбора.Добавить(ЭлементДанных.Значение, ЭлементДанных.Представление);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьФормуС2015Года()
	
	Если ДатаДокумента >= '20150101'
		ИЛИ Объект.Дата >= '20150101' Тогда // Приказ Минфина №126н от 30.10.2014.
		Элементы.ПоказательТипа.Видимость = Ложь;
		Объект.ПоказательТипа = "";
	Иначе
		Элементы.ПоказательТипа.Видимость = Истина;
		Объект.ПоказательТипа = "0";
	КонецЕсли;
	
КонецПроцедуры

// Процедура заполняет текст плательщика.
//
Процедура ЗаполнитьТекстПлательщика(СтруктураДанные)
	
	Если Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийПлатежноеПоручение.ПеречислениеНалога")
		И ЗначениеЗаполнено(СтруктураДанные.НаименованиеПлательщикаПриПеречисленииНалогов) Тогда
		
		Объект.ТекстПлательщика = СтруктураДанные.НаименованиеПлательщикаПриПеречисленииНалогов;
		
	ИначеЕсли ПустаяСтрока(СтруктураДанные.ТекстКорреспондента) Тогда
		
		Объект.ТекстПлательщика = СтруктураДанные.НаименованиеПолное;
		
	Иначе
		
		Объект.ТекстПлательщика = СтруктураДанные.ТекстКорреспондента;
		
	КонецЕсли;
	
КонецПроцедуры // ЗаполнитьТекстПлательщика()

// Процедура заполняет текст получателя.
//
Процедура ЗаполнитьТекстПолучателя(СтруктураДанные)
	
	Если ПустаяСтрока(СтруктураДанные.ТекстКорреспондента) Тогда
		
		Объект.ТекстПолучателя = СтруктураДанные.НаименованиеПолное;
		
	Иначе
		
		Объект.ТекстПолучателя = СтруктураДанные.ТекстКорреспондента;
		
	КонецЕсли;
	
КонецПроцедуры // ЗаполнитьТекстПолучателя()

// Процедура вызывает обработку заполнения документа по основанию.
//
&НаСервере
Процедура ЗаполнитьПоДокументу(ДокОснование)
	
	Документ = РеквизитФормыВЗначение("Объект");
	Документ.Заполнить(ДокОснование);
	ЗначениеВРеквизитФормы(Документ, "Объект");
	Модифицированность = Истина;
	ВидОперации = Объект.ВидОперации;
	
	Если ТипЗнч(Объект.ДокументОснование) = Тип("ДокументСсылка.СчетНаОплатуПоставщика") Тогда
		СтруктураДанные = ПолучитьДанныеДокументОснованиеПриИзменении(Объект.ДокументОснование);
		НомерВходящегоДокумента = СтруктураДанные.НомерВходящегоДокумента;
		ДатаВходящегоДокумента = СтруктураДанные.ДатаВходящегоДокумента;
	Иначе
		НомерВходящегоДокумента = "";
		ДатаВходящегоДокумента = '00010101';
	КонецЕсли;
	
КонецПроцедуры // ЗаполнитьПоДокументу()

// Процедура вызывает процедуру заполнения документа по основанию.
// и устанавливает доступность реквизитов после заполнения
//
&НаКлиенте
Процедура ЗаполнитьПоДокументуИУстановитьДоступность()
	
	Ответ = Неопределено;

	
	ПоказатьВопрос(Новый ОписаниеОповещения("ЗаполнитьПоДокументуИУстановитьДоступностьЗавершение", ЭтотОбъект), НСтр("ru='Документ будет очищен и заполнен по ""Основанию""! Продолжить выполнение операции?';uk='Документ буде очищений і заповнений по ""Підставі""! Продовжити виконання операції?'"), РежимДиалогаВопрос.ДаНет, 0);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоДокументуИУстановитьДоступностьЗавершение(Результат, ДополнительныеПараметры) Экспорт
    
    Ответ = Результат;
    Если Ответ = КодВозвратаДиалога.Да Тогда
        ЗаполнитьПоДокументу(Объект.ДокументОснование);
        УстановитьВидимостьДоступность();
        УстановитьДоступностьРеквизитовПеречисленияНалогов();
    КонецЕсли;

КонецПроцедуры // ЗаполнитьПоДокументуИУстановитьДоступность()

// Процедура выполняет необходимые действия формирования текста назначения
// платежа.
//
&НаКлиенте
Процедура СформироватьНазначениеПлатежа(ИзменениеСуммы = Ложь)
	
	КодЯзыкаПечать = "uk";	
	
	ЭтоПеречислениеВБюджет = Объект.ВидОперации =  ПредопределенноеЗначение("Перечисление.ВидыОперацийПлатежноеПоручение.ПеречислениеНалога");
	Если ИзменениеСуммы Тогда
		ПозицияСуммы = СтрНайти(Объект.НазначениеПлатежа, "Сумма ");
		Если ПозицияСуммы = 0
		   И ЗначениеЗаполнено(Объект.НазначениеПлатежа) Тогда
			ТекстНазначение = Объект.НазначениеПлатежа;
		Иначе
			ТекстНазначение = Лев(Объект.НазначениеПлатежа, ПозицияСуммы - 1);
		КонецЕсли;
		Если Прав(ТекстНазначение, 1) = Символы.ПС Тогда
			ТекстНазначение = Лев(ТекстНазначение, СтрДлина(ТекстНазначение) - 1);
		КонецЕсли;
		Если НЕ ЗначениеЗаполнено(ТекстНазначение) Тогда
			ТекстНазначение = НазначениеПлатежа;
		КонецЕсли;
	Иначе
		Если ЭтоПеречислениеВБюджет И ЗначениеЗаполнено(Объект.Налог) Тогда
			ЗаполнитьРеквизитыПоНалогу();
			ТекстНазначение = Объект.НазначениеПлатежа;
		Иначе
			Если ПустаяСтрока(НазначениеПлатежа)
				И ЗначениеЗаполнено(Объект.ДокументОснование)
				И ТипЗнч(Объект.ДокументОснование) = Тип("ДокументСсылка.СчетНаОплатуПоставщика")
				И ЗначениеЗаполнено(НомерВходящегоДокумента) Тогда
			    НазначениеПлатежа = НСтр("ru='Оплата по счету N %НомерСчета%';uk='Оплата за рахунком N %НомерСчета%'", КодЯзыкаПечать);
				НазначениеПлатежа = СтрЗаменить(НазначениеПлатежа, "%НомерСчета%", СокрЛП(Строка(НомерВходящегоДокумента)));
				Если ЗначениеЗаполнено(ДатаВходящегоДокумента) Тогда
					НазначениеПлатежа = НазначениеПлатежа + НСтр("ru=' от ';uk=' від '", КодЯзыкаПечать) + СокрЛП(Строка(Формат(ДатаВходящегоДокумента, "Л="+КодЯзыкаПечать+"; ДФ='dd MMMM yyyy'"))) + НСтр("ru=' г.';uk=' р.'", КодЯзыкаПечать);
				КонецЕсли;
			КонецЕсли;
			ТекстНазначение = НазначениеПлатежа;
		КонецЕсли;
	КонецЕсли;
	
	ТекстСумма = Строка(Формат(Объект.СуммаДокумента, "ЧЦ=15; ЧДЦ=2; ЧРД=,; ЧН=0,00; ЧГ="));
	
	ТекстНДС = "";
	
	СтавкаНДС = УправлениеНебольшойФирмойПовтИсп.ПолучитьЗначениеСтавкиНДС(Объект.СтавкаНДС);
	
	Если ЗначениеЗаполнено(Объект.СтавкаНДС) И НЕ БезНалогаНДС Тогда
		ТекстНДС = НСтр("ru='НДС(%СтавкаНДС%) %СуммаНДС%';uk='ПДВ(%СтавкаНДС%) %СуммаНДС%'", КодЯзыкаПечать);
		ТекстНДС = СтрЗаменить(ТекстНДС, "%СтавкаНДС%", Строка(Объект.СтавкаНДС));
		ТекстНДС = СтрЗаменить(ТекстНДС, "%СуммаНДС%", Строка(Формат(Объект.СуммаНДС, "ЧЦ=15; ЧДЦ=2; ЧРД=,; ЧН=0,00; ЧГ=")));
	КонецЕсли;
	
	ТекстНазначениеПлатежа = НСтр("ru='%ТекстНазначение%"
"Сумма %ТекстСумма%"
"%ЗначениеСтавкиНДС% %ТекстНДС%';uk='%ТекстНазначение%"
"Сума %ТекстСумма%"
"%ЗначениеСтавкиНДС% %ТекстНДС%'",
	КодЯзыкаПечать);

	ТекстНазначениеПлатежа = СтрЗаменить(ТекстНазначениеПлатежа, "%ТекстНазначение%", ТекстНазначение);
	ТекстНазначениеПлатежа = СтрЗаменить(ТекстНазначениеПлатежа, "%ТекстСумма%", ТекстСумма);
	ТекстНазначениеПлатежа = СтрЗаменить(ТекстНазначениеПлатежа, "%ЗначениеСтавкиНДС%", ?(БезНалогаНДС ИЛИ НЕ ЗначениеЗаполнено(Объект.СтавкаНДС), НСтр("ru = 'Без налога (НДС)', uk = 'Без податку (ПДВ)'", КодЯзыкаПечать), НСтр("ru = 'В т.ч. ', uk = 'В т.ч.'", КодЯзыкаПечать)));
	ТекстНазначениеПлатежа = СтрЗаменить(ТекстНазначениеПлатежа, "%ТекстНДС%", ТекстНДС);
	
	Объект.НазначениеПлатежа = ТекстНазначениеПлатежа;
	
КонецПроцедуры // СформироватьНазначениеПлатежа()

// Процедура выполняет необходимые действия по формированию реквизита
// ПоказательПериода.
//
&НаКлиенте
Процедура УстановитьПоказательПериода()
	
	Если Объект.ВидПеречисленияВБюджет = ПредопределенноеЗначение("Перечисление.ВидыПеречисленийВБюджет.ТаможенныйПлатеж") Тогда
		Объект.ПоказательПериода = "";
	ИначеЕсли ПериодичностьУплаты = "0" Тогда
		Объект.ПоказательПериода = "0";
	ИначеЕсли ПериодичностьУплаты = "-" Тогда
		Объект.ПоказательПериода = Формат(ДатаПлатежа, "ДФ=dd.MM.yyyy");
	Иначе
		Объект.ПоказательПериода = Лев(ПериодичностьУплаты, 2)
								 + "."
								 + Формат(ПериодПлатежа, "ЧЦ=2; ЧН=; ЧВН=")
								 + "."
								 + Формат(ГодПлатежа, "ЧЦ=4; ЧГ=");
	КонецЕсли;
	
КонецПроцедуры // УстановитьПоказательПериода()

// Получает набор данных с сервера для процедуры ДоговорПриИзменении.
//
&НаСервереБезКонтекста
Функция ПолучитьДанныеДокументОснованиеПриИзменении(ДокументОснование)
	
	СтруктураДанные = Новый Структура();
	
	СтруктураДанные.Вставить("ДатаВходящегоДокумента",  ДокументОснование.ДатаВходящегоДокумента);
	СтруктураДанные.Вставить("НомерВходящегоДокумента", ДокументОснование.НомерВходящегоДокумента);
	
	Возврат СтруктураДанные;
	
КонецФункции // ПолучитьДанныеДокументОснованиеПриИзменении()

// Получает набор данных с сервера для процедуры ДоговорПриИзменении.
//
&НаСервере
Функция ПолучитьДанныеОрганизацияПриИзменении(Организация, БанковскийСчет, Контрагент, СчетКонтрагента, ВидОперации, Период)
	
	СтруктураДанные = Новый Структура();
	
	СтруктураДанные.Вставить("Компания", 				УправлениеНебольшойФирмойСервер.ПолучитьОрганизацию(Организация));
	СтруктураДанные.Вставить("НаименованиеПолное",		Организация.НаименованиеПолное);
	СтруктураДанные.Вставить("НаименованиеПлательщикаПриПеречисленииНалогов", Организация.НаименованиеПлательщикаПриПеречисленииНалогов);
	СтруктураДанные.Вставить("КодКОАТУУ",               Организация.КодКОАТУУ);
	
	//  Банковский счет организации
	ЗначениеДляСтруктуры	= ?(БанковскийСчет.Владелец = Организация, БанковскийСчет, Организация.БанковскийСчетПоУмолчанию);
	СтруктураДанные.Вставить("БанковскийСчет", 			ЗначениеДляСтруктуры);
	
	//  Валюта документа
	ЗначениеДляСтруктуры	= ?(ЗначениеЗаполнено(ЗначениеДляСтруктуры), ЗначениеДляСтруктуры.ВалютаДенежныхСредств, Константы.НациональнаяВалюта.Получить());
	СтруктураДанные.Вставить("ВалютаДокумента", 		ЗначениеДляСтруктуры);
	
	//  Банковский счет контрагента
	ЗначениеДляСтруктуры	= ?(ЗначениеЗаполнено(СчетКонтрагента) И ЗначениеДляСтруктуры = СчетКонтрагента.ВалютаДенежныхСредств, СчетКонтрагента, Справочники.БанковскиеСчета.ПустаяСсылка());
	СтруктураДанные.Вставить("СчетКонтрагента", 		ЗначениеДляСтруктуры);
	
	СтруктураДанные.Вставить("ТекстКорреспондента", 	Организация.БанковскийСчетПоУмолчанию.ТекстКорреспондента);
	СтруктураДанные.Вставить("ИННПлательщика", 			Организация.ИНН);
	СтруктураДанные.Вставить("ЕДРПОУПлательщика", 		Организация.КодПоЕДРПОУ);
	СтруктураДанные.Вставить("ЭтоФизЛицо", 				Организация.ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо);
	
	ОбновитьВидимостьИОстаткиДС(СтруктураДанные.БанковскийСчет);
	
	Возврат СтруктураДанные;
	
КонецФункции // ПолучитьДанныеОрганизацияПриИзменении()

// Получает набор данных с сервера для процедуры СчетОрганизацииПриИзменении.
//
&НаСервере
Функция ПолучитьДанныеСчетОрганизацииПриИзменении(Организация, СчетОрганизации, ВалютаПередИзменением, Дата, ВидОперации)
	
	СтруктураДанные = Новый Структура();
	СтруктураДанные.Вставить("ТекстКорреспондента", СчетОрганизации.ТекстКорреспондента);
	СтруктураДанные.Вставить("НаименованиеПолное", ?(ЗначениеЗаполнено(СчетОрганизации), СчетОрганизации.Владелец.НаименованиеПолное, ""));
	СтруктураДанные.Вставить("НаименованиеПлательщикаПриПеречисленииНалогов", ?(ЗначениеЗаполнено(СчетОрганизации), СчетОрганизации.Владелец.НаименованиеПлательщикаПриПеречисленииНалогов, ""));
	СтруктураДанные.Вставить("ВалютаДенежныхСредств", СчетОрганизации.ВалютаДенежныхСредств);
	
	СтруктураДанные.Вставить(
		"ВалютаКурсКратность",
		РегистрыСведений.КурсыВалют.ПолучитьПоследнее(
			Дата,
			Новый Структура("Валюта", СчетОрганизации.ВалютаДенежныхСредств)
		)
	);
	
	СтруктураДанные.Вставить(
		"ВалютаКурсКратностьПередИзменением",
		РегистрыСведений.КурсыВалют.ПолучитьПоследнее(
			Дата,
			Новый Структура("Валюта", ВалютаПередИзменением)
		)
	);
	
	ОбновитьВидимостьИОстаткиДС(СчетОрганизации);
	
	Возврат СтруктураДанные;
	
КонецФункции // ПолучитьДанныеСчетОрганизацииПриИзменении()

// Получает набор данных с сервера для процедуры ДоговорПриИзменении.
//
&НаСервереБезКонтекста
Функция ПолучитьДанныеДатаПриИзменении(ДокументСсылка, ДатаНовая, ДатаПередИзменением)
	
	РазностьДат = УправлениеНебольшойФирмойСервер.ПроверитьНомерДокумента(ДокументСсылка, ДатаНовая, ДатаПередИзменением);
		
	СтруктураДанные = Новый Структура();
	СтруктураДанные.Вставить("РазностьДат", РазностьДат);
		
	Возврат СтруктураДанные;
	
КонецФункции // ПолучитьДанныеДатаПриИзменении()

// Получает набор данных с сервера для процедуры ДоговорПриИзменении.
//
&НаСервере
Функция ПолучитьДанныеКонтрагентПриИзменении(Контрагент, ВалютаДокумента, ВидОперации)
	
	СтруктураДанные = Новый Структура();
	СтруктураДанные.Вставить("НаименованиеПолное", 	Контрагент.НаименованиеПолное);
	СтруктураДанные.Вставить("СчетКонтрагента", 	?(Контрагент.БанковскийСчетПоУмолчанию.ВалютаДенежныхСредств = ВалютаДокумента, Контрагент.БанковскийСчетПоУмолчанию, Справочники.БанковскиеСчета.ПустаяСсылка()));
	СтруктураДанные.Вставить("ИННПолучателя", 		Контрагент.ИНН);
	СтруктураДанные.Вставить("ЕДРПОУПолучателя",    Контрагент.КодПоЕДРПОУ);
	СтруктураДанные.Вставить("НазначениеПлатежа",	Контрагент.БанковскийСчетПоУмолчанию.ТекстНазначения);
	СтруктураДанные.Вставить("ТекстКорреспондента",	Контрагент.БанковскийСчетПоУмолчанию.ТекстКорреспондента);
	
	ОбновитьВидимостьИОстаткиВзаиморасчетов();
	
	Возврат СтруктураДанные;
	
КонецФункции // ПолучитьДанныеКонтрагентПриИзменении()

// Получает набор данных с сервера для процедуры ДоговорПриИзменении.
//
&НаСервереБезКонтекста
Функция ПолучитьДанныеСчетКонтрагентаПриИзменении(Контрагент, СчетКонтрагента, ВидОперации)
	
	СтруктураДанные = Новый Структура();
	СтруктураДанные.Вставить("НазначениеПлатежа", СчетКонтрагента.ТекстНазначения);
	СтруктураДанные.Вставить("ТекстКорреспондента", СчетКонтрагента.ТекстКорреспондента);
	Если ЗначениеЗаполнено(СчетКонтрагента.Владелец) Тогда
		СтруктураДанные.Вставить("НаименованиеПолное", СчетКонтрагента.Владелец.НаименованиеПолное);
	Иначе
		СтруктураДанные.Вставить("НаименованиеПолное", Контрагент.НаименованиеПолное);
	КонецЕсли;
	
	Возврат СтруктураДанные;
	
КонецФункции // ПолучитьДанныеСчетКонтрагентаПриИзменении()

// Получает набор данных с сервера для процедуры СтавкаНДСПриИзменении.
//
&НаСервереБезКонтекста
Функция ПолучитьДанныеСтавкаНДСПриИзменении(СтавкаНДС)
	
	СтруктураДанные = Новый Структура();
	
	СтруктураДанные.Вставить("НеОблагается", СтавкаНДС.НеОблагается);
	СтруктураДанные.Вставить("Ставка", СтавкаНДС.Ставка);	
		
	Возврат СтруктураДанные;
	
КонецФункции // ПолучитьДанныеСтавкаНДСПриИзменении()

// Заполняет реквизиты платежа в бюджет по умолчанию.
//
&НаКлиенте
Процедура ЗаполнитьРеквизитыПлатежаВБюджетПоУмолчанию()
	
	Если Объект.ВидПеречисленияВБюджет = ПредопределенноеЗначение("Перечисление.ВидыПеречисленийВБюджет.НалоговыйПлатеж") Тогда
		Объект.СтатусСоставителя = "01";
		Объект.ПоказательОснования = "0";
		Объект.ПоказательПериода   = "МС." + Формат(Месяц(Объект.Дата), "ЧЦ=2; ЧВН=") + "." + Формат(Год(Объект.Дата), "ЧГ=");		
		ПериодичностьУплаты = "МС";
		ГодПлатежа = Год(Объект.Дата);
		ПериодПлатежа = Месяц(Объект.Дата);
		Объект.ПоказательНомера = "";
		Объект.ПоказательДаты = "";
	ИначеЕсли Объект.ВидПеречисленияВБюджет = ПредопределенноеЗначение("Перечисление.ВидыПеречисленийВБюджет.ТаможенныйПлатеж") Тогда
		Объект.СтатусСоставителя   = "06";
		Объект.ПоказательОснования = "0";
		Объект.ПоказательПериода   = "";
		Объект.ПоказательНомера    = "";
		Объект.ПоказательДаты = "";
		ПериодичностьУплаты = "0";
		ГодПлатежа = 0;
	Иначе
		Объект.СтатусСоставителя   = "08";
		Объект.ПоказательОснования = "0";
		Объект.ПоказательПериода   = "0";
		Объект.ПоказательНомера    = "";
		Объект.ПоказательДаты = "";
		ПериодичностьУплаты = "0";
		ГодПлатежа = 0;
	КонецЕсли;
	
	Если ДатаДокумента >= '20150101'
	 ИЛИ Объект.Дата >= '20150101' Тогда // Приказ Минфина №126н от 30.10.2014.
		Объект.ПоказательТипа = "";
	Иначе
		Объект.ПоказательТипа = "0";
	КонецЕсли;
	
	УстановитьПоказательПериода();
	УстановитьДоступностьРеквизитовПеречисленияНалогов();
	
КонецПроцедуры // ЗаполнитьРеквизитыПлатежаВБюджетПоУмолчанию()

// Устанавливает текущую страницу в зависимости от вида перечисления в бюджет.
//
&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьВидимостьВЗависимостиОтВидаПеречисленияВБюджет(Форма)
	Элементы =  Форма.Элементы;
	Элементы.НалоговыйПлатеж.Видимость = Ложь;
	Элементы.ТаможенныйПлатеж.Видимость = Ложь;
	Элементы.ИнойПлатеж.Видимость = Ложь;
	
	Если Форма.Объект.ВидПеречисленияВБюджет = ПредопределенноеЗначение("Перечисление.ВидыПеречисленийВБюджет.НалоговыйПлатеж") Тогда
		Элементы.НалоговыйПлатеж.Видимость = Истина;
	ИначеЕсли Форма.Объект.ВидПеречисленияВБюджет = ПредопределенноеЗначение("Перечисление.ВидыПеречисленийВБюджет.ТаможенныйПлатеж") Тогда
		Элементы.ТаможенныйПлатеж.Видимость = Истина;
	Иначе
		Элементы.ИнойПлатеж.Видимость = Истина;
	КонецЕсли;
	
КонецПроцедуры // УстановитьТекущуюСтраницуВЗависимостиОтВидаПеречисленияВБюджет()

&НаСервере
Процедура ОбновитьВидимостьИОстаткиВзаиморасчетов()
	
	Элементы.ОстатокВзаиморасчетов.Видимость = НЕ ЗначениеЗаполнено(Объект.Ссылка) И ЗначениеЗаполнено(Объект.Контрагент);

	Если НЕ Элементы.ОстатокВзаиморасчетов.Видимость Тогда
		//Только для новых объектов
		Элементы.Контрагент.АвтоМаксимальнаяШирина = Истина;
		возврат;
	КонецЕсли;
	
	Элементы.Контрагент.АвтоМаксимальнаяШирина = Ложь;
	Элементы.Контрагент.МаксимальнаяШирина = 22;
	Элементы.ОстатокВзаиморасчетов.Заголовок = РаботаСКонтрагентамиУНФ.ЗаголовокНадписиВзаиморасчетов(Объект.Контрагент);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьВидимостьИОстаткиДС(БанковскийСчет = Неопределено)
	
	Если БанковскийСчет = Неопределено Тогда
		ТекущийБанковскийСчет = Объект.БанковскийСчет;
	Иначе
		ТекущийБанковскийСчет = БанковскийСчет;
	КонецЕсли;
	
	Элементы.ОстатокДенежныхСредств.Видимость = НЕ ЗначениеЗаполнено(Объект.Ссылка) И ЗначениеЗаполнено(ТекущийБанковскийСчет);

	Если НЕ Элементы.ОстатокДенежныхСредств.Видимость Тогда
		Элементы.СчетОрганизации.АвтоМаксимальнаяШирина = Истина;
		//Только для новых объектов
		возврат;
	КонецЕсли;
	
	Элементы.СчетОрганизации.АвтоМаксимальнаяШирина = Ложь;
	Элементы.СчетОрганизации.МаксимальнаяШирина = 22;
	Элементы.ОстатокДенежныхСредств.Заголовок = ДвиженияДенежныхСредствВызовСервера.ЗаголовокНадписиОстатковДС(ТекущийБанковскийСчет, Объект.Организация);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ДанныеВыбораНалогаПоКБК(Знач КодБК)
	
	Возврат Справочники.ВидыНалогов.ДанныеВыбораПоКБК(КодБК);
	
КонецФункции

&НаСервере
Процедура ЗаполнитьРеквизитыПоНалогу()
	Перем ВидНалога;
	
	Если ЗначениеЗаполнено(Объект.Дата) Тогда
		Период = Объект.Дата;
	Иначе
		Период = НачалоДня(ОбщегоНазначения.ТекущаяДатаПользователя());
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.Налог) Тогда
		ВидНалога = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Налог, "ВидНалога");
	КонецЕсли;
	
	ПоказателиНалоговогоПериода = ПлатежиВБюджетКлиентСервер.ПоказателиНалоговогоПериода(Объект.Организация, ВидНалога, Период);
	
	Если ЗначениеЗаполнено(Объект.Налог) Тогда
		Объект.НазначениеПлатежа = Справочники.ВидыНалогов.НазначениеПлатежа(
			Объект.Налог,,
			Объект.Организация,
			Макс(ПоказателиНалоговогоПериода.Период, Период),
			ПоказателиНалоговогоПериода.ПредставлениеНалоговогоПериода);
		
		Объект.КодБК = Справочники.ВидыНалогов.КБК(
			Объект.Налог,,
			Макс(ПоказателиНалоговогоПериода.Период, Период));
	КонецЕсли;
	
	Если ПлатежиВБюджетКлиентСервер.ВидГосударственногоОргана(Объект.КодБК) = Перечисления.ВидыГосударственныхОрганов.НалоговыйОрган Тогда
		Объект.ВидПеречисленияВБюджет = Перечисления.ВидыПеречисленийВБюджет.НалоговыйПлатеж;
	ИначеЕсли ПлатежиВБюджетКлиентСервер.ПлатежАдминистрируетсяТаможеннымиОрганами(Объект.КодБК) Тогда
		Объект.ВидПеречисленияВБюджет = Перечисления.ВидыПеречисленийВБюджет.ТаможенныйПлатеж;
	Иначе
		Объект.ВидПеречисленияВБюджет = Перечисления.ВидыПеречисленийВБюджет.ИнойПлатеж;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область УправлениеВнешнимВидомФормы

// Процедура устанавливает доступность реквизитов перечисления налогов.
//
&НаКлиенте
Процедура УстановитьДоступностьРеквизитовПеречисленияНалогов()
	
	Если ПериодичностьУплаты = "0" Тогда
		Элементы.ГодПериод.Видимость = Ложь;
		Элементы.ДатаПлатежа.Видимость = Ложь;
	Иначе
		Если ПериодичностьУплаты = "-" Тогда
			Элементы.ГодПериод.Видимость = Ложь;
			Элементы.ДатаПлатежа.Видимость = Истина;
		Иначе
			Элементы.ГодПериод.Видимость = Истина;
			Элементы.ДатаПлатежа.Видимость = Ложь;
			Элементы.ПериодПлатежа.Видимость = ПериодичностьУплаты <> "ГД";
			Если ПериодичностьУплаты = "МС" Тогда
				Элементы.ПериодПлатежа.МинимальноеЗначение = 1;
				Элементы.ПериодПлатежа.МаксимальноеЗначение = 12;
				Элементы.ПериодПлатежа.Заголовок = "Месяц";
			ИначеЕсли ПериодичностьУплаты = "КВ" Тогда
				Элементы.ПериодПлатежа.МинимальноеЗначение = 1;
				Элементы.ПериодПлатежа.МаксимальноеЗначение = 4;
				Элементы.ПериодПлатежа.Заголовок = "Квартал";
			ИначеЕсли ПериодичностьУплаты = "ПЛ" Тогда
				Элементы.ПериодПлатежа.МинимальноеЗначение = 1;
				Элементы.ПериодПлатежа.МаксимальноеЗначение = 2;
				Элементы.ПериодПлатежа.Заголовок = "Полугодие";
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // УстановитьДоступностьРеквизитовПеречисленияНалогов()

// Процедура устанавливает доступность реквизитов формы, в зависимости от вида 
// операции.
//
&НаКлиенте
Процедура УстановитьВидимостьДоступность()
	
	Элементы.РеквизитыДляПеречисленияНалога.Видимость = Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийПлатежноеПоручение.ПеречислениеНалога");
	Элементы.Налог.Видимость     = Элементы.РеквизитыДляПеречисленияНалога.Видимость;
	Элементы.СтавкаНДС.Видимость = Объект.ВидОперации <> ПредопределенноеЗначение("Перечисление.ВидыОперацийПлатежноеПоручение.ПеречислениеНалога");
	Элементы.СуммаНДС.Видимость  = Объект.ВидОперации <> ПредопределенноеЗначение("Перечисление.ВидыОперацийПлатежноеПоручение.ПеречислениеНалога");
	
КонецПроцедуры // УстановитьДоступность()

#КонецОбласти

#Область ЗаполнениеОбъектов

&НаКлиенте
Процедура СохранитьДокументКакШаблон(Параметр) Экспорт
	
	ЗаполнениеОбъектовУНФКлиент.СохранитьДокументКакШаблон(Объект, ОтображаемыеРеквизиты(), Параметр);
	
КонецПроцедуры

&НаСервере
Функция ОтображаемыеРеквизиты()
	
	Возврат ЗаполнениеОбъектовУНФ.ОтображаемыеРеквизиты(ЭтотОбъект);
	
КонецФункции

#КонецОбласти

#Область ОбменСGoogle

&НаКлиенте
Процедура Подключаемый_ОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ОбменСGoogleКлиент.Подключаемый_ОбработкаВыбора(
	ЭтотОбъект,
	Элемент,
	ВыбранноеЗначение,
	СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_АвтоПодбор(Элемент, Текст, ДанныеВыбора, Параметры, Ожидание, СтандартнаяОбработка)
	
	ОбменСGoogleКлиент.Подключаемый_АвтоПодбор(
	ЭтотОбъект,
	Элемент,
	Текст,
	ДанныеВыбора,
	Параметры,
	Ожидание,
	СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиБиблиотек

// ЭДО
&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуЭДО(Команда)
	
	ЭлектронноеВзаимодействиеСлужебныйКлиент.ВыполнитьПодключаемуюКомандуЭДО(Команда, ЭтотОбъект, Объект);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьТекстСостоянияЭДНаСервере()
	
	ТекстСостоянияЭД = ОбменСКонтрагентамиКлиентСервер.ПолучитьТекстСостоянияЭД(Объект.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура СостояниеЭДНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОбменСКонтрагентамиКлиент.ОткрытьАктуальныйЭД(Объект.Ссылка, ЭтотОбъект);

КонецПроцедуры
// Конец ЭДО

// СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
&НаКлиенте
Процедура Подключаемый_ВыполнитьНазначаемуюКоманду(Команда)
	Если НЕ ДополнительныеОтчетыИОбработкиКлиент.ВыполнитьНазначаемуюКомандуНаКлиенте(ЭтотОбъект, Команда.Имя) Тогда
		РезультатВыполнения = Неопределено;
		ДополнительныеОтчетыИОбработкиВыполнитьНазначаемуюКомандуНаСервере(Команда.Имя, РезультатВыполнения);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ДополнительныеОтчетыИОбработкиВыполнитьНазначаемуюКомандуНаСервере(ИмяЭлемента, РезультатВыполнения)
	ДополнительныеОтчетыИОбработки.ВыполнитьНазначаемуюКомандуНаСервере(ЭтотОбъект, ИмяЭлемента, РезультатВыполнения);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки

// СтандартныеПодсистемы.Печать
&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуПечати(Команда)
	УправлениеПечатьюКлиент.ВыполнитьПодключаемуюКомандуПечати(Команда, ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.Печать

#КонецОбласти

#Область ОбработчикиКомандФормы

//&НаКлиенте
//Процедура ВыгрузитьВКлиентБанкаТекущийДокумент(Команда)
//	
//	Если Объект.Ссылка.Пустая() Тогда
//		ПоказатьПредупреждение(Неопределено, "Сначала запишите документ.");
//		Возврат;
//	КонецЕсли;
//	
//	СписокВыгрузки = Новый СписокЗначений;
//	СписокВыгрузки.Добавить(Объект.Ссылка);
//	ПараметрыОткрытия = Новый Структура("СписокПлатежек, БанковскийСчет", СписокВыгрузки, Объект.БанковскийСчет);
//	ОткрытьФорму("Обработка.КлиентБанк.Форма.СохранениеПлатежек", ПараметрыОткрытия,,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
//	
//КонецПроцедуры

#КонецОбласти

#Область Основание

&НаСервереБезКонтекста
Функция ПолучитьСписокДляВыбораДокументаОснования()
	
	СписокОснований = Новый СписокЗначений;
	
	СписокОснований.Добавить("Документ.ЗаказПоставщику.ФормаВыбора", НСтр("ru='Заказ поставщику';uk='Замовлення постачальникові'"));
	СписокОснований.Добавить("Документ.НачислениеНалогов.ФормаВыбора", НСтр("ru='Начисление налогов';uk='Нарахування податків'"));
	СписокОснований.Добавить("Документ.СчетНаОплатуПоставщика.ФормаВыбора", НСтр("ru='Счет на оплату (полученный)';uk='Рахунок на оплату (отриманий)'"));
	Если ПолучитьФункциональнуюОпцию("ПлатежныйКалендарь") Тогда
		СписокОснований.Добавить("Документ.РасходДСПлан.ФормаВыбора", НСтр("ru='Заявка на расход денег';uk='Заявка на витрату грошей'"));
	КонецЕсли;
	
	СписокОснований.СортироватьПоПредставлению(НаправлениеСортировки.Возр);
	
	Возврат СписокОснований;
	
КонецФункции

&НаКлиенте
Процедура ДокументОснованиеНадписьОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если НавигационнаяСсылкаФорматированнойСтроки = "удалить" Тогда
		
		Объект.ДокументОснование = Неопределено;
		Элементы.ДокументОснованиеНадпись.Заголовок = РаботаСФормойДокументаКлиентСервер.СформироватьНадписьДокументОснование(Неопределено);
		Модифицированность = Истина;
		
		НомерВходящегоДокумента = "";
		ДатаВходящегоДокумента = '00010101';
		
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки = "заполнить" Тогда
		ЗаполнитьПоОснованиюНачало();
		
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки = "выбрать" Тогда
		
		//Выбрать основание
		ОписаниеОповещения = Новый ОписаниеОповещения("ВыборТипаОснованияЗавершение", ЭтотОбъект);
		ПоказатьВыборИзМеню(ОписаниеОповещения, ПолучитьСписокДляВыбораДокументаОснования(), Элемент);
		
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки = "открыть" Тогда
		
		Если НЕ ЗначениеЗаполнено(Объект.ДокументОснование) тогда
			возврат;
		КонецЕсли;
		
		РаботаСФормойДокументаКлиент.ОткрытьФормуДокументаПоТипу(Объект.ДокументОснование);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборТипаОснованияЗавершение(ВыбИмяФормы, Параметры) Экспорт
	
	Если ВыбИмяФормы<>Неопределено Тогда
		
		СтруктураПараметровОтбора = Новый Структура();
		Для каждого элОтбора Из ПараметрыВыбораДокументаОснования Цикл
			ИмяПоляОтбора = СтрЗаменить(элОтбора.Имя,"Отбор.","");
			СтруктураПараметровОтбора.Вставить(ИмяПоляОтбора, элОтбора.Значение);
		КонецЦикла;
		_ОповещениеОЗакрытии = Новый ОписаниеОповещения("ВыбратьОснованиеЗавершение", ЭтотОбъект);
		ОткрытьФорму(ВыбИмяФормы.Значение, СтруктураПараметровОтбора, ЭтотОбъект, ,,,_ОповещениеОЗакрытии);
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьОснованиеЗавершение(ВыбЗначение, Параметры) Экспорт

	Если ВыбЗначение<>Неопределено Тогда
		Объект.ДокументОснование = ВыбЗначение;
		Элементы.ДокументОснованиеНадпись.Заголовок = РаботаСФормойДокументаКлиентСервер.СформироватьНадписьДокументОснование(ВыбЗначение);
		Модифицированность = Истина;
		
		ЗаполнитьПоОснованиюНачало();
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоОснованиюНачало() Экспорт

	ОписаниеОповещения = Новый ОписаниеОповещения("ЗаполнитьПоДокументуИУстановитьДоступностьЗавершение", ЭтотОбъект);
	ПоказатьВопрос(
		ОписаниеОповещения, 
		НСтр("ru='Заполнить документ по выбранному основанию?';uk='Заповнити документ по обраній підставі?'"), 
		РежимДиалогаВопрос.ДаНет, 0);		

КонецПроцедуры

#КонецОбласти
