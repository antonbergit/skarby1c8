#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ДЛЯ ОБЕСПЕЧЕНИЯ ПРОВЕДЕНИЯ ДОКУМЕНТА

Функция СформироватьЗапросПоПачкамДокументовДляПроверки()
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ПачкиДокументов", ПачкиДокументов);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПачкиДокументов.ПачкаДокументов,
	|	ПачкиДокументов.НомерСтроки
	|ПОМЕСТИТЬ ВТПачкиДокументов
	|ИЗ
	|	&ПачкиДокументов КАК ПачкиДокументов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(ПачкиДокументов.ПачкаДокументов.ОтчетныйПериод, ДАТАВРЕМЯ(1,1,1)) КАК ОтчетныйПериод,
	|	ЕСТЬNULL(ПачкиДокументов.ПачкаДокументов.Организация, ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)) КАК Организация,
	|	ЕСТЬNULL(ПачкиДокументов.ПачкаДокументов.НомерПачки, 0) КАК НомерПачки,
	|	ПачкиДокументов.ПачкаДокументов КАК Документ,
	|	ЕСТЬNULL(ПачкиДокументов.ПачкаДокументов.ФайлСформирован, ЛОЖЬ) КАК ФайлСформирован,
	|	ПачкиДокументов.ПачкаДокументов,
	|	ПачкиДокументов.НомерСтроки,
	|	ПачкиДокументовДубль.НомерСтроки КАК НомерСтрокиДубль
	|ИЗ
	|	ВТПачкиДокументов КАК ПачкиДокументов
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТПачкиДокументов КАК ПачкиДокументовДубль
	|		ПО ПачкиДокументов.ПачкаДокументов = ПачкиДокументовДубль.ПачкаДокументов
	|			И ПачкиДокументов.НомерСтроки > ПачкиДокументовДубль.НомерСтроки";
	
	Возврат Запрос.Выполнить();
		
КонецФункции	

//////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПроверкаЗаполненияДокумента(ПриверяемыеРеквизиты, НепроверяемыеРеквизиты, Отказ)Экспорт
	ДанныеОрганизации = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Организация, "Наименование, КодПоОКПО");
			
	ВыборкаДокументовДляПроверки = СформироватьЗапросПоПачкамДокументовДляПроверки().Выбрать();
	
	Пока ВыборкаДокументовДляПроверки.Следующий() Цикл
		
		Если Не ВыборкаДокументовДляПроверки.ФайлСформирован  Тогда
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='У документа не сформирован файл!';uk='У документа не сформований файл!'"), ВыборкаДокументовДляПроверки.НомерСтроки);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, Ссылка, "Объект.ПачкиДокументов[" + Формат(ВыборкаДокументовДляПроверки.НомерСтроки - 1, "ЧН=0; ЧГ=0") + "].ПачкаДокументов",,Отказ);
		КонецЕсли;
		
		Если ВыборкаДокументовДляПроверки.ОтчетныйПериод <> ОтчетныйПериод Тогда
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='Отчетный период документа-пачки отличается от указанного!';uk='Звітний період документа-пачки відрізняється від зазначеного!'"), ВыборкаДокументовДляПроверки.НомерСтроки);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, Ссылка, "Объект.ПачкиДокументов[" + Формат(ВыборкаДокументовДляПроверки.НомерСтроки - 1, "ЧН=0; ЧГ=0") + "].ПачкаДокументов",,Отказ);
		КонецЕсли;
		
		Если ВыборкаДокументовДляПроверки.Организация <> Организация Тогда
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='Документ-пачка оформлен на другую организацию!';uk='Документ-пачка оформлений на іншу організацію!'"), ВыборкаДокументовДляПроверки.НомерСтроки);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, Ссылка, "Объект.ПачкиДокументов[" + Формат(ВыборкаДокументовДляПроверки.НомерСтроки - 1, "ЧН=0; ЧГ=0") + "].ПачкаДокументов",,Отказ);
		КонецЕсли;
		
		Если ВыборкаДокументовДляПроверки.НомерПачки = НомерПачки Тогда
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='Документ-пачка имеет такой же номер как и документ описи, рекомендуется изменить номер пачки в документе описи!';uk='Документ-пачка має такий же номер як і документ опису, рекомендується змінити номер пачки в документі опису!'"), ВыборкаДокументовДляПроверки.НомерСтроки);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, Ссылка, "Объект.ПачкиДокументов[" + Формат(ВыборкаДокументовДляПроверки.НомерСтроки - 1, "ЧН=0; ЧГ=0") + "].ПачкаДокументов",,Отказ);
		КонецЕсли;
		
		Если ВыборкаДокументовДляПроверки.НомерСтрокиДубль <> Null Тогда
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='Документ-пачка указан дважды!';uk='Документ-пачка зазначений двічі!'"), ВыборкаДокументовДляПроверки.НомерСтроки);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, Ссылка, "Объект.ПачкиДокументов[" + Формат(ВыборкаДокументовДляПроверки.НомерСтроки - 1, "ЧН=0; ЧГ=0") + "].ПачкаДокументов",,Отказ);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецЕсли
